// Гарант+ Килипенко 11.12.2024 [F00232032] установки зависимостей приборов учета Контрагента "Квадра" ++
#Область ПрограммныйИнтерфейс

// Параметры:
//  ДатаАктуальностиДанных - Дата, Неопределено
// Возвращаемое значение:
//  - Структура
//      * Успех - Булево
//      * ТекстСообщения - Строка, Неопределено
//      * КоличествоЗаписей - Число
Функция УстановитьЗависимостиПриборовУчетаКвадра(Знач ДатаАктуальностиДанных = Неопределено) Экспорт
    РезультатФункции = Новый Структура("Успех, КоличествоЗаписей, ТекстСообщения", Истина, 0);

    ДанныеЗаполнения = ПолучитьДанныеУстановкиЗависимостейПриборовУчетаКвадра(ДатаАктуальностиДанных);

    НачатьТранзакцию();
    Попытка
        Для Каждого СтрокаДанных Из ДанныеЗаполнения Цикл
            НаборЗаписей = РегистрыСведений.КВП_ЗависимыеСчетчики.СоздатьНаборЗаписей();
            НаборЗаписей.Отбор.ВыходнойПриборУчета.Установить(СтрокаДанных.ПриборУчетаВыход);
            НаборЗаписей.Отбор.ВходнойПриборУчета.Установить(СтрокаДанных.ПриборУчетаВход);

            НоваяЗапись = НаборЗаписей.Добавить();
            НоваяЗапись.ВыходнойПриборУчета = СтрокаДанных.ПриборУчетаВыход;
            НоваяЗапись.ВходнойПриборУчета = СтрокаДанных.ПриборУчетаВход;
            НоваяЗапись.ОтключитьПроверку = Ложь;

            РезультатФункции.КоличествоЗаписей = РезультатФункции.КоличествоЗаписей + 1;
            НаборЗаписей.Записать(Истина);
        КонецЦикла;

        ЗафиксироватьТранзакцию();
    Исключение
        ОтменитьТранзакцию();

        РезультатФункции.Успех = Ложь;
        РезультатФункции.КоличествоЗаписей = 0;
        РезультатФункции.ТекстСообщения = СтрШаблон(
                "Ошибка при записи зависимостей ПУ контрагента Квадра.
                |Информация: %1", ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
    КонецПопытки;

    Возврат РезультатФункции;
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс
// Гарант+ Килипенко 11.12.2024 [F00232032] установки зависимостей приборов учета Контрагента "Квадра" --

// Гарант+ Килипенко 11.12.2024 [F00232032] установки зависимостей приборов учета Контрагента "Квадра" ++
#Область СлужебныйПрограммныйИнтерфейс

// Параметры:
//  ДатаАктуальности - Дата, Неопределено
// Возвращаемое значение:
//  - ТаблицаЗначений
//      * ПриборУчетаВход - СправочникСсылка.КВП_Счетчики
//      * ПриборУчетаВыход - СправочникСсылка.КВП_Счетчики
//      * ЛицевойСчетВход - СправочникСсылка.КВП_ЛицевыеСчета
//      * ЛицевойСчетВыход - СправочникСсылка.КВП_ЛицевыеСчета
//      * ЗданиеВход - СправочникСсылка.КВП_Здания
//      * ЗданиеВыход - СправочникСсылка.КВП_Здания
//      * ПомещениеВход - СправочникСсылка.УПЖКХ_Помещения
//      * ПомещениеВыход - СправочникСсылка.УПЖКХ_Помещения
Функция ПолучитьДанныеУстановкиЗависимостейПриборовУчетаКвадра(Знач ДатаАктуальности = Неопределено) Экспорт
    Запрос = Новый Запрос;
    Запрос.Текст = ПолучитьТекстЗапросаУстановкиЗависимостейПриборовУчетаКвадра();

    КонтрагентКвадра = ПолучитьКонтрагентаКвадра(Истина).Ссылка;
    Запрос.УстановитьПараметр("Контрагенты", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(КонтрагентКвадра));

    Если ДатаАктуальности = Неопределено Тогда
        Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДатаАктуальности", "");
    Иначе
        Запрос.УстановитьПараметр("ДатаАктуальности", ДатаАктуальности);
    КонецЕсли;

    РезультатЗапроса = Запрос.Выполнить();
    РезультатФункции = РезультатЗапроса.Выгрузить();
    Возврат РезультатФункции;
КонецФункции

// Параметры:
//  ИгнорироватьПомеченныеНаУдаление - Булево
// Возвращаемое значение:
//  - Структура
//      * Ссылка - СправочникСсылка.Контрагенты
//      * Код - Строка
//      * Наименование - Строка
Функция ПолучитьКонтрагентаКвадра(Знач ИгнорироватьПомеченныеНаУдаление = Истина) Экспорт
    РезультатФункции = ГП_ОбщегоНазначения.ПолучитьЭлементСправочникаПоКоду(
            "Контрагенты", "00-000025", ИгнорироватьПомеченныеНаУдаление);

    ОжидаемоеНаименованиеКвадра = "Филиал АО Квадра-Курская генерация";
    Если РезультатФункции.Наименование <> ОжидаемоеНаименованиеКвадра Тогда
        ВызватьИсключение(СтрШаблон(
                "Наименование контрагента Квадра не соответствует ожидаемой. Ожидается: ""%1"", получено: ""%2""",
                ОжидаемоеНаименованиеКвадра, РезультатФункции.Наименование));
    КонецЕсли;

    Возврат РезультатФункции;
КонецФункции

#КонецОбласти // СлужебныйПрограммныйИнтерфейс
// Гарант+ Килипенко 11.12.2024 [F00232032] установки зависимостей приборов учета Контрагента "Квадра" --

// Гарант+ Килипенко 11.12.2024 [F00232032] установки зависимостей приборов учета Контрагента "Квадра" ++
#Область СлужебныеПроцедурыИФункции

// Возвращает текст запроса для установки зависимостей приборов учета (вход/выход) для Контрагента "Квадра"
Функция ПолучитьТекстЗапросаУстановкиЗависимостейПриборовУчетаКвадра()
    ТекстЗапроса =
        "ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	Контрагенты.Ссылка КАК Ссылка
        |ПОМЕСТИТЬ ВТ_Контрагенты
        |ИЗ
        |	Справочник.Контрагенты КАК Контрагенты
        |ГДЕ
        |	Контрагенты.Ссылка В (&Контрагенты)
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ОтветственныйСобственникНанимательЛицевогоСчета.Период КАК Период,
        |	ОтветственныйСобственникНанимательЛицевогоСчета.ЛицевойСчет КАК ЛицевойСчет,
        |	ВЫРАЗИТЬ(ОтветственныйСобственникНанимательЛицевогоСчета.ОтветственныйВладелец КАК Справочник.Контрагенты) КАК Контрагент
        |ПОМЕСТИТЬ ВТ_ЛицевыеСчетаКонтрагентов
        |ИЗ
        |	РегистрСведений.УПЖКХ_ОтветственныйСобственникНанимательЛицевогоСчета.СрезПоследних(&ДатаАктуальности, ) КАК ОтветственныйСобственникНанимательЛицевогоСчета
        |ГДЕ
        |	ОтветственныйСобственникНанимательЛицевогоСчета.ОтветственныйВладелец ССЫЛКА Справочник.Контрагенты
        |	И ОтветственныйСобственникНанимательЛицевогоСчета.ОтветственныйВладелец В
        |			(ВЫБРАТЬ
        |				Т.Ссылка
        |			ИЗ
        |				ВТ_Контрагенты КАК Т)
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ИсторияСостоянийПриборовУчета.Период КАК Период,
        |	ВЫРАЗИТЬ(ИсторияСостоянийПриборовУчета.Объект КАК Справочник.КВП_ЛицевыеСчета) КАК ЛицевойСчет,
        |	ИсторияСостоянийПриборовУчета.ПриборУчета КАК ПриборУчета,
        |	ИсторияСостоянийПриборовУчета.Действует КАК Действует,
        |	ИсторияСостоянийПриборовУчета.ДатаНачала КАК ДатаНачала,
        |	ИсторияСостоянийПриборовУчета.ДатаПоверки КАК ДатаПоверки,
        |	ИсторияСостоянийПриборовУчета.НомерПломбы КАК НомерПломбы,
        |	ИсторияСостоянийПриборовУчета.ПричинаОтключения КАК ПричинаОтключения
        |ПОМЕСТИТЬ ВТ_СчетчикиЛицевыхСчетов_Подготовка
        |ИЗ
        |	РегистрСведений.УПЖКХ_ИсторияСостоянийПриборовУчета.СрезПоследних(&ДатаАктуальности, ) КАК ИсторияСостоянийПриборовУчета
        |ГДЕ
        |	ИсторияСостоянийПриборовУчета.Объект ССЫЛКА Справочник.КВП_ЛицевыеСчета
        |	И ИсторияСостоянийПриборовУчета.Объект В
        |			(ВЫБРАТЬ
        |				Т.ЛицевойСчет
        |			ИЗ
        |				ВТ_ЛицевыеСчетаКонтрагентов КАК Т)
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВТ_СчетчикиЛицевыхСчетов_Подготовка.Период КАК Период,
        |	ВТ_СчетчикиЛицевыхСчетов_Подготовка.ЛицевойСчет КАК ЛицевойСчет,
        |	ВЫРАЗИТЬ(ВТ_СчетчикиЛицевыхСчетов_Подготовка.ЛицевойСчет.Адрес КАК Справочник.УПЖКХ_Помещения) КАК Помещение,
        |	ВЫРАЗИТЬ(ВТ_СчетчикиЛицевыхСчетов_Подготовка.ЛицевойСчет.Адрес.Владелец КАК Справочник.КВП_Здания) КАК Здание,
        |	ВТ_СчетчикиЛицевыхСчетов_Подготовка.ПриборУчета КАК ПриборУчета,
        |	ВТ_СчетчикиЛицевыхСчетов_Подготовка.Действует КАК Действует,
        |	ВТ_СчетчикиЛицевыхСчетов_Подготовка.ДатаНачала КАК ДатаНачала,
        |	ВТ_СчетчикиЛицевыхСчетов_Подготовка.ДатаПоверки КАК ДатаПоверки,
        |	ВТ_СчетчикиЛицевыхСчетов_Подготовка.НомерПломбы КАК НомерПломбы,
        |	ВТ_СчетчикиЛицевыхСчетов_Подготовка.ПричинаОтключения КАК ПричинаОтключения
        |ПОМЕСТИТЬ ВТ_СчетчикиЛицевыхСчетов
        |ИЗ
        |	ВТ_СчетчикиЛицевыхСчетов_Подготовка КАК ВТ_СчетчикиЛицевыхСчетов_Подготовка
        |ГДЕ
        |	(ВЫРАЗИТЬ(ВТ_СчетчикиЛицевыхСчетов_Подготовка.ЛицевойСчет.Адрес.Владелец КАК Справочник.КВП_Здания)) <> ЗНАЧЕНИЕ(Справочник.КВП_Здания.ПустаяСсылка)
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВТ_СчетчикиЛицевыхСчетов.Период КАК Период,
        |	ВТ_СчетчикиЛицевыхСчетов.ЛицевойСчет КАК ЛицевойСчет,
        |	ВТ_СчетчикиЛицевыхСчетов.Помещение КАК Помещение,
        |	ВТ_СчетчикиЛицевыхСчетов.Здание КАК Здание,
        |	ВТ_СчетчикиЛицевыхСчетов.ПриборУчета КАК ПриборУчета,
        |	ВТ_СчетчикиЛицевыхСчетов.Действует КАК Действует,
        |	ВТ_СчетчикиЛицевыхСчетов.ДатаНачала КАК ДатаНачала,
        |	ВТ_СчетчикиЛицевыхСчетов.ДатаПоверки КАК ДатаПоверки,
        |	ВТ_СчетчикиЛицевыхСчетов.НомерПломбы КАК НомерПломбы,
        |	ВТ_СчетчикиЛицевыхСчетов.ПричинаОтключения КАК ПричинаОтключения
        |ПОМЕСТИТЬ ВТ_ВходныеПриборыУчета
        |ИЗ
        |	ВТ_СчетчикиЛицевыхСчетов КАК ВТ_СчетчикиЛицевыхСчетов
        |ГДЕ
        |	ВТ_СчетчикиЛицевыхСчетов.ПриборУчета.ГП_ЭтоВиртуальныйСчетчик = ИСТИНА
        |	И ВТ_СчетчикиЛицевыхСчетов.Здание.Наименование ПОДОБНО ""%на_цели_приготовления%насел%""
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВТ_СчетчикиЛицевыхСчетов.Период КАК Период,
        |	ВТ_СчетчикиЛицевыхСчетов.ЛицевойСчет КАК ЛицевойСчет,
        |	ВТ_СчетчикиЛицевыхСчетов.Помещение КАК Помещение,
        |	ВТ_СчетчикиЛицевыхСчетов.Здание КАК Здание,
        |	ВТ_СчетчикиЛицевыхСчетов.ПриборУчета КАК ПриборУчета,
        |	ВТ_СчетчикиЛицевыхСчетов.Действует КАК Действует,
        |	ВТ_СчетчикиЛицевыхСчетов.ДатаНачала КАК ДатаНачала,
        |	ВТ_СчетчикиЛицевыхСчетов.ДатаПоверки КАК ДатаПоверки,
        |	ВТ_СчетчикиЛицевыхСчетов.НомерПломбы КАК НомерПломбы,
        |	ВТ_СчетчикиЛицевыхСчетов.ПричинаОтключения КАК ПричинаОтключения
        |ПОМЕСТИТЬ ВТ_ВыходныеПриборыУчета
        |ИЗ
        |	ВТ_СчетчикиЛицевыхСчетов КАК ВТ_СчетчикиЛицевыхСчетов
        |ГДЕ
        |	ВТ_СчетчикиЛицевыхСчетов.ПриборУчета.ГП_ЭтоВиртуальныйСчетчик = ЛОЖЬ
        |	И НЕ ВТ_СчетчикиЛицевыхСчетов.ПриборУчета В
        |				(ВЫБРАТЬ
        |					Т.ПриборУчета
        |				ИЗ
        |					ВТ_ВходныеПриборыУчета КАК Т)
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВТ_ВыходныеПриборыУчета.ЛицевойСчет КАК ЛицевойСчетВыход,
        |	ВТ_ВыходныеПриборыУчета.Помещение КАК ПомещениеВыход,
        |	ВТ_ВыходныеПриборыУчета.Здание КАК ЗданиеВыход,
        |	ВТ_ВходныеПриборыУчета.ЛицевойСчет КАК ЛицевойСчетВход,
        |	ВТ_ВходныеПриборыУчета.Помещение КАК ПомещениеВход,
        |	ВТ_ВходныеПриборыУчета.Здание КАК ЗданиеВход,
        |	ВТ_ВыходныеПриборыУчета.ПриборУчета КАК ПриборУчетаВыход,
        |	ВТ_ВходныеПриборыУчета.ПриборУчета КАК ПриборУчетаВход
        |ИЗ
        |	ВТ_ВыходныеПриборыУчета КАК ВТ_ВыходныеПриборыУчета
        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ВходныеПриборыУчета КАК ВТ_ВходныеПриборыУчета
        |		ПО ВТ_ВыходныеПриборыУчета.ПриборУчета <> ВТ_ВходныеПриборыУчета.ПриборУчета
        |			И (ВТ_ВходныеПриборыУчета.Здание.Наименование ПОДОБНО ВТ_ВыходныеПриборыУчета.Здание.Наименование + ""%на_цели_приготовления%насел%"")
        |";

    Возврат ТекстЗапроса;
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
// Гарант+ Килипенко 11.12.2024 [F00232032] установки зависимостей приборов учета Контрагента "Квадра" --
