// Гарант+ Килипенко 06.03.2025 [F00235776] Web-касса (счетмаш) ++
#Область ПрограммныйИнтерфейс

// Параметры:
//  ДанныеАутентификации - Структура
//      * Логин - Строка
//      * Пароль - Строка
//  Параметры - Структура
//      * ОбновлятьПринудительно - Булево
// Возвращаемое значение:
//  - Структура
//      * Успех - Булево
//      * ТекстСообщения - Строка, Неопределено
//      * ДатаВремяПолученияТокена - Дата, Неопределено
//      * Токен - Строка
Функция ПолучитьТокен(Знач ДанныеАутентификации, Знач Параметры) Экспорт
    РезультатФункции = Новый Структура("Успех, ТекстСообщения", Истина);
    РезультатФункции.Вставить("ДатаВремяПолученияТокена");
    РезультатФункции.Вставить("Токен", "");

    НужноОбновитьТокен = Истина;
    Если НЕ Параметры.Свойство("ОбновлятьПринудительно") ИЛИ НЕ Параметры.ОбновлятьПринудительно Тогда
        СтруктураДанныхБезопасногоХраненияДанныхТокена = НовыйСтруктураДанныхБезопасногоХраненияДанныхТокена();
        РезультатЧтенияДанныхТокена = ПрочитатьДанныеИзБезопасногоХранилища(
                СтруктураДанныхБезопасногоХраненияДанныхТокена, ДанныеАутентификации.Логин);
        НужноОбновитьТокен = ТипЗнч(РезультатЧтенияДанныхТокена) <> Тип("Структура")
            ИЛИ ПустаяСтрока(РезультатЧтенияДанныхТокена.Токен)
            ИЛИ (РезультатЧтенияДанныхТокена.ДатаОкончания <> Неопределено
                И РезультатЧтенияДанныхТокена.ДатаОкончания > ТекущаяДатаСеанса());
    КонецЕсли;

    Если НужноОбновитьТокен = Истина Тогда
        РезультатОбновленияТокена = ОбновитьТокен(ДанныеАутентификации);

        Если РезультатОбновленияТокена <> Неопределено Тогда
            Если РезультатОбновленияТокена.Успех = Ложь Тогда
                // Ошибка обновления токена
                РезультатФункции.Успех = Ложь;
                РезультатФункции.ТекстСообщения = РезультатОбновленияТокена.ТекстСообщения;
            Иначе
                // Успех. Токен обновлен
                РезультатФункции.Успех = Истина;
                РезультатФункции.Токен = РезультатОбновленияТокена.Токен;
                РезультатФункции.ДатаВремяПолученияТокена = РезультатОбновленияТокена.ДатаВремяПолученияТокена;
            КонецЕсли;
        КонецЕсли;
    КонецЕсли;

    Возврат РезультатФункции;
КонецФункции

// Параметры:
//  ДанныеАутентификации - Структура
//      * Логин - Строка
//      * Пароль - Строка
// Возвращаемое значение:
//  - Структура
//      * Успех - Булево
//      * ТекстСообщения - Строка, Неопределено
//      * ДатаВремяПолученияТокена - Дата, Неопределено
//      * Токен - Строка
Функция ОбновитьТокен(Знач ДанныеАутентификации) Экспорт
    РезультатФункции = Новый Структура("Успех, ТекстСообщения", Истина);
    РезультатФункции.Вставить("ДатаВремяПолученияТокена");
    РезультатФункции.Вставить("Токен", "");

    ШаблонСообщенияОшибкиЗапросаТокена =
        "Ошибка получения токена.
        |Информация: %1";

    ТелоЗапросаJSON = ЗаписатьЗначениеJSON(Новый Структура("login, password", ДанныеАутентификации.Логин, ДанныеАутентификации.Пароль));

    HTTPСоединение = НовыйHTTPСоединениеСчетмаш();
    ЗапросPOST = НовыйHTTPЗапросТокенаСчетмаш(ТелоЗапросаJSON);

    HTTPОтвет = Неопределено;
    Попытка
        HTTPОтвет = HTTPСоединение.ОтправитьДляОбработки(ЗапросPOST);
    Исключение
        РезультатФункции.Успех = Ложь;
        РезультатФункции.ТекстСообщения = СтрШаблон(
                ШаблонСообщенияОшибкиЗапросаТокена,
                ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
        Возврат РезультатФункции; // Ошибка получения токена
    КонецПопытки;

    Если HTTPОтвет = Неопределено ИЛИ HTTPОтвет.КодСостояния <> 200 Тогда
        РезультатФункции.Успех = Ложь;
        РезультатФункции.ТекстСообщения = СтрШаблон(
                ШаблонСообщенияОшибкиЗапросаТокена,
                ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
    Иначе
        СтрокаТелаОтвета = HTTPОтвет.ПолучитьТелоКакСтроку();
        СтруктураТелаОтвета = ПрочитатьЗначениеJSON(?(СтрокаТелаОтвета = Неопределено, "{}", СтрокаТелаОтвета));
        ЭтоВалиднаяСтруктураОтвета = ТипЗнч(СтруктураТелаОтвета) = Тип("Структура")
            И СтруктураТелаОтвета.Свойство("message") И СтруктураТелаОтвета.Свойство("code") И СтруктураТелаОтвета.Свойство("token");
        Если ЭтоВалиднаяСтруктураОтвета = Ложь Тогда
            РезультатФункции.Успех = Ложь;
            РезультатФункции.ТекстСообщения = СтрШаблон(
                    ШаблонСообщенияОшибкиЗапросаТокена, "Ошибка структуры ответа сервера");

        ИначеЕсли СтруктураТелаОтвета.code = 1 ИЛИ ТипЗнч(СтруктураТелаОтвета.token) <> Тип("Строка") Тогда
            РезультатФункции.Успех = Ложь;
            РезультатФункции.ТекстСообщения = СтрШаблон(
                    "Ошибка структуры ответа токена.
                    |Сообщение сервера: (код: %1) %2", Строка(СтруктураТелаОтвета.code), Строка(СтруктураТелаОтвета.message));

        Иначе
            РезультатФункции.Токен = СтруктураТелаОтвета.token;
            РезультатФункции.ДатаВремяПолученияТокена = ТекущаяДатаСеанса();

            // Запись данных токена в хранилище
            СтруктураДанныхБезопасногоХраненияДанныхТокена = НовыйСтруктураДанныхБезопасногоХраненияДанныхТокена();
            СтруктураДанныхБезопасногоХраненияДанныхТокена.Данные.Токен = РезультатФункции.Токен;
            СтруктураДанныхБезопасногоХраненияДанныхТокена.Данные.ДатаСоздания = РезультатФункции.ДатаВремяПолученияТокена;
            СтруктураДанныхБезопасногоХраненияДанныхТокена.Данные.ДатаОкончания =
                РезультатФункции.ДатаВремяПолученияТокена + ВремяЖизниТокенаСек();
            ЗаписатьДанныеВБезопасноеХранилище(
                СтруктураДанныхБезопасногоХраненияДанныхТокена, ДанныеАутентификации.Логин);
        КонецЕсли;
    КонецЕсли;

    Возврат РезультатФункции;
КонецФункции

Функция НовыйHTTPСоединениеСчетмаш() Экспорт
    Возврат Новый HTTPСоединение("online.schetmash.com", , , , , 5, Новый ЗащищенноеСоединениеOpenSSL);
КонецФункции

Функция НовыйHTTPЗапросТокенаСчетмаш(Знач СтрокаJS) Экспорт
    ЗапросPOST = Новый HTTPЗапрос("/lk/api/v1/token");
    ЗапросPOST.Заголовки.Вставить("Content-Type", "application/x-www-form-urlencoded");
    ЗапросPOST.УстановитьТелоИзСтроки(СтрокаJS, КодировкаТекста.Системная, ИспользованиеByteOrderMark.НеИспользовать);

    Возврат ЗапросPOST;
КонецФункции

// Возвращаемое значение:
//  - Структура
Функция НовыйСтруктураДанныхБезопасногоХраненияДанныхТокена() Экспорт
    РезультатФункции = Новый Структура("Владелец", Неопределено);
    РезультатФункции.Вставить("БазовыйВладелец", "ГП_APIСервисСчетмашОнлайнВ2");
    РезультатФункции.Вставить("КлючДанных", "ТОКЕН");
    РезультатФункции.Вставить("Данные", Новый Структура("Токен, ДатаСоздания, ДатаОкончания"));

    Возврат РезультатФункции;
КонецФункции

Функция ЗаписатьДанныеВБезопасноеХранилище(Знач СтруктураДанныхБезопасногоХраненияДанных, Знач Логин) Экспорт
    СтруктураДанныхБезопасногоХраненияДанных = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(СтруктураДанныхБезопасногоХраненияДанных);
    СтруктураДанныхБезопасногоХраненияДанных.Владелец = ПолучитьКлючВладельцаХранилищаДанных(
            СтруктураДанныхБезопасногоХраненияДанных.БазовыйВладелец, Логин);

    ДатаСоздания = СтруктураДанныхБезопасногоХраненияДанных.Данные.ДатаСоздания;
    ДатаСоздания = ?(ДатаСоздания = Неопределено, "", Формат(ДатаСоздания, "ДФ='yyyy.MM.dd hh:mm:ss'"));
    СтруктураДанныхБезопасногоХраненияДанных.Данные.ДатаСоздания = ДатаСоздания;

    ДатаОкончания = СтруктураДанныхБезопасногоХраненияДанных.Данные.ДатаОкончания;
    ДатаОкончания = ?(ДатаОкончания = Неопределено, "", Формат(ДатаОкончания, "ДФ='yyyy.MM.dd hh:mm:ss'"));
    СтруктураДанныхБезопасногоХраненияДанных.Данные.ДатаОкончания = ДатаОкончания;

    УстановитьПривилегированныйРежим(Истина);

    СтруктураДанныхБезопасногоХраненияДанных.Данные = ЗаписатьЗначениеJSON(СтруктураДанныхБезопасногоХраненияДанных.Данные);
    ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(
        СтруктураДанныхБезопасногоХраненияДанных.Владелец,
        СтруктураДанныхБезопасногоХраненияДанных.Данные,
        СтруктураДанныхБезопасногоХраненияДанных.КлючДанных);

    УстановитьПривилегированныйРежим(Ложь);

    Возврат Истина;
КонецФункции

// Возвращаемое значение:
//  - Структура
Функция ПрочитатьДанныеИзБезопасногоХранилища(Знач СтруктураДанныхБезопасногоХраненияДанных, Знач Логин) Экспорт
    РезультатФункции = Неопределено;

    СтруктураДанныхБезопасногоХраненияДанных = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(СтруктураДанныхБезопасногоХраненияДанных);
    СтруктураДанныхБезопасногоХраненияДанных.Владелец = ПолучитьКлючВладельцаХранилищаДанных(
            СтруктураДанныхБезопасногоХраненияДанных.БазовыйВладелец, Логин);

    УстановитьПривилегированныйРежим(Истина);

    РезультатФункции = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
            СтруктураДанныхБезопасногоХраненияДанных.Владелец,
            СтруктураДанныхБезопасногоХраненияДанных.КлючДанных, Истина);

    УстановитьПривилегированныйРежим(Ложь);

    Если РезультатФункции <> Неопределено Тогда
        РезультатФункции = ПрочитатьЗначениеJSON(РезультатФункции);

        ДатаСоздания = РезультатФункции.ДатаСоздания;
        ДатаСоздания = СтроковыеФункцииКлиентСервер.СтрокаВДату(ДатаСоздания, ЧастиДаты.ДатаВремя);
        РезультатФункции.ДатаСоздания = ДатаСоздания;

        ДатаОкончания = РезультатФункции.ДатаОкончания;
        ДатаОкончания = СтроковыеФункцииКлиентСервер.СтрокаВДату(ДатаОкончания, ЧастиДаты.ДатаВремя);
        РезультатФункции.ДатаОкончания = ДатаОкончания;
    КонецЕсли;

    Возврат РезультатФункции;
КонецФункции

Функция НовыйПользовательСчетмаш() Экспорт
    Возврат Новый Структура("Наименование, Логин");
КонецФункции

Функция ПолучитьДемоПользователяСчетмаш() Экспорт
    ПользовательСчетмаш = НовыйПользовательСчетмаш();
    ПользовательСчетмаш.Наименование = "Демо";
    ПользовательСчетмаш.Логин = "test_api";
    Возврат ПользовательСчетмаш;
КонецФункции

Функция ПолучитьОсновногоПользователяСчетмаш() Экспорт
    ПользовательСчетмаш = НовыйПользовательСчетмаш();
    ПользовательСчетмаш.Наименование = "Основной";
    ПользовательСчетмаш.Логин = "wkx_53";
    Возврат ПользовательСчетмаш;
КонецФункции

Функция ПолучитьПользователейСчетмаш() Экспорт
    РезультатФункции = Новый Соответствие;

    // Демо доступ
    ПользовательСчетмаш = ПолучитьДемоПользователяСчетмаш();
    РезультатФункции.Вставить(ПользовательСчетмаш.Наименование, ПользовательСчетмаш);

    // Основной доступ
    ПользовательСчетмаш = ПолучитьОсновногоПользователяСчетмаш();
    РезультатФункции.Вставить(ПользовательСчетмаш.Наименование, ПользовательСчетмаш);

    Возврат РезультатФункции;
КонецФункции

Функция ЭтоДемоЛогинСчетмаш(Знач Логин) Экспорт
    ДемоПользовательСчетмаш = ПолучитьДемоПользователяСчетмаш();
    Возврат Логин = ДемоПользовательСчетмаш.Логин;
КонецФункции

Функция ВремяЖизниТокенаСек() Экспорт
    Возврат 60 * 60 * 23;
КонецФункции

// Возвращаемое значение:
//  - Соответствие из КлючИЗначение
//      * Ключ - ПеречислениеСсылка.ПризнакиПредметаРасчета
//      * Значение - Число
Функция ПолучитьСоответствиеПризнаковПредметаРасчетаСчетмаш() Экспорт
    РезультатФункции = Новый Соответствие;

    РезультатФункции.Вставит(Перечисления.ПризнакиПредметаРасчета.Товар, 1);
    РезультатФункции.Вставит(Перечисления.ПризнакиПредметаРасчета.ПодакцизныйТовар, 2);
    РезультатФункции.Вставит(Перечисления.ПризнакиПредметаРасчета.Работа, 3);
    РезультатФункции.Вставит(Перечисления.ПризнакиПредметаРасчета.Услуга, 4);
    // ...
    РезультатФункции.Вставит(Перечисления.ПризнакиПредметаРасчета.Платеж, 10);

    Возврат РезультатФункции;
КонецФункции

// Возвращаемое значение:
//  - Соответствие из КлючИЗначение
//      * Ключ - ПеречислениеСсылка.ПризнакиПредметаРасчета
//      * Значение - Число
Функция ПолучитьСоответствиеПризнаковСпособаРасчетаСчетмаш() Экспорт
    РезультатФункции = Новый Соответствие;

    РезультатФункции.Вставит(Перечисления.ПризнакиСпособаРасчета.ПредоплатаПолная, 1);
    РезультатФункции.Вставит(Перечисления.ПризнакиСпособаРасчета.ПредоплатаЧастичная, 2);
    РезультатФункции.Вставит(Перечисления.ПризнакиСпособаРасчета.Аванс, 3);
    РезультатФункции.Вставит(Перечисления.ПризнакиСпособаРасчета.ПередачаСПолнойОплатой, 4);
    РезультатФункции.Вставит(Перечисления.ПризнакиСпособаРасчета.ПередачаСЧастичнойОплатой, 5);
    РезультатФункции.Вставит(Перечисления.ПризнакиСпособаРасчета.ПередачаБезОплаты, 6);
    РезультатФункции.Вставит(Перечисления.ПризнакиСпособаРасчета.ОплатаКредита, 7);

    Возврат РезультатФункции;
КонецФункции

// Параметры:
//  Значение - ПеречислениеСсылка.ПризнакиПредметаРасчета, Число
// Возвращаемое значение:
//  - ПеречислениеСсылка.ПризнакиПредметаРасчета, Число, Неопределено
Функция ПолучитьПризнакПредметаРасчетаСчетмашПоЗначению(Знач Значение) Экспорт
    РезультатФункции = Неопределено;

    СоответствиеПризнаковПредметаРасчета = ПолучитьСоответствиеПризнаковПредметаРасчетаСчетмаш();
    Если ТипЗнч(Значение) = Тип("ПеречислениеСсылка.ПризнакиПредметаРасчета") Тогда
        НайденноеЗначение = СоответствиеПризнаковПредметаРасчета.Получить(Значение);
        РезультатФункции = НайденноеЗначение;
    ИначеЕсли ТипЗнч(Значение) = Тип("Число") Тогда
        Для Каждого ЭлементКЗ Из СоответствиеПризнаковПредметаРасчета Цикл
            Если ЭлементКЗ.Значение = Значение Тогда
                РезультатФункции = ЭлементКЗ.Ключ;
                Прервать;
            КонецЕсли;
        КонецЦикла;
    КонецЕсли;

    Возврат РезультатФункции;
КонецФункции

// Параметры:
//  Значение - ПеречислениеСсылка.ПризнакиСпособаРасчета, Число
// Возвращаемое значение:
//  - ПеречислениеСсылка.ПризнакиСпособаРасчета, Число, Неопределено
Функция ПолучитьПризнакСпособаРасчетаСчетмашПоЗначению(Знач Значение) Экспорт
    РезультатФункции = Неопределено;

    СоответствиеПризнаковПредметаРасчета = ПолучитьСоответствиеПризнаковПредметаРасчетаСчетмаш();
    Если ТипЗнч(Значение) = Тип("ПеречислениеСсылка.ПризнакиСпособаРасчета") Тогда
        НайденноеЗначение = СоответствиеПризнаковПредметаРасчета.Получить(Значение);
        РезультатФункции = НайденноеЗначение;
    ИначеЕсли ТипЗнч(Значение) = Тип("Число") Тогда
        Для Каждого ЭлементКЗ Из СоответствиеПризнаковПредметаРасчета Цикл
            Если ЭлементКЗ.Значение = Значение Тогда
                РезультатФункции = ЭлементКЗ.Ключ;
                Прервать;
            КонецЕсли;
        КонецЦикла;
    КонецЕсли;

    Возврат РезультатФункции;
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс
// Гарант+ Килипенко 06.03.2025 [F00235776] Web-касса (счетмаш) --

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьКлючВладельцаХранилищаДанных(Знач Владелец, Знач Логин)
    Возврат СтрШаблон("%1_u%2", Владелец, Логин);
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
