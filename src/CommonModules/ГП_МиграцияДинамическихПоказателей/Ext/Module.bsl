// Гарант+ Килипенко 10.09.2024 [F00227690] Заполнение динамических показателей ++
#Область ПрограммныйИнтерфейс

// Параметры:
//  Период - Дата
// Возвращаемое значение:
//  - Структура
//      * Успех - Булево
//      * КоличествоЗаписанных - Число
//      * ТекстСообщения - Строка, Неопределено
Функция ЗаполнитьДанныеДинамическихПоказателей(Знач Период) Экспорт
    РезультатФункции = Новый Структура("Успех, КоличествоЗаписанных, ТекстСообщения", Истина, 0);

    ДанныеДляЗаполнения = ПолучитьДанныеЗаполненияДинамическихПоказателей(Период);

    НачатьТранзакцию();
    Попытка
        Для Каждого СтрокаПоказателей Из ДанныеДляЗаполнения Цикл
            НаборЗаписейДинамическихПоказателей = СоздатьНаборЗаписейДинамическихПоказателейЛС(
                    СтрокаПоказателей.Период, СтрокаПоказателей.ЛицевойСчет);

            НоваяЗаписьПоказателей = НаборЗаписейДинамическихПоказателей.Добавить();
            НоваяЗаписьПоказателей.Период = СтрокаПоказателей.Период;
            НоваяЗаписьПоказателей.ЛицевойСчет = СтрокаПоказателей.ЛицевойСчет;

            НоваяЗаписьПоказателей.ПроцентВодоотведенияХВС = СтрокаПоказателей.ПроцентВодоотведенияХВС;
            НоваяЗаписьПоказателей.ПроцентВодоотведенияГВС = СтрокаПоказателей.ПроцентВодоотведенияГВС;
            НоваяЗаписьПоказателей.ВидПотребления = СтрокаПоказателей.ВидПотребления;
            НоваяЗаписьПоказателей.Финансирование = СтрокаПоказателей.Финансирование;
            НоваяЗаписьПоказателей.Водозабор = СтрокаПоказателей.Водозабор;
            НоваяЗаписьПоказателей.Договородержатель = СтрокаПоказателей.Договородержатель;
            НоваяЗаписьПоказателей.Контролер = СтрокаПоказателей.Контролер;
            НоваяЗаписьПоказателей.Характеристика = СтрокаПоказателей.Характеристика;

            НаборЗаписейДинамическихПоказателей.Записать(Истина);
            РезультатФункции.КоличествоЗаписанных = РезультатФункции.КоличествоЗаписанных + 1;
        КонецЦикла;

        ЗафиксироватьТранзакцию(); // Записано успешно
    Исключение
        ОтменитьТранзакцию();

        ОбщаяЧастьСообщения = "Ошибка при записи данных в регистр Динамические показатели лицевых счетов.";
        СтруктураОшибки = ГП_МиграцияОбщегоНазначения.ЗаписатьОшибкуВЖурнал(
                ОбщаяЧастьСообщения, ИнформацияОбОшибке(), Ложь, Истина);

        РезультатФункции.Успех = Ложь;
        РезультатФункции.ТекстСообщения = СтрШаблон(
                "%1
                |Информация об ошибке: %2",
                ОбщаяЧастьСообщения,
                СтруктураОшибки.КраткоеПредставлениеОшибки);

        РезультатФункции.КоличествоЗаписанных = 0;
    КонецПопытки;

    Возврат РезультатФункции;
КонецФункции

// Выполняет обновление (корректировку) значений динамических показателей л/с (Контролер, Договородержатель) по данным БП 7.7
//  для указанного наименования Контролера. Обновление выполняется в одной транзакции
// Параметры:
//  Период - Дата
//  НаименованиеКонтролера - Строка
// Возвращаемое значение:
//  - Структура
//      * Успех - Булево
//      * ТекстСообщения - Строка, Неопределено
//      * КоличествоОбновленныхКонтролеров - Число
//      * КоличествоОбновленныхДоговородержателей - Число
Функция ОбновитьДинамическиеПоказателиКонтролеровДоговородержателей(Знач Период, Знач НаименованиеКонтролера) Экспорт
    РезультатФункции = Новый Структура(
            "Успех, КоличествоОбновленныхКонтролеров, КоличествоОбновленныхДоговородержателей, ТекстСообщения", Истина, 0, 0);

    // Получение данных для обновления
    ДанныеЗаполнения = ПолучитьДанныхОбновленияКонтролеровДоговородержателей(Период, НаименованиеКонтролера);

    НачатьТранзакцию();
    Попытка
        Для Каждого СтрокаКоллекции Из ДанныеЗаполнения Цикл
            НаборЗаписейДинамическихПоказателей = СоздатьНаборЗаписейДинамическихПоказателейЛС(
                    СтрокаКоллекции.Период, СтрокаКоллекции.ЛицевойСчет);
            НаборЗаписейДинамическихПоказателей.Прочитать();

            Если НаборЗаписейДинамическихПоказателей.Количество() = 0 Тогда
                НоваяЗапись = НаборЗаписейДинамическихПоказателей.Добавить();
                ЗаполнитьЗначенияСвойств(НоваяЗапись, СтрокаКоллекции);
            КонецЕсли;

            НужноВыполнитьЗапись = Ложь;
            Для Каждого СтрокаЗаписи Из НаборЗаписейДинамическихПоказателей Цикл
                // Обновление Контролера
                Если СтрокаЗаписи.Контролер <> СтрокаКоллекции.КонтролерЗамены Тогда
                    СтрокаЗаписи.Контролер = СтрокаКоллекции.КонтролерЗамены;

                    РезультатФункции.КоличествоОбновленныхКонтролеров = РезультатФункции.КоличествоОбновленныхКонтролеров + 1;
                    НужноВыполнитьЗапись = Истина;
                КонецЕсли;

                // Обновление Договородержателя
                Если СтрокаЗаписи.Договородержатель <> СтрокаКоллекции.ДоговородержательЗамены Тогда
                    СтрокаЗаписи.Договородержатель = СтрокаКоллекции.ДоговородержательЗамены;

                    РезультатФункции.КоличествоОбновленныхДоговородержателей = РезультатФункции.КоличествоОбновленныхДоговородержателей + 1;
                    НужноВыполнитьЗапись = Истина;
                КонецЕсли;
            КонецЦикла;

            Если НужноВыполнитьЗапись = Истина Тогда
                НаборЗаписейДинамическихПоказателей.Записать(Истина);
            КонецЕсли;
        КонецЦикла;

        ЗафиксироватьТранзакцию();
    Исключение
        ОтменитьТранзакцию();

        РезультатФункции.Успех = Ложь;
        РезультатФункции.КоличествоОбновленныхКонтролеров = 0;
        РезультатФункции.КоличествоОбновленныхДоговородержателей = 0;
        РезультатФункции.ТекстСообщения = СтрШаблон(
                "Ошибка обновления динамических показателей л/с Контролеров / Договородержателей.
                |Информация: %1", ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
    КонецПопытки;

    Возврат РезультатФункции;
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс
// Гарант+ Килипенко 10.09.2024 [F00227690] Заполнение динамических показателей --

// Гарант+ Килипенко 10.09.2024 [F00227690] Заполнение динамических показателей ++
#Область СлужебныеПроцедурыИФункции

// Параметры:
//  Период - Дата
//  ЛицевойСчет - СправочникСсылка.КВП_ЛицевыеСчета
// Возвращаемое значение:
//  - РегистрСведений.lc_ДинамическиеПоказателиЛицевыхСчетов.НаборЗаписей
Функция СоздатьНаборЗаписейДинамическихПоказателейЛС(Знач Период, Знач ЛицевойСчет)
    НаборЗаписей = РегистрыСведений.lc_ДинамическиеПоказателиЛицевыхСчетов.СоздатьНаборЗаписей();
    НаборЗаписей.Отбор.Период.Установить(Период);
    НаборЗаписей.Отбор.ЛицевойСчет.Установить(ЛицевойСчет);

    Возврат НаборЗаписей;
КонецФункции

// Параметры:
//  Период - Дата
// Возвращаемое значение:
//  - ТаблицаЗначений
//      * Период - Дата
//      * ПроцентВодоотведенияХВС - Число
//      * ПроцентВодоотведенияГВС - Число
//      * ЛицевойСчет - СправочникСсылка.ЛицевыеСчета
//      * ВидПотребления - СправочникСсылка.lc_ГруппыПотребителей
//      * Финансирование - СправочникСсылка.lc_СпособыФинансирования
//      * Водозабор - СправочникСсылка.lc_Водозаборы
//      * Договородержатель - СправочникСсылка.lc_Контролеры
//      * Контролер - СправочникСсылка.lc_Контролеры
//      * Характеристика - СправочникСсылка.lc_ХарактеристикиОбъекта
Функция ПолучитьДанныеЗаполненияДинамическихПоказателей(Знач Период)
    Запрос = Новый Запрос;
    Запрос.Текст = ПолучитьТекстЗапросаДанныхДинамическихПоказателей(Ложь);
    Запрос.УстановитьПараметр("ДатаПоказателей", Период);

    РезультатЗапроса = Запрос.Выполнить();
    РезультатФункции = РезультатЗапроса.Выгрузить();

    Возврат РезультатФункции;
КонецФункции

// Параметры:
//  ИсключитьОИ - Булево
// Возвращаемое значение:
//  - Строка
Функция ПолучитьТекстЗапросаДанныхДинамическихПоказателей(Знач ИсключитьОИ = Ложь)
    РезультатФункции =
        "ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ГП_ЗданияБП77.КонтрагентКод КАК КонтрагентКод,
        |	ГП_ЗданияБП77.ОбъектАбонентаКод КАК ОбъектАбонентаКод,
        |	ГП_ЗданияБП77.Здание КАК Здание,
        |	ГП_ЗданияБП77.ОбъектАбонентаНаименование КАК ОбъектАбонентаНаименование,
        |	ГП_ЗданияБП77.УлицаКод КАК УлицаКод,
        |	ГП_ЗданияБП77.УлицаНаименование КАК УлицаНаименование,
        |	ГП_ЗданияБП77.УлицаКодГорода КАК УлицаКодГорода,
        |	ГП_ЗданияБП77.Улица2 КАК Улица2,
        |	ГП_ЗданияБП77.Дом КАК Дом,
        |	ГП_ЗданияБП77.Корпус КАК Корпус,
        |	ГП_ЗданияБП77.Квартира КАК Квартира,
        |	ГП_ЗданияБП77.КодМКД КАК КодМКД,
        |	ГП_ЗданияБП77.Реквизиты КАК Реквизиты,
        |	ГП_ЗданияБП77.КоличествоПроживающих КАК КоличествоПроживающих,
        |	ГП_ЗданияБП77.ТолькоДляКанализации КАК ТолькоДляКанализации,
        |	ГП_ЗданияБП77.ПроцентХВ КАК ПроцентХВ,
        |	ГП_ЗданияБП77.ПроцентГВ КАК ПроцентГВ,
        |	ГП_ЗданияБП77.КонтролерКод КАК КонтролерКод,
        |	ГП_ЗданияБП77.КонтролерНаименование КАК КонтролерНаименование,
        |	ГП_ЗданияБП77.РасчетчикКод КАК РасчетчикКод,
        |	ГП_ЗданияБП77.РасчетчикНаименование КАК РасчетчикНаименование,
        |	ГП_ЗданияБП77.ВидПотребителяКод КАК ВидПотребителяКод,
        |	ГП_ЗданияБП77.ВидПотребителяНаименование КАК ВидПотребителяНаименование,
        |	ГП_ЗданияБП77.ЭтоНегативноеВоздействиеЦСВ КАК ЭтоНегативноеВоздействиеЦСВ,
        |	ГП_ЗданияБП77.ЭтоПлатаЗаХолодноеВодоснабжениеОИ КАК ЭтоПлатаЗаХолодноеВодоснабжениеОИ,
        |	ГП_ЗданияБП77.МетодРасчетаХВ КАК МетодРасчетаХВ,
        |	ГП_ЗданияБП77.МетодРасчетаГВ КАК МетодРасчетаГВ,
        |	ГП_ЗданияБП77.МетодРасчетаКан КАК МетодРасчетаКан,
        |	ГП_ЗданияБП77.НаПодогрев КАК НаПодогрев,
        |	ГП_ЗданияБП77.НеНачислять КАК НеНачислять,
        |	ГП_ЗданияБП77.ДоговорКод КАК ДоговорКод,
        |	ГП_ЗданияБП77.ДоговорНаименование КАК ДоговорНаименование,
        |	ГП_ЗданияБП77.ДоговорНомер КАК ДоговорНомер,
        |	ГП_ЗданияБП77.ВидДоговораКод КАК ВидДоговораКод,
        |	ГП_ЗданияБП77.ВидДоговораНаименование КАК ВидДоговораНаименование,
        |	ГП_ЗданияБП77.ВодозаборКод КАК ВодозаборКод,
        |	ГП_ЗданияБП77.ВодозаборНаименование КАК ВодозаборНаименование,
        |	ГП_ЗданияБП77.СпособФинансированияКод КАК СпособФинансированияКод,
        |	ГП_ЗданияБП77.СпособФинансированияНаименование КАК СпособФинансированияНаименование,
        |	ГП_ЗданияБП77.ХарактеристикаОбъектаКод КАК ХарактеристикаОбъектаКод,
        |	ГП_ЗданияБП77.ХарактеристикаОбъектаНаименование КАК ХарактеристикаОбъектаНаименование
        |ПОМЕСТИТЬ ВТ_ЗданияБП77
        |ИЗ
        |	РегистрСведений.ГП_ЗданияБП77 КАК ГП_ЗданияБП77
        |ГДЕ
        |   ИСТИНА
        |	И ГП_ЗданияБП77.ЭтоПлатаЗаХолодноеВодоснабжениеОИ = ЛОЖЬ
        |	И ГП_ЗданияБП77.ЭтоНегативноеВоздействиеЦСВ = ЛОЖЬ
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ГруппыПотребителей.Ссылка КАК Ссылка,
        |	ГруппыПотребителей.ВерсияДанных КАК ВерсияДанных,
        |	ГруппыПотребителей.ПометкаУдаления КАК ПометкаУдаления,
        |	ГруппыПотребителей.Родитель КАК Родитель,
        |	ГруппыПотребителей.ЭтоГруппа КАК ЭтоГруппа,
        |	ГруппыПотребителей.Код КАК Код,
        |	ГруппыПотребителей.Наименование КАК Наименование,
        |	ГруппыПотребителей.Шифр КАК Шифр,
        |	ГруппыПотребителей.ШифрСтрокой КАК ШифрСтрокой,
        |	ГруппыПотребителей.ВидТарифа КАК ВидТарифа,
        |	ГруппыПотребителей.СпособФинансирования КАК СпособФинансирования,
        |	ГруппыПотребителей.МетодПересчетаКоличестваЗаНеполныйМесяцМетодаРасч КАК МетодПересчетаКоличестваЗаНеполныйМесяцМетодаРасч,
        |	ГруппыПотребителей.Комментарий КАК Комментарий,
        |	ГруппыПотребителей.Предопределенный КАК Предопределенный,
        |	ГруппыПотребителей.ИмяПредопределенныхДанных КАК ИмяПредопределенныхДанных
        |ПОМЕСТИТЬ ВТ_ГруппыПотребителей
        |ИЗ
        |	Справочник.lc_ГруппыПотребителей КАК ГруппыПотребителей
        |ГДЕ
        |	ГруппыПотребителей.ПометкаУдаления = ЛОЖЬ
        |	И ГруппыПотребителей.ЭтоГруппа = ЛОЖЬ
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ТарифыОбъектовАбонентовБП77.КонтрагентКод КАК КонтрагентКод,
        |	ТарифыОбъектовАбонентовБП77.ОбъектАбонентаКод КАК ОбъектАбонентаКод,
        |	ТарифыОбъектовАбонентовБП77.ТарифКод КАК ТарифКод,
        |	ТарифыОбъектовАбонентовБП77.ТарифВода КАК ТарифВода,
        |	ТарифыОбъектовАбонентовБП77.ТарифКанализация КАК ТарифКанализация,
        |	ТарифыОбъектовАбонентовБП77.ТарифНаименование КАК ТарифНаименование,
        |	ТарифыОбъектовАбонентовБП77.КонтрагентНаименование КАК КонтрагентНаименование,
        |	ТарифыОбъектовАбонентовБП77.ОбъектАбонентаНаименование КАК ОбъектАбонентаНаименование
        |ПОМЕСТИТЬ ВТ_ТарифыОбъектовАбонентовБП77
        |ИЗ
        |	РегистрСведений.ГП_ТарифыОбъектовАбонентовБП77 КАК ТарифыОбъектовАбонентовБП77
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	Водозаборы.Ссылка КАК Ссылка,
        |	Водозаборы.Код КАК Код,
        |	Водозаборы.Наименование КАК Наименование,
        |	Водозаборы.Комментарий КАК Комментарий
        |ПОМЕСТИТЬ ВТ_Водозаборы
        |ИЗ
        |	Справочник.lc_Водозаборы КАК Водозаборы
        |ГДЕ
        |	Водозаборы.ПометкаУдаления = ЛОЖЬ
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	СпособыФинансирования.Ссылка КАК Ссылка,
        |	СпособыФинансирования.Код КАК Код,
        |	СпособыФинансирования.Наименование КАК Наименование
        |ПОМЕСТИТЬ ВТ_СпособыФинансирования
        |ИЗ
        |	Справочник.lc_СпособыФинансирования КАК СпособыФинансирования
        |ГДЕ
        |	СпособыФинансирования.ПометкаУдаления = ЛОЖЬ
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ХарактеристикиОбъекта.Ссылка КАК Ссылка,
        |	ХарактеристикиОбъекта.Код КАК Код,
        |	ХарактеристикиОбъекта.Наименование КАК Наименование
        |ПОМЕСТИТЬ ВТ_ХарактеристикиОбъекта
        |ИЗ
        |	Справочник.lc_ХарактеристикиОбъекта КАК ХарактеристикиОбъекта
        |ГДЕ
        |	ХарактеристикиОбъекта.ПометкаУдаления = ЛОЖЬ
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	lc_Контролеры.Ссылка КАК Ссылка,
        |	lc_Контролеры.Код КАК Код,
        |	lc_Контролеры.Наименование КАК Наименование,
        |	lc_Контролеры.ФизическоеЛицо КАК ФизическоеЛицо
        |ПОМЕСТИТЬ ВТ_Контролеры
        |ИЗ
        |	Справочник.lc_Контролеры КАК lc_Контролеры
        |ГДЕ
        |	lc_Контролеры.ПометкаУдаления = ЛОЖЬ
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	&ДатаПоказателей КАК Период,
        |	ВТ_ЗданияБП77.КонтрагентКод КАК КонтрагентКод,
        |	ВТ_ЗданияБП77.ОбъектАбонентаКод КАК ОбъектАбонентаКод,
        |	ВТ_ЗданияБП77.ПроцентХВ КАК ПроцентВодоотведенияХВС,
        |	ВТ_ЗданияБП77.ПроцентГВ КАК ПроцентВодоотведенияГВС,
        |	ВТ_ЗданияБП77.КонтролерКод КАК КонтролерКод,
        |	ВТ_ЗданияБП77.КонтролерНаименование КАК КонтролерНаименование,
        |	ВТ_ЗданияБП77.РасчетчикКод КАК РасчетчикКод,
        |	ВТ_ЗданияБП77.РасчетчикНаименование КАК РасчетчикНаименование,
        |	ВТ_ЗданияБП77.ВидПотребителяКод КАК ВидПотребителяКод,
        |	ВТ_ЗданияБП77.ВидПотребителяНаименование КАК ВидПотребителяНаименование,
        |	ВТ_ЗданияБП77.ВодозаборКод КАК ВодозаборКод,
        |	ВТ_ЗданияБП77.ВодозаборНаименование КАК ВодозаборНаименование,
        |	КВП_ЛицевыеСчета.Ссылка КАК ЛицевойСчет,
        |	ЕСТЬNULL(ВТ_ГруппыПотребителей.Ссылка, ЗНАЧЕНИЕ(Справочник.lc_ГруппыПотребителей.ПустаяСсылка)) КАК ВидПотребления,
        |	ЕСТЬNULL(ВТ_СпособыФинансирования.Ссылка, ЗНАЧЕНИЕ(Справочник.lc_СпособыФинансирования.ПустаяСсылка)) КАК Финансирование,
        |	ЕСТЬNULL(ВТ_Водозаборы.Ссылка, ЗНАЧЕНИЕ(Справочник.lc_Водозаборы.ПустаяСсылка)) КАК Водозабор,
        |	ЕСТЬNULL(ВТ_КонтролерыДоговородержатель.Ссылка, ЗНАЧЕНИЕ(Справочник.lc_Контролеры.ПустаяСсылка)) КАК Договородержатель,
        |	ЕСТЬNULL(ВТ_Контролеры.Ссылка, ЗНАЧЕНИЕ(Справочник.lc_Контролеры.ПустаяСсылка)) КАК Контролер,
        |	ЕСТЬNULL(ВТ_ХарактеристикиОбъекта.Ссылка, ЗНАЧЕНИЕ(Справочник.lc_ХарактеристикиОбъекта.ПустаяСсылка)) КАК Характеристика
        |ИЗ
        |	ВТ_ЗданияБП77 КАК ВТ_ЗданияБП77
        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГП_КонтрагентыБП77 КАК ГП_КонтрагентыБП77
        |		ПО ВТ_ЗданияБП77.КонтрагентКод = ГП_КонтрагентыБП77.Код
        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КВП_ЛицевыеСчета КАК КВП_ЛицевыеСчета
        |		ПО (КВП_ЛицевыеСчета.Идентификатор = ВТ_ЗданияБП77.ОбъектАбонентаКод + ""_"" + ВТ_ЗданияБП77.КонтрагентКод)
        |			И (КВП_ЛицевыеСчета.ЭтоГруппа = ЛОЖЬ)
        |			И (КВП_ЛицевыеСчета.ПометкаУдаления = ЛОЖЬ)
        |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ГруппыПотребителей КАК ВТ_ГруппыПотребителей
        |		ПО (ВТ_ЗданияБП77.ВидПотребителяКод = СТРОКА(ВТ_ГруппыПотребителей.Шифр))
        |			И ВТ_ЗданияБП77.ВидПотребителяНаименование = ВТ_ГруппыПотребителей.Наименование
        |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Водозаборы КАК ВТ_Водозаборы
        |		ПО ВТ_ЗданияБП77.ВодозаборНаименование = ВТ_Водозаборы.Наименование
        |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Контролеры КАК ВТ_КонтролерыДоговородержатель
        |		ПО ВТ_ЗданияБП77.РасчетчикНаименование = ВТ_КонтролерыДоговородержатель.Наименование
        |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Контролеры КАК ВТ_Контролеры
        |		ПО ВТ_ЗданияБП77.КонтролерНаименование = ВТ_Контролеры.Наименование
        |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СпособыФинансирования КАК ВТ_СпособыФинансирования
        |		ПО ВТ_ЗданияБП77.СпособФинансированияНаименование = ВТ_СпособыФинансирования.Наименование
        |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ХарактеристикиОбъекта КАК ВТ_ХарактеристикиОбъекта
        |		ПО ВТ_ЗданияБП77.ХарактеристикаОбъектаНаименование = ВТ_ХарактеристикиОбъекта.Наименование
        |";

    Если ИсключитьОИ = Ложь Тогда
        РезультатФункции = СтрЗаменить(РезультатФункции, "И ГП_ЗданияБП77.ЭтоПлатаЗаХолодноеВодоснабжениеОИ = ЛОЖЬ", "");
    КонецЕсли;

    Возврат РезультатФункции;
КонецФункции

// Возвращает таблицу данных для обновления динамических показателей: Контролер, Договородержатель по данным БП 7.7
// Параметры:
//  Период - Дата
//  НаименованиеКонтролера - Строка
// Возвращаемое значение:
//  - ТаблицаЗначений
//      * Период - Дата
//      * ЛицевойСчет - СправочникСсылка.КВП_ЛицевыеСчета
//      * ИдентификаторЛС - Строка
//      * ПроцентВодоотведенияХВС - Число
//      * ПроцентВодоотведенияГВС - Число
//      * ВидПотребления - СправочникСсылка.lc_ГруппыПотребителей
//      * Финансирование - СправочникСсылка.lc_СпособыФинансирования
//      * Водозабор - СправочникСсылка.lc_Водозаборы
//      * Характеристика - СправочникСсылка.lc_ХарактеристикиОбъекта
//      * КонтролерИсходный - СправочникСсылка.lc_Контролеры
//      * КонтролерЗамены - СправочникСсылка.lc_Контролеры
//      * ДоговородержательИсходный - СправочникСсылка.lc_Контролеры
//      * ДоговородержательЗамены - СправочникСсылка.lc_Контролеры
Функция ПолучитьДанныхОбновленияКонтролеровДоговородержателей(Знач Период, НаименованиеКонтролера)
    Запрос = Новый Запрос;
    Запрос.Текст =
        "ВЫБРАТЬ
        |	МАКСИМУМ(Контролеры.Ссылка) КАК Контролер
        |ПОМЕСТИТЬ ВТ_Контролеры
        |ИЗ
        |	Справочник.lc_Контролеры КАК Контролеры
        |ГДЕ
        |	Контролеры.Наименование = &НаименованиеКонтролера
        |	И Контролеры.ПометкаУдаления = ЛОЖЬ
        |
        |СГРУППИРОВАТЬ ПО
        |	Контролеры.Наименование
        |;
        |
        |ВЫБРАТЬ
        |	ВТ_Контролеры.Контролер КАК Контролер,
        |	ВТ_Контролеры.Контролер.Наименование КАК Наименование
        |ИЗ
        |	ВТ_Контролеры КАК ВТ_Контролеры
        |";

    Запрос.УстановитьПараметр("НаименованиеКонтролера", НаименованиеКонтролера);
    РезультатЗапроса = Запрос.Выполнить();
    ТаблицаКонтролеров = РезультатЗапроса.Выгрузить();

    Запрос = Новый Запрос;
    Запрос.Текст = ПолучитьТекстЗапросаДанныхОбновленияКонтролеровДоговородержателей();
    Запрос.УстановитьПараметр("Период", Период);
    Запрос.УстановитьПараметр("ТаблицаКонтролеров", ТаблицаКонтролеров);

    РезультатЗапроса = Запрос.Выполнить();
    РезультатФункции = РезультатЗапроса.Выгрузить();
    Возврат РезультатФункции;
КонецФункции

Функция ПолучитьТекстЗапросаДанныхОбновленияКонтролеровДоговородержателей()
    ТекстЗапроса =
        "ВЫБРАТЬ
        |	ТаблицаКонтролеров.Контролер КАК Контролер,
        |	ТаблицаКонтролеров.Наименование КАК Наименование
        |ПОМЕСТИТЬ ВТ_ТаблицаКонтролеров
        |ИЗ
        |	&ТаблицаКонтролеров КАК ТаблицаКонтролеров
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	ЗданияБП77.КонтрагентКод КАК КонтрагентКод,
        |	ЗданияБП77.ОбъектАбонентаКод КАК ОбъектАбонентаКод,
        |	ЗданияБП77.КонтролерНаименование КАК КонтролерНаименование,
        |	ЗданияБП77.ОбъектАбонентаКод + ""_"" + ЗданияБП77.КонтрагентКод КАК ИдентификаторЛС
        |ПОМЕСТИТЬ ВТ_КонтролерыБП77
        |ИЗ
        |	РегистрСведений.ГП_ЗданияБП77 КАК ЗданияБП77
        |ГДЕ
        |	ЗданияБП77.КонтролерНаименование В
        |			(ВЫБРАТЬ
        |				Т.Наименование
        |			ИЗ
        |				ВТ_ТаблицаКонтролеров КАК Т)
        |	И ЗданияБП77.КонтрагентКод <> """"
        |	И ЗданияБП77.ОбъектАбонентаКод <> """"
        |	И ЗданияБП77.ЭтоНегативноеВоздействиеЦСВ = ЛОЖЬ
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	ЗданияБП77.КонтрагентКод КАК КонтрагентКод,
        |	ЗданияБП77.ОбъектАбонентаКод КАК ОбъектАбонентаКод,
        |	ЗданияБП77.РасчетчикНаименование КАК ДоговородержательНаименование,
        |	ЗданияБП77.ОбъектАбонентаКод + ""_"" + ЗданияБП77.КонтрагентКод КАК ИдентификаторЛС
        |ПОМЕСТИТЬ ВТ_ДоговородержателиБП77
        |ИЗ
        |	РегистрСведений.ГП_ЗданияБП77 КАК ЗданияБП77
        |ГДЕ
        |	ЗданияБП77.РасчетчикНаименование В
        |			(ВЫБРАТЬ
        |				Т.Наименование
        |			ИЗ
        |				ВТ_ТаблицаКонтролеров КАК Т)
        |	И ЗданияБП77.КонтрагентКод <> """"
        |	И ЗданияБП77.ОбъектАбонентаКод <> """"
        |	И ЗданияБП77.ЭтоНегативноеВоздействиеЦСВ = ЛОЖЬ
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	ЕСТЬNULL(ВТ_КонтролерыБП77.ИдентификаторЛС, ВТ_ДоговородержателиБП77.ИдентификаторЛС) КАК ИдентификаторЛС,
        |	ЕСТЬNULL(ВТ_КонтролерыБП77.КонтролерНаименование, ВТ_ДоговородержателиБП77.ДоговородержательНаименование) КАК КонтролерДоговородержатель,
        |	ЕСТЬNULL(ВТ_КонтролерыБП77.КонтролерНаименование, """") КАК КонтролерНаименование,
        |	ЕСТЬNULL(ВТ_ДоговородержателиБП77.ДоговородержательНаименование, """") КАК ДоговородержательНаименование
        |ПОМЕСТИТЬ ВТ_КонтролерыИДоговородержателиБП77
        |ИЗ
        |	ВТ_КонтролерыБП77 КАК ВТ_КонтролерыБП77
        |	ПОЛНОЕ СОЕДИНЕНИЕ ВТ_ДоговородержателиБП77 КАК ВТ_ДоговородержателиБП77
        |	ПО ВТ_КонтролерыБП77.ИдентификаторЛС = ВТ_ДоговородержателиБП77.ИдентификаторЛС
        |	И ВТ_КонтролерыБП77.ИдентификаторЛС <> """"
        |	И ВТ_ДоговородержателиБП77.ИдентификаторЛС <> """"
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	ЛицевыеСчета.Ссылка КАК ДицевойСчет,
        |	ЛицевыеСчета.Идентификатор КАК Идентификатор
        |ПОМЕСТИТЬ ВТ_ЛицевыеСчета
        |ИЗ
        |	Справочник.КВП_ЛицевыеСчета КАК ЛицевыеСчета
        |ГДЕ
        |	ЛицевыеСчета.ПометкаУдаления = ЛОЖЬ
        |	И ЛицевыеСчета.ЭтоГруппа = ЛОЖЬ
        |	И ЛицевыеСчета.Идентификатор В
        |			(ВЫБРАТЬ РАЗЛИЧНЫЕ
        |				Т.ИдентификаторЛС
        |			ИЗ
        |				ВТ_КонтролерыИДоговородержателиБП77 КАК Т)
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	ДинамическиеПоказателиЛицевыхСчетов.Период КАК Период,
        |	ДинамическиеПоказателиЛицевыхСчетов.ЛицевойСчет КАК ЛицевойСчет,
        |	ДинамическиеПоказателиЛицевыхСчетов.ЛицевойСчет.Идентификатор КАК ИдентификаторЛС,
        |	ДинамическиеПоказателиЛицевыхСчетов.ПроцентВодоотведенияХВС КАК ПроцентВодоотведенияХВС,
        |	ДинамическиеПоказателиЛицевыхСчетов.ПроцентВодоотведенияГВС КАК ПроцентВодоотведенияГВС,
        |	ДинамическиеПоказателиЛицевыхСчетов.ВидПотребления КАК ВидПотребления,
        |	ДинамическиеПоказателиЛицевыхСчетов.Финансирование КАК Финансирование,
        |	ДинамическиеПоказателиЛицевыхСчетов.Водозабор КАК Водозабор,
        |	ДинамическиеПоказателиЛицевыхСчетов.Договородержатель КАК Договородержатель,
        |	ДинамическиеПоказателиЛицевыхСчетов.Контролер КАК Контролер,
        |	ДинамическиеПоказателиЛицевыхСчетов.Характеристика КАК Характеристика
        |ПОМЕСТИТЬ ВТ_ДинамическиеПоказателиЛицевыхСчетов
        |ИЗ
        |	РегистрСведений.lc_ДинамическиеПоказателиЛицевыхСчетов.СрезПоследних(
        |			&Период,
        |			ЛицевойСчет В
        |				(ВЫБРАТЬ РАЗЛИЧНЫЕ
        |					Т.ДицевойСчет
        |				ИЗ
        |					ВТ_ЛицевыеСчета КАК Т)) КАК ДинамическиеПоказателиЛицевыхСчетов
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	ВТ_ДинамическиеПоказателиЛицевыхСчетов.Период КАК Период,
        |	ВТ_ДинамическиеПоказателиЛицевыхСчетов.ЛицевойСчет КАК ЛицевойСчет,
        |	ВТ_ДинамическиеПоказателиЛицевыхСчетов.ИдентификаторЛС КАК ИдентификаторЛС,
        |	ВТ_ДинамическиеПоказателиЛицевыхСчетов.ПроцентВодоотведенияХВС КАК ПроцентВодоотведенияХВС,
        |	ВТ_ДинамическиеПоказателиЛицевыхСчетов.ПроцентВодоотведенияГВС КАК ПроцентВодоотведенияГВС,
        |	ВТ_ДинамическиеПоказателиЛицевыхСчетов.ВидПотребления КАК ВидПотребления,
        |	ВТ_ДинамическиеПоказателиЛицевыхСчетов.Финансирование КАК Финансирование,
        |	ВТ_ДинамическиеПоказателиЛицевыхСчетов.Водозабор КАК Водозабор,
        |	ВТ_ДинамическиеПоказателиЛицевыхСчетов.Характеристика КАК Характеристика,
        |	ВТ_ДинамическиеПоказателиЛицевыхСчетов.Контролер КАК КонтролерИсходный,
        |	ВЫБОР
        |		КОГДА ВТ_КонтролерыИДоговородержателиБП77.КонтролерНаименование = ВТ_ТаблицаКонтролеров.Наименование
        |			ТОГДА ВТ_ТаблицаКонтролеров.Контролер
        |		ИНАЧЕ ВТ_ДинамическиеПоказателиЛицевыхСчетов.Контролер
        |	КОНЕЦ КАК КонтролерЗамены,
        |	ВТ_ДинамическиеПоказателиЛицевыхСчетов.Договородержатель КАК ДоговородержательИсходный,
        |	ВЫБОР
        |		КОГДА ВТ_КонтролерыИДоговородержателиБП77.ДоговородержательНаименование = ВТ_ТаблицаКонтролеров.Наименование
        |			ТОГДА ВТ_ТаблицаКонтролеров.Контролер
        |		ИНАЧЕ ВТ_ДинамическиеПоказателиЛицевыхСчетов.Договородержатель
        |	КОНЕЦ КАК ДоговородержательЗамены
        |ПОМЕСТИТЬ ВТ_Результат_Подготовка
        |ИЗ
        |	ВТ_ДинамическиеПоказателиЛицевыхСчетов КАК ВТ_ДинамическиеПоказателиЛицевыхСчетов
        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_КонтролерыИДоговородержателиБП77 КАК ВТ_КонтролерыИДоговородержателиБП77
        |		ПО ВТ_ДинамическиеПоказателиЛицевыхСчетов.ИдентификаторЛС = ВТ_КонтролерыИДоговородержателиБП77.ИдентификаторЛС
        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ТаблицаКонтролеров КАК ВТ_ТаблицаКонтролеров
        |		ПО (ВТ_КонтролерыИДоговородержателиБП77.КонтролерДоговородержатель = ВТ_ТаблицаКонтролеров.Наименование)
        |ГДЕ
        |	ВТ_ТаблицаКонтролеров.Контролер <> ЗНАЧЕНИЕ(Справочник.lc_Контролеры.ПустаяСсылка)
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	ВТ_Результат_Подготовка.Период КАК Период,
        |	ВТ_Результат_Подготовка.ЛицевойСчет КАК ЛицевойСчет,
        |	ВТ_Результат_Подготовка.ИдентификаторЛС КАК ИдентификаторЛС,
        |	ВТ_Результат_Подготовка.ПроцентВодоотведенияХВС КАК ПроцентВодоотведенияХВС,
        |	ВТ_Результат_Подготовка.ПроцентВодоотведенияГВС КАК ПроцентВодоотведенияГВС,
        |	ВТ_Результат_Подготовка.ВидПотребления КАК ВидПотребления,
        |	ВТ_Результат_Подготовка.Финансирование КАК Финансирование,
        |	ВТ_Результат_Подготовка.Водозабор КАК Водозабор,
        |	ВТ_Результат_Подготовка.Характеристика КАК Характеристика,
        |	ВТ_Результат_Подготовка.КонтролерИсходный КАК КонтролерИсходный,
        |	ВТ_Результат_Подготовка.КонтролерЗамены КАК КонтролерЗамены,
        |	ВТ_Результат_Подготовка.ДоговородержательИсходный КАК ДоговородержательИсходный,
        |	ВТ_Результат_Подготовка.ДоговородержательЗамены КАК ДоговородержательЗамены
        |ИЗ
        |	ВТ_Результат_Подготовка КАК ВТ_Результат_Подготовка
        |ГДЕ
        |	ИСТИНА
        |	И (ВТ_Результат_Подготовка.КонтролерИсходный <> ВТ_Результат_Подготовка.КонтролерЗамены
        |		ИЛИ ВТ_Результат_Подготовка.ДоговородержательИсходный <> ВТ_Результат_Подготовка.ДоговородержательЗамены)
        |";

    Возврат ТекстЗапроса;
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
// Гарант+ Килипенко 10.09.2024 [F00227690] Заполнение динамических показателей --
