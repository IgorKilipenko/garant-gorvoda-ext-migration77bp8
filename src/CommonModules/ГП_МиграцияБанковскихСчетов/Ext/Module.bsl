#Область ПрограммныйИнтерфейс

Функция ЗаполнитьРегистрСчетаДляОплат(Знач Период, Знач ДанныеЗаполнения = Неопределено) Экспорт
    РезультатФункции = Новый Структура("Успех, КоличествоЗаписей, ТекстСообщения", Истина, 0);

    // Формирование данных для записи
    ДанныеЗаполнения = ?(ДанныеЗаполнения = Неопределено,
            ПолучитьДанныеЗаполненияСчетовДляОплат(), ДанныеЗаполнения);

    НачатьТранзакцию();
    Попытка
        Для Каждого СтрокаДанных Из ДанныеЗаполнения Цикл
            НовыйНаборЗаписей = РегистрыСведений.ГП_СчетаДляОплат.СоздатьНаборЗаписей();
            НовыйНаборЗаписей.Отбор.Период.Установить(Период);
            НовыйНаборЗаписей.Отбор.Контрагент.Установить(СтрокаДанных.Контрагент);

            НоваяЗапись = НовыйНаборЗаписей.Добавить();
            НоваяЗапись.Период = Период;
            НоваяЗапись.Контрагент = СтрокаДанных.Контрагент;
            НоваяЗапись.БанковскийСчет = СтрокаДанных.БанковскийСчет;

            НовыйНаборЗаписей.Записать(Истина);
            РезультатФункции.КоличествоЗаписей = РезультатФункции.КоличествоЗаписей + 1;
        КонецЦикла;

        ЗафиксироватьТранзакцию();

    Исключение
        ОтменитьТранзакцию();

        РезультатФункции.Успех = Ложь;
        РезультатФункции.КоличествоЗаписей = 0;
        РезультатФункции.ТекстСообщения = СтрШаблон(
                "Ошибка при записи в регистр ""ГП_СчетаДляОплат"".
                |Информация: %1", ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
    КонецПопытки;

    Возврат РезультатФункции;
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныйПрограммныйИнтерфейс

// Возвращаемое значение:
//  - Соответствие
Функция ПолучитьСоответствиеНаименованийБанковскихСчетовПоКодамБП77() Экспорт
    РезультатФункции = Новый Соответствие;

    РезультатФункции.Вставить("00024", "Основной 108");
    РезультатФункции.Вставить("00002", "Основной 107");
    РезультатФункции.Вставить("00019", "Основной 107");
    РезультатФункции.Вставить("00013", "Основной 211");
    РезультатФункции.Вставить("00020", "Основной 004");
    РезультатФункции.Вставить("00014", "Основной 004");
    РезультатФункции.Вставить("00022", "Основной 2305");
    РезультатФункции.Вставить("00016", "Основной517");
    РезультатФункции.Вставить("00012", "Основной752");
    РезультатФункции.Вставить("00018", "Основной1361");
    РезультатФункции.Вставить("00021", "Основной 90658");
    РезультатФункции.Вставить("00017", "Основной1164");
    РезультатФункции.Вставить("00027", "Основной 850");
    РезультатФункции.Вставить("00026", "Основной 071");
    РезультатФункции.Вставить("00029", "Основной 030");
    РезультатФункции.Вставить("00025", "Основной 005");

    Возврат РезультатФункции;
КонецФункции

// Параметры:
//  Наименование - Строка
// Возвращаемое значение:
//  - СправочникСсылка.БанковскиеСчета, Неопределено
Функция ПолучитьБанковскийСчетПоНаименованию(Знач Наименование) Экспорт
    Запрос = Новый Запрос;
    Запрос.Текст =
        "ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
        |   *
        |ИЗ
        |   Справочник.БанковскиеСчета КАК БанковскиеСчета
        |ГДЕ
        |   ИСТИНА
        |   БанковскиеСчета.Наименование = &Наименование
        |   И БанковскиеСчета.ПометкаУдаления = ЛОЖЬ
        |";

    Запрос.УстановитьПараметр("Наименование", Наименование);
    РезультатЗапроса = Запрос.Выполнить();
    НайденныеБанковскиеСчета = РезультатЗапроса.Выгрузить();

    Если НайденныеБанковскиеСчета.Количество() = 0 Тогда
        Возврат Неопределено;
    КонецЕсли;

    Возврат НайденныеБанковскиеСчета[0];
КонецФункции

Функция ПолучитьДанныеЗаполненияСчетовДляОплат() Экспорт
    СоответствияНомеровСуществующихДоговоров = ГП_РаботаСДоговорами.ПолучитьТаблицуСоответствийНомеровНаименованийДоговоров();

    СоответствияКодовБанковскихСчетов = ГП_МиграцияБанковскихСчетов.ПолучитьСоответствиеНаименованийБанковскихСчетовПоКодамБП77();
    СоответствияБанковскихСчетов = Новый ТаблицаЗначений;
    СоответствияБанковскихСчетов.Колонки.Добавить("КодБП77", ОбщегоНазначения.ОписаниеТипаСтрока(15));
    СоответствияБанковскихСчетов.Колонки.Добавить("Наименование", ОбщегоНазначения.ОписаниеТипаСтрока(100));
    Для Каждого ЭлементКЗ Из СоответствияКодовБанковскихСчетов Цикл
        НоваяСтрока = СоответствияБанковскихСчетов.Добавить();
        НоваяСтрока.КодБП77 = ЭлементКЗ.Ключ;
        НоваяСтрока.Наименование = ЭлементКЗ.Значение;
    КонецЦикла;

    Запрос = Новый Запрос;
    Запрос.Текст =
        "ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВЫРАЗИТЬ(СоответствияНомеровСуществующихДоговоров.Ссылка КАК Справочник.ДоговорыКонтрагентов) КАК Ссылка,
        |	СоответствияНомеровСуществующихДоговоров.Наименование КАК Наименование,
        |	СоответствияНомеровСуществующихДоговоров.НомерИзНаименования КАК НомерИзНаименования
        |ПОМЕСТИТЬ ВТ_СоответствияНомеровСуществующихДоговоров
        |ИЗ
        |	&СоответствияНомеровСуществующихДоговоров КАК СоответствияНомеровСуществующихДоговоров
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	СоответствияБанковскихСчетов.КодБП77 КАК КодБП77,
        |	СоответствияБанковскихСчетов.Наименование КАК Наименование
        |ПОМЕСТИТЬ ВТ_СоответствияБанковскихСчетовПодготовка
        |ИЗ
        |	&СоответствияБанковскихСчетов КАК СоответствияБанковскихСчетов
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	БанковскиеСчета.Ссылка КАК Ссылка,
        |	ВТ_СоответствияБанковскихСчетовПодготовка.КодБП77 КАК КодБП77
        |ПОМЕСТИТЬ ВТ_СоответствияБанковскихСчетов
        |ИЗ
        |	Справочник.БанковскиеСчета КАК БанковскиеСчета
        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СоответствияБанковскихСчетовПодготовка КАК ВТ_СоответствияБанковскихСчетовПодготовка
        |		ПО БанковскиеСчета.Наименование = ВТ_СоответствияБанковскихСчетовПодготовка.Наименование
        |			И (БанковскиеСчета.ПометкаУдаления = ЛОЖЬ)
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	КонтрагентыБП77.Код КАК Код,
        |	КонтрагентыБП77.Наименование КАК Наименование,
        |	КонтрагентыБП77.ВидКонтрагента КАК ВидКонтрагента,
        |	КонтрагентыБП77.ИНН КАК ИНН,
        |	КонтрагентыБП77.ОКПО КАК ОКПО,
        |	КонтрагентыБП77.КПП КАК КПП,
        |	КонтрагентыБП77.ДокументСерия КАК ДокументСерия,
        |	КонтрагентыБП77.ДокументНомер КАК ДокументНомер,
        |	КонтрагентыБП77.ДокументКемВыдан КАК ДокументКемВыдан,
        |	КонтрагентыБП77.ДокументДатаВыдачи КАК ДокументДатаВыдачи,
        |	КонтрагентыБП77.ОсновнойДоговорКод КАК ОсновнойДоговорКод,
        |	КонтрагентыБП77.ОсновнойДоговорНаименование КАК ОсновнойДоговорНаименование,
        |	КонтрагентыБП77.БанковскийСчетКод КАК БанковскийСчетКод,
        |	КонтрагентыБП77.Группа КАК Группа,
        |	КонтрагентыБП77.ПолнНаименование КАК ПолнНаименование
        |ПОМЕСТИТЬ ВТ_АбонентыБП77
        |ИЗ
        |	РегистрСведений.ГП_КонтрагентыБП77 КАК КонтрагентыБП77
        |ГДЕ
        |	КонтрагентыБП77.Группа = ""Абоненты""
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
        |	ВТ_АбонентыБП77.БанковскийСчетКод КАК БанковскийСчетКод,
        |	ВТ_СоответствияНомеровСуществующихДоговоров.Ссылка КАК Договор,
        |	ВТ_СоответствияНомеровСуществующихДоговоров.Ссылка.Владелец КАК Контрагент
        |ПОМЕСТИТЬ ВТ_РезультатПодготовка
        |ИЗ
        |	РегистрСведений.ГП_ЗданияБП77 КАК ЗданияБП77
        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АбонентыБП77 КАК ВТ_АбонентыБП77
        |		ПО ЗданияБП77.КонтрагентКод = ВТ_АбонентыБП77.Код
        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СоответствияНомеровСуществующихДоговоров КАК ВТ_СоответствияНомеровСуществующихДоговоров
        |		ПО ЗданияБП77.ДоговорНомер = ВТ_СоответствияНомеровСуществующихДоговоров.НомерИзНаименования
        |			И (НЕ ВТ_СоответствияНомеровСуществующихДоговоров.Ссылка.Владелец ЕСТЬ NULL)
        |			И (ВЫБОР
        |				КОГДА ВТ_АбонентыБП77.ИНН <> """"
        |					ТОГДА ВТ_СоответствияНомеровСуществующихДоговоров.Ссылка.Владелец.ИНН = ВТ_АбонентыБП77.ИНН
        |				ИНАЧЕ ВТ_СоответствияНомеровСуществующихДоговоров.Ссылка.Владелец.Наименование = ВТ_АбонентыБП77.Наименование
        |			КОНЕЦ)
        |ГДЕ
        |	ВТ_АбонентыБП77.БанковскийСчетКод <> """"
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВТ_РезультатПодготовка.БанковскийСчетКод КАК БанковскийСчетКод,
        |	ВТ_РезультатПодготовка.Договор КАК Договор,
        |	ВТ_РезультатПодготовка.Контрагент КАК Контрагент,
        |	ВТ_СоответствияБанковскихСчетов.Ссылка КАК БанковскийСчет
        |ИЗ
        |	ВТ_РезультатПодготовка КАК ВТ_РезультатПодготовка
        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СоответствияБанковскихСчетов КАК ВТ_СоответствияБанковскихСчетов
        |		ПО ВТ_РезультатПодготовка.БанковскийСчетКод = ВТ_СоответствияБанковскихСчетов.КодБП77
        |";

    Запрос.УстановитьПараметр("СоответствияНомеровСуществующихДоговоров", СоответствияНомеровСуществующихДоговоров);
    Запрос.УстановитьПараметр("СоответствияБанковскихСчетов", СоответствияБанковскихСчетов);

    РезультатЗапроса = Запрос.Выполнить();
    РезультатФункции = РезультатЗапроса.Выгрузить();
    Возврат РезультатФункции;
КонецФункции

#КонецОбласти // СлужебныйПрограммныйИнтерфейс
