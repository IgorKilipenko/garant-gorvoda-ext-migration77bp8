#Область ПрограммныйИнтерфейс

// Возвращаемое значение:
//  - СправочникСсылка.БанковскиеСчета, Неопределено
Функция ПолучитьБанковскийСчетКонтрагентаДляРасчета(Знач Контрагент, Знач ДатаАктуальности = Неопределено) Экспорт
    Запрос = Новый Запрос;
    Запрос.Текст =
        "ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
        |   СчетаДляОплат.БанковскийСчет КАК БанковскийСчет
        |ИЗ
        |   РегистрСведений.ГП_СчетаДляОплат.СрезПоследних(&ДатаАктуальности) КАК СчетаДляОплат
        |ГДЕ
        |   СчетаДляОплат.Контрагент = &Контрагент
        |";

    Запрос.УстановитьПараметр("Контрагент", Контрагент);
    Если ДатаАктуальности = Неопределено Тогда
        Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДатаАктуальности", "");
    Иначе
        Запрос.УстановитьПараметр("ДатаАктуальности", ДатаАктуальности);
    КонецЕсли;

    РезультатЗапроса = Запрос.Выполнить();
    Если РезультатЗапроса.Пустой() Тогда
        Возврат Неопределено;
    КонецЕсли;

    Выборка = РезультатЗапроса.Выбрать();
    Выборка.Следующий();
    Возврат Выборка.БанковскийСчет;
КонецФункции

Функция ЭтотКонтрагентФизическоеЛицо(Знач Контрагент) Экспорт
    СтруктураДанныхКонтрагента = Новый Структура("ЮридическоеФизическоеЛицо, ИндивидуальныйПредприниматель, Самозанятый");

    Если ТипЗнч(Контрагент) = Тип("СправочникСсылка.Контрагенты") Тогда
        СтруктураДанныхКонтрагента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Контрагент, СтруктураДанныхКонтрагента);
    Иначе
        ЗаполнитьЗначенияСвойств(СтруктураДанныхКонтрагента, Контрагент);
    КонецЕсли;

    Если СтруктураДанныхКонтрагента.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо
        И СтруктураДанныхКонтрагента.ИндивидуальныйПредприниматель = Ложь
        И СтруктураДанныхКонтрагента.Самозанятый = Ложь Тогда

        Возврат Истина; // Это физ. лицо
    Иначе
        Возврат Ложь;
    КонецЕсли;
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс
