// Гарант+ Килипенко 09.12.2024 [F00232154] Загрузка заводских номеров счетчиков и выгрузка в старт ++
#Область ПрограммныйИнтерфейс

// Параметры:
//  НачалоПериода - Дата
//  КонецПериода - Дата
//  Услуга - СправочникСсылка.КВП_Услуги
// Возвращаемое значение:
//  - ТаблицаЗначений
//      * ЛицевойСчет - СправочникСсылка.КВП_ЛицевыеСчета
//      * КодМКД - Строка
//      * ЛицевойСчетДействует - Булево
//      * Счетчик - СправочникСсылка.КВП_Счетчики
//      * ЗаводскойНомер - Строка
//      * Услуга - СправочникСсылка.КВП_Услуги
//      * Расход - Число
//      * СчетчикДействует - Булево
//      * ДатаПоверки - Дата
//      * ПричинаОтключения - ПеречислениеСсылка.УПЖКХ_ПричиныОтключенияПриборовУчета
Функция ПолучитьДанныеПриборовУчетаДляВыгрузки(Знач НачалоПериода, Знач КонецПериода, Знач Услуга) Экспорт
    Запрос = Новый Запрос;
    Запрос.Текст = ПолучитьТекстЗапросаДанныхСчетчиковВыгрузки();

    Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
    Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
    Запрос.УстановитьПараметр("Услуги", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Услуга));

    РезультатЗапроса = Запрос.Выполнить();
    РезультатФункции = РезультатЗапроса.Выгрузить();
    Возврат РезультатФункции;
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс
// Гарант+ Килипенко 09.12.2024 [F00232154] Загрузка заводских номеров счетчиков и выгрузка в старт --

// Гарант+ Килипенко 09.12.2024 [F00232154] Загрузка заводских номеров счетчиков и выгрузка в старт ++
#Область СлужебныеПроцедурыИФункции

// Возвращаемое значение:
//  - Строка
Функция ПолучитьТекстЗапросаДанныхСчетчиковВыгрузки()
    ТекстЗапроса =
        "ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ЛицевыеСчета.Период КАК Период,
        |	ЛицевыеСчета.Объект КАК Объект,
        |	ЛицевыеСчета.ЛицевойСчет КАК ЛицевойСчет
        |ПОМЕСТИТЬ ВТ_ЛицвыеСчета_НаНачалоПериода
        |ИЗ
        |	РегистрСведений.КВП_ЛицевыеСчета.СрезПоследних(&НачалоПериода, ) КАК ЛицевыеСчета
        |ГДЕ
        |	ЛицевыеСчета.ЛицевойСчет.lc_КодМКД <> """"
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ЛицевыеСчета.Период КАК Период,
        |	ЛицевыеСчета.Объект КАК Объект,
        |	ЛицевыеСчета.ЛицевойСчет КАК ЛицевойСчет
        |ПОМЕСТИТЬ ВТ_ЛицвыеСчета_НаКонецПериода
        |ИЗ
        |	РегистрСведений.КВП_ЛицевыеСчета.СрезПоследних(&КонецПериода, ) КАК ЛицевыеСчета
        |ГДЕ
        |	ЛицевыеСчета.ЛицевойСчет.lc_КодМКД <> """"
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВТ_ЛицвыеСчета_НаНачалоПериода.Период КАК Период,
        |	ВТ_ЛицвыеСчета_НаНачалоПериода.Объект КАК Объект,
        |	ВТ_ЛицвыеСчета_НаНачалоПериода.ЛицевойСчет КАК ЛицевойСчет
        |ПОМЕСТИТЬ ВТ_ЛицвыеСчета_Объединение
        |ИЗ
        |	ВТ_ЛицвыеСчета_НаНачалоПериода КАК ВТ_ЛицвыеСчета_НаНачалоПериода
        |
        |ОБЪЕДИНИТЬ ВСЕ
        |
        |ВЫБРАТЬ
        |	ВТ_ЛицвыеСчета_НаКонецПериода.Период,
        |	ВТ_ЛицвыеСчета_НаКонецПериода.Объект,
        |	ВТ_ЛицвыеСчета_НаКонецПериода.ЛицевойСчет
        |ИЗ
        |	ВТ_ЛицвыеСчета_НаКонецПериода КАК ВТ_ЛицвыеСчета_НаКонецПериода
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	МАКСИМУМ(ВТ_ЛицвыеСчета_Объединение.Период) КАК Период,
        |	ВТ_ЛицвыеСчета_Объединение.Объект КАК Объект,
        |	ВТ_ЛицвыеСчета_Объединение.ЛицевойСчет КАК ЛицевойСчет
        |ПОМЕСТИТЬ ВТ_ЛицвыеСчета_ПериодыОтбора
        |ИЗ
        |	ВТ_ЛицвыеСчета_Объединение КАК ВТ_ЛицвыеСчета_Объединение
        |
        |СГРУППИРОВАТЬ ПО
        |	ВТ_ЛицвыеСчета_Объединение.Объект,
        |	ВТ_ЛицвыеСчета_Объединение.ЛицевойСчет
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ЛицевыеСчета.Период КАК Период,
        |	ЛицевыеСчета.Регистратор КАК Регистратор,
        |	ЛицевыеСчета.НомерСтроки КАК НомерСтроки,
        |	ЛицевыеСчета.Объект КАК Объект,
        |	ЛицевыеСчета.ЛицевойСчет КАК ЛицевойСчет,
        |	ЛицевыеСчета.ЛицевойСчет.lc_КодМКД КАК КодМКД,
        |	ЛицевыеСчета.Действует КАК Действует
        |ПОМЕСТИТЬ ВТ_ЛицвыеСчета
        |ИЗ
        |	РегистрСведений.КВП_ЛицевыеСчета КАК ЛицевыеСчета
        |ГДЕ
        |	ЛицевыеСчета.Активность = ИСТИНА
        |	И (ЛицевыеСчета.Период, ЛицевыеСчета.Объект, ЛицевыеСчета.ЛицевойСчет) В
        |			(ВЫБРАТЬ
        |				Т.Период,
        |				Т.Объект,
        |				Т.ЛицевойСчет
        |			ИЗ
        |				ВТ_ЛицвыеСчета_ПериодыОтбора КАК Т)
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	МАКСИМУМ(РасчетПриборовУчета.Период) КАК Период,
        |	РасчетПриборовУчета.Объект КАК Объект,
        |	РасчетПриборовУчета.Счетчик КАК Счетчик,
        |	РасчетПриборовУчета.Услуга КАК Услуга,
        |	СУММА(РасчетПриборовУчета.Показание) КАК Расход
        |ПОМЕСТИТЬ ВТ_РасчетПриборовУчета
        |ИЗ
        |	РегистрНакопления.УПЖКХ_РасчетПриборовУчета КАК РасчетПриборовУчета
        |ГДЕ
        |	РасчетПриборовУчета.Активность = ИСТИНА
        |	И РасчетПриборовУчета.Период >= &НачалоПериода
        |	И РасчетПриборовУчета.Период <= &КонецПериода
        |	И РасчетПриборовУчета.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
        |	И РасчетПриборовУчета.ВидПоказания = ЗНАЧЕНИЕ(Перечисление.КВП_ВидыТарифов.Дневной)
        |	И РасчетПриборовУчета.Услуга В(&Услуги)
        |
        |СГРУППИРОВАТЬ ПО
        |	РасчетПриборовУчета.Объект,
        |	РасчетПриборовУчета.Счетчик,
        |	РасчетПриборовУчета.Услуга
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ИсторияСостоянийПриборовУчета.Период КАК Период,
        |	ИсторияСостоянийПриборовУчета.Объект КАК Объект,
        |	ИсторияСостоянийПриборовУчета.ПриборУчета КАК ПриборУчета
        |ПОМЕСТИТЬ ВТ_СостоянияПриборповУчета_НаНачалоПериода
        |ИЗ
        |	РегистрСведений.УПЖКХ_ИсторияСостоянийПриборовУчета.СрезПоследних(&НачалоПериода, ) КАК ИсторияСостоянийПриборовУчета
        |ГДЕ
        |	ИСТИНА
        |	И ИсторияСостоянийПриборовУчета.ПриборУчета.ВидУслуги В(&Услуги)
        |	И ИсторияСостоянийПриборовУчета.Объект В
        |			(ВЫБРАТЬ
        |				Т.ЛицевойСчет
        |			ИЗ
        |				ВТ_ЛицвыеСчета КАК Т)
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ИсторияСостоянийПриборовУчета.Период КАК Период,
        |	ИсторияСостоянийПриборовУчета.Объект КАК Объект,
        |	ИсторияСостоянийПриборовУчета.ПриборУчета КАК ПриборУчета
        |ПОМЕСТИТЬ ВТ_СостоянияПриборповУчета_НаКонецПериода
        |ИЗ
        |	РегистрСведений.УПЖКХ_ИсторияСостоянийПриборовУчета.СрезПоследних(&КонецПериода, ) КАК ИсторияСостоянийПриборовУчета
        |ГДЕ
        |	ИСТИНА
        |	И ИсторияСостоянийПриборовУчета.ПриборУчета.ВидУслуги В(&Услуги)
        |	И ИсторияСостоянийПриборовУчета.Объект В
        |			(ВЫБРАТЬ
        |				Т.ЛицевойСчет
        |			ИЗ
        |				ВТ_ЛицвыеСчета КАК Т)
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВТ_СостоянияПриборповУчета_НаНачалоПериода.Период КАК Период,
        |	ВТ_СостоянияПриборповУчета_НаНачалоПериода.Объект КАК Объект,
        |	ВТ_СостоянияПриборповУчета_НаНачалоПериода.ПриборУчета КАК ПриборУчета
        |ПОМЕСТИТЬ ВТ_СостоянияПриборповУчета_Объединение
        |ИЗ
        |	ВТ_СостоянияПриборповУчета_НаНачалоПериода КАК ВТ_СостоянияПриборповУчета_НаНачалоПериода
        |
        |ОБЪЕДИНИТЬ ВСЕ
        |
        |ВЫБРАТЬ
        |	ВТ_СостоянияПриборповУчета_НаКонецПериода.Период,
        |	ВТ_СостоянияПриборповУчета_НаКонецПериода.Объект,
        |	ВТ_СостоянияПриборповУчета_НаКонецПериода.ПриборУчета
        |ИЗ
        |	ВТ_СостоянияПриборповУчета_НаКонецПериода КАК ВТ_СостоянияПриборповУчета_НаКонецПериода
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	МАКСИМУМ(ВТ_СостоянияПриборповУчета_Объединение.Период) КАК Период,
        |	ВТ_СостоянияПриборповУчета_Объединение.Объект КАК Объект,
        |	ВТ_СостоянияПриборповУчета_Объединение.ПриборУчета КАК ПриборУчета
        |ПОМЕСТИТЬ ВТ_СостоянияПриборповУчета_ПериодыОтбора
        |ИЗ
        |	ВТ_СостоянияПриборповУчета_Объединение КАК ВТ_СостоянияПриборповУчета_Объединение
        |
        |СГРУППИРОВАТЬ ПО
        |	ВТ_СостоянияПриборповУчета_Объединение.Объект,
        |	ВТ_СостоянияПриборповУчета_Объединение.ПриборУчета
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ИсторияСостоянийПриборовУчета.Период КАК Период,
        |	ИсторияСостоянийПриборовУчета.Объект КАК Объект,
        |	ИсторияСостоянийПриборовУчета.ПриборУчета КАК ПриборУчета,
        |	ИсторияСостоянийПриборовУчета.Действует КАК Действует,
        |	ИсторияСостоянийПриборовУчета.ДатаНачала КАК ДатаНачала,
        |	ИсторияСостоянийПриборовУчета.ДатаПоверки КАК ДатаПоверки,
        |	ИсторияСостоянийПриборовУчета.НомерПломбы КАК НомерПломбы,
        |	ИсторияСостоянийПриборовУчета.ПричинаОтключения КАК ПричинаОтключения
        |ПОМЕСТИТЬ ВТ_СостоянияПриборповУчета
        |ИЗ
        |	РегистрСведений.УПЖКХ_ИсторияСостоянийПриборовУчета КАК ИсторияСостоянийПриборовУчета
        |ГДЕ
        |	ИсторияСостоянийПриборовУчета.Активность = ИСТИНА
        |	И (ИсторияСостоянийПриборовУчета.Период, ИсторияСостоянийПриборовУчета.Объект, ИсторияСостоянийПриборовУчета.ПриборУчета) В
        |			(ВЫБРАТЬ
        |				Т.Период,
        |				Т.Объект,
        |				Т.ПриборУчета
        |			ИЗ
        |				ВТ_СостоянияПриборповУчета_ПериодыОтбора КАК Т)
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВТ_ЛицвыеСчета.Период КАК Период,
        |	ВТ_ЛицвыеСчета.Регистратор КАК Регистратор,
        |	ВТ_ЛицвыеСчета.НомерСтроки КАК НомерСтроки,
        |	ВТ_ЛицвыеСчета.Объект КАК Объект,
        |	ВТ_ЛицвыеСчета.ЛицевойСчет КАК ЛицевойСчет,
        |	ВТ_ЛицвыеСчета.КодМКД КАК КодМКД,
        |	ВТ_ЛицвыеСчета.Действует КАК ЛицевойСчетДействует,
        |	ВТ_СостоянияПриборповУчета.ПриборУчета КАК ПриборУчета,
        |	ВТ_СостоянияПриборповУчета.ПриборУчета.ВидУслуги КАК Услуга,
        |	ВТ_СостоянияПриборповУчета.Действует КАК СчетчикДействует,
        |	ВТ_СостоянияПриборповУчета.ДатаНачала КАК ДатаНачала,
        |	ВТ_СостоянияПриборповУчета.ДатаПоверки КАК ДатаПоверки,
        |	ВТ_СостоянияПриборповУчета.НомерПломбы КАК НомерПломбы,
        |	ВТ_СостоянияПриборповУчета.ПричинаОтключения КАК ПричинаОтключения
        |ПОМЕСТИТЬ ВТ_СчетчикиЛицевыхСчетов
        |ИЗ
        |	ВТ_ЛицвыеСчета КАК ВТ_ЛицвыеСчета
        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СостоянияПриборповУчета КАК ВТ_СостоянияПриборповУчета
        |		ПО ВТ_ЛицвыеСчета.ЛицевойСчет = ВТ_СостоянияПриборповУчета.Объект
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВЫРАЗИТЬ(ВТ_РасчетПриборовУчета.Объект КАК Справочник.КВП_ЛицевыеСчета) КАК ЛицевойСчет,
        |	ВТ_РасчетПриборовУчета.Счетчик КАК Счетчик,
        |	ВТ_РасчетПриборовУчета.Услуга КАК Услуга,
        |	ВТ_РасчетПриборовУчета.Расход КАК Расход
        |ПОМЕСТИТЬ ВТ_РасчетПриборовУчетаЛС
        |ИЗ
        |	ВТ_РасчетПриборовУчета КАК ВТ_РасчетПриборовУчета
        |ГДЕ
        |	ВТ_РасчетПриборовУчета.Объект ССЫЛКА Справочник.КВП_ЛицевыеСчета
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВТ_СчетчикиЛицевыхСчетов.ЛицевойСчет КАК ЛицевойСчет,
        |	ВТ_СчетчикиЛицевыхСчетов.КодМКД КАК КодМКД,
        |	ВТ_СчетчикиЛицевыхСчетов.ЛицевойСчетДействует КАК ЛицевойСчетДействует,
        |	ВТ_СчетчикиЛицевыхСчетов.ПриборУчета КАК Счетчик,
        |	ВТ_СчетчикиЛицевыхСчетов.ПриборУчета.ЗаводскойНомер КАК ЗаводскойНомер,
        |	ВТ_СчетчикиЛицевыхСчетов.СчетчикДействует КАК СчетчикДействует,
        |	ВТ_СчетчикиЛицевыхСчетов.ДатаПоверки КАК ДатаПоверки,
        |	ВТ_СчетчикиЛицевыхСчетов.ПричинаОтключения КАК ПричинаОтключения,
        |	ЕСТЬNULL(ВТ_РасчетПриборовУчета.Услуга, ВТ_СчетчикиЛицевыхСчетов.Услуга) КАК Услуга,
        |	ЕСТЬNULL(ВТ_РасчетПриборовУчета.Расход, 0) КАК Расход
        |ИЗ
        |	ВТ_СчетчикиЛицевыхСчетов КАК ВТ_СчетчикиЛицевыхСчетов
        |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РасчетПриборовУчетаЛС КАК ВТ_РасчетПриборовУчета
        |		ПО ВТ_СчетчикиЛицевыхСчетов.ЛицевойСчет = ВТ_РасчетПриборовУчета.ЛицевойСчет
        |			И ВТ_СчетчикиЛицевыхСчетов.Объект = ВТ_РасчетПриборовУчета.ЛицевойСчет.Адрес
        |			И ВТ_СчетчикиЛицевыхСчетов.ПриборУчета = ВТ_РасчетПриборовУчета.Счетчик
        |ГДЕ
        |	ВЫБОР
        |			КОГДА ВТ_РасчетПриборовУчета.ЛицевойСчет ЕСТЬ NULL
        |				ТОГДА ВТ_СчетчикиЛицевыхСчетов.ЛицевойСчетДействует
        |						И (ВТ_СчетчикиЛицевыхСчетов.СчетчикДействует
        |							ИЛИ ВТ_СчетчикиЛицевыхСчетов.ПричинаОтключения = ЗНАЧЕНИЕ(Перечисление.УПЖКХ_ПричиныОтключенияПриборовУчета.Поверка))
        |			ИНАЧЕ ИСТИНА
        |		КОНЕЦ
        |";

    Возврат ТекстЗапроса;
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
// Гарант+ Килипенко 09.12.2024 [F00232154] Загрузка заводских номеров счетчиков и выгрузка в старт --
