// Гарант+ Килипенко 28.11.2024 [F00231535] Ввод начального сальдо ++
#Область ПрограммныйИнтерфейс

// Устарела. Не используется. Остатки вводятся по данным БП77 в `ГП_МиграцияВзаиморасчетов.ЗаполнитьНачальноеСальдоПоАбонентам`
//
// Выполняет создание документа Ввод начального сальдо с заполнением ТЧ л/с по данным остатков БУ по счетам 62.01, 62.02
// Параметры:
//  ДатаОстатков - Дата - ПериодСальдо
//  ДатаДокумента - Дата
//  Организация - СправочникСсылка.Организации, Неопределено
// Возвращаемое значение:
//  - Структура
//      * Успех - Булево
//      * СозданныйДокумент - ДокументСсылка.КВП_ВводНачальногоСальдо, Неопределено
//      * ТекстСообщения - Строка, Неопределено
Функция ЗаполнитьНачальноеСальдоНачислений(Знач ДатаОстатков, Знач ДатаДокумента, Знач Организация = Неопределено) Экспорт
    РезультатФункции = Новый Структура("Успех, СозданныйДокумент, ТекстСообщения", Истина);

    Организация = ?(Организация = Неопределено, УПЖКХ_ТиповыеМетодыВызовСервера.ПолучитьОсновнуюОрганизацию(), Организация);

    ДанныеДляЗаполнения = ПолучитьДанныеЗаполненияНачальногоСальдоНачислений(ДатаОстатков, Организация);

    ДокументОбъект = Документы.КВП_ВводНачальногоСальдо.СоздатьДокумент();
    ДокументОбъект.Ответственный = Пользователи.ТекущийПользователь();
    ДокументОбъект.Организация = Организация;
    ДокументОбъект.Дата = ДатаДокумента;
    ДокументОбъект.ПериодСальдо = ДатаОстатков;
    ДокументОбъект.Комментарий = "#создано автоматически (ГарантПлюс)";

    Для Каждого СтрокаДанныхКонтрагента Из ДанныеДляЗаполнения.Строки Цикл
        ОстатокСуммыПоСтроке = СтрокаДанныхКонтрагента.ОбщийОстатокПоКонтрагенту;
        СтрокаМаксСуммы = Неопределено;
        Для Каждого СтрокаДанныхЛС Из СтрокаДанныхКонтрагента.Строки Цикл
            НоваяСтрокаЛС = ДокументОбъект.ЛицевыеСчета.Добавить();
            НоваяСтрокаЛС.Объект = СтрокаДанныхЛС.ЛицевойСчет;
            НоваяСтрокаЛС.Услуга = СтрокаДанныхЛС.Услуга;
            НоваяСтрокаЛС.МесяцНачисления = ДокументОбъект.ПериодСальдо;
            НоваяСтрокаЛС.МесяцНачисленияСтрока = Формат(ДокументОбъект.ПериодСальдо, "ДФ='MMMM yyyy'");
            НоваяСтрокаЛС.РазделУчета = Перечисления.УПЖКХ_РазделыУчета.НачислениеУслуг;
            НоваяСтрокаЛС.Сумма = Окр(СтрокаДанныхЛС.ОбщийОстатокПоКонтрагенту / СтрокаДанныхКонтрагента.КоличествоЛС, 2, 2);

            ДокументОбъект.СуммаДокумента = ДокументОбъект.СуммаДокумента + НоваяСтрокаЛС.Сумма;

            ОстатокСуммыПоСтроке = ОстатокСуммыПоСтроке - НоваяСтрокаЛС.Сумма;
            Если СтрокаМаксСуммы = Неопределено Тогда
                СтрокаМаксСуммы = НоваяСтрокаЛС;
            КонецЕсли;

            Если НоваяСтрокаЛС.Сумма > СтрокаМаксСуммы.Сумма Тогда
                СтрокаМаксСуммы = НоваяСтрокаЛС;
            КонецЕсли;
        КонецЦикла;

        Если СтрокаМаксСуммы <> Неопределено Тогда
            СтрокаМаксСуммы.Сумма = СтрокаМаксСуммы.Сумма + ОстатокСуммыПоСтроке;
            ДокументОбъект.СуммаДокумента = ДокументОбъект.СуммаДокумента + ОстатокСуммыПоСтроке;
        КонецЕсли;
    КонецЦикла;

    Попытка
        ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
        РезультатФункции.СозданныйДокумент = ДокументОбъект.Ссылка;
    Исключение
        РезультатФункции.Успех = Ложь;
        РезультатФункции.СозданныйДокумент = Неопределено;
        РезультатФункции.ТекстСообщения = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
    КонецПопытки;

    Возврат РезультатФункции;
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс
// Гарант+ Килипенко 28.11.2024 [F00231535] Ввод начального сальдо --

#Область СлужебныйПрограммныйИнтерфейс

// Устарела. Не используется. Остатки вводятся по данным БП77 в Модуле `ГП_МиграцияВзаиморасчетов`
//
// Получает данные для заполнения документа Ввод начального сальдо
// Возвращаемое значение:
//  - ДеревоЗначений
//      * Организация - СправочникСсылка.Организации
//      * Контрагент - СправочникСсылка.Контрагенты
//      * ЛицевойСчет - СправочникСсылка.КВП_ЛицевыеСчета
//      * Услуга - СправочникСсылка.КВП_Услуги
//      * ОбщийОстатокПоКонтрагенту - Число
//      * КоличествоЛС - Число
Функция ПолучитьДанныеЗаполненияНачальногоСальдоНачислений(Знач ДатаОстатков, Знач Организации = Неопределено) Экспорт
    ТекстЗапроса = ПолучитьТекстЗапросаДанныхНачальногоСальдоНачислений();
    Запрос = Новый Запрос;
    Запрос.Текст = ТекстЗапроса;

    УслугаПоУмолчанию = ГП_МиграцияОбщегоНазначения.ПолучитьУслугуХолодноеВодоснабжение().Ссылка;
    Запрос.УстановитьПараметр("УслугаПоУмолчанию", УслугаПоУмолчанию);

    Организация = ?(Организация = Неопределено, УПЖКХ_ТиповыеМетодыВызовСервера.ПолучитьОсновнуюОрганизацию(), Организация);
    Запрос.УстановитьПараметр("Организация", Организация);

    ВидыСубконто = Новый Массив;
    ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
    ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры);
    Запрос.УстановитьПараметр("ВидыСубконто", ВидыСубконто);

    Запрос.УстановитьПараметр("Счет6201", ПланыСчетов.Хозрасчетный.РасчетыСПокупателями);
    Запрос.УстановитьПараметр("Счет6202", ПланыСчетов.Хозрасчетный.РасчетыПоАвансамПолученным);

    Запрос.УстановитьПараметр("ДатаОстатков", ДатаОстатков);

    РезультатЗапроса = Запрос.Выполнить();
    РезультатФункции = РезультатЗапроса.Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
    Возврат РезультатФункции;
КонецФункции

// Параметры:
//  ДатаОстатков - Дата - ПериодСальдо
//  ДатаДокумента - Дата
//  Организация - СправочникСсылка.Организации, Неопределено
// Возвращаемое значение:
//  - ДокументОбъект.КВП_ВводНачальногоСальдо
Функция НовыйДокументВводаНачальногоСальдо(Знач ДатаОстатков, Знач ДатаДокумента, Знач Организация = Неопределено) Экспорт
    Организация = ?(Организация = Неопределено, УПЖКХ_ТиповыеМетодыВызовСервера.ПолучитьОсновнуюОрганизацию(), Организация);

    ДокументОбъект = Документы.КВП_ВводНачальногоСальдо.СоздатьДокумент();
    ДокументОбъект.Ответственный = Пользователи.ТекущийПользователь();
    ДокументОбъект.Организация = Организация;
    ДокументОбъект.Дата = ДатаДокумента;
    ДокументОбъект.ПериодСальдо = ДатаОстатков;
    ДокументОбъект.Комментарий = "#создано автоматически (ГарантПлюс)";

    Возврат ДокументОбъект;
КонецФункции

#КонецОбласти // СлужебныйПрограммныйИнтерфейс

// Гарант+ Килипенко 28.11.2024 [F00231535] Ввод начального сальдо ++
#Область СлужебныеПроцедурыИФункции

// Устарела. Не используется. Остатки вводятся по данным БП77 в Модуле `ГП_МиграцияВзаиморасчетов`
//
// Возвращаемое значение:
//  - Строка
Функция ПолучитьТекстЗапросаДанныхНачальногоСальдоНачислений()
    ТекстЗапроса =
        "ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	Остатки.Счет КАК Счет,
        |	ВЫРАЗИТЬ(Остатки.Субконто1 КАК Справочник.Контрагенты) КАК Контрагент,
        |	ВЫРАЗИТЬ(Остатки.Субконто2 КАК Справочник.ДоговорыКонтрагентов) КАК Договор,
        |	Остатки.Организация КАК Организация,
        |	Остатки.Валюта КАК Валюта,
        |	Остатки.Подразделение КАК Подразделение,
        |	ВЫБОР
        |		КОГДА Остатки.Счет В ИЕРАРХИИ (&Счет6202)
        |			ТОГДА Остатки.СуммаОстаток
        |		ИНАЧЕ Остатки.СуммаОстаток
        |	КОНЕЦ КАК СуммаОстаток
        |ПОМЕСТИТЬ ВТ_ОстаткиБУ
        |ИЗ
        |	РегистрБухгалтерии.Хозрасчетный.Остатки(&ДатаОстатков, Счет В (&Счет6201, &Счет6202), &ВидыСубконто, ) КАК Остатки
        |ГДЕ
        |	Остатки.Организация = &Организация
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	СведенияДляВзаиморасчетовПоЛС.Организация КАК Организация,
        |	СведенияДляВзаиморасчетовПоЛС.Контрагент КАК Контрагент,
        |	СведенияДляВзаиморасчетовПоЛС.ЛицевойСчет КАК ЛицевойСчет
        |ПОМЕСТИТЬ ВТ_ЛицевыеСчетаКонтрагентов
        |ИЗ
        |	РегистрСведений.УПЖКХ_СведенияДляВзаиморасчетовПоЛС.СрезПоследних(&ДатаОстатков, ) КАК СведенияДляВзаиморасчетовПоЛС
        |ГДЕ
        |	(СведенияДляВзаиморасчетовПоЛС.Организация, СведенияДляВзаиморасчетовПоЛС.Контрагент) В
        |			(ВЫБРАТЬ
        |				Т.Организация,
        |				Т.Контрагент
        |			ИЗ
        |				ВТ_ОстаткиБУ КАК Т)
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	НазначенныеНачисления.Организация КАК Организация,
        |	ВЫРАЗИТЬ(НазначенныеНачисления.Объект КАК Справочник.КВП_ЛицевыеСчета) КАК ЛицевойСчет,
        |	НазначенныеНачисления.Услуга КАК Услуга
        |ПОМЕСТИТЬ ВТ_ВсеНазначенныеУслугиЛС
        |ИЗ
        |	РегистрСведений.КВП_НазначенныеНачисления.СрезПоследних(&ДатаОстатков, ) КАК НазначенныеНачисления
        |ГДЕ
        |	НазначенныеНачисления.Объект ССЫЛКА Справочник.КВП_ЛицевыеСчета
        |	И НазначенныеНачисления.Действует = ИСТИНА
        |	И (НазначенныеНачисления.Организация, НазначенныеНачисления.Объект) В
        |			(ВЫБРАТЬ
        |				Т.Организация,
        |				Т.ЛицевойСчет
        |			ИЗ
        |				ВТ_ЛицевыеСчетаКонтрагентов КАК Т)
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВТ_ВсеНазначенныеУслугиЛС.Организация КАК Организация,
        |	ВТ_ВсеНазначенныеУслугиЛС.ЛицевойСчет КАК ЛицевойСчет,
        |	МИНИМУМ(ВТ_ВсеНазначенныеУслугиЛС.Услуга) КАК Услуга
        |ПОМЕСТИТЬ ВТ_ОсновныеУслугиЛС
        |ИЗ
        |	ВТ_ВсеНазначенныеУслугиЛС КАК ВТ_ВсеНазначенныеУслугиЛС
        |
        |СГРУППИРОВАТЬ ПО
        |	ВТ_ВсеНазначенныеУслугиЛС.Организация,
        |	ВТ_ВсеНазначенныеУслугиЛС.ЛицевойСчет
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВТ_ЛицевыеСчетаКонтрагентов.Организация КАК Организация,
        |	ВТ_ЛицевыеСчетаКонтрагентов.Контрагент КАК Контрагент,
        |	ВТ_ЛицевыеСчетаКонтрагентов.ЛицевойСчет КАК ЛицевойСчет,
        |	ЕСТЬNULL(ВТ_ОсновныеУслугиЛС.Услуга, &УслугаПоУмолчанию) КАК Услуга
        |ПОМЕСТИТЬ ВТ_ЛицевыеСчетаКонтрагентовИУслуги
        |ИЗ
        |	ВТ_ЛицевыеСчетаКонтрагентов КАК ВТ_ЛицевыеСчетаКонтрагентов
        |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОсновныеУслугиЛС КАК ВТ_ОсновныеУслугиЛС
        |		ПО ВТ_ЛицевыеСчетаКонтрагентов.ЛицевойСчет = ВТ_ОсновныеУслугиЛС.ЛицевойСчет
        |			И ВТ_ЛицевыеСчетаКонтрагентов.Организация = ВТ_ОсновныеУслугиЛС.Организация
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВТ_ОстаткиБУ.Организация КАК Организация,
        |	ВТ_ОстаткиБУ.Контрагент КАК Контрагент,
        |	СУММА(ВТ_ОстаткиБУ.СуммаОстаток) КАК СуммаОстаток
        |ПОМЕСТИТЬ ВТ_ОстаткиПоКонтрагентам
        |ИЗ
        |	ВТ_ОстаткиБУ КАК ВТ_ОстаткиБУ
        |
        |СГРУППИРОВАТЬ ПО
        |	ВТ_ОстаткиБУ.Организация,
        |	ВТ_ОстаткиБУ.Контрагент
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВТ_ОстаткиПоКонтрагентам.Организация КАК Организация,
        |	ВТ_ОстаткиПоКонтрагентам.Контрагент КАК Контрагент,
        |	ВТ_ОстаткиПоКонтрагентам.СуммаОстаток КАК ОбщийОстатокПоКонтрагенту,
        |	ВТ_ЛицевыеСчетаКонтрагентовИУслуги.ЛицевойСчет КАК ЛицевойСчет,
        |	ВТ_ЛицевыеСчетаКонтрагентовИУслуги.Услуга КАК Услуга
        |ПОМЕСТИТЬ ВТ_ОстаткиПоКонтрагентамЛС
        |ИЗ
        |	ВТ_ОстаткиПоКонтрагентам КАК ВТ_ОстаткиПоКонтрагентам
        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ЛицевыеСчетаКонтрагентовИУслуги КАК ВТ_ЛицевыеСчетаКонтрагентовИУслуги
        |		ПО ВТ_ОстаткиПоКонтрагентам.Контрагент = ВТ_ЛицевыеСчетаКонтрагентовИУслуги.Контрагент
        |			И ВТ_ОстаткиПоКонтрагентам.Организация = ВТ_ЛицевыеСчетаКонтрагентовИУслуги.Организация
        |ГДЕ
        |	ВТ_ОстаткиПоКонтрагентам.СуммаОстаток <> 0
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВТ_ОстаткиПоКонтрагентамЛС.Контрагент КАК Контрагент,
        |	ВТ_ОстаткиПоКонтрагентамЛС.Организация КАК Организация,
        |	ВТ_ОстаткиПоКонтрагентамЛС.ЛицевойСчет КАК ЛицевойСчет,
        |	ВТ_ОстаткиПоКонтрагентамЛС.Услуга КАК Услуга,
        |	ВТ_ОстаткиПоКонтрагентамЛС.ОбщийОстатокПоКонтрагенту КАК ОбщийОстатокПоКонтрагенту,
        |	1 КАК КоличествоЛС
        |ИЗ
        |	ВТ_ОстаткиПоКонтрагентамЛС КАК ВТ_ОстаткиПоКонтрагентамЛС
        |ИТОГИ
        |	МАКСИМУМ(ОбщийОстатокПоКонтрагенту),
        |	СУММА(КоличествоЛС)
        |ПО
        |	Контрагент
        |";

    Возврат ТекстЗапроса;
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
// Гарант+ Килипенко 28.11.2024 [F00231535] Ввод начального сальдо --
