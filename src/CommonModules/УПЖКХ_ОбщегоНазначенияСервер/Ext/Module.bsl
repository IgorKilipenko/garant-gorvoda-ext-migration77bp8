// Гарант+ Килипенко 24.02.2025 [F00235125] Печать чеков по контрагенту документа ++
#Область ПереопределениеСтандартногоФункционала
	
&ИзменениеИКонтроль("ДополнитьПозицииЧекаСтрокамиСРасшифровкойПоУслугам")
Процедура ГП_ДополнитьПозицииЧекаСтрокамиСРасшифровкойПоУслугам(ПозицииЧека, РасшифровкаТекущейОплаты, НомерСекции, СтруктураПараметров)
	
	// Верхний разделитель области расшифровки.
	СтрокаРазделительРасшифровки = ".......Расшифровка оплаты.......";
	ТекстоваяСтрокаПозицииЧека = УПЖКХ_ТиповыеМетодыКлиентСервер.ПараметрыТекстовойСтрокиЧека(СтрокаРазделительРасшифровки);
	ПозицииЧека.Добавить(ТекстоваяСтрокаПозицииЧека);
	
	ПризнакиАгента_Агент = Перечисления.ПризнакиАгента.Агент;
	
	// Строки расшифровки по услугам.
	Для Каждого ТекущаяСтрокаОплаты Из РасшифровкаТекущейОплаты Цикл
		ФискальнаяСтрокаПозицииЧека = УПЖКХ_ТиповыеМетодыКлиентСервер.ПараметрыФискальнойСтрокиЧека();
		ФискальнаяСтрокаПозицииЧека.Наименование = СтрЗаменить(СокрЛП(ТекущаяСтрокаОплаты.ВидУслугиПредставление), "  ", " ");
        
		// Определение количества (объема) и тарифа оказанной услуги:
		// 1. Если в настройках параметров учета отключена опция для расчета тарифа и объема,
		// или не удалось определить тариф, или это платеж по пени,
		// то количество равно 1, а тариф равен сумме платежа.
		// 2. В остальных случаях количество оказанной услуги рассчитываем
		// как отношение достоверно известных показателей: суммы платежа и тарифа.
		Тариф = Окр(ТекущаяСтрокаОплаты.Тариф, 2);
        #Вставка // Гарант+ Килипенко 24.02.2025 [F00235125] Печать чеков по контрагенту документа ++

        // Гарант+ Килипенко 10.03.2025 [F00235125] Печать чеков по контрагенту документа ++
        // ~ Исключено т.к. тариф учитывать не нужно ~
        //
        //// Учет НДС в тарифе (сверху)
        //ТарифСУчетомНДС = ТекущаяСтрокаОплаты.Тариф;
        //ТарифСУчетомНДС = ТарифСУчетомНДС + УчетНДСКлиентСервер.РассчитатьСуммуНДС(ТарифСУчетомНДС,
        //    Ложь, УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(ТекущаяСтрокаОплаты.СтавкаНДС));
        //Тариф = Окр(ТарифСУчетомНДС, 2);
        //
        // Гарант+ Килипенко 10.03.2025 [F00235125] Печать чеков по контрагенту документа --

        #КонецВставки // Гарант+ Килипенко 24.02.2025 [F00235125] Печать чеков по контрагенту документа --
		
		Если Не СтруктураПараметров.РассчитыватьКоличествоИЦенуВПлатежах
		    Или ТекущаяСтрокаОплаты.ЭтоПени
		    Или Тариф = 0 Тогда
			
			Тариф      = ТекущаяСтрокаОплаты.Сумма;
			Количество = 1;
		Иначе
			Количество = Окр(ТекущаяСтрокаОплаты.Сумма / Тариф, 6);
			
			// В драйвере ККТ выполняется проверка Тариф*Количество дб равно Сумме
			// с точностью до 2 знаков после запятой.
			// В редких случаях рассчитанное с точностью до 6 знаков количество
			// при перемножении с тарифом и последующем округлении до 2 знаков полученной
			// расчетной суммы может отличаться от значения Суммы, фактически передаваемого
			// в ККТ.
			// Чтобы избежать ошибки при печати чека по такому платежу, необходимо
			// корректировать количество на минимальную необходимую для выполнения проверки сумму.
			СуммаРасчетная = Тариф * Количество;
			
			Если Окр(СуммаРасчетная, 2) <> ТекущаяСтрокаОплаты.Сумма Тогда
				РазностьСуммПриОкруглении = ТекущаяСтрокаОплаты.Сумма - СуммаРасчетная;
				
				КорректировкаКоличества = РазностьСуммПриОкруглении / Тариф;
				
				Количество = Окр(Количество + КорректировкаКоличества, 6);
			КонецЕсли;
			
			// Если в результате округления получено значение тарифа, равное 0, то
			// количество принимаем как 1, тариф = сумме платежа.
			Если Количество = 0 Тогда
				Тариф      = ТекущаяСтрокаОплаты.Сумма;
				Количество = 1;
			КонецЕсли;
			
		КонецЕсли;
		
		ФискальнаяСтрокаПозицииЧека.Количество             = Количество;
		ФискальнаяСтрокаПозицииЧека.Цена                   = Тариф;
		ФискальнаяСтрокаПозицииЧека.ЦенаСоСкидками         = ФискальнаяСтрокаПозицииЧека.Цена;
		ФискальнаяСтрокаПозицииЧека.Сумма                  = ТекущаяСтрокаОплаты.Сумма;
		
		ФискальнаяСтрокаПозицииЧека.СтавкаНДС              = ТекущаяСтрокаОплаты.СтавкаНДС;
		ФискальнаяСтрокаПозицииЧека.НомерСекции            = НомерСекции;
		
		ФискальнаяСтрокаПозицииЧека.ПризнакПредметаРасчета = ТекущаяСтрокаОплаты.ПризнакПредметаРасчета;
		ФискальнаяСтрокаПозицииЧека.ПризнакСпособаРасчета  = ТекущаяСтрокаОплаты.ПризнакСпособаРасчета;
		
		Если ТекущаяСтрокаОплаты.УказыватьВЧекеПризнакАгентаВПлатежах Тогда
			ФискальнаяСтрокаПозицииЧека.ПризнакАгентаПоПредметуРасчета = ПризнакиАгента_Агент;
			
			ДанныеПоставщика = ФискальнаяСтрокаПозицииЧека.ДанныеПоставщика;
			ДанныеПоставщика.Наименование = ТекущаяСтрокаОплаты.ПоставщикУслугиПредставление;
			ДанныеПоставщика.ИНН          = ТекущаяСтрокаОплаты.ИННПоставщикаУслуги;
			ДанныеПоставщика.Телефон      = ТекущаяСтрокаОплаты.ТелефонПоставщикаУслуги;
		КонецЕсли;
		
		ПозицииЧека.Добавить(ФискальнаяСтрокаПозицииЧека);
	КонецЦикла;
	
	// Нижний разделитель области расшифровки.
	СтрокаРазделительРасшифровки = "................................";
	ТекстоваяСтрокаПозицииЧека = УПЖКХ_ТиповыеМетодыКлиентСервер.ПараметрыТекстовойСтрокиЧека(СтрокаРазделительРасшифровки);
	
	ПозицииЧека.Добавить(ТекстоваяСтрокаПозицииЧека);
	
КонецПроцедуры

#КонецОбласти // ПереопределениеСтандартногоФункционала
// Гарант+ Килипенко 24.02.2025 [F00235125] Печать чеков по контрагенту документа --
