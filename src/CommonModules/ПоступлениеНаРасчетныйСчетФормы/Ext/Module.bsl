// Гарант+ Килипенко 22.01.2025 Сохранение ставки НДС при изменении Договора ++
#Область ПереопределениеСтандартногоФункционала

&ИзменениеИКонтроль("ДоговорКонтрагентаПриИзмененииНаСервере")
Процедура ГП_ДоговорКонтрагентаПриИзмененииНаСервере(СтрокаПлатеж, Форма)

	Объект = Форма.Объект;

	ПараметрыОбъекта = ПоступлениеНаРасчетныйСчетФормыКлиентСервер.ТекущиеПараметрыОбъекта(Форма);
	ПараметрыОбъекта.ДоговорКонтрагента = СтрокаПлатеж.ДоговорКонтрагента;
	Форма.СвойстваПлатежа = ПоступлениеНаРасчетныйСчетФормыКлиентСервер.СвойстваСтрокРасшифровкиПлатежа(ПараметрыОбъекта, Ложь);
    #Вставка // Гарант+ Килипенко 22.01.2025 Сохранение ставки НДС при изменении Договора ++

    Если Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ОплатаПокупателя
            И Форма.СвойстваПлатежа.Свойство("СтавкаНДС")
            И Форма.СвойстваПлатежа.СтавкаНДС <> СтрокаПлатеж.СтавкаНДС
            И ПустаяСтрока(Строка(СтрокаПлатеж.СтавкаНДС)) = Ложь Тогда

        Форма.СвойстваПлатежа.СтавкаНДС = СтрокаПлатеж.СтавкаНДС; // Сохраняем исходную ставку НДС
        ОбщегоНазначения.СообщитьПользователю("Внимание! Ставка НДС осталась без изменений");
    КонецЕсли;

    #КонецВставки // Гарант+ Килипенко 22.01.2025 Сохранение ставки НДС при изменении Договора --

	ЗаполнитьЗначенияСвойств(СтрокаПлатеж, Форма.СвойстваПлатежа,, "СуммаПлатежа");

	ПараметрыЗаполненияСчетовУчета = ПоступлениеНаРасчетныйСчетФормыКлиентСервер.НачатьЗаполнениеСчетовУчета(
		"РасшифровкаПлатежа.ДоговорКонтрагента", Объект, СтрокаПлатеж);
	Изменения = СчетаУчетаВДокументах.ЗаполнитьРеквизитыПриИзменении(
		Документы.ПоступлениеНаРасчетныйСчет,
		ПараметрыЗаполненияСчетовУчета.КЗаполнению,
		Объект,
		"РасшифровкаПлатежа",
		СтрокаПлатеж,
		Ложь);
	ЗаполнитьЗначенияСвойств(СтрокаПлатеж,          Изменения);
	ЗаполнитьЗначенияСвойств(Форма.СвойстваПлатежа, Изменения);

	Если Форма.ПрименениеУСН Или Форма.ПрименяетсяАУСН Тогда
		ПоступлениеНаРасчетныйСчетФормыКлиентСервер.ЗаполнитьОтражениеСтрокиВУСН(СтрокаПлатеж, Форма);
	КонецЕсли;

	ЗаполнитьЗначенияСвойств(СтрокаПлатеж, Форма.СвойстваПлатежа,, "СуммаПлатежа");

	ПоступлениеНаРасчетныйСчетФормыКлиентСервер.ПересчитатьСуммуНДС(СтрокаПлатеж);
	ПоступлениеНаРасчетныйСчетФормыКлиентСервер.РассчитатьСуммуВзаиморасчетов(СтрокаПлатеж, Форма);

	Если ПоступлениеНаРасчетныйСчетФормыКлиентСервер.ФормаДокументаОднострочная(Форма) Тогда
		Если ЗначениеЗаполнено(Форма.РасшифровкаПлатежаДоговорКонтрагента) Тогда
			Форма.ЕстьРасчетыВУсловныхЕдиницах = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
				Форма.РасшифровкаПлатежаДоговорКонтрагента, "РасчетыВУсловныхЕдиницах");
		Иначе
			Форма.ЕстьРасчетыВУсловныхЕдиницах = Ложь;
		КонецЕсли;
	Иначе
		ЗаполнитьДобавленныеКолонкиТаблиц(Форма);
	КонецЕсли;

	Если ЗначениеЗаполнено(СтрокаПлатеж.ДоговорКонтрагента) Тогда
		Если ЗначениеЗаполнено(СтрокаПлатеж.СчетНаОплату) Тогда
			ДоговорСчетаПокупателю = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаПлатеж.СчетНаОплату, "ДоговорКонтрагента");
			Если ЗначениеЗаполнено(ДоговорСчетаПокупателю)
				И ДоговорСчетаПокупателю <> СтрокаПлатеж.ДоговорКонтрагента Тогда
				СтрокаПлатеж.СчетНаОплату = Неопределено;
			КонецЕсли;
		КонецЕсли;
		Форма.ПредлагатьНовыйДоговор = Ложь;

		Если ЗначениеЗаполнено(СтрокаПлатеж.ПорядокОтраженияАванса) Тогда
			ПредставлениеАванса = БанкИКассаФормыКлиентСервер.ВариантОтраженияДоходовПредставление(
				СтрокаПлатеж.ПорядокОтраженияАванса,
				БанкИКассаФормыКлиентСервер.ВариантыОтраженияДоходов(Форма));
			СтрокаПлатеж.ОтражениеДоходаПредставление = ПредставлениеАванса;
			СтрокаПлатеж.ОтражениеАвансаПредставление = ПредставлениеАванса;
		КонецЕсли;
	КонецЕсли;

	УправлениеФормой(Форма);

КонецПроцедуры

// Гарант+ Килипенко 31.01.2025 Сохранение ставки НДС при изменении Контрагента ++
//
&ИзменениеИКонтроль("КонтрагентПриИзмененииСервер")
Процедура ГП_КонтрагентПриИзмененииСервер(Форма)

	Объект = Форма.Объект;

	ПричиныИзменения = Новый Массив;
	ПричиныИзменения.Добавить("Контрагент");

	УстановитьВладельцаСчетаКонтрагента(Форма);
	УстановитьПараметрыВыбораБанковскихСчетов(Форма);

	ПараметрыОбъекта = ТекущиеПараметрыОбъекта(Форма);
	НовыеПараметры   = НовыеПараметрыПриИзмененииКонтрагента(ПараметрыОбъекта);

	ЗаполнитьЗначенияСвойств(Объект, НовыеПараметры, "СчетКонтрагента");

	ИнициализироватьСвойстваПлатежа(Форма, Истина);
	ЗаполнитьЗначенияСвойств(Форма.СвойстваПлатежа, НовыеПараметры);
    #Вставка // Гарант+ Килипенко 31.01.2025 Сохранение ставки НДС при изменении Контрагента ++

    ИсходнаяРасшифровкаПлатежа = ПоступлениеНаРасчетныйСчетФормыКлиентСервер.ПолучитьРасшифровкаПлатежа(Форма);

    ИсходнаяСтавкаНДС = Неопределено;
    Если Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ОплатаПокупателя
            И Форма.СвойстваПлатежа.Свойство("СтавкаНДС") Тогда

        ИсходнаяСтавкаНДС = ПолучитьСтавкуНДСРасшифровкиДляПерезаполнения(ИсходнаяРасшифровкаПлатежа);
    КонецЕсли;

    Если ПустаяСтрока(Строка(ИсходнаяСтавкаНДС)) = Ложь И Форма.СвойстваПлатежа.СтавкаНДС <> ИсходнаяСтавкаНДС Тогда
        Форма.СвойстваПлатежа.СтавкаНДС = ИсходнаяСтавкаНДС;
        ОбщегоНазначения.СообщитьПользователю(
            "Внимание! Ставка НДС осталась без изменений (принята из первой строки расшифровки платежа)");
    КонецЕсли;

    #КонецВставки // Гарант+ Килипенко 31.01.2025 Сохранение ставки НДС при изменении Контрагента --
	ПерезаполнитьРасшифровкуПлатежа(Форма);

	Если Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ОплатаПокупателя И Форма.ВестиУчетПоДоговорам Тогда

		Если ЗначениеЗаполнено(НовыеПараметры.ДоговорКонтрагента)
			ИЛИ Форма.ОплатаВВалюте Тогда
			Форма.ПредлагатьНовыйДоговор = Ложь;
		Иначе
			Форма.ПредлагатьНовыйДоговор = РаботаСДоговорамиКонтрагентовБП.ПредлагатьНовыйДоговор(Объект.Организация, Объект.Контрагент);
		КонецЕсли;

	КонецЕсли;

	Если Не БанкИКассаФормыКлиентСервер.ФормаДокументаИнтеграцииСБанком(Форма) Тогда
		РаботаСДоговорамиКонтрагентовБП.УстановитьПредлагатьНовыйДоговорСНумерацией(Форма);
	КонецЕсли;

	ПоступлениеНаРасчетныйСчетФормыКлиентСервер.ПоказатьПредупреждениеПечатьЧека(Форма);

	ЗаполнитьОтражениеВУСННаСервере(Форма);

	УправлениеФормой(Форма);

	ЗаполнитьСчетаУчета(Форма, "Контрагент", ПричиныИзменения);

КонецПроцедуры // Гарант+ Килипенко 31.01.2025 Сохранение ставки НДС при изменении Контрагента --

#КонецОбласти // ПереопределениеСтандартногоФункционала
// Гарант+ Килипенко 22.01.2025 Сохранение ставки НДС при изменении Договора --

// Гарант+ Килипенко 31.01.2025 Сохранение ставки НДС при изменении Контрагента ++
#Область СлужебныйПрограммныйИнтерфейс

Функция ПолучитьСтавкуНДСРасшифровкиДляПерезаполнения(Знач РасшифровкаПлатежа) Экспорт
    Если РасшифровкаПлатежа.Количество() = 0 Тогда
        Возврат Неопределено;
    КонецЕсли;

    Возврат РасшифровкаПлатежа[0].СтавкаНДС;
КонецФункции

#КонецОбласти // СлужебныйПрограммныйИнтерфейс
// Гарант+ Килипенко 31.01.2025 Сохранение ставки НДС при изменении Контрагента --
