// Гарант+ Килипенко 11.02.2025 [F00232767] Выгрузка в ОФД доработка ++
#Область ПрограммныйИнтерфейс

// Параметры:
//  НачалоПериода - Дата
//  КонецПериода - Дата
//  ЛицевойСчет - СправочникСсылка.КВП_ЛицевыеСчета, Неопределено
//  Организация - СправочникСсылка.Организации, Неопределено
// Возвращаемое значение:
//  - ТаблицаЗначений
//      * Ссылка - ДокументСсылка.КВП_РегистрацияОплаты
//      * Объект - СправочникСсылка.КВП_ЛицевыеСчета
//      * Контрагент - СправочникСсылка.Контрагенты
//      * Договор - СправочникСсылка.ДоговорыКонтрагентов
//      * Услуга - СправочникСсылка.КВП_Услуги
//      * ДокументОплаты - ДокументСсылка
//      * ЭтоАванс - Строка
//      * МесяцНачисления - Строка
//      * Сумма - Число
//      * Пени - Число
//      * АвансСумма - Число
//      * ДолгСумма - Число
Функция ПолучитьОбщуюТаблицуРасшифровкиОплатДляОФД(
        Знач НачалоПериода, Знач КонецПериода, Знач ЛицевойСчет = Неопределено, Организация = Неопределено) Экспорт

    Организация = ?(Организация = Неопределено, УПЖКХ_ТиповыеМетодыВызовСервера.ПолучитьОсновнуюОрганизацию(), Организация);

    Запрос = Новый Запрос;
    Запрос.Текст = ПолучитьТекстЗапросаОбщейТаблицыРасшифровкиОплатДляОФД();

    ДополнительныеУсловияОтбора = "ИСТИНА";
    Если ЛицевойСчет <> Неопределено И НЕ ЛицевойСчет.Пустая() Тогда
        ДополнительныеУсловияОтбора = ДополнительныеУсловияОтбора + " И РегистрацияОплатыРасшифровкаПлатежа.Объект = &ЛицевойСчет";
        Запрос.УстановитьПараметр("ЛицевойСчет", ЛицевойСчет);
    КонецЕсли;

    Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДополнительныеУсловияОтбора", ДополнительныеУсловияОтбора);

    Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
    Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
    Запрос.УстановитьПараметр("Организация", Организация);
    Запрос.УстановитьПараметр("ВидыОпераций",
        ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Перечисления.КВП_ВидыОперацийРегистрацииОплаты.ПоступлениеНаБанковскийСчет));

    РезультатЗапроса = Запрос.Выполнить();
    РезультатФункции = РезультатЗапроса.Выгрузить();
    Возврат РезультатФункции;
КонецФункции

// Параметры:
//  ТаблицаРасшифровкиОплат - ТаблицаЗначений
Процедура СвернутьОбщуюТаблицуРасшифровкиОплатДляОФД(ТаблицаРасшифровкиОплат) Экспорт
    ТаблицаРасшифровкиОплат.Свернуть("Ссылка, Объект, Услуга, ЭтоАванс", "Сумма, Пени, АвансСумма, ДолгСумма");
КонецПроцедуры

// Параметры:
//  НачалоПериода - Дата
//  КонецПериода - Дата
//  ЛицевойСчет - СправочникСсылка.КВП_ЛицевыеСчета, Неопределено
//  Организация - СправочникСсылка.Организации, Неопределено
// Возвращаемое значение:
//  - ТаблицаЗначений
//      * Ссылка - ДокументСсылка.КВП_РегистрацияОплаты
//      * Объект - СправочникСсылка.КВП_ЛицевыеСчета
//      * Услуга - СправочникСсылка.КВП_Услуги
//      * ЭтоАванс - Строка
//      * МесяцНачисления - Строка
//      * Сумма - Число
//      * Пени - Число
//      * АвансСумма - Число
//      * ДолгСумма - Число
Функция ПолучитьСвернутуюОбщуюТаблицуРасшифровкиОплатДляОФД(
        Знач НачалоПериода, Знач КонецПериода, Знач ЛицевойСчет = Неопределено, Организация = Неопределено) Экспорт

    РезультатФункции = ПолучитьОбщуюТаблицуРасшифровкиОплатДляОФД(НачалоПериода, КонецПериода, ЛицевойСчет, Организация);
    СвернутьОбщуюТаблицуРасшифровкиОплатДляОФД(РезультатФункции);

    Возврат РезультатФункции;
КонецФункции

// Параметры:
//  НачалоПериода - Дата
//  КонецПериода - Дата
//  ЛицевойСчет - СправочникСсылка.КВП_ЛицевыеСчета, Неопределено
//  Организация - СправочникСсылка.Организации, Неопределено
// Возвращаемое значение:
//  - ТаблицаЗначений
//      * Ссылка - ДокументСсылка.КВП_РегистрацияОплаты
//      * Объект - СправочникСсылка.КВП_ЛицевыеСчета
//      * Сумма - Число
Функция ПолучитьТаблицуЛицевыхСчетовРегистрацииОплатДляОФД(
        Знач НачалоПериода, Знач КонецПериода, Знач ЛицевойСчет = Неопределено, Организация = Неопределено) Экспорт

    ТаблицаРасшифровкиОплатДляОФД = ПолучитьСвернутуюОбщуюТаблицуРасшифровкиОплатДляОФД(
            НачалоПериода, КонецПериода, ЛицевойСчет, Организация);

    ПоляГруппировки = "Ссылка, Объект";
    ТаблицаРасшифровкиОплатДляОФД.Свернуть(ПоляГруппировки, "Сумма");
    ТаблицаРасшифровкиОплатДляОФД.Сортировать(ПоляГруппировки);

    Возврат ТаблицаРасшифровкиОплатДляОФД;
КонецФункции

Функция ПолучитьРасшифровкуОплатДляЧеков(ТаблицаОплатСДопДанными, СтруктураПараметров) Экспорт
    Запрос = Новый Запрос;
    Запрос.Текст =
        "ВЫБРАТЬ
        |	ТаблицаОбъектов.Организация КАК Организация,
        |	ТаблицаОбъектов.Документ КАК Документ,
        |	ТаблицаОбъектов.ЛицевойСчет КАК ЛицевойСчет,
        |	ТаблицаОбъектов.ВариантРаспределенияОплатКапРемонт КАК ВариантРаспределенияОплатКапРемонт
        |ПОМЕСТИТЬ ТаблицаОбъектов
        |ИЗ
        |	&ТаблицаОбъектов КАК ТаблицаОбъектов
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	КВП_УчетнаяПолитикаТСЖСрезПоследних.Организация КАК Организация,
        |	ВЫБОР
        |		КОГДА КВП_УчетнаяПолитикаТСЖСрезПоследних.СтавкаНДСДляОтраженияПени = ЗНАЧЕНИЕ(Перечисление.УПЖКХ_СтавкиНДСДляПениПриОтраженииВРеглУчете.БезНДС)
        |			ТОГДА ИСТИНА
        |		ИНАЧЕ ЛОЖЬ
        |	КОНЕЦ КАК ОтражатьПениПоСтавкеБезНДС
        |ПОМЕСТИТЬ врОтражатьПениПоСтавкеБезНДС
        |ИЗ
        |	РегистрСведений.КВП_УчетнаяПолитикаТСЖ.СрезПоследних(
        |			&ТекущаяДата,
        |			Организация В
        |				(ВЫБРАТЬ
        |					ТаблицаОбъектов.Организация КАК ДокументОрганизация
        |				ИЗ
        |					ТаблицаОбъектов КАК ТаблицаОбъектов)) КАК КВП_УчетнаяПолитикаТСЖСрезПоследних
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ТаблицаОбъектов.Документ КАК Документ,
        |	ТаблицаОбъектов.Организация КАК Организация,
        |	ТаблицаОбъектов.ЛицевойСчет КАК ЛицевойСчет,
        |	КВП_РегистрацияОплатыРасшифровкаПлатежа.Услуга КАК Услуга,
        |	КВП_РегистрацияОплатыРасшифровкаПлатежа.Услуга.ВидУслуги КАК ВидУслуги,
        |	ПРЕДСТАВЛЕНИЕ(КВП_РегистрацияОплатыРасшифровкаПлатежа.Услуга.ВидУслуги) КАК ВидУслугиПредставление,
        |	КВП_РегистрацияОплатыРасшифровкаПлатежа.МесяцНачисления КАК МесяцНачисления,
        |	ВЫБОР
        |		КОГДА НЕ КВП_РегистрацияОплатыРасшифровкаПлатежа.Услуга.ВидУслуги = ЗНАЧЕНИЕ(Справочник.КВП_ВидыУслуг.ПустаяСсылка)
        |				И КВП_РегистрацияОплатыРасшифровкаПлатежа.Услуга.ВидУслуги.СпособВыводаВКвитанциях = ""Тариф сворачивать, количество суммировать""
        |			ТОГДА ИСТИНА
        |		ИНАЧЕ ЛОЖЬ
        |	КОНЕЦ КАК СуммироватьКоличество,
        |	ВЫБОР
        |		КОГДА НЕ КВП_РегистрацияОплатыРасшифровкаПлатежа.Услуга.ВидУслуги = ЗНАЧЕНИЕ(Справочник.КВП_ВидыУслуг.ПустаяСсылка)
        |				И КВП_РегистрацияОплатыРасшифровкаПлатежа.Услуга.ВидУслуги.СпособВыводаВКвитанциях = ""Количество сворачивать, тариф суммировать""
        |			ТОГДА ИСТИНА
        |		ИНАЧЕ ЛОЖЬ
        |	КОНЕЦ КАК СуммироватьТариф,
        |	СУММА(ВЫБОР
        |			КОГДА НЕ КВП_РегистрацияОплатыРасшифровкаПлатежа.Сумма = 0
        |					И КВП_РегистрацияОплатыРасшифровкаПлатежа.Сумма > КВП_РегистрацияОплатыРасшифровкаПлатежа.ДолгСумма
        |				ТОГДА КВП_РегистрацияОплатыРасшифровкаПлатежа.Сумма - КВП_РегистрацияОплатыРасшифровкаПлатежа.ДолгСумма
        |			ИНАЧЕ 0
        |		КОНЕЦ) КАК ПереплатаПоНачислениям,
        |	СУММА(ВЫБОР
        |			КОГДА КВП_РегистрацияОплатыРасшифровкаПлатежа.Сумма = 0
        |				ТОГДА 0
        |			КОГДА КВП_РегистрацияОплатыРасшифровкаПлатежа.Сумма <= КВП_РегистрацияОплатыРасшифровкаПлатежа.ДолгСумма
        |				ТОГДА КВП_РегистрацияОплатыРасшифровкаПлатежа.Сумма
        |			КОГДА КВП_РегистрацияОплатыРасшифровкаПлатежа.Сумма > КВП_РегистрацияОплатыРасшифровкаПлатежа.ДолгСумма
        |				ТОГДА КВП_РегистрацияОплатыРасшифровкаПлатежа.ДолгСумма
        |			ИНАЧЕ 0
        |		КОНЕЦ + КВП_РегистрацияОплатыРасшифровкаПлатежа.Рассрочка) КАК Сумма,
        |	СУММА(КВП_РегистрацияОплатыРасшифровкаПлатежа.Пени) КАК Пени,
        |	ЕСТЬNULL(КВП_РегистрацияОплатыРасшифровкаПлатежа.Услуга.Услуга.ВидСтавкиНДС, ВЫБОР
        |			КОГДА ТаблицаОбъектов.ВариантРаспределенияОплатКапРемонт = ЗНАЧЕНИЕ(Перечисление.УПЖКХ_ВариантыРаспределенияОплатыПриРаздельномУчетеКР.УслугиКапРемонт)
        |				ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСтавокНДС.БезНДС)
        |			ИНАЧЕ &ВидСтавкиНДСИзПерсональныхНастроек
        |		КОНЕЦ) КАК ВидСтавкиНДС,
        |	ВЫБОР
        |		КОГДА ЕСТЬNULL(КВП_РегистрацияОплатыРасшифровкаПлатежа.Услуга.Услуга.Услуга, ИСТИНА) = ИСТИНА
        |			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПризнакиПредметаРасчета.Услуга)
        |		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ПризнакиПредметаРасчета.Товар)
        |	КОНЕЦ КАК ПризнакПредметаРасчета,
        |	ВЫБОР
        |		КОГДА &ВестиУчетНачисленийПоДобровольномуСтрахованию
        |				И КВП_РегистрацияОплатыРасшифровкаПлатежа.Услуга = &УслугаДобровольногоСтрахования
        |			ТОГДА ИСТИНА
        |		ИНАЧЕ ЛОЖЬ
        |	КОНЕЦ КАК ЭтоДобровольноеСтрахование
        |ПОМЕСТИТЬ втТаблицаРасшифровка
        |ИЗ
        |	ТаблицаОбъектов КАК ТаблицаОбъектов
        |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КВП_РегистрацияОплаты.РасшифровкаПлатежа КАК КВП_РегистрацияОплатыРасшифровкаПлатежа
        |		ПО ТаблицаОбъектов.Документ = КВП_РегистрацияОплатыРасшифровкаПлатежа.Ссылка
        |			И ТаблицаОбъектов.ЛицевойСчет = КВП_РегистрацияОплатыРасшифровкаПлатежа.Объект
        |
        |СГРУППИРОВАТЬ ПО
        |	ТаблицаОбъектов.Документ,
        |	ТаблицаОбъектов.Организация,
        |	ТаблицаОбъектов.ЛицевойСчет,
        |	КВП_РегистрацияОплатыРасшифровкаПлатежа.Услуга,
        |	КВП_РегистрацияОплатыРасшифровкаПлатежа.Услуга.ВидУслуги,
        |	КВП_РегистрацияОплатыРасшифровкаПлатежа.МесяцНачисления,
        |	ВЫБОР
        |		КОГДА НЕ КВП_РегистрацияОплатыРасшифровкаПлатежа.Услуга.ВидУслуги = ЗНАЧЕНИЕ(Справочник.КВП_ВидыУслуг.ПустаяСсылка)
        |				И (КВП_РегистрацияОплатыРасшифровкаПлатежа.Услуга.ВидУслуги.СпособВыводаВКвитанциях = ""Тариф сворачивать, количество суммировать""
        |					ИЛИ КВП_РегистрацияОплатыРасшифровкаПлатежа.Услуга.ВидУслуги.СпособВыводаВКвитанциях = ""Количество сворачивать, тариф суммировать"")
        |			ТОГДА ИСТИНА
        |		ИНАЧЕ ЛОЖЬ
        |	КОНЕЦ,
        |	ЕСТЬNULL(КВП_РегистрацияОплатыРасшифровкаПлатежа.Услуга.Услуга.ВидСтавкиНДС, ВЫБОР
        |			КОГДА ТаблицаОбъектов.ВариантРаспределенияОплатКапРемонт = ЗНАЧЕНИЕ(Перечисление.УПЖКХ_ВариантыРаспределенияОплатыПриРаздельномУчетеКР.УслугиКапРемонт)
        |				ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСтавокНДС.БезНДС)
        |			ИНАЧЕ &ВидСтавкиНДСИзПерсональныхНастроек
        |		КОНЕЦ),
        |	ВЫБОР
        |		КОГДА ЕСТЬNULL(КВП_РегистрацияОплатыРасшифровкаПлатежа.Услуга.Услуга.Услуга, ИСТИНА) = ИСТИНА
        |			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПризнакиПредметаРасчета.Услуга)
        |		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ПризнакиПредметаРасчета.Товар)
        |	КОНЕЦ,
        |	ВЫБОР
        |		КОГДА НЕ КВП_РегистрацияОплатыРасшифровкаПлатежа.Услуга.ВидУслуги = ЗНАЧЕНИЕ(Справочник.КВП_ВидыУслуг.ПустаяСсылка)
        |				И КВП_РегистрацияОплатыРасшифровкаПлатежа.Услуга.ВидУслуги.СпособВыводаВКвитанциях = ""Тариф сворачивать, количество суммировать""
        |			ТОГДА ИСТИНА
        |		ИНАЧЕ ЛОЖЬ
        |	КОНЕЦ,
        |	ВЫБОР
        |		КОГДА НЕ КВП_РегистрацияОплатыРасшифровкаПлатежа.Услуга.ВидУслуги = ЗНАЧЕНИЕ(Справочник.КВП_ВидыУслуг.ПустаяСсылка)
        |				И КВП_РегистрацияОплатыРасшифровкаПлатежа.Услуга.ВидУслуги.СпособВыводаВКвитанциях = ""Количество сворачивать, тариф суммировать""
        |			ТОГДА ИСТИНА
        |		ИНАЧЕ ЛОЖЬ
        |	КОНЕЦ,
        |	ВЫБОР
        |		КОГДА &ВестиУчетНачисленийПоДобровольномуСтрахованию
        |				И КВП_РегистрацияОплатыРасшифровкаПлатежа.Услуга = &УслугаДобровольногоСтрахования
        |			ТОГДА ИСТИНА
        |		ИНАЧЕ ЛОЖЬ
        |	КОНЕЦ
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	втТаблицаРасшифровка.Документ КАК Документ,
        |	втТаблицаРасшифровка.ЛицевойСчет КАК ЛицевойСчет,
        |	втТаблицаРасшифровка.Услуга КАК Услуга,
        |	втТаблицаРасшифровка.ВидУслуги КАК ВидУслуги,
        |	втТаблицаРасшифровка.ВидУслугиПредставление КАК ВидУслугиПредставление,
        |	втТаблицаРасшифровка.МесяцНачисления КАК МесяцНачисления,
        |	втТаблицаРасшифровка.СуммироватьКоличество КАК СуммироватьКоличество,
        |	втТаблицаРасшифровка.СуммироватьТариф КАК СуммироватьТариф,
        |	втТаблицаРасшифровка.ВидСтавкиНДС КАК ВидСтавкиНДС,
        |	втТаблицаРасшифровка.ПереплатаПоНачислениям КАК Сумма,
        |	ЛОЖЬ КАК ЭтоПени,
        |	ИСТИНА КАК ЭтоАванс,
        |	втТаблицаРасшифровка.ПризнакПредметаРасчета КАК ПризнакПредметаРасчета,
        |	ЗНАЧЕНИЕ(Перечисление.ПризнакиСпособаРасчета.Аванс) КАК ПризнакСпособаРасчета,
        |	втТаблицаРасшифровка.ЭтоДобровольноеСтрахование КАК ЭтоДобровольноеСтрахование
        |ПОМЕСТИТЬ втДанныеПоВидамОплатБезТарифа
        |ИЗ
        |	втТаблицаРасшифровка КАК втТаблицаРасшифровка
        |ГДЕ
        |	втТаблицаРасшифровка.ПереплатаПоНачислениям > 0
        |	И НЕ втТаблицаРасшифровка.ЭтоДобровольноеСтрахование
        |
        |ОБЪЕДИНИТЬ ВСЕ
        |
        |ВЫБРАТЬ
        |	втТаблицаРасшифровка.Документ,
        |	втТаблицаРасшифровка.ЛицевойСчет,
        |	втТаблицаРасшифровка.Услуга,
        |	втТаблицаРасшифровка.ВидУслуги,
        |	втТаблицаРасшифровка.ВидУслугиПредставление,
        |	втТаблицаРасшифровка.МесяцНачисления,
        |	втТаблицаРасшифровка.СуммироватьКоличество,
        |	втТаблицаРасшифровка.СуммироватьТариф,
        |	втТаблицаРасшифровка.ВидСтавкиНДС,
        |	втТаблицаРасшифровка.Сумма,
        |	ЛОЖЬ,
        |	ЛОЖЬ,
        |	втТаблицаРасшифровка.ПризнакПредметаРасчета,
        |	&ПризнакСпособаРасчетаВПлатежахНаОказаниеУслугВЧекахРО,
        |	втТаблицаРасшифровка.ЭтоДобровольноеСтрахование
        |ИЗ
        |	втТаблицаРасшифровка КАК втТаблицаРасшифровка
        |ГДЕ
        |	втТаблицаРасшифровка.Сумма > 0
        |	И НЕ втТаблицаРасшифровка.ЭтоДобровольноеСтрахование
        |
        |ОБЪЕДИНИТЬ ВСЕ
        |
        |ВЫБРАТЬ
        |	втТаблицаРасшифровка.Документ,
        |	втТаблицаРасшифровка.ЛицевойСчет,
        |	втТаблицаРасшифровка.Услуга,
        |	втТаблицаРасшифровка.ВидУслуги,
        |	втТаблицаРасшифровка.ВидУслугиПредставление,
        |	втТаблицаРасшифровка.МесяцНачисления,
        |	втТаблицаРасшифровка.СуммироватьКоличество,
        |	втТаблицаРасшифровка.СуммироватьТариф,
        |	ВЫБОР
        |		КОГДА ЕСТЬNULL(врОтражатьПениПоСтавкеБезНДС.ОтражатьПениПоСтавкеБезНДС, ИСТИНА) = ИСТИНА
        |			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСтавокНДС.БезНДС)
        |		ИНАЧЕ втТаблицаРасшифровка.ВидСтавкиНДС
        |	КОНЕЦ,
        |	втТаблицаРасшифровка.Пени,
        |	ИСТИНА,
        |	ЛОЖЬ,
        |	втТаблицаРасшифровка.ПризнакПредметаРасчета,
        |	&ПризнакСпособаРасчетаВПлатежахНаОказаниеУслугВЧекахРО,
        |	втТаблицаРасшифровка.ЭтоДобровольноеСтрахование
        |ИЗ
        |	втТаблицаРасшифровка КАК втТаблицаРасшифровка
        |		ЛЕВОЕ СОЕДИНЕНИЕ врОтражатьПениПоСтавкеБезНДС КАК врОтражатьПениПоСтавкеБезНДС
        |		ПО втТаблицаРасшифровка.Организация = врОтражатьПениПоСтавкеБезНДС.Организация
        |ГДЕ
        |	втТаблицаРасшифровка.Пени > 0
        |
        |ОБЪЕДИНИТЬ ВСЕ
        |
        |ВЫБРАТЬ
        |	втТаблицаРасшифровка.Документ,
        |	втТаблицаРасшифровка.ЛицевойСчет,
        |	втТаблицаРасшифровка.Услуга,
        |	втТаблицаРасшифровка.ВидУслуги,
        |	втТаблицаРасшифровка.ВидУслугиПредставление,
        |	втТаблицаРасшифровка.МесяцНачисления,
        |	втТаблицаРасшифровка.СуммироватьКоличество,
        |	втТаблицаРасшифровка.СуммироватьТариф,
        |	втТаблицаРасшифровка.ВидСтавкиНДС,
        |	ВЫБОР
        |		КОГДА втТаблицаРасшифровка.ПереплатаПоНачислениям > 0
        |			ТОГДА втТаблицаРасшифровка.ПереплатаПоНачислениям
        |		ИНАЧЕ втТаблицаРасшифровка.Сумма
        |	КОНЕЦ,
        |	ЛОЖЬ,
        |	ИСТИНА,
        |	ЗНАЧЕНИЕ(Перечисление.ПризнакиПредметаРасчета.ПлатежВыплата),
        |	ЗНАЧЕНИЕ(Перечисление.ПризнакиСпособаРасчета.ПредоплатаПолная),
        |	втТаблицаРасшифровка.ЭтоДобровольноеСтрахование
        |ИЗ
        |	втТаблицаРасшифровка КАК втТаблицаРасшифровка
        |ГДЕ
        |	(втТаблицаРасшифровка.ПереплатаПоНачислениям > 0
        |			ИЛИ втТаблицаРасшифровка.Сумма > 0)
        |	И втТаблицаРасшифровка.ЭтоДобровольноеСтрахование
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |УНИЧТОЖИТЬ втТаблицаРасшифровка
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |УНИЧТОЖИТЬ врОтражатьПениПоСтавкеБезНДС";

    // Если чек печатается с расшифровкой, то получим:
    //  1. Суммы в разрезе услуг и ставок НДС;
    //  2. Информацию об агентских услугах (услуги, который оказываются сторонней организацией, а не самой УК).
    //     Агентские услуги в чеке выводятся по особенному: дополнительно выводятся реквизиты поставщика услуги.
    //  3. Если ведется учет по добровольному страхованию, информацию об услугах добровольного страхования.
    //     Услуги добровольного страхования в чеке выводятся по особенному: дополнительно выводятся реквизиты поставщика услуги.
    //     Услуга по добровольному страхованию не считается агентской услугой,
    //     но по ней собирается информация похожим образом:
    //      - Если нашли поставщика услуги, используем его реквизиты
    //      - Иначе используем реквизиты организации (УК).
    // Если без расшифровки, то получим суммы только в разрезе ставок НДС.

    Если СтруктураПараметров.ПечататьЧекиСРасшифровкойПоУслугам Тогда

        Запрос.Текст = Запрос.Текст + "
            |;
            |
            |////////////////////////////////////////////////////////////////////////////////";

        Если СтруктураПараметров.РассчитыватьКоличествоИЦенуВПлатежах Тогда
            Запрос.Текст = Запрос.Текст + "
                |ВЫБРАТЬ РАЗЛИЧНЫЕ
                |	втДанныеПоВидамОплатБезТарифа.Документ КАК Документ,
                |	КВП_РегистрацияОплаты.Дата КАК Дата,
                |	втДанныеПоВидамОплатБезТарифа.ЛицевойСчет КАК ЛицевойСчет,
                |	втДанныеПоВидамОплатБезТарифа.Услуга КАК Услуга,
                |	втДанныеПоВидамОплатБезТарифа.МесяцНачисления КАК МесяцНачисления,
                |	втДанныеПоВидамОплатБезТарифа.ЭтоПени КАК ЭтоПени
                |ПОМЕСТИТЬ втТаблицаРасшифровкаДляТарифаИОбъема
                |ИЗ
                |	втДанныеПоВидамОплатБезТарифа КАК втДанныеПоВидамОплатБезТарифа
                |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КВП_РегистрацияОплаты КАК КВП_РегистрацияОплаты
                |		ПО втДанныеПоВидамОплатБезТарифа.Документ = КВП_РегистрацияОплаты.Ссылка
                |;
                |
                |////////////////////////////////////////////////////////////////////////////////
                |" + УПЖКХ_ОбщегоНазначенияСервер.ИнформацияОТарифахПоПлатежамЧека_ТекстЗапроса() + "
                |;
                |
                |////////////////////////////////////////////////////////////////////////////////
                |УНИЧТОЖИТЬ втТаблицаРасшифровкаДляТарифаИОбъема
                |;
                |
                |////////////////////////////////////////////////////////////////////////////////
                |ВЫБРАТЬ
                |	втДанныеПоВидамОплатБезТарифа.Документ КАК Документ,
                |	втДанныеПоВидамОплатБезТарифа.ЛицевойСчет КАК ЛицевойСчет,
                |	втДанныеПоВидамОплатБезТарифа.Услуга КАК Услуга,
                |	втДанныеПоВидамОплатБезТарифа.ВидУслуги КАК ВидУслуги,
                |	втДанныеПоВидамОплатБезТарифа.ВидУслугиПредставление КАК ВидУслугиПредставление,
                |	втДанныеПоВидамОплатБезТарифа.СуммироватьКоличество КАК СуммироватьКоличество,
                |	втДанныеПоВидамОплатБезТарифа.СуммироватьТариф КАК СуммироватьТариф,
                |	втДанныеПоВидамОплатБезТарифа.ВидСтавкиНДС КАК ВидСтавкиНДС,
                |	втДанныеПоВидамОплатБезТарифа.Сумма КАК Сумма,
                |	ЕСТЬNULL(втТариф.Тариф, 0) КАК Тариф,
                |	втДанныеПоВидамОплатБезТарифа.ЭтоПени КАК ЭтоПени,
                |	втДанныеПоВидамОплатБезТарифа.ЭтоАванс КАК ЭтоАванс,
                |	втДанныеПоВидамОплатБезТарифа.ПризнакПредметаРасчета КАК ПризнакПредметаРасчета,
                |	втДанныеПоВидамОплатБезТарифа.ПризнакСпособаРасчета КАК ПризнакСпособаРасчета,
                |	втДанныеПоВидамОплатБезТарифа.МесяцНачисления КАК МесяцНачисления,
                |	втДанныеПоВидамОплатБезТарифа.ЭтоДобровольноеСтрахование КАК ЭтоДобровольноеСтрахование
                |ПОМЕСТИТЬ втДанныеПоВидамОплат
                |ИЗ
                |	втДанныеПоВидамОплатБезТарифа КАК втДанныеПоВидамОплатБезТарифа
                |		ЛЕВОЕ СОЕДИНЕНИЕ втТариф КАК втТариф
                |		ПО втДанныеПоВидамОплатБезТарифа.Документ = втТариф.Документ
                |			И втДанныеПоВидамОплатБезТарифа.ЛицевойСчет = втТариф.ЛицевойСчет
                |			И втДанныеПоВидамОплатБезТарифа.Услуга = втТариф.Услуга
                |			И втДанныеПоВидамОплатБезТарифа.МесяцНачисления = втТариф.МесяцНачисления
                |;";
        Иначе
            Запрос.Текст = Запрос.Текст + "
                |ВЫБРАТЬ
                |	втДанныеПоВидамОплатБезТарифа.Документ КАК Документ,
                |	втДанныеПоВидамОплатБезТарифа.ЛицевойСчет КАК ЛицевойСчет,
                |	втДанныеПоВидамОплатБезТарифа.Услуга КАК Услуга,
                |	втДанныеПоВидамОплатБезТарифа.ВидУслуги КАК ВидУслуги,
                |	втДанныеПоВидамОплатБезТарифа.ВидУслугиПредставление КАК ВидУслугиПредставление,
                |	втДанныеПоВидамОплатБезТарифа.СуммироватьКоличество КАК СуммироватьКоличество,
                |	втДанныеПоВидамОплатБезТарифа.СуммироватьТариф КАК СуммироватьТариф,
                |	втДанныеПоВидамОплатБезТарифа.ВидСтавкиНДС КАК ВидСтавкиНДС,
                |	втДанныеПоВидамОплатБезТарифа.Сумма КАК Сумма,
                |	втДанныеПоВидамОплатБезТарифа.Сумма КАК Тариф,
                |	втДанныеПоВидамОплатБезТарифа.ЭтоПени КАК ЭтоПени,
                |	втДанныеПоВидамОплатБезТарифа.ЭтоАванс КАК ЭтоАванс,
                |	втДанныеПоВидамОплатБезТарифа.ПризнакПредметаРасчета КАК ПризнакПредметаРасчета,
                |	втДанныеПоВидамОплатБезТарифа.ПризнакСпособаРасчета КАК ПризнакСпособаРасчета,
                |	втДанныеПоВидамОплатБезТарифа.МесяцНачисления КАК МесяцНачисления,
                |	втДанныеПоВидамОплатБезТарифа.ЭтоДобровольноеСтрахование КАК ЭтоДобровольноеСтрахование
                |ПОМЕСТИТЬ втДанныеПоВидамОплат
                |ИЗ
                |	втДанныеПоВидамОплатБезТарифа КАК втДанныеПоВидамОплатБезТарифа
                |;";
        КонецЕсли;

        Запрос.Текст = Запрос.Текст + "
            |
            |////////////////////////////////////////////////////////////////////////////////
            |УНИЧТОЖИТЬ втДанныеПоВидамОплатБезТарифа
            |;
            |
            |////////////////////////////////////////////////////////////////////////////////
            |ВЫБРАТЬ
            |	втДанныеПоВидамОплат.Документ КАК Документ,
            |	втДанныеПоВидамОплат.ЛицевойСчет КАК ЛицевойСчет,
            |	втДанныеПоВидамОплат.ВидУслуги КАК ВидУслуги,
            |	втДанныеПоВидамОплат.ВидУслугиПредставление КАК ВидУслугиПредставление,
            |	втДанныеПоВидамОплат.ВидСтавкиНДС КАК ВидСтавкиНДС,
            |	СУММА(втДанныеПоВидамОплат.Сумма) КАК Сумма,
            |	МАКСИМУМ(втДанныеПоВидамОплат.Тариф) КАК Тариф,
            |	втДанныеПоВидамОплат.ЭтоПени КАК ЭтоПени,
            |	втДанныеПоВидамОплат.ЭтоАванс КАК ЭтоАванс,
            |	втДанныеПоВидамОплат.ПризнакПредметаРасчета КАК ПризнакПредметаРасчета,
            |	втДанныеПоВидамОплат.ПризнакСпособаРасчета КАК ПризнакСпособаРасчета,
            |	втДанныеПоВидамОплат.МесяцНачисления КАК МесяцНачисления,
            |	втДанныеПоВидамОплат.ЭтоДобровольноеСтрахование КАК ЭтоДобровольноеСтрахование
            |ПОМЕСТИТЬ врРасшифровкаОплатДляЧеков
            |ИЗ
            |	втДанныеПоВидамОплат КАК втДанныеПоВидамОплат
            |ГДЕ
            |	втДанныеПоВидамОплат.СуммироватьКоличество
            |
            |СГРУППИРОВАТЬ ПО
            |	втДанныеПоВидамОплат.Документ,
            |	втДанныеПоВидамОплат.ЛицевойСчет,
            |	втДанныеПоВидамОплат.ВидУслуги,
            |	втДанныеПоВидамОплат.ВидУслугиПредставление,
            |	втДанныеПоВидамОплат.ВидСтавкиНДС,
            |	втДанныеПоВидамОплат.ЭтоПени,
            |	втДанныеПоВидамОплат.ЭтоАванс,
            |	втДанныеПоВидамОплат.ПризнакПредметаРасчета,
            |	втДанныеПоВидамОплат.ПризнакСпособаРасчета,
            |	втДанныеПоВидамОплат.МесяцНачисления,
            |	втДанныеПоВидамОплат.ЭтоДобровольноеСтрахование
            |
            |ОБЪЕДИНИТЬ ВСЕ
            |
            |ВЫБРАТЬ
            |	втДанныеПоВидамОплат.Документ,
            |	втДанныеПоВидамОплат.ЛицевойСчет,
            |	втДанныеПоВидамОплат.ВидУслуги,
            |	втДанныеПоВидамОплат.ВидУслугиПредставление,
            |	втДанныеПоВидамОплат.ВидСтавкиНДС,
            |	СУММА(втДанныеПоВидамОплат.Сумма),
            |	СУММА(втДанныеПоВидамОплат.Тариф),
            |	втДанныеПоВидамОплат.ЭтоПени,
            |	втДанныеПоВидамОплат.ЭтоАванс,
            |	втДанныеПоВидамОплат.ПризнакПредметаРасчета,
            |	втДанныеПоВидамОплат.ПризнакСпособаРасчета,
            |	втДанныеПоВидамОплат.МесяцНачисления,
            |	втДанныеПоВидамОплат.ЭтоДобровольноеСтрахование
            |ИЗ
            |	втДанныеПоВидамОплат КАК втДанныеПоВидамОплат
            |ГДЕ
            |	втДанныеПоВидамОплат.СуммироватьТариф
            |
            |СГРУППИРОВАТЬ ПО
            |	втДанныеПоВидамОплат.Документ,
            |	втДанныеПоВидамОплат.ЛицевойСчет,
            |	втДанныеПоВидамОплат.ВидУслуги,
            |	втДанныеПоВидамОплат.ВидСтавкиНДС,
            |	втДанныеПоВидамОплат.ЭтоПени,
            |	втДанныеПоВидамОплат.ЭтоАванс,
            |	втДанныеПоВидамОплат.ПризнакПредметаРасчета,
            |	втДанныеПоВидамОплат.ПризнакСпособаРасчета,
            |	втДанныеПоВидамОплат.ВидУслугиПредставление,
            |	втДанныеПоВидамОплат.МесяцНачисления,
            |	втДанныеПоВидамОплат.ЭтоДобровольноеСтрахование
            |
            |ОБЪЕДИНИТЬ ВСЕ
            |
            |ВЫБРАТЬ
            |	втДанныеПоВидамОплат.Документ,
            |	втДанныеПоВидамОплат.ЛицевойСчет,
            |	втДанныеПоВидамОплат.ВидУслуги,
            |	втДанныеПоВидамОплат.ВидУслугиПредставление,
            |	втДанныеПоВидамОплат.ВидСтавкиНДС,
            |	СУММА(втДанныеПоВидамОплат.Сумма),
            |	МАКСИМУМ(втДанныеПоВидамОплат.Тариф),
            |	втДанныеПоВидамОплат.ЭтоПени,
            |	втДанныеПоВидамОплат.ЭтоАванс,
            |	втДанныеПоВидамОплат.ПризнакПредметаРасчета,
            |	втДанныеПоВидамОплат.ПризнакСпособаРасчета,
            |	втДанныеПоВидамОплат.МесяцНачисления,
            |	втДанныеПоВидамОплат.ЭтоДобровольноеСтрахование
            |ИЗ
            |	втДанныеПоВидамОплат КАК втДанныеПоВидамОплат
            |ГДЕ
            |	НЕ втДанныеПоВидамОплат.СуммироватьКоличество
            |	И НЕ втДанныеПоВидамОплат.СуммироватьТариф
            |
            |СГРУППИРОВАТЬ ПО
            |	втДанныеПоВидамОплат.Документ,
            |	втДанныеПоВидамОплат.ЛицевойСчет,
            |	втДанныеПоВидамОплат.Услуга,
            |	втДанныеПоВидамОплат.ВидУслуги,
            |	втДанныеПоВидамОплат.ВидСтавкиНДС,
            |	втДанныеПоВидамОплат.ЭтоПени,
            |	втДанныеПоВидамОплат.ЭтоАванс,
            |	втДанныеПоВидамОплат.ПризнакПредметаРасчета,
            |	втДанныеПоВидамОплат.ПризнакСпособаРасчета,
            |	втДанныеПоВидамОплат.ВидУслугиПредставление,
            |	втДанныеПоВидамОплат.МесяцНачисления,
            |	втДанныеПоВидамОплат.ЭтоДобровольноеСтрахование";

        Если СтруктураПараметров.УказыватьВЧекеПризнакАгентаВПлатежах Тогда

            Запрос.Текст = Запрос.Текст + "
                |;
                |
                |////////////////////////////////////////////////////////////////////////////////
                |" +
                "ВЫБРАТЬ РАЗЛИЧНЫЕ
                |	втДанныеПоВидамОплат.Документ КАК Документ,
                |	КВП_РегистрацияОплаты.Дата КАК Дата,
                |	КВП_РегистрацияОплаты.Организация КАК Организация,
                |	втДанныеПоВидамОплат.ЛицевойСчет КАК ЛицевойСчет,
                |	КВП_ЛицевыеСчета.Адрес.Подъезд КАК Подъезд,
                |	КВП_ЛицевыеСчета.Адрес.Владелец КАК Здание,
                |	втДанныеПоВидамОплат.ВидУслуги КАК ВидУслуги,
                |	втДанныеПоВидамОплат.ЭтоДобровольноеСтрахование КАК ЭтоДобровольноеСтрахование
                |ПОМЕСТИТЬ врИнформацияОбУслугах
                |ИЗ
                |	втДанныеПоВидамОплат КАК втДанныеПоВидамОплат
                |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КВП_РегистрацияОплаты КАК КВП_РегистрацияОплаты
                |		ПО втДанныеПоВидамОплат.Документ = КВП_РегистрацияОплаты.Ссылка
                |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КВП_ЛицевыеСчета КАК КВП_ЛицевыеСчета
                |		ПО втДанныеПоВидамОплат.ЛицевойСчет = КВП_ЛицевыеСчета.Ссылка
                |;
                |
                |////////////////////////////////////////////////////////////////////////////////
                |УНИЧТОЖИТЬ втДанныеПоВидамОплат" + "
                |;
                |
                |////////////////////////////////////////////////////////////////////////////////
                |" + УПЖКХ_ОбщегоНазначенияСервер.ИнформацияОбАгентскихУслугахДляЧека_ТекстЗапроса() + "
                |;
                |
                |////////////////////////////////////////////////////////////////////////////////
                |" +
                "УНИЧТОЖИТЬ врИнформацияОбУслугах
                |;
                |
                |////////////////////////////////////////////////////////////////////////////////
                |ВЫБРАТЬ
                |	врРасшифровкаОплатДляЧеков.Документ КАК Документ,
                |	врРасшифровкаОплатДляЧеков.ЛицевойСчет КАК ЛицевойСчет,
                |	врРасшифровкаОплатДляЧеков.ВидУслугиПредставление КАК ВидУслугиПредставление,
                |	врРасшифровкаОплатДляЧеков.ВидСтавкиНДС КАК ВидСтавкиНДС,
                |	врРасшифровкаОплатДляЧеков.Сумма КАК Сумма,
                |	врРасшифровкаОплатДляЧеков.ЭтоПени КАК ЭтоПени,
                |	врРасшифровкаОплатДляЧеков.ЭтоАванс КАК ЭтоАванс,
                |	врРасшифровкаОплатДляЧеков.ПризнакПредметаРасчета КАК ПризнакПредметаРасчета,
                |	врРасшифровкаОплатДляЧеков.ПризнакСпособаРасчета КАК ПризнакСпособаРасчета,
                |	врРасшифровкаОплатДляЧеков.Тариф КАК Тариф,
                |	врРасшифровкаОплатДляЧеков.ЭтоДобровольноеСтрахование КАК ЭтоДобровольноеСтрахование,
                |	ВЫБОР
                |		КОГДА врПоставщикиАгентскихУслуг.ЭтоАгентскаяУслуга
                |				ИЛИ врРасшифровкаОплатДляЧеков.ЭтоДобровольноеСтрахование
                |			ТОГДА ИСТИНА
                |		ИНАЧЕ ЛОЖЬ
                |	КОНЕЦ КАК УказыватьВЧекеПризнакАгентаВПлатежах,
                |	ПРЕДСТАВЛЕНИЕ(врПоставщикиАгентскихУслуг.ПоставщикУслуги) КАК ПоставщикУслугиПредставление,
                |	врПоставщикиАгентскихУслуг.ИННПоставщикаУслуги КАК ИННПоставщикаУслуги,
                |	врРасшифровкаОплатДляЧеков.МесяцНачисления КАК МесяцНачисления,
                |	врПоставщикиАгентскихУслуг.ТелефонПоставщикаУслуги КАК ТелефонПоставщикаУслуги
                |ИЗ
                |	врРасшифровкаОплатДляЧеков КАК врРасшифровкаОплатДляЧеков
                |		ЛЕВОЕ СОЕДИНЕНИЕ врПоставщикиАгентскихУслуг КАК врПоставщикиАгентскихУслуг
                |		ПО врРасшифровкаОплатДляЧеков.Документ = врПоставщикиАгентскихУслуг.Документ
                |			И врРасшифровкаОплатДляЧеков.ЛицевойСчет = врПоставщикиАгентскихУслуг.ЛицевойСчет
                |			И врРасшифровкаОплатДляЧеков.ВидУслуги = врПоставщикиАгентскихУслуг.ВидУслуги";

        Иначе

            Запрос.Текст = Запрос.Текст + "
                |;
                |
                |////////////////////////////////////////////////////////////////////////////////
                |" +
                "ВЫБРАТЬ
                |	врРасшифровкаОплатДляЧеков.Документ КАК Документ,
                |	врРасшифровкаОплатДляЧеков.ЛицевойСчет КАК ЛицевойСчет,
                |	врРасшифровкаОплатДляЧеков.ВидУслугиПредставление КАК ВидУслугиПредставление,
                |	врРасшифровкаОплатДляЧеков.ВидСтавкиНДС КАК ВидСтавкиНДС,
                |	врРасшифровкаОплатДляЧеков.Сумма КАК Сумма,
                |	врРасшифровкаОплатДляЧеков.ЭтоПени КАК ЭтоПени,
                |	врРасшифровкаОплатДляЧеков.ЭтоАванс КАК ЭтоАванс,
                |	врРасшифровкаОплатДляЧеков.ПризнакПредметаРасчета КАК ПризнакПредметаРасчета,
                |	врРасшифровкаОплатДляЧеков.ПризнакСпособаРасчета КАК ПризнакСпособаРасчета,
                |	врРасшифровкаОплатДляЧеков.Тариф КАК Тариф,
                |	врРасшифровкаОплатДляЧеков.ЭтоДобровольноеСтрахование КАК ЭтоДобровольноеСтрахование,
                |	врРасшифровкаОплатДляЧеков.МесяцНачисления,
                |	ЛОЖЬ КАК УказыватьВЧекеПризнакАгентаВПлатежах
                |ИЗ
                |	врРасшифровкаОплатДляЧеков КАК врРасшифровкаОплатДляЧеков";

        КонецЕсли;

    Иначе

        Запрос.Текст = Запрос.Текст + "
            |;
            |
            |////////////////////////////////////////////////////////////////////////////////
            |" +
            "ВЫБРАТЬ
            |	втДанныеПоВидамОплатБезТарифа.Документ КАК Документ,
            |	втДанныеПоВидамОплатБезТарифа.ЛицевойСчет КАК ЛицевойСчет,
            |	втДанныеПоВидамОплатБезТарифа.ВидСтавкиНДС КАК ВидСтавкиНДС,
            |	СУММА(втДанныеПоВидамОплатБезТарифа.Сумма) КАК Сумма,
            |	втДанныеПоВидамОплатБезТарифа.ЭтоПени КАК ЭтоПени,
            |	втДанныеПоВидамОплатБезТарифа.ЭтоАванс КАК ЭтоАванс,
            |	втДанныеПоВидамОплатБезТарифа.ПризнакПредметаРасчета КАК ПризнакПредметаРасчета,
            |	втДанныеПоВидамОплатБезТарифа.ПризнакСпособаРасчета КАК ПризнакСпособаРасчета,
            |	втДанныеПоВидамОплатБезТарифа.МесяцНачисления КАК МесяцНачисления,
            |	втДанныеПоВидамОплатБезТарифа.ЭтоДобровольноеСтрахование КАК ЭтоДобровольноеСтрахование
            |ИЗ
            |	втДанныеПоВидамОплатБезТарифа КАК втДанныеПоВидамОплатБезТарифа
            |
            |СГРУППИРОВАТЬ ПО
            |	втДанныеПоВидамОплатБезТарифа.Документ,
            |	втДанныеПоВидамОплатБезТарифа.ЛицевойСчет,
            |	втДанныеПоВидамОплатБезТарифа.ВидСтавкиНДС,
            |	втДанныеПоВидамОплатБезТарифа.ЭтоПени,
            |	втДанныеПоВидамОплатБезТарифа.ЭтоАванс,
            |	втДанныеПоВидамОплатБезТарифа.ПризнакПредметаРасчета,
            |	втДанныеПоВидамОплатБезТарифа.ПризнакСпособаРасчета,
            |	втДанныеПоВидамОплатБезТарифа.МесяцНачисления,
            |	втДанныеПоВидамОплатБезТарифа.ЭтоДобровольноеСтрахование";

    КонецЕсли;

    ТекущаяДата = ТекущаяДатаСеанса();
    СтавкаНДСПеречисление = УПЖКХ_ОбщегоНазначенияСервер.ПолучитьЗначениеОсновнойСтавкиНДСПоУмолчанию(ТекущаяДата);
    НастройкаДобровольногоСтрахования = УПЖКХ_ПараметрыУчетаСервер.ПолучитьНастройкиУчетаНачисленийПоДобровольномуСтрахованию();

    Запрос.УстановитьПараметр("ТекущаяДата",
        ТекущаяДата);
    Запрос.УстановитьПараметр("ТаблицаОбъектов",
        ТаблицаОплатСДопДанными);
    Запрос.УстановитьПараметр("УслугаДобровольногоСтрахования",
        НастройкаДобровольногоСтрахования.УслугаДобровольногоСтрахования);
    Запрос.УстановитьПараметр("ВидСтавкиНДСИзПерсональныхНастроек",
        Перечисления.ВидыСтавокНДС.ВидСтавки(СтавкаНДСПеречисление));
    Запрос.УстановитьПараметр("ВестиУчетНачисленийПоДобровольномуСтрахованию",
        НастройкаДобровольногоСтрахования.ВестиУчетНачисленийПоДобровольномуСтрахованию);
    Запрос.УстановитьПараметр("ПризнакСпособаРасчетаВПлатежахНаОказаниеУслугВЧекахРО",
        СтруктураПараметров.ПризнакСпособаРасчетаВПлатежахНаОказаниеУслугВЧекахРО);
    // #Вставка // Гарант+ Килипенко 24.02.2025 [F00235125] Печать чеков по контрагенту документа ++

    Если СтруктураПараметров.ГП_ФормироватьЧекиЖКХПоКонтрагентуДокумента = Истина Тогда
        ТаблицаОплатСДопДаннымиИЛицевымиСчетами = ТаблицаОплатСДопДанными.Скопировать();
        ТаблицаОплатСДопДаннымиИЛицевымиСчетами.Очистить();

        // Заполнение л/с временной таблицы оплат (используется только в запросе)
        Для Каждого ТекущаяСтрокаОплат Из ТаблицаОплатСДопДанными Цикл
            // Контроль типа документа оплаты
            Если ТипЗнч(ТекущаяСтрокаОплат.Документ) <> Тип("ДокументСсылка.КВП_РегистрацияОплаты") Тогда
                ВызватьИсключение(СтрШаблон("Тип документа ""%1"" не поддерживается для формирования чеков по контрагентам.",
                        ТипЗнч(ТекущаяСтрокаОплат.Документ)));
            КонецЕсли;

            ДанныеДокументаРаспределения =
                ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТекущаяСтрокаОплат.Документ, Новый Структура("ЛицевыеСчета"));
            ДанныеДокументаРаспределения.ЛицевыеСчета =
                ОбщегоНазначения.ВыгрузитьКолонку(ДанныеДокументаРаспределения.ЛицевыеСчета.Выгрузить(), "Объект", Истина);
            Для Каждого ТекущийЛицевойСчет Из ДанныеДокументаРаспределения.ЛицевыеСчета Цикл
                НоваяСтрокаОплаты = ТаблицаОплатСДопДаннымиИЛицевымиСчетами.Добавить();
                ЗаполнитьЗначенияСвойств(НоваяСтрокаОплаты, ТекущаяСтрокаОплат);
                НоваяСтрокаОплаты.ЛицевойСчет = ТекущийЛицевойСчет;
            КонецЦикла;
        КонецЦикла;

        Запрос.УстановитьПараметр("ТаблицаОбъектов", ТаблицаОплатСДопДаннымиИЛицевымиСчетами);
    КонецЕсли;

    // #КонецВставки // Гарант+ Килипенко 24.02.2025 [F00235125] Печать чеков по контрагенту документа --

    РасшифровкаОплатДляЧеков = Запрос.Выполнить().Выгрузить();

    // Определяем, что версия ФФД больше или равна 1.0.5.
    // Пример определения признака см. в типовом ОМ ПечатьФискальныхДокументов.ПараметрыОперацииФискализацииЧека.
    ВерсияФормата = СтруктураПараметров.ВерсииФорматовФД.ВерсияФД;

    ЭтоВерсия105 = ЗначениеЗаполнено(ВерсияФормата)
        И НЕ (УПЖКХ_ТиповыеМетодыКлиентСервер.СравнитьВерсии("1.0.5.0", ВерсияФормата) > 0);

    РасшифровкаОплатДляЧеков.Колонки.Добавить("СтавкаНДС");
    // #Вставка // Гарант+ Килипенко 24.02.2025 [F00235125] Печать чеков по контрагенту документа ++

    КэшКонтрагентыДокументов = Новый Соответствие;
    Если СтруктураПараметров.ГП_ФормироватьЧекиЖКХПоКонтрагентуДокумента = Истина Тогда
        ГП_РегистрацияОплатыСлужебный.ДополнитьТаблицуКолонкойКонтрагентДокументаОплаты(РасшифровкаОплатДляЧеков);
    КонецЕсли;

    // #КонецВставки // Гарант+ Килипенко 24.02.2025 [F00235125] Печать чеков по контрагенту документа --

    Для Каждого ТекущаяСтрока Из РасшифровкаОплатДляЧеков Цикл

        Если ТекущаяСтрока.ЭтоПени И СтруктураПараметров.ПечататьЧекиСРасшифровкойПоУслугам Тогда
            ТекущаяСтрока.ВидУслугиПредставление = "" + ТекущаяСтрока.ВидУслугиПредставление + " (пени)";
        КонецЕсли;

        // Расчетные ставки НДС применяются только на версиях ФФД 1.0.5 и выше,
        // для платежей по авансу за услуги, не относящимся к услугам добровольного страхования.
        ПрименяютсяРасчетныеСтавкиНДС = ЭтоВерсия105
            И ТекущаяСтрока.ЭтоАванс
            И НЕ ТекущаяСтрока.ЭтоДобровольноеСтрахование;

        СтавкаНДССУчетомУСН = УПЖКХ_НалоговыйУчетУСН.СтавкаНДС(ТекущаяСтрока.МесяцНачисления,
                ТекущаяСтрока.Документ.Организация,
                ТекущаяСтрока.ВидСтавкиНДС);
        ТекущаяСтрока.СтавкаНДС = УПЖКХ_ОбщегоНазначенияСервер.ОпределитьСтавкуНДСВСтрокеОплатЧека(ТекущаяСтрока.ВидСтавкиНДС,
                ТекущаяДата,
                ПрименяютсяРасчетныеСтавкиНДС,
                СтавкаНДССУчетомУСН);
        // #Вставка // Гарант+ Килипенко 24.02.2025 [F00235125] Печать чеков по контрагенту документа ++

        Если СтруктураПараметров.ГП_ФормироватьЧекиЖКХПоКонтрагентуДокумента = Истина Тогда
            Если ТипЗнч(ТекущаяСтрока.Документ) = Тип("ДокументСсылка.КВП_РегистрацияОплаты") Тогда
                Если КэшКонтрагентыДокументов.Получить(ТекущаяСтрока.Документ) = Неопределено Тогда
                    ТекущийКонтрагентДокументаОплаты = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущаяСтрока.Документ, "Контрагент");
                    КэшКонтрагентыДокументов.Вставить(ТекущаяСтрока.Документ, ТекущийКонтрагентДокументаОплаты);
                КонецЕсли;

                ТекущийКонтрагентДокументаОплаты = КэшКонтрагентыДокументов.Получить(ТекущаяСтрока.Документ);
                ТекущаяСтрока.ГП_КонтрагентДокументаОплаты = ТекущийКонтрагентДокументаОплаты;
            КонецЕсли;
        КонецЕсли;

        // #КонецВставки // Гарант+ Килипенко 24.02.2025 [F00235125] Печать чеков по контрагенту документа --

    КонецЦикла;

    // Колонка не используется в дальнейших операциях печати чека.
    РасшифровкаОплатДляЧеков.Колонки.Удалить("ВидСтавкиНДС");

    // Если выполняется сбор данных без разреза по услугам, то после
    // определения ставки НДС таблицу нужно повторно группировать,
    // чтобы исключить разрез по признакам ЭтоАванс и ЭтоДобровольноеСтрахование.
    Если НЕ СтруктураПараметров.ПечататьЧекиСРасшифровкойПоУслугам Тогда
        КолонкиГруппировки = "Документ, ЛицевойСчет, ЭтоПени,"
            + " ПризнакПредметаРасчета, ПризнакСпособаРасчета, СтавкаНДС";
        // #Вставка // Гарант+ Килипенко 24.02.2025 [F00235125] Печать чеков по контрагенту документа ++

        Если СтруктураПараметров.ГП_ФормироватьЧекиЖКХПоКонтрагентуДокумента = Истина Тогда
            КолонкиГруппировки = КолонкиГруппировки + "ГП_КонтрагентДокументаОплаты";
        КонецЕсли;

        // #КонецВставки // Гарант+ Килипенко 24.02.2025 [F00235125] Печать чеков по контрагенту документа --
        РасшифровкаОплатДляЧеков.Свернуть(КолонкиГруппировки, "Сумма");
    КонецЕсли;
    // #Вставка // Гарант+ Килипенко 24.02.2025 [F00235125] Печать чеков по контрагенту документа ++

    Если СтруктураПараметров.ГП_ФормироватьЧекиЖКХПоКонтрагентуДокумента = Истина Тогда
        ГП_РегистрацияОплатыСлужебный.СвернутьТаблицуРасшифровкаОплатДляЧековПоТарифамУслуг(
            РасшифровкаОплатДляЧеков, СтруктураПараметров);
    КонецЕсли;

    // #КонецВставки // Гарант+ Килипенко 24.02.2025 [F00235125] Печать чеков по контрагенту документа --

    Возврат РасшифровкаОплатДляЧеков;

КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс
// Гарант+ Килипенко 11.02.2025 [F00232767] Выгрузка в ОФД доработка --

// Гарант+ Килипенко 11.02.2025 [F00232767] Выгрузка в ОФД доработка ++
#Область СлужебныеПроцедурыИФункции

Функция ПолучитьТекстЗапросаОбщейТаблицыРасшифровкиОплатДляОФД()
    ТекстЗапроса =
        "ВЫБРАТЬ
        |	РегистрацияОплатыРасшифровкаПлатежа.Ссылка КАК Ссылка,
        |	РегистрацияОплатыРасшифровкаПлатежа.Объект КАК Объект,
        |	ЕСТЬNULL(РегистрацияОплатыРасшифровкаПлатежа.Сумма, 0) КАК Сумма,
        |	РегистрацияОплатыРасшифровкаПлатежа.Услуга КАК Услуга,
        |	РегистрацияОплатыРасшифровкаПлатежа.МесяцНачисления КАК МесяцНачисления,
        |	ЕСТЬNULL(РегистрацияОплатыРасшифровкаПлатежа.Пени, 0) КАК Пени,
        |	ЕСТЬNULL(РегистрацияОплатыРасшифровкаПлатежа.АвансСумма, 0) КАК АвансСумма,
        |	ЕСТЬNULL(РегистрацияОплатыРасшифровкаПлатежа.ДолгСумма, 0) КАК ДолгСумма,
        |	ВЫБОР
        |		КОГДА ЕСТЬNULL(РегистрацияОплатыРасшифровкаПлатежа.ДолгСумма, 0) = 0
        |				И ЕСТЬNULL(РегистрацияОплатыРасшифровкаПлатежа.Сумма, 0) > 0
        |			ТОГДА ""Да""
        |		ИНАЧЕ ""Нет""
        |	КОНЕЦ КАК ЭтоАванс
        |ПОМЕСТИТЬ ВТ_РегистрацияОплатыРасшифровкаПлатежа_Подготовка
        |ИЗ
        |	Документ.КВП_РегистрацияОплаты.РасшифровкаПлатежа КАК РегистрацияОплатыРасшифровкаПлатежа
        |ГДЕ
        |	РегистрацияОплатыРасшифровкаПлатежа.Ссылка.Дата >= &НачалоПериода
        |	И РегистрацияОплатыРасшифровкаПлатежа.Ссылка.Дата <= &КонецПериода
        |	И РегистрацияОплатыРасшифровкаПлатежа.Ссылка.Проведен = ИСТИНА
        |	И РегистрацияОплатыРасшифровкаПлатежа.Ссылка.Организация = &Организация
        |	И РегистрацияОплатыРасшифровкаПлатежа.Ссылка.ВидОперации В(&ВидыОпераций)
        |	И &ДополнительныеУсловияОтбора
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	РегистрацияОплатыЛицевыеСчета.Ссылка КАК Ссылка,
        |	РегистрацияОплатыЛицевыеСчета.Объект КАК Объект,
        |	МАКСИМУМ(РегистрацияОплатыЛицевыеСчета.ДокументОплаты) КАК ДокументОплаты
        |ПОМЕСТИТЬ ВТ_РегистрацияОплатыЛицевыеСчета
        |ИЗ
        |	Документ.КВП_РегистрацияОплаты.ЛицевыеСчета КАК РегистрацияОплатыЛицевыеСчета
        |ГДЕ
        |	(РегистрацияОплатыЛицевыеСчета.Ссылка, РегистрацияОплатыЛицевыеСчета.Объект) В
        |			(ВЫБРАТЬ РАЗЛИЧНЫЕ
        |				Т.Ссылка,
        |				Т.Объект
        |			ИЗ
        |				ВТ_РегистрацияОплатыРасшифровкаПлатежа_Подготовка КАК Т)
        |
        |СГРУППИРОВАТЬ ПО
        |	РегистрацияОплатыЛицевыеСчета.Ссылка,
        |	РегистрацияОплатыЛицевыеСчета.Объект
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	ВТ_РегистрацияОплатыРасшифровкаПлатежа_Подготовка.*,
        |	ВЫБОР
        |		КОГДА ВТ_РегистрацияОплатыРасшифровкаПлатежа_Подготовка.Ссылка.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
        |			ТОГДА ВТ_РегистрацияОплатыРасшифровкаПлатежа_Подготовка.Ссылка.Контрагент
        |		ИНАЧЕ
        |			ЕСТЬNULL(ВТ_РегистрацияОплатыЛицевыеСчета.ДокументОплаты.ДоговорКонтрагента.Владелец, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
        |	КОНЕЦ КАК Контрагент,
        |	ЕСТЬNULL(ВТ_РегистрацияОплатыЛицевыеСчета.ДокументОплаты.ДоговорКонтрагента, ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)) КАК Договор,
        |	ВТ_РегистрацияОплатыЛицевыеСчета.ДокументОплаты КАК ДокументОплаты
        |ПОМЕСТИТЬ ВТ_РегистрацияОплатыРасшифровкаПлатежа
        |ИЗ
        |	ВТ_РегистрацияОплатыРасшифровкаПлатежа_Подготовка КАК ВТ_РегистрацияОплатыРасшифровкаПлатежа_Подготовка
        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_РегистрацияОплатыЛицевыеСчета КАК ВТ_РегистрацияОплатыЛицевыеСчета
        |		ПО ВТ_РегистрацияОплатыРасшифровкаПлатежа_Подготовка.Ссылка = ВТ_РегистрацияОплатыЛицевыеСчета.Ссылка
        |			И ВТ_РегистрацияОплатыРасшифровкаПлатежа_Подготовка.Объект = ВТ_РегистрацияОплатыЛицевыеСчета.Объект
        |ГДЕ
        |	ВЫБОР
        |		КОГДА ВТ_РегистрацияОплатыРасшифровкаПлатежа_Подготовка.Ссылка.ГП_ОплатаОтПокупателяФизЛица = ИСТИНА ТОГДА ИСТИНА
        |		КОГДА ВТ_РегистрацияОплатыРасшифровкаПлатежа_Подготовка.Ссылка.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
        |			ТОГДА
        |				// Только фих. лица
        |				ВТ_РегистрацияОплатыРасшифровкаПлатежа_Подготовка.Ссылка.Контрагент.ЮридическоеФизическоеЛицо
        |					= ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
        |						И НЕ ВТ_РегистрацияОплатыРасшифровкаПлатежа_Подготовка.Ссылка.Контрагент.ИндивидуальныйПредприниматель
        |						И НЕ ВТ_РегистрацияОплатыРасшифровкаПлатежа_Подготовка.Ссылка.Контрагент.Самозанятый
        |		ИНАЧЕ
        |			ВЫБОР
        |				КОГДА
        |					ЕСТЬNULL(ВТ_РегистрацияОплатыЛицевыеСчета.ДокументОплаты.ДоговорКонтрагента.Владелец, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
        |							= ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) ТОГДА ЛОЖЬ
        |				ИНАЧЕ
        |					// Только фих. лица
        |					ВТ_РегистрацияОплатыЛицевыеСчета.ДокументОплаты.ДоговорКонтрагента.Владелец.ЮридическоеФизическоеЛицо
        |						= ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
        |							И НЕ ВТ_РегистрацияОплатыЛицевыеСчета.ДокументОплаты.ДоговорКонтрагента.Владелец.ИндивидуальныйПредприниматель
        |							И НЕ ВТ_РегистрацияОплатыЛицевыеСчета.ДокументОплаты.ДоговорКонтрагента.Владелец.Самозанятый
        |			КОНЕЦ
        |	КОНЕЦ
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	ВТ_РегистрацияОплатыРасшифровкаПлатежа.Ссылка КАК Ссылка,
        |	ВТ_РегистрацияОплатыРасшифровкаПлатежа.Объект КАК Объект,
        |	ВТ_РегистрацияОплатыРасшифровкаПлатежа.Услуга КАК Услуга,
        |	ВТ_РегистрацияОплатыРасшифровкаПлатежа.ЭтоАванс КАК ЭтоАванс,
        |   ВТ_РегистрацияОплатыРасшифровкаПлатежа.МесяцНачисления КАК МесяцНачисления,
        |	СУММА(ВТ_РегистрацияОплатыРасшифровкаПлатежа.Сумма) КАК Сумма,
        |	СУММА(ВТ_РегистрацияОплатыРасшифровкаПлатежа.Пени) КАК Пени,
        |	СУММА(ВТ_РегистрацияОплатыРасшифровкаПлатежа.АвансСумма) КАК АвансСумма,
        |	СУММА(ВТ_РегистрацияОплатыРасшифровкаПлатежа.ДолгСумма) КАК ДолгСумма,
        |	ВТ_РегистрацияОплатыРасшифровкаПлатежа.ДокументОплаты КАК ДокументОплаты,
        |   ВТ_РегистрацияОплатыРасшифровкаПлатежа.Контрагент КАК Контрагент,
        |   ВТ_РегистрацияОплатыРасшифровкаПлатежа.Договор КАК Договор
        |ИЗ
        |	ВТ_РегистрацияОплатыРасшифровкаПлатежа КАК ВТ_РегистрацияОплатыРасшифровкаПлатежа
        |
        |СГРУППИРОВАТЬ ПО
        |	ВТ_РегистрацияОплатыРасшифровкаПлатежа.Ссылка,
        |	ВТ_РегистрацияОплатыРасшифровкаПлатежа.Объект,
        |	ВТ_РегистрацияОплатыРасшифровкаПлатежа.ЭтоАванс,
        |	ВТ_РегистрацияОплатыРасшифровкаПлатежа.Услуга,
        |	ВТ_РегистрацияОплатыРасшифровкаПлатежа.ДокументОплаты,
        |	ВТ_РегистрацияОплатыРасшифровкаПлатежа.Контрагент,
        |	ВТ_РегистрацияОплатыРасшифровкаПлатежа.Договор,
        |	ВТ_РегистрацияОплатыРасшифровкаПлатежа.МесяцНачисления
        |";

    Возврат ТекстЗапроса;
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
// Гарант+ Килипенко 11.02.2025 [F00232767] Выгрузка в ОФД доработка --
