// Гарант+ Килипенко 11.02.2025 [F00232767] Выгрузка в ОФД доработка ++
#Область ПрограммныйИнтерфейс

// Параметры:
//  НачалоПериода - Дата
//  КонецПериода - Дата
//  ЛицевойСчет - СправочникСсылка.КВП_ЛицевыеСчета, Неопределено
//  Организация - СправочникСсылка.Организации, Неопределено
// Возвращаемое значение:
//  - ТаблицаЗначений
//      * Ссылка - ДокументСсылка.КВП_РегистрацияОплаты
//      * Объект - СправочникСсылка.КВП_ЛицевыеСчета
//      * Контрагент - СправочникСсылка.Контрагенты
//      * Договор - СправочникСсылка.ДоговорыКонтрагентов
//      * Услуга - СправочникСсылка.КВП_Услуги
//      * ДокументОплаты - ДокументСсылка
//      * ЭтоАванс - Строка
//      * МесяцНачисления - Строка
//      * Сумма - Число
//      * Пени - Число
//      * АвансСумма - Число
//      * ДолгСумма - Число
Функция ПолучитьОбщуюТаблицуРасшифровкиОплатДляОФД(
        Знач НачалоПериода, Знач КонецПериода, Знач ЛицевойСчет = Неопределено, Организация = Неопределено) Экспорт

    Организация = ?(Организация = Неопределено, УПЖКХ_ТиповыеМетодыВызовСервера.ПолучитьОсновнуюОрганизацию(), Организация);

    Запрос = Новый Запрос;
    Запрос.Текст = ПолучитьТекстЗапросаОбщейТаблицыРасшифровкиОплатДляОФД();

    ДополнительныеУсловияОтбора = "ИСТИНА";
    Если ЛицевойСчет <> Неопределено И НЕ ЛицевойСчет.Пустая() Тогда
        ДополнительныеУсловияОтбора = ДополнительныеУсловияОтбора + " И РегистрацияОплатыРасшифровкаПлатежа.Объект = &ЛицевойСчет";
        Запрос.УстановитьПараметр("ЛицевойСчет", ЛицевойСчет);
    КонецЕсли;

    Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДополнительныеУсловияОтбора", ДополнительныеУсловияОтбора);

    Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
    Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
    Запрос.УстановитьПараметр("Организация", Организация);
    Запрос.УстановитьПараметр("ВидыОпераций",
        ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Перечисления.КВП_ВидыОперацийРегистрацииОплаты.ПоступлениеНаБанковскийСчет));

    РезультатЗапроса = Запрос.Выполнить();
    РезультатФункции = РезультатЗапроса.Выгрузить();
    Возврат РезультатФункции;
КонецФункции

// Параметры:
//  ТаблицаРасшифровкиОплат - ТаблицаЗначений
Процедура СвернутьОбщуюТаблицуРасшифровкиОплатДляОФД(ТаблицаРасшифровкиОплат) Экспорт
    ТаблицаРасшифровкиОплат.Свернуть("Ссылка, Объект, Услуга, ЭтоАванс", "Сумма, Пени, АвансСумма, ДолгСумма");
КонецПроцедуры

// Параметры:
//  НачалоПериода - Дата
//  КонецПериода - Дата
//  ЛицевойСчет - СправочникСсылка.КВП_ЛицевыеСчета, Неопределено
//  Организация - СправочникСсылка.Организации, Неопределено
// Возвращаемое значение:
//  - ТаблицаЗначений
//      * Ссылка - ДокументСсылка.КВП_РегистрацияОплаты
//      * Объект - СправочникСсылка.КВП_ЛицевыеСчета
//      * Услуга - СправочникСсылка.КВП_Услуги
//      * ЭтоАванс - Строка
//      * МесяцНачисления - Строка
//      * Сумма - Число
//      * Пени - Число
//      * АвансСумма - Число
//      * ДолгСумма - Число
Функция ПолучитьСвернутуюОбщуюТаблицуРасшифровкиОплатДляОФД(
        Знач НачалоПериода, Знач КонецПериода, Знач ЛицевойСчет = Неопределено, Организация = Неопределено) Экспорт

    РезультатФункции = ПолучитьОбщуюТаблицуРасшифровкиОплатДляОФД(НачалоПериода, КонецПериода, ЛицевойСчет, Организация);
    СвернутьОбщуюТаблицуРасшифровкиОплатДляОФД(РезультатФункции);

    Возврат РезультатФункции;
КонецФункции

// Параметры:
//  НачалоПериода - Дата
//  КонецПериода - Дата
//  ЛицевойСчет - СправочникСсылка.КВП_ЛицевыеСчета, Неопределено
//  Организация - СправочникСсылка.Организации, Неопределено
// Возвращаемое значение:
//  - ТаблицаЗначений
//      * Ссылка - ДокументСсылка.КВП_РегистрацияОплаты
//      * Объект - СправочникСсылка.КВП_ЛицевыеСчета
//      * Сумма - Число
Функция ПолучитьТаблицуЛицевыхСчетовРегистрацииОплатДляОФД(
        Знач НачалоПериода, Знач КонецПериода, Знач ЛицевойСчет = Неопределено, Организация = Неопределено) Экспорт

    ТаблицаРасшифровкиОплатДляОФД = ПолучитьСвернутуюОбщуюТаблицуРасшифровкиОплатДляОФД(
        НачалоПериода, КонецПериода, ЛицевойСчет, Организация);

    ПоляГруппировки = "Ссылка, Объект";
    ТаблицаРасшифровкиОплатДляОФД.Свернуть(ПоляГруппировки, "Сумма");
    ТаблицаРасшифровкиОплатДляОФД.Сортировать(ПоляГруппировки);

    Возврат ТаблицаРасшифровкиОплатДляОФД;
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс
// Гарант+ Килипенко 11.02.2025 [F00232767] Выгрузка в ОФД доработка --

// Гарант+ Килипенко 11.02.2025 [F00232767] Выгрузка в ОФД доработка ++
#Область СлужебныеПроцедурыИФункции

Функция ПолучитьТекстЗапросаОбщейТаблицыРасшифровкиОплатДляОФД()
    ТекстЗапроса =
        "ВЫБРАТЬ
        |	РегистрацияОплатыРасшифровкаПлатежа.Ссылка КАК Ссылка,
        |	РегистрацияОплатыРасшифровкаПлатежа.Объект КАК Объект,
        |	ЕСТЬNULL(РегистрацияОплатыРасшифровкаПлатежа.Сумма, 0) КАК Сумма,
        |	РегистрацияОплатыРасшифровкаПлатежа.Услуга КАК Услуга,
        |	РегистрацияОплатыРасшифровкаПлатежа.МесяцНачисления КАК МесяцНачисления,
        |	ЕСТЬNULL(РегистрацияОплатыРасшифровкаПлатежа.Пени, 0) КАК Пени,
        |	ЕСТЬNULL(РегистрацияОплатыРасшифровкаПлатежа.АвансСумма, 0) КАК АвансСумма,
        |	ЕСТЬNULL(РегистрацияОплатыРасшифровкаПлатежа.ДолгСумма, 0) КАК ДолгСумма,
        |	ВЫБОР
        |		КОГДА ЕСТЬNULL(РегистрацияОплатыРасшифровкаПлатежа.ДолгСумма, 0) = 0
        |				И ЕСТЬNULL(РегистрацияОплатыРасшифровкаПлатежа.Сумма, 0) > 0
        |			ТОГДА ""Да""
        |		ИНАЧЕ ""Нет""
        |	КОНЕЦ КАК ЭтоАванс
        |ПОМЕСТИТЬ ВТ_РегистрацияОплатыРасшифровкаПлатежа_Подготовка
        |ИЗ
        |	Документ.КВП_РегистрацияОплаты.РасшифровкаПлатежа КАК РегистрацияОплатыРасшифровкаПлатежа
        |ГДЕ
        |	РегистрацияОплатыРасшифровкаПлатежа.Ссылка.Дата >= &НачалоПериода
        |	И РегистрацияОплатыРасшифровкаПлатежа.Ссылка.Дата <= &КонецПериода
        |	И РегистрацияОплатыРасшифровкаПлатежа.Ссылка.Проведен = ИСТИНА
        |	И РегистрацияОплатыРасшифровкаПлатежа.Ссылка.Организация = &Организация
        |	И РегистрацияОплатыРасшифровкаПлатежа.Ссылка.ВидОперации В(&ВидыОпераций)
        |	И &ДополнительныеУсловияОтбора
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	РегистрацияОплатыЛицевыеСчета.Ссылка КАК Ссылка,
        |	РегистрацияОплатыЛицевыеСчета.Объект КАК Объект,
        |	МАКСИМУМ(РегистрацияОплатыЛицевыеСчета.ДокументОплаты) КАК ДокументОплаты
        |ПОМЕСТИТЬ ВТ_РегистрацияОплатыЛицевыеСчета
        |ИЗ
        |	Документ.КВП_РегистрацияОплаты.ЛицевыеСчета КАК РегистрацияОплатыЛицевыеСчета
        |ГДЕ
        |	(РегистрацияОплатыЛицевыеСчета.Ссылка, РегистрацияОплатыЛицевыеСчета.Объект) В
        |			(ВЫБРАТЬ РАЗЛИЧНЫЕ
        |				Т.Ссылка,
        |				Т.Объект
        |			ИЗ
        |				ВТ_РегистрацияОплатыРасшифровкаПлатежа_Подготовка КАК Т)
        |
        |СГРУППИРОВАТЬ ПО
        |	РегистрацияОплатыЛицевыеСчета.Ссылка,
        |	РегистрацияОплатыЛицевыеСчета.Объект
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	ВТ_РегистрацияОплатыРасшифровкаПлатежа_Подготовка.*,
        |	ВЫБОР
        |		КОГДА ВТ_РегистрацияОплатыРасшифровкаПлатежа_Подготовка.Ссылка.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
        |			ТОГДА ВТ_РегистрацияОплатыРасшифровкаПлатежа_Подготовка.Ссылка.Контрагент
        |		ИНАЧЕ
        |			ЕСТЬNULL(ВТ_РегистрацияОплатыЛицевыеСчета.ДокументОплаты.ДоговорКонтрагента.Владелец, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
        |	КОНЕЦ КАК Контрагент,
        |	ЕСТЬNULL(ВТ_РегистрацияОплатыЛицевыеСчета.ДокументОплаты.ДоговорКонтрагента, ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)) КАК Договор,
        |	ВТ_РегистрацияОплатыЛицевыеСчета.ДокументОплаты КАК ДокументОплаты
        |ПОМЕСТИТЬ ВТ_РегистрацияОплатыРасшифровкаПлатежа
        |ИЗ
        |	ВТ_РегистрацияОплатыРасшифровкаПлатежа_Подготовка КАК ВТ_РегистрацияОплатыРасшифровкаПлатежа_Подготовка
        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_РегистрацияОплатыЛицевыеСчета КАК ВТ_РегистрацияОплатыЛицевыеСчета
        |		ПО ВТ_РегистрацияОплатыРасшифровкаПлатежа_Подготовка.Ссылка = ВТ_РегистрацияОплатыЛицевыеСчета.Ссылка
        |			И ВТ_РегистрацияОплатыРасшифровкаПлатежа_Подготовка.Объект = ВТ_РегистрацияОплатыЛицевыеСчета.Объект
        |ГДЕ
        |	ВЫБОР
        |		КОГДА ВТ_РегистрацияОплатыРасшифровкаПлатежа_Подготовка.Ссылка.ГП_ОплатаОтПокупателяФизЛица = ИСТИНА ТОГДА ИСТИНА
        |		КОГДА ВТ_РегистрацияОплатыРасшифровкаПлатежа_Подготовка.Ссылка.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
        |			ТОГДА
        |				// Только фих. лица
        |				ВТ_РегистрацияОплатыРасшифровкаПлатежа_Подготовка.Ссылка.Контрагент.ЮридическоеФизическоеЛицо
        |					= ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
        |						И НЕ ВТ_РегистрацияОплатыРасшифровкаПлатежа_Подготовка.Ссылка.Контрагент.ИндивидуальныйПредприниматель
        |						И НЕ ВТ_РегистрацияОплатыРасшифровкаПлатежа_Подготовка.Ссылка.Контрагент.Самозанятый
        |		ИНАЧЕ
        |			ВЫБОР
        |				КОГДА
        |					ЕСТЬNULL(ВТ_РегистрацияОплатыЛицевыеСчета.ДокументОплаты.ДоговорКонтрагента.Владелец, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
        |							= ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) ТОГДА ЛОЖЬ
        |				ИНАЧЕ
        |					// Только фих. лица
        |					ВТ_РегистрацияОплатыЛицевыеСчета.ДокументОплаты.ДоговорКонтрагента.Владелец.ЮридическоеФизическоеЛицо
        |						= ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
        |							И НЕ ВТ_РегистрацияОплатыЛицевыеСчета.ДокументОплаты.ДоговорКонтрагента.Владелец.ИндивидуальныйПредприниматель
        |							И НЕ ВТ_РегистрацияОплатыЛицевыеСчета.ДокументОплаты.ДоговорКонтрагента.Владелец.Самозанятый
        |			КОНЕЦ
        |	КОНЕЦ
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ
        |	ВТ_РегистрацияОплатыРасшифровкаПлатежа.Ссылка КАК Ссылка,
        |	ВТ_РегистрацияОплатыРасшифровкаПлатежа.Объект КАК Объект,
        |	ВТ_РегистрацияОплатыРасшифровкаПлатежа.Услуга КАК Услуга,
        |	ВТ_РегистрацияОплатыРасшифровкаПлатежа.ЭтоАванс КАК ЭтоАванс,
        |   ВТ_РегистрацияОплатыРасшифровкаПлатежа.МесяцНачисления КАК МесяцНачисления,
        |	СУММА(ВТ_РегистрацияОплатыРасшифровкаПлатежа.Сумма) КАК Сумма,
        |	СУММА(ВТ_РегистрацияОплатыРасшифровкаПлатежа.Пени) КАК Пени,
        |	СУММА(ВТ_РегистрацияОплатыРасшифровкаПлатежа.АвансСумма) КАК АвансСумма,
        |	СУММА(ВТ_РегистрацияОплатыРасшифровкаПлатежа.ДолгСумма) КАК ДолгСумма,
        |	ВТ_РегистрацияОплатыРасшифровкаПлатежа.ДокументОплаты КАК ДокументОплаты,
        |   ВТ_РегистрацияОплатыРасшифровкаПлатежа.Контрагент КАК Контрагент,
        |   ВТ_РегистрацияОплатыРасшифровкаПлатежа.Договор КАК Договор
        |ИЗ
        |	ВТ_РегистрацияОплатыРасшифровкаПлатежа КАК ВТ_РегистрацияОплатыРасшифровкаПлатежа
        |
        |СГРУППИРОВАТЬ ПО
        |	ВТ_РегистрацияОплатыРасшифровкаПлатежа.Ссылка,
        |	ВТ_РегистрацияОплатыРасшифровкаПлатежа.Объект,
        |	ВТ_РегистрацияОплатыРасшифровкаПлатежа.ЭтоАванс,
        |	ВТ_РегистрацияОплатыРасшифровкаПлатежа.Услуга,
        |	ВТ_РегистрацияОплатыРасшифровкаПлатежа.ДокументОплаты,
        |	ВТ_РегистрацияОплатыРасшифровкаПлатежа.Контрагент,
        |	ВТ_РегистрацияОплатыРасшифровкаПлатежа.Договор,
        |	ВТ_РегистрацияОплатыРасшифровкаПлатежа.МесяцНачисления
        |";

    Возврат ТекстЗапроса;
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
// Гарант+ Килипенко 11.02.2025 [F00232767] Выгрузка в ОФД доработка --
