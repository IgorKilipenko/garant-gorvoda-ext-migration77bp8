#Область ПрограммныйИнтерфейс

// Параметры:
//  КаталогДляВременныхФайлов - Строка
//  ДанныеАутентификации - Структура
//      * Логин - Строка
//      * Пароль - Строка
//  Тестовый - Булево
&НаКлиенте
Асинх Функция ПолучитьТокенАсинх(Знач КаталогДляВременныхФайлов, Знач ДанныеАутентификации, Знач Тестовый) Экспорт
    РезультатФункции = Новый Структура("Успех, ТекстСообщения", Истина);
    РезультатФункции.Вставить("ДатаВремяПолученияТокена");
    РезультатФункции.Вставить("Токен", "");

    ВремяЖизниТокенаСек = 60 * 60 * 23;
    ИмяФайлаДатыТокена = "ДатаВремяПолученияТокена.txt";
    ИмяФайлаТокена = ГП_СчетмашAPI.ПолучитьИмяФайлаТокенаПоУмолчанию(Тестовый);

    // Проверка актуальности токена, активность токена - 23 часа.
    ФайлДатаВремяТокена = Новый Файл(КаталогДляВременныхФайлов + "/" + ИмяФайлаДатыТокена);
    Если ФайлДатаВремяТокена.Существует() Тогда

        ТекстФайла = Новый ТекстовыйДокумент;
        Попытка
            ТекстФайла.Прочитать(КаталогДляВременныхФайлов + "/" + ИмяФайлаДатыТокена);
            РезультатФункции.ДатаВремяПолученияТокена = Дата(ТекстФайла.ПолучитьСтроку(1));
        Исключение
            РезультатФункции.ДатаВремяПолученияТокена = Дата(1, 1, 1, 1, 1, 1);
        КонецПопытки;
    КонецЕсли;

    НужноПолучитьТокен = (РезультатФункции.ДатаВремяПолученияТокена = Неопределено)
        ИЛИ ((ТекущаяДатаСеанса() - РезультатФункции.ДатаВремяПолученияТокена) > ВремяЖизниТокенаСек);

    РезультатЧтенияТокена = Неопределено;
    Если НужноПолучитьТокен = Истина Тогда
        ЗаписьJSON = Новый ЗаписьJSON;
        ЗаписьJSON.УстановитьСтроку(Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Нет, " ", Истина));

        СтруктураДанныхJSON = Новый Структура;

        СтруктураДанныхJSON.Вставить("login", СокрЛП(ДанныеАутентификации.Логин));
        СтруктураДанныхJSON.Вставить("password", СокрЛП(ДанныеАутентификации.Пароль));

        ЗаписатьJSON(ЗаписьJSON, СтруктураДанныхJSON);

        СтрокаJS = ЗаписьJSON.Закрыть();
        HTTPСоединение = НовыйHTTPСоединениеСчетмаш();
        ЗапросPOST = ГП_СчетмашAPI.НовыйHTTPЗапросТокенаСчетмаш(СтрокаJS);

        ПутьФайлаОтвета = КаталогДляВременныхФайлов + "\" + ИмяФайлаТокена;
        HTTPОтвет = Неопределено;
        Попытка
            HTTPОтвет = Ждать HTTPСоединение.ОтправитьДляОбработкиАсинх(ЗапросPOST, ПутьФайлаОтвета);
        Исключение
            РезультатФункции.Успех = Ложь;
            РезультатФункции.ТекстСообщения = СтрШаблон(
                    "Ошибка запроса токена.
                    |Информация: %1", ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
            Возврат РезультатФункции; // Ошибка получения токена
        КонецПопытки;

        Если HTTPОтвет = Неопределено ИЛИ HTTPОтвет.КодСостояния <> 200 Тогда
            РезультатФункции.Успех = Ложь;
            РезультатФункции.ТекстСообщения = СтрШаблон(
                    "Ошибка запроса токена.
                    |Информация: %1", ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
            Возврат РезультатФункции; // Ошибка получения ответа
        КонецЕсли;

        РезультатЧтенияТокена = ПрочитатьТокенИзФайла(КаталогДляВременныхФайлов + "\" + ИмяФайлаТокена);
        Если РезультатЧтенияТокена.Успех Тогда
            Попытка
                Текст = Новый ТекстовыйДокумент;
                Текст.ДобавитьСтроку(РезультатФункции.ДатаВремяПолученияТокена);
                Текст.Записать(КаталогДляВременныхФайлов + ИмяФайлаДатыТокена);
                РезультатФункции.ТекстСообщения = "Токен обновлен";
            Исключение
                РезультатФункции.ТекстСообщения = СтрШаблон(
                        "Ошибка записи файла времени токена.
                        |Информация: %1", ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
                Возврат РезультатФункции; // Ошибка записи файла времени (не влияет на текущее получение токена)
            КонецПопытки;
        КонецЕсли;
    КонецЕсли;

    Если РезультатФункции.Успех = Ложь Тогда
        Возврат РезультатФункции; // Ошибка получения токена
    КонецЕсли;

    // Чтение токена
    Если РезультатЧтенияТокена = Неопределено Тогда
        РезультатЧтенияТокена = ПрочитатьТокенИзФайла(КаталогДляВременныхФайлов + "\" + ИмяФайлаТокена);;
    КонецЕсли;
    Если РезультатЧтенияТокена.Успех = Ложь Тогда
        РезультатФункции.Успех = Ложь;
        РезультатФункции.Токен = "";
        РезультатФункции.ТекстСообщения = РезультатЧтенияТокена.ТекстСообщения;
    Иначе
        РезультатФункции.Токен = РезультатЧтенияТокена.Токен;
    КонецЕсли;

    Возврат РезультатФункции;
КонецФункции

#КонецОбласти
