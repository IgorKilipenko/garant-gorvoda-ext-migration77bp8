#Область ПрограммныйИнтерфейс

// Выполняет чтение данных взаиморасчетов (из БП 7.7) из файла и записывает
//  данные в регистр `ГП_ВзаиморасчетыБП77`
// Параметры:
//  ПараметрыЗагрузки - Структура
//      * ДвоичныеДанныеФайла - ДвоичныеДанные
//  АдресХранилища - Строка, Неопределено
// Возвращаемое значение:
//  - Структура
//      * Успех - Булево
//      * КоличествоЗаписанных - Число
//      * ТекстСообщения - Строка, Неопределено
//  - Неопределено
Функция ЗагрузитьДанныеВзаиморасчетовПоАбонентамВРегистр(ПараметрыЗагрузки, АдресХранилища = Неопределено) Экспорт
    РезультатФункции = Новый Структура("Успех, КоличествоЗаписанных, ТекстСообщения", Ложь, 0);

    // Чтение данных из файла XML
    РезультатЧтенияДанных = ГП_МиграцияОбщегоНазначения.ПрочитатьДанныеИзФайлаXML(
            ПараметрыЗагрузки.ДвоичныеДанныеФайла, "ВзаиморасчетПоАбоненту");
    Если РезультатЧтенияДанных.Успех = Ложь Тогда
        РезультатФункции.ТекстСообщения = РезультатЧтенияДанных.ТекстСообщения;
        Возврат ГП_МиграцияОбщегоНазначения.ПоместитьВХранилищеИВернуть(РезультатФункции, АдресХранилища);
    КонецЕсли;

    // Проверка структуры данных
    Если ТипЗнч(РезультатЧтенияДанных.Данные) <> Тип("СписокXDTO") Тогда
        РезультатФункции.ТекстСообщения = "Ошибка структуры данных.";
        Возврат ГП_МиграцияОбщегоНазначения.ПоместитьВХранилищеИВернуть(РезультатФункции, АдресХранилища);
    КонецЕсли;

    // Запись данных в регистр
    ДанныеВзаиморасчетовАбонентовБП77 = РезультатЧтенияДанных.Данные;
    РезультатЗаписи = ЗаписатьДанныеВзаиморасчетовАбонентовВРегистр(ДанныеВзаиморасчетовАбонентовБП77);

    // Формирование результата
    РезультатФункции.Успех = РезультатЗаписи.Успех;
    РезультатФункции.ТекстСообщения = РезультатЗаписи.ТекстСообщения;
    РезультатФункции.КоличествоЗаписанных = РезультатЗаписи.КоличествоЗаписанных;

    Возврат ГП_МиграцияОбщегоНазначения.ПоместитьВХранилищеИВернуть(РезультатФункции, АдресХранилища);
КонецФункции

// Параметры:
//  Отбор - Структура
//      * КонтрагентКод - Строка
//      * ДоговорКод - Строка
// Возвращаемое значение:
//  - РегистрСведений.ГП_ВзаиморасчетыБП77.НаборЗаписей
Функция СоздатьНаборЗаписейВзаиморасчетовАбонентовБП77(Знач Отбор) Экспорт
    НаборЗаписей = РегистрыСведений.ГП_ВзаиморасчетыБП77.СоздатьНаборЗаписей();
    НаборЗаписей.Отбор.КонтрагентКод.Установить(Отбор.КонтрагентКод);
    НаборЗаписей.Отбор.ДоговорКод.Установить(Отбор.ДоговорКод);

    Возврат НаборЗаписей;
КонецФункции

// Выполняет создание документа Ввод начального сальдо с заполнением ТЧ л/с по данным остатков БУ по счетам 62.01, 62.02
// Параметры:
//  ДатаОстатков - Дата - ПериодСальдо
//  ДатаДокумента - Дата
//  ВидУслугиБП77 - Строка - Допустимые значения: ["Водоснабжение", "Водоотведение", "Негативное воздействие"]
// Возвращаемое значение:
//  - Структура
//      * Успех - Булево
//      * СозданныйДокумент - ДокументСсылка.КВП_ВводНачальногоСальдо, Неопределено
//      * ТекстСообщения - Строка, Неопределено
Функция ЗаполнитьНачальноеСальдоПоАбонентам(Знач ДатаОстатков, Знач ДатаДокумента, Знач ВидУслугиБП77) Экспорт
    РезультатФункции = Новый Структура("Успех, ТекстСообщения, СозданныйДокумент");

    Организация = УПЖКХ_ТиповыеМетодыВызовСервера.ПолучитьОсновнуюОрганизацию();

    ДанныеЗаполнения = ПолучитьДанныеЗаполненияВводаНачальногоСальдо(ВидУслугиБП77, Организация, ДатаДокумента);

    ДокументОбъект =
        ГП_ВводНачальногоСальдоНачислений.НовыйДокументВводаНачальногоСальдо(ДатаОстатков, ДатаДокумента, Организация);
    ДокументОбъект.Комментарий = СтрШаблон("%1 Для вида услуги: %2",
            ДокументОбъект.Комментарий, ВидУслугиБП77);

    Для Каждого СтрокаДанных Из ДанныеЗаполнения Цикл
        НоваяСтрокаЛС = ДокументОбъект.ЛицевыеСчета.Добавить();
        НоваяСтрокаЛС.Объект = СтрокаДанных.ЛицевойСчет;
        НоваяСтрокаЛС.Услуга = СтрокаДанных.Услуга;
        НоваяСтрокаЛС.МесяцНачисления = ДокументОбъект.ПериодСальдо;
        НоваяСтрокаЛС.МесяцНачисленияСтрока = Формат(ДокументОбъект.ПериодСальдо, "ДФ='MMMM yyyy'");
        НоваяСтрокаЛС.РазделУчета = Перечисления.УПЖКХ_РазделыУчета.НачислениеУслуг;
        НоваяСтрокаЛС.Сумма = СтрокаДанных.Сумма;

        ДокументОбъект.СуммаДокумента = ДокументОбъект.СуммаДокумента + НоваяСтрокаЛС.Сумма;
    КонецЦикла;

    Попытка
        ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
        РезультатФункции.СозданныйДокумент = ДокументОбъект.Ссылка;
    Исключение
        РезультатФункции.Успех = Ложь;
        РезультатФункции.СозданныйДокумент = Неопределено;
        РезультатФункции.ТекстСообщения = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
    КонецПопытки;

    Возврат РезультатФункции;
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныйПрограммныйИнтерфейс

// Выполняет запись исходных данных взаиморасчетов по абонентам в регистр `ГП_ВзаиморасчетыБП77` (по данным из БП77)
//  Существующие записи по отбору составного кода будут перезаписаны
// Параметры:
//  ДанныеВзаиморасчетовАбонентовБП77 - Массив из Структура - Данные БП77
// Возвращаемое значение:
//  Структура:
//      * Успех - Булево
//      * КоличествоЗаписанных - Число
//      * ТекстСообщения - Строка, Неопределено
Функция ЗаписатьДанныеВзаиморасчетовАбонентовВРегистр(Знач ДанныеВзаиморасчетовАбонентовБП77) Экспорт
    РезультатФункции = Новый Структура(
            "Успех, КоличествоЗаписанных, ТекстСообщения", Истина, 0);

    Если ДанныеВзаиморасчетовАбонентовБП77 = Неопределено ИЛИ ДанныеВзаиморасчетовАбонентовБП77.Количество() = 0 Тогда
        Возврат РезультатФункции; // Нет данных
    КонецЕсли;

    // Поля для предобработки (приведения типов)
    КлючиЧисел = "Дебет,Кредит,Сальдо,Водоснабжение,Водоотведение";

    // Транзакция записи в регистр
    НачатьТранзакцию();
    Попытка

        Для Каждого СтрокаДанных Из ДанныеВзаиморасчетовАбонентовБП77 Цикл
            НаборЗаписейВзаиморасчетов = СоздатьНаборЗаписейВзаиморасчетовАбонентовБП77(СтрокаДанных);
            НоваяЗапись = НаборЗаписейВзаиморасчетов.Добавить();

            // Заполнение данных
            ЗаполнитьЗначенияСвойств(НоваяЗапись, СтрокаДанных, ,
                СтрШаблон("%1", КлючиЧисел));

            // Заполнение полей с преобразованием типов
            Для Каждого Ключ Из СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(КлючиЧисел, ",", Ложь, Истина) Цикл
                НоваяЗапись[Ключ] = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(СтрокаДанных[Ключ]);
            КонецЦикла;

            // Заполнение поля ДоговорНомер
            СтруктураНомера = ГП_МиграцияОбщегоНазначения.ПолучитьНомерДоговораПоНаименованию(СтрокаДанных.ДоговорНаименование);
            Если СтруктураНомера.Успех = Истина Тогда
                НоваяЗапись.ДоговорНомер = СтруктураНомера.Номер + ?(ПустаяСтрока(СтруктураНомера.Суффикс),
                        "", СтрШаблон("-%1", СтруктураНомера.Суффикс));
            КонецЕсли;

            НаборЗаписейВзаиморасчетов.Записать(Истина);
            РезультатФункции.КоличествоЗаписанных = РезультатФункции.КоличествоЗаписанных + 1;
        КонецЦикла;

        ЗафиксироватьТранзакцию(); // Записано успешно
    Исключение
        ОтменитьТранзакцию();

        РезультатФункции.Успех = Ложь;
        РезультатФункции.КоличествоЗаписанных = 0;
        РезультатФункции.ТекстСообщения = СтрШаблон(
                "Ошибка при записи в регистр ""ГП_ВзаиморасчетыБП77""
                |Информация: %1",
                ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));

    КонецПопытки;

    Возврат РезультатФункции;
КонецФункции

// Параметры:
//  - ВидУслугиБП77 - Строка - Допустимые значения: ["Водоснабжение", "Водоотведение", "Негативное воздействие"]
//  - Организация - СправочникСсылка.Организации, Неопределено
//  - ДатаАктуальности - Дата, Неопределено
// Получает данные для заполнения документа Ввод начального сальдо
// Возвращаемое значение:
//  - ТаблицаЗначений
//      * Договор - СправочникСсылка.ДоговорыКонтрагентов
//      * Контрагент - СправочникСсылка.Контрагенты
//      * ЛицевойСчет - СправочникСсылка.КВП_ЛицевыеСчета
//      * Услуга - СправочникСсылка.КВП_Услуги
//      * Сумма - Число
Функция ПолучитьДанныеЗаполненияВводаНачальногоСальдо(
        Знач ВидУслугиБП77, Знач Организация = Неопределено, Знач ДатаАктуальности = Неопределено) Экспорт

    ТекстЗапроса = ПолучитьТекстЗапросаВводаНачальногоСальдо(ВидУслугиБП77 = "Негативное воздействие");
    Запрос = Новый Запрос;
    Запрос.Текст = ТекстЗапроса;

    Организация = ?(Организация = Неопределено, УПЖКХ_ТиповыеМетодыВызовСервера.ПолучитьОсновнуюОрганизацию(), Организация);
    Запрос.УстановитьПараметр("Организация", Организация);
    Запрос.УстановитьПараметр("ВидДоговораАбонентскийОтдел",
        ГП_РаботаСДоговорами.ПолучитьДополнительныйВидДоговораАбонентскийОтдел().Ссылка);
    Запрос.УстановитьПараметр("ВидУслуги", ВидУслугиБП77);
    Если ДатаАктуальности = Неопределено Тогда
        Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Период", "");
    Иначе
        Запрос.УстановитьПараметр("Период", ДатаАктуальности);
    КонецЕсли;

    УслугиВодоснабжения = Новый Массив;
    УслугиВодоснабжения.Добавить(ГП_МиграцияОбщегоНазначения.ПолучитьУслугуХолодноеВодоснабжение().Ссылка);
    УслугиВодоснабжения.Добавить(ГП_МиграцияОбщегоНазначения.ПолучитьУслугуГорячееВодоснабжение().Ссылка);
    Запрос.УстановитьПараметр("УслугиВодоснабжения", УслугиВодоснабжения);

    УслугиВодоотведения = Новый Массив;
    УслугиВодоотведения.Добавить(ГП_МиграцияОбщегоНазначения.ПолучитьУслугуКанализации().Ссылка);
    Запрос.УстановитьПараметр("УслугиВодоотведения", УслугиВодоотведения);

    УслугиНВ = Новый Массив;
    УслугиНВ.Добавить(ГП_МиграцияОбщегоНазначения.ПолучитьУслугуНегативногоВоздействияНаЦСВ().Ссылка);
    Запрос.УстановитьПараметр("УслугиНВ", УслугиНВ);

    РезультатЗапроса = Запрос.Выполнить();
    РезультатФункции = РезультатЗапроса.Выгрузить();
    Возврат РезультатФункции;
КонецФункции

#КонецОбласти // СлужебныйПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

// Возвращаемое значение:
//  - Строка
Функция ПолучитьТекстЗапросаВводаНачальногоСальдо(Знач ДляНегативногоВоздействия = Ложь)
    ТекстЗапроса =
        "ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВзаиморасчетыБП77.КонтрагентКод КАК КонтрагентКод,
        |	ВзаиморасчетыБП77.ДоговорКод КАК ДоговорКод,
        |	ВзаиморасчетыБП77.Сальдо КАК Сальдо,
        |	ВзаиморасчетыБП77.Водоснабжение КАК Водоснабжение,
        |	ВзаиморасчетыБП77.Водоотведение КАК Водоотведение,
        |	ВзаиморасчетыБП77.ДоговорНаименование КАК ДоговорНаименование,
        |	ВзаиморасчетыБП77.КонтрагентНаименование КАК КонтрагентНаименование,
        |	ВзаиморасчетыБП77.ДоговорНомер КАК ДоговорНомер,
        |	КонтрагентыБП77.ИНН КАК КонтрагентИНН,
        |	КонтрагентыБП77.ВидКонтрагента КАК ВидКонтрагента
        |ПОМЕСТИТЬ ВТ_ВзаиморасчетыБП77Подготовка
        |ИЗ
        |	РегистрСведений.ГП_ВзаиморасчетыБП77 КАК ВзаиморасчетыБП77
        |		// #[TAG] ЗАМЕНА_ДОГОВОРОВ_НВ
        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГП_КонтрагентыБП77 КАК КонтрагентыБП77
        |		ПО ВзаиморасчетыБП77.КонтрагентКод = КонтрагентыБП77.Код
        |ГДЕ
        |	ВзаиморасчетыБП77.Сальдо <> 0
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ДоговорыБП77.КонтрагентКод КАК КонтрагентКод,
        |	ДоговорыБП77.Код КАК ДоговорКод,
        |	ДоговорыБП77.ВидДоговораКод КАК ВидДоговораКод,
        |	ДоговорыБП77.ВидДоговораНаименование КАК ВидДоговораНаименование,
        |	ДоговорыБП77.Наименование КАК Наименование,
        |	ДоговорыБП77.ВидДоговораКод = ""00018"" КАК ЭтоДоговорАбонентскогоОтдела,
        |	ДоговорыБП77.ВидДоговораКод = ""00037"" КАК ЭтоДоговорПлатыЗаНВ
        |ПОМЕСТИТЬ ВТ_ДоговорыБП77
        |ИЗ
        |	РегистрСведений.ГП_ДоговорыБП77 КАК ДоговорыБП77
        |ГДЕ
        |	(ДоговорыБП77.КонтрагентКод, ДоговорыБП77.Код) В
        |			(ВЫБРАТЬ
        |				Т.КонтрагентКод,
        |				Т.ДоговорКод
        |			ИЗ
        |				ВТ_ВзаиморасчетыБП77Подготовка КАК Т)
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВТ_ВзаиморасчетыБП77Подготовка.КонтрагентКод КАК КонтрагентКод,
        |	ВТ_ВзаиморасчетыБП77Подготовка.ДоговорКод КАК ДоговорКод,
        |	ВТ_ВзаиморасчетыБП77Подготовка.Сальдо КАК Сальдо,
        |	ВТ_ВзаиморасчетыБП77Подготовка.Водоснабжение КАК Водоснабжение,
        |	ВТ_ВзаиморасчетыБП77Подготовка.Водоотведение КАК Водоотведение,
        |	ВТ_ВзаиморасчетыБП77Подготовка.ДоговорНаименование КАК ДоговорНаименование,
        |	ВТ_ВзаиморасчетыБП77Подготовка.КонтрагентНаименование КАК КонтрагентНаименование,
        |	ВТ_ВзаиморасчетыБП77Подготовка.ДоговорНомер КАК ДоговорНомер,
        |	ВТ_ВзаиморасчетыБП77Подготовка.КонтрагентИНН КАК КонтрагентИНН,
        |	ВТ_ДоговорыБП77.ЭтоДоговорАбонентскогоОтдела КАК ЭтоДоговорАбонентскогоОтдела,
        |	ВТ_ДоговорыБП77.ЭтоДоговорПлатыЗаНВ КАК ЭтоДоговорПлатыЗаНВ
        |ПОМЕСТИТЬ ВТ_ВзаиморасчетыБП77
        |ИЗ
        |	ВТ_ВзаиморасчетыБП77Подготовка КАК ВТ_ВзаиморасчетыБП77Подготовка
        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ДоговорыБП77 КАК ВТ_ДоговорыБП77
        |		ПО ВТ_ВзаиморасчетыБП77Подготовка.КонтрагентКод = ВТ_ДоговорыБП77.КонтрагентКод
        |			И ВТ_ВзаиморасчетыБП77Подготовка.ДоговорКод = ВТ_ДоговорыБП77.ДоговорКод
        |			И (ВТ_ДоговорыБП77.ЭтоДоговорПлатыЗаНВ = ЛОЖЬ)
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ДоговорыКонтрагентов.Ссылка КАК Договор,
        |	ВЫРАЗИТЬ(ДоговорыКонтрагентов.Владелец КАК Справочник.Контрагенты) КАК Контрагент,
        |	ДоговорыКонтрагентов.Номер КАК Номер
        |ПОМЕСТИТЬ ВТ_ДоговорыКонтрагентов
        |ИЗ
        |	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
        |ГДЕ
        |	ДоговорыКонтрагентов.ЭтоГруппа = ЛОЖЬ
        |	И ДоговорыКонтрагентов.ПометкаУдаления = ЛОЖЬ
        |	И ДоговорыКонтрагентов.lc_ВидДоговора В(&ВидДоговораАбонентскийОтдел)
        |	И ДоговорыКонтрагентов.Владелец ССЫЛКА Справочник.Контрагенты
        |	И ДоговорыКонтрагентов.Владелец <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВТ_ВзаиморасчетыБП77.КонтрагентКод КАК КонтрагентКод,
        |	ВТ_ВзаиморасчетыБП77.ДоговорКод КАК ДоговорКод,
        |	ВТ_ВзаиморасчетыБП77.Сальдо КАК Сальдо,
        |	ВТ_ВзаиморасчетыБП77.Водоснабжение КАК Водоснабжение,
        |	ВТ_ВзаиморасчетыБП77.Водоотведение КАК Водоотведение,
        |	ВТ_ВзаиморасчетыБП77.ДоговорНаименование КАК ДоговорНаименование,
        |	ВТ_ВзаиморасчетыБП77.КонтрагентНаименование КАК КонтрагентНаименование,
        |	ВТ_ВзаиморасчетыБП77.ДоговорНомер КАК ДоговорНомер,
        |	ВТ_ВзаиморасчетыБП77.КонтрагентИНН КАК КонтрагентИНН,
        |	ВТ_ВзаиморасчетыБП77.ЭтоДоговорАбонентскогоОтдела КАК ЭтоДоговорАбонентскогоОтдела,
        |	ВТ_ВзаиморасчетыБП77.ЭтоДоговорПлатыЗаНВ КАК ЭтоДоговорПлатыЗаНВ,
        |	ВТ_ДоговорыКонтрагентов.Договор КАК Договор,
        |	ВТ_ДоговорыКонтрагентов.Контрагент КАК Контрагент,
        |	ВТ_ДоговорыКонтрагентов.Номер КАК Номер
        |ПОМЕСТИТЬ ВТ_ВзаиморасчетыПоКонтрагентамБезНВ
        |ИЗ
        |	ВТ_ВзаиморасчетыБП77 КАК ВТ_ВзаиморасчетыБП77
        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ДоговорыКонтрагентов КАК ВТ_ДоговорыКонтрагентов
        |		ПО ВТ_ВзаиморасчетыБП77.ДоговорНомер = ВТ_ДоговорыКонтрагентов.Номер
        |			И (ВТ_ВзаиморасчетыБП77.ЭтоДоговорПлатыЗаНВ = (&ВидУслуги = ""Негативное воздействие""))
        |			И (ВТ_ВзаиморасчетыБП77.КонтрагентИНН = ВТ_ДоговорыКонтрагентов.Контрагент.ИНН
        |				ИЛИ ВТ_ВзаиморасчетыБП77.КонтрагентНаименование = ВТ_ДоговорыКонтрагентов.Контрагент.Наименование)
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВТ_ВзаиморасчетыПоКонтрагентамБезНВ.КонтрагентКод КАК КонтрагентКод,
        |	ВТ_ВзаиморасчетыПоКонтрагентамБезНВ.ДоговорКод КАК ДоговорКод,
        |	ВТ_ВзаиморасчетыПоКонтрагентамБезНВ.Сальдо КАК Сальдо,
        |	ВТ_ВзаиморасчетыПоКонтрагентамБезНВ.Водоснабжение КАК Водоснабжение,
        |	ВТ_ВзаиморасчетыПоКонтрагентамБезНВ.Водоотведение КАК Водоотведение,
        |	ВТ_ВзаиморасчетыПоКонтрагентамБезНВ.ДоговорНаименование КАК ДоговорНаименование,
        |	ВТ_ВзаиморасчетыПоКонтрагентамБезНВ.КонтрагентНаименование КАК КонтрагентНаименование,
        |	ВТ_ВзаиморасчетыПоКонтрагентамБезНВ.ДоговорНомер КАК ДоговорНомер,
        |	ВТ_ВзаиморасчетыПоКонтрагентамБезНВ.КонтрагентИНН КАК КонтрагентИНН,
        |	ВТ_ВзаиморасчетыПоКонтрагентамБезНВ.ЭтоДоговорАбонентскогоОтдела КАК ЭтоДоговорАбонентскогоОтдела,
        |	ВТ_ВзаиморасчетыПоКонтрагентамБезНВ.ЭтоДоговорПлатыЗаНВ КАК ЭтоДоговорПлатыЗаНВ,
        |	ВТ_ВзаиморасчетыПоКонтрагентамБезНВ.Договор КАК Договор,
        |	ВТ_ВзаиморасчетыПоКонтрагентамБезНВ.Контрагент КАК Контрагент,
        |	ВТ_ВзаиморасчетыПоКонтрагентамБезНВ.Номер КАК Номер
        |ПОМЕСТИТЬ ВТ_ВзаиморасчетыПоКонтрагентамВодоснабжение
        |ИЗ
        |	ВТ_ВзаиморасчетыПоКонтрагентамБезНВ КАК ВТ_ВзаиморасчетыПоКонтрагентамБезНВ
        |ГДЕ
        |	ИСТИНА
        |	И ВЫБОР
        |			КОГДА &ВидУслуги = ""Водоснабжение""
        |				ТОГДА ВТ_ВзаиморасчетыПоКонтрагентамБезНВ.Водоснабжение <> 0
        |			КОГДА &ВидУслуги = ""Водоотведение""
        |				ТОГДА ВТ_ВзаиморасчетыПоКонтрагентамБезНВ.Водоотведение <> 0
        |			КОГДА &ВидУслуги = ""Негативное воздействие""
        |				ТОГДА ВТ_ВзаиморасчетыПоКонтрагентамБезНВ.Водоснабжение = 0
        |						И ВТ_ВзаиморасчетыПоКонтрагентамБезНВ.Водоотведение = 0
        |						И ВТ_ВзаиморасчетыПоКонтрагентамБезНВ.Сальдо <> 0
        |			ИНАЧЕ ЛОЖЬ
        |		КОНЕЦ
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	СведенияДляВзаиморасчетовПоЛС.ЛицевойСчет КАК ЛицевойСчет,
        |	СведенияДляВзаиморасчетовПоЛС.Договор КАК Договор,
        |	СведенияДляВзаиморасчетовПоЛС.Контрагент КАК Контрагент
        |ПОМЕСТИТЬ ВТ_ЛицевыеСчетаВодоснабжения
        |ИЗ
        |	РегистрСведений.УПЖКХ_СведенияДляВзаиморасчетовПоЛС.СрезПоследних(&Период, ) КАК СведенияДляВзаиморасчетовПоЛС
        |ГДЕ
        |	СведенияДляВзаиморасчетовПоЛС.Организация = &Организация
        |	И (СведенияДляВзаиморасчетовПоЛС.Контрагент, СведенияДляВзаиморасчетовПоЛС.Договор) В
        |			(ВЫБРАТЬ
        |				Т.Контрагент,
        |				Т.Договор
        |			ИЗ
        |				ВТ_ВзаиморасчетыПоКонтрагентамВодоснабжение КАК Т)
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВЫРАЗИТЬ(НазначенныеНачисления.Объект КАК Справочник.КВП_ЛицевыеСчета) КАК ЛицевойСчет,
        |	НазначенныеНачисления.Услуга КАК Услуга,
        |	ВТ_ЛицевыеСчетаВодоснабжения.Договор КАК Договор,
        |	ВТ_ЛицевыеСчетаВодоснабжения.Контрагент КАК Контрагент,
        |	АВТОНОМЕРЗАПИСИ() КАК АвтоНомер
        |ПОМЕСТИТЬ ВТ_ЛицевыеСчетаДляРаспределенияВодоснабжения_Подготовка
        |ИЗ
        |	РегистрСведений.КВП_НазначенныеНачисления.СрезПоследних(&Период, ) КАК НазначенныеНачисления
        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ЛицевыеСчетаВодоснабжения КАК ВТ_ЛицевыеСчетаВодоснабжения
        |		ПО ((ВЫРАЗИТЬ(НазначенныеНачисления.Объект КАК Справочник.КВП_ЛицевыеСчета)) = ВТ_ЛицевыеСчетаВодоснабжения.ЛицевойСчет)
        |ГДЕ
        |	НазначенныеНачисления.Действует = ИСТИНА
        |	И НазначенныеНачисления.Организация = &Организация
        |	И ВЫБОР
        |			КОГДА &ВидУслуги = ""Водоснабжение""
        |				ТОГДА НазначенныеНачисления.Услуга В (&УслугиВодоснабжения)
        |			КОГДА &ВидУслуги = ""Водоотведение""
        |				ТОГДА НазначенныеНачисления.Услуга В (&УслугиВодоотведения)
        |			КОГДА &ВидУслуги = ""Негативное воздействие""
        |				ТОГДА НазначенныеНачисления.Услуга В (&УслугиНВ)
        |			ИНАЧЕ ЛОЖЬ
        |		КОНЕЦ
        |	И НазначенныеНачисления.Объект ССЫЛКА Справочник.КВП_ЛицевыеСчета
        |	И НазначенныеНачисления.Объект В
        |			(ВЫБРАТЬ
        |				Т.ЛицевойСчет
        |			ИЗ
        |				ВТ_ЛицевыеСчетаВодоснабжения КАК Т)
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВТ_ЛицевыеСчетаДляРаспределенияВодоснабжения_Подготовка.ЛицевойСчет КАК ЛицевойСчет,
        |	ВТ_ЛицевыеСчетаДляРаспределенияВодоснабжения_Подготовка.Услуга КАК Услуга,
        |	ВТ_ЛицевыеСчетаДляРаспределенияВодоснабжения_Подготовка.Договор КАК Договор,
        |	ВТ_ЛицевыеСчетаДляРаспределенияВодоснабжения_Подготовка.Контрагент КАК Контрагент
        |ПОМЕСТИТЬ ВТ_ЛицевыеСчетаДляРаспределенияВодоснабжения
        |ИЗ
        |	ВТ_ЛицевыеСчетаДляРаспределенияВодоснабжения_Подготовка КАК ВТ_ЛицевыеСчетаДляРаспределенияВодоснабжения_Подготовка
        |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЛицевыеСчетаДляРаспределенияВодоснабжения_Подготовка КАК Т
        |		ПО ВТ_ЛицевыеСчетаДляРаспределенияВодоснабжения_Подготовка.Контрагент = Т.Контрагент
        |			И ВТ_ЛицевыеСчетаДляРаспределенияВодоснабжения_Подготовка.Договор = Т.Договор
        |			И ВТ_ЛицевыеСчетаДляРаспределенияВодоснабжения_Подготовка.АвтоНомер <> Т.АвтоНомер
        |			И ВТ_ЛицевыеСчетаДляРаспределенияВодоснабжения_Подготовка.Услуга >= Т.Услуга
        |			И ВТ_ЛицевыеСчетаДляРаспределенияВодоснабжения_Подготовка.ЛицевойСчет >= Т.ЛицевойСчет
        |ГДЕ
        |	Т.ЛицевойСчет ЕСТЬ NULL
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВТ_ВзаиморасчетыПоКонтрагентамВодоснабжение.КонтрагентКод КАК КонтрагентКод,
        |	ВТ_ВзаиморасчетыПоКонтрагентамВодоснабжение.ДоговорКод КАК ДоговорКод,
        |	ВЫБОР
        |		КОГДА &ВидУслуги = ""Водоснабжение"" ТОГДА ВТ_ВзаиморасчетыПоКонтрагентамВодоснабжение.Водоснабжение
        |		КОГДА &ВидУслуги = ""Водоотведение"" ТОГДА ВТ_ВзаиморасчетыПоКонтрагентамВодоснабжение.Водоотведение
        |		КОГДА &ВидУслуги = ""Негативное воздействие"" ТОГДА ВТ_ВзаиморасчетыПоКонтрагентамВодоснабжение.Сальдо
        |	КОНЕЦ КАК СуммаРаспределения,
        |	ВТ_ВзаиморасчетыПоКонтрагентамВодоснабжение.Договор КАК Договор,
        |	ВТ_ВзаиморасчетыПоКонтрагентамВодоснабжение.Контрагент КАК Контрагент,
        |	ВТ_ЛицевыеСчетаДляРаспределенияВодоснабжения.ЛицевойСчет КАК ЛицевойСчет,
        |	ВТ_ЛицевыеСчетаДляРаспределенияВодоснабжения.Услуга КАК Услуга,
        |	АВТОНОМЕРЗАПИСИ() КАК АвтоНомер
        |ПОМЕСТИТЬ ВТ_Результат_Подготовка
        |ИЗ
        |	ВТ_ВзаиморасчетыПоКонтрагентамВодоснабжение КАК ВТ_ВзаиморасчетыПоКонтрагентамВодоснабжение
        |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЛицевыеСчетаДляРаспределенияВодоснабжения КАК ВТ_ЛицевыеСчетаДляРаспределенияВодоснабжения
        |		ПО ВТ_ВзаиморасчетыПоКонтрагентамВодоснабжение.Контрагент = ВТ_ЛицевыеСчетаДляРаспределенияВодоснабжения.Контрагент
        |			И ВТ_ВзаиморасчетыПоКонтрагентамВодоснабжение.Договор = ВТ_ЛицевыеСчетаДляРаспределенияВодоснабжения.Договор
        |ГДЕ
        |	НЕ ВТ_ЛицевыеСчетаДляРаспределенияВодоснабжения.Контрагент ЕСТЬ NULL
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВТ_Результат_Подготовка.Договор КАК Договор,
        |	ВТ_Результат_Подготовка.Контрагент КАК Контрагент,
        |	ВТ_Результат_Подготовка.ЛицевойСчет КАК ЛицевойСчет,
        |	ВТ_Результат_Подготовка.Услуга КАК Услуга,
        |	ВТ_Результат_Подготовка.СуммаРаспределения КАК Сумма
        |ИЗ
        |	ВТ_Результат_Подготовка КАК ВТ_Результат_Подготовка
        |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Результат_Подготовка КАК Т
        |		ПО ВТ_Результат_Подготовка.Контрагент = Т.Контрагент
        |			И ВТ_Результат_Подготовка.Договор = Т.Договор
        |			И ВТ_Результат_Подготовка.СуммаРаспределения = Т.СуммаРаспределения
        |			И ВТ_Результат_Подготовка.АвтоНомер <> Т.АвтоНомер
        |			И ВТ_Результат_Подготовка.Услуга >= Т.Услуга
        |ГДЕ
        |	Т.ЛицевойСчет ЕСТЬ NULL
        |
        |УПОРЯДОЧИТЬ ПО
        |	ВТ_Результат_Подготовка.Контрагент,
        |	ВТ_Результат_Подготовка.Договор
        |";

    Возврат ТекстЗапроса;
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
