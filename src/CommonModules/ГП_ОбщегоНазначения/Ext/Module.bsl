#Область ПрограммныйИнтерфейс

// Параметры:
//  ИмяСправочника - Строка
//  Код - Строка
//  ИгнорироватьПомеченныеНаУдаление - Булево
// Возвращаемое значение:
//  - Структура
//      * Ссылка - СправочникСсылка
//      * Код - Строка
//      * Наименование - Строка
Функция ПолучитьЭлементСправочникаПоКоду(Знач ИмяСправочника, Знач Код, Знач ИгнорироватьПомеченныеНаУдаление = Истина) Экспорт
    Запрос = Новый Запрос;
    Запрос.Текст =
        "ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |   %%ИмяСправочника%%.Ссылка КАК Ссылка,
        |   %%ИмяСправочника%%.Код КАК Код,
        |   %%ИмяСправочника%%.Наименование КАК Наименование
        |ИЗ
        |   Справочник.%%ИмяСправочника%% КАК %%ИмяСправочника%%
        |ГДЕ
        |   %%ИмяСправочника%%.Код = &Код
        |   И %%ИмяСправочника%%.ПометкаУдаления = ЛОЖЬ
        |";

    Запрос.УстановитьПараметр("Код", Код);
    Если ИгнорироватьПомеченныеНаУдаление = Ложь Тогда
        Запрос.Текст = СтрЗаменить(Запрос.Текст, "И %%ИмяСправочника%%.ПометкаУдаления = ЛОЖЬ", "");
    КонецЕсли;

    Запрос.Текст = СтрЗаменить(Запрос.Текст, "%%ИмяСправочника%%", ИмяСправочника);

    РезультатЗапроса = Запрос.Выполнить();
    ТаблицаЭлементов = РезультатЗапроса.Выгрузить();

    Если ТаблицаЭлементов.Количество() <> 1 Тогда
        ВызватьИсключение(СтрШаблон("Ошибка при получении элемента %3 с кодом ""%1"". %2",
                Код,
                ?(ТаблицаЭлементов.Количество() > 1, "Код не уникален.", "Элемент отсутствует в ИБ."),
                ИмяСправочника));
    КонецЕсли;

    РезультатФункции = Новый Структура("Ссылка, Код, Наименование");
    ЗаполнитьЗначенияСвойств(РезультатФункции, ТаблицаЭлементов[0]);

    Возврат РезультатФункции;
КонецФункции

// Параметры:
//  Период - Дата, Неопределено
// Возвращаемое значение:
//  - ПеречислениеСсылка.СтавкиНДС
Функция ПолучитьОбщуюСтавкуНДСНачисленийЖКХ(Знач Период = Неопределено) Экспорт
    Если Период = Неопределено
        ИЛИ (ТипЗнч(Период) = Тип("Дата") И ЗначениеЗаполнено(Период) = Ложь) Тогда

        Период = ТекущаяДатаСеанса();
    КонецЕсли;

    РезультатФункции = Перечисления.СтавкиНДС.СтавкаНДС(Перечисления.ВидыСтавокНДС.Общая, Период);
    Возврат РезультатФункции;
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс
