// Гарант+ Килипенко 07.04.2025 [F00237150] Доработка шапки чека ++ {
#Область ПереопределениеСтандартногоФункционала

&ИзменениеИКонтроль("ШаблонКассовыйЧекШапка")
Процедура ГП_ШаблонКассовыйЧекШапка(Шаблон, Данные, Параметры)
	
	Модуль = ШаблоныФискальныхДокументов;
	
	ФорматРазделитель    = Модуль.ФорматРазделитель();
	ФорматЦентр    = Модуль.ФорматСтрока(ГоризонтальноеПоложение.Центр);
	ФорматПраво    = Модуль.ФорматСтрока(ГоризонтальноеПоложение.Право);
	ФорматЛево     = Модуль.ФорматСтрока(ГоризонтальноеПоложение.Лево);
	ФорматПоШирине = Модуль.ФорматСтрока(ГоризонтальноеПоложение.ПоШирине);
    
    #Удаление // Гарант+ Килипенко 07.04.2025 [F00237150] Доработка шапки чека
	Модуль.ДобавитьСтроку (Шаблон, Параметры.Заголовок,        ФорматЦентр);
	Модуль.ДобавитьСтроку (Шаблон, "=======",                  ФорматРазделитель);
	Модуль.ДобавитьСтроку (Шаблон, Данные.ОрганизацияНазвание, ФорматЦентр);
	Модуль.ДобавитьСтроку (Шаблон, Данные.АдресРасчетов,       ФорматЦентр);
	Модуль.ДобавитьСтроку (Шаблон, Данные.МестоРасчетов,       ФорматЦентр);
	Модуль.ДобавитьСтроку (Шаблон, Данные.АдресМагазина,       ФорматЦентр);
	Модуль.ДобавитьСтроку (Шаблон, Данные.ТекстШапки,          ФорматЦентр);
	Модуль.ДобавитьСтроку (Шаблон, НСтр("ru='КАССОВЫЙ ЧЕК'"),  ФорматЦентр);
    #КонецУдаления
    #Вставка // Гарант+ Килипенко 07.04.2025 [F00237150] Доработка шапки чека ++ {

	Модуль.ДобавитьСтроку(Шаблон, ВРег(Параметры.Заголовок), ФорматЦентр);
	Модуль.ДобавитьСтроку(Шаблон, "=======", ФорматРазделитель);
	Модуль.ДобавитьСтроку(Шаблон, ВРег(Данные.ОрганизацияНазвание), ФорматЦентр);
    
    Модуль.ДобавитьСтроку (Шаблон, " ", ФорматРазделитель);
    Модуль.ДобавитьСтроку (Шаблон, СтрШаблон("ИНН %1", СокрЛП(Данные.ОрганизацияИНН)), ФорматЦентр);
    
    Модуль.ДобавитьСтроку (Шаблон, " ", ФорматРазделитель);
    Модуль.ДобавитьСтроку (Шаблон, НСтр("ru='КАССОВЫЙ ЧЕК'"),  ФорматЦентр);
       
    Модуль.ДобавитьСтроку (Шаблон, " ", ФорматРазделитель);
	Модуль.ДобавитьСтроку(Шаблон, СтрШаблон("Место расчетов: %1, КАССА", ?(ПустаяСтрока(Данные.МестоРасчетов),
        ВРег(Данные.АдресМагазина), ВРег(Данные.МестоРасчетов))), ФорматЦентр);

    #КонецВставки // Гарант+ Килипенко 07.04.2025 [F00237150] Доработка шапки чека -- }
	
	КодРасчета = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.КодРасчетаДенежнымиСредствами(Данные.ТипРасчета);
	ТипЧека    = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.НаименованиеКодаРасчетаДенежнымиСредствами(КодРасчета);
    #Удаление // Гарант+ Килипенко 07.04.2025 [F00237150] Доработка шапки чека
	Модуль.ДобавитьСтроку (Шаблон, НСтр("ru='ИНН'")+" ", Данные.ОрганизацияИНН, ФорматЛево);
	Модуль.ДобавитьКолонку(Шаблон, ТипЧека, ФорматПраво);
    #КонецУдаления
    #Вставка // Гарант+ Килипенко 07.04.2025 [F00237150] Доработка шапки чека ++ {

    Модуль.ДобавитьСтроку(Шаблон, "", ФорматЛево);
	Модуль.ДобавитьКолонку(Шаблон, ТипЧека, ФорматПраво);

    #КонецВставки // Гарант+ Килипенко 07.04.2025 [F00237150] Доработка шапки чека -- }
	
	ДатаВремя  = ?(НЕ ПустаяСтрока(Данные.ДатаВремя), Данные.ДатаВремя, ОбщегоНазначенияБПО.ДатаСеанса());
	ДатаВремя  = Формат(ДатаВремя, НСтр("ru = 'ДФ=""дд.ММ.гггг ЧЧ:мм""'"));
	Модуль.ДобавитьСтроку (Шаблон, НСтр("ru='ЧЕК №'"), Данные.НомерЧека, ФорматЛево);
	Модуль.ДобавитьКолонку(Шаблон, ДатаВремя, ФорматПраво);
	
	Покупатель    = Данные.Получатель;
	ПокупательИНН = Данные.ПолучательИНН;
	ПокупательАдрес = "";
	СведенияОПокупателе = Данные.СведенияОПокупателе;
	Покупатель      = ?(Не ПустаяСтрока(СведенияОПокупателе.Покупатель),      СведенияОПокупателе.Покупатель,      Покупатель);
	ПокупательИНН   = ?(Не ПустаяСтрока(СведенияОПокупателе.ПокупательИНН),   СведенияОПокупателе.ПокупательИНН,   ПокупательИНН);
	ПокупательАдрес = ?(Не ПустаяСтрока(СведенияОПокупателе.ПокупательАдрес), СведенияОПокупателе.ПокупательАдрес, ПокупательАдрес);
	Модуль.ДобавитьСтроку (Шаблон, НСтр("ru='ПОКУПАТЕЛЬ:'"),     Покупатель,    ФорматПраво);
	Модуль.ДобавитьСтроку (Шаблон, НСтр("ru='ИНН ПОКУПАТЕЛЯ:'"), ПокупательИНН, ФорматПраво);
    #Удаление // Гарант+ Килипенко 07.04.2025 [F00237150] Доработка шапки чека
	Модуль.ДобавитьСтроку (Шаблон, НСтр("ru='АДРЕС ПОКУПАТЕЛЯ:'"), ПокупательАдрес, ФорматПраво);
    #КонецУдаления
	
	Модуль.ДобавитьСтроку (Шаблон, ВРег(Данные.ПризнакАгента), ФорматЛево);

	// ++ Локализация
	ДанныеПоставщика = Данные.ДанныеПоставщика;
	Модуль.ДобавитьСтроку (Шаблон, НСтр("ru='ПОСТАВЩИК:'"),      ДанныеПоставщика.Наименование, ФорматПраво);
	Модуль.ДобавитьСтроку (Шаблон, НСтр("ru='ТЛФ.ПОСТ.:'"),      ДанныеПоставщика.Телефон,      ФорматПраво);
	Модуль.ДобавитьСтроку (Шаблон, НСтр("ru='ИНН ПОСТАВЩИКА:'"), ДанныеПоставщика.ИНН,          ФорматПраво);

	ПлатежныйАгент           = Данные.ДанныеАгента.ПлатежныйАгент;
	ОператорПеревода         = Данные.ДанныеАгента.ОператорПеревода;
	ОператорПоПриемуПлатежей = Данные.ДанныеАгента.ОператорПоПриемуПлатежей;
	Модуль.ДобавитьСтроку (Шаблон, НСтр("ru='ПЛ.АГЕНТ ОПЕР.:'"),    ПлатежныйАгент.Операция,       ФорматПраво);
	Модуль.ДобавитьСтроку (Шаблон, НСтр("ru='ПЛ.АГЕНТ.ТЛФ.:'"),     ПлатежныйАгент.Телефон,        ФорматПраво);
	Модуль.ДобавитьСтроку (Шаблон, НСтр("ru='ОПЕРАТОР ПЕРЕВОДА:'"), ОператорПеревода.Наименование, ФорматПраво);
	Модуль.ДобавитьСтроку (Шаблон, НСтр("ru='ТЛФ.ОП.ПЕРЕВОДА:'"),   ОператорПеревода.Телефон,      ФорматПраво);
	Модуль.ДобавитьСтроку (Шаблон, НСтр("ru='АДР.ОП.ПЕРЕВОДА:'"),   ОператорПеревода.Адрес,        ФорматПраво);
	Модуль.ДобавитьСтроку (Шаблон, НСтр("ru='ИНН ОП.ПЕРЕВОДА:'"),   ОператорПеревода.ИНН,          ФорматПраво);
	Модуль.ДобавитьСтроку (Шаблон, НСтр("ru='ОПЕР.ПО ПРИЕМУ ПЛАТ.ТЛФ:'"), ОператорПоПриемуПлатежей.Телефон, ФорматПраво);
	// -- Локализация
	
	ДополнительныйРеквизитПользователя = Данные.ДополнительныйРеквизитПользователя;
	Модуль.ДобавитьСтроку (Шаблон, ДополнительныйРеквизитПользователя.Наименование, ДополнительныйРеквизитПользователя.Значение, ФорматПраво);
	
	Модуль.ДобавитьСтроку(Шаблон, "-", ФорматРазделитель);
	
КонецПроцедуры

#КонецОбласти // ПереопределениеСтандартногоФункционала
// Гарант+ Килипенко 07.04.2025 [F00237150] Доработка шапки чека -- }
