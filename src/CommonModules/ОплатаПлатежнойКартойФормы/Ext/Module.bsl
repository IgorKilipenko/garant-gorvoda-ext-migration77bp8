// Гарант+ Килипенко 22.01.2025 Сохранение ставки НДС при изменении Договора ++
#Область ПереопределениеСтандартногоФункционала

&ИзменениеИКонтроль("ДоговорКонтрагентаПриИзменении")
Процедура ГП_ДоговорКонтрагентаПриИзменении(Форма, СтрокаПлатеж)

    Объект = Форма.Объект;

    ПараметрыОбъекта = ОплатаПлатежнойКартойКлиентСервер.ТекущиеПараметрыОбъекта(Форма);
    ПараметрыОбъекта.ДоговорКонтрагента = СтрокаПлатеж.ДоговорКонтрагента;
    Форма.СвойстваПлатежа  = ОплатаПлатежнойКартойКлиентСервер.СвойстваСтрокРасшифровкиПлатежа(ПараметрыОбъекта, Ложь);
    #Вставка // Гарант+ Килипенко 22.01.2025 Сохранение ставки НДС при изменении Договора ++

    Если Объект.ВидОперации = Перечисления.ВидыОперацийОплатаПлатежнойКартой.ОплатаПокупателя
            И Форма.СвойстваПлатежа.Свойство("СтавкаНДС")
            И Форма.СвойстваПлатежа.СтавкаНДС <> СтрокаПлатеж.СтавкаНДС
            И ПустаяСтрока(Строка(СтрокаПлатеж.СтавкаНДС)) = Ложь Тогда

        Форма.СвойстваПлатежа.СтавкаНДС = СтрокаПлатеж.СтавкаНДС; // Сохраняем исходную ставку НДС
        ОбщегоНазначения.СообщитьПользователю("Внимание! Ставка НДС осталась без изменений");
    КонецЕсли;

    #КонецВставки // Гарант+ Килипенко 22.01.2025 Сохранение ставки НДС при изменении Договора --

    ЗаполнитьЗначенияСвойств(СтрокаПлатеж, Форма.СвойстваПлатежа);

    ПерезаполнитьСчетаУчета(Форма, СтрокаПлатеж);

    Если Форма.ПрименениеУСН Тогда
        ЗаполнитьОтражениеВУСН(Форма);
    КонецЕсли;

    ЗаполнитьЗначенияСвойств(СтрокаПлатеж, Форма.СвойстваПлатежа);

    Если ЗначениеЗаполнено(СтрокаПлатеж.ДоговорКонтрагента) Тогда
        Если ЗначениеЗаполнено(СтрокаПлатеж.СчетНаОплату) Тогда
            ДоговорСчетаПокупателю = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаПлатеж.СчетНаОплату, "ДоговорКонтрагента");
            Если ЗначениеЗаполнено(ДоговорСчетаПокупателю)
                И ДоговорСчетаПокупателю <> СтрокаПлатеж.ДоговорКонтрагента Тогда
                СтрокаПлатеж.СчетНаОплату = Неопределено;
            КонецЕсли;
        КонецЕсли;
    КонецЕсли;

    ЗаполнитьДобавленныеКолонкиТаблиц(Форма);
    Если ОплатаПлатежнойКартойКлиентСервер.ФормаРасшифровкиПлатежа(Форма) Тогда
        ОплатаПлатежнойКартойКлиентСервер.ЗаполнитьНадписиВРасшифровке(СтрокаПлатеж);
    Иначе
        ОплатаПлатежнойКартойКлиентСервер.УправлениеФормой(Форма);
    КонецЕсли;

КонецПроцедуры

// Гарант+ Килипенко 31.01.2025 Сохранение ставки НДС при изменении Контрагента ++
//
&ИзменениеИКонтроль("ПерезаполнитьРасшифровкуПлатежа")
Процедура ГП_ПерезаполнитьРасшифровкуПлатежа(Форма, ЭтоИзменениеОрганизации)
	
	Объект   = Форма.Объект;
	Элементы = Форма.Элементы;
	
	СворачиватьТЧПриИзмененииОрганизации = ОплатаПлатежнойКартойКлиентСервер.ЭтоРасчетыСКонтрагентом(Объект.ВидОперации);
	
	Если ЭтоИзменениеОрганизации И Не СворачиватьТЧПриИзмененииОрганизации Тогда
		Возврат;
	КонецЕсли;
	
	ИмяТабличнойЧасти = ОплатаПлатежнойКартойКлиентСервер.НаименованиеТабличнойЧасти();
	ИнициализироватьСвойстваПлатежаПоПараметрам(Форма);
    #Вставка // Гарант+ Килипенко 31.01.2025 Сохранение ставки НДС при изменении Контрагента ++
    
    ИсходнаяСтавкаНДС = Неопределено;
    Если Форма.Объект.ВидОперации = Перечисления.ВидыОперацийОплатаПлатежнойКартой.ОплатаПокупателя
            И Форма.СвойстваПлатежа.Свойство("СтавкаНДС") Тогда
            
        ИмяТабличнойЧастиИсходнойРасшифровки = ОплатаПлатежнойКартойКлиентСервер.НаименованиеТабличнойЧасти();
        ИсходнаяРасшифровкаПлатежа = Форма.Объект[ИмяТабличнойЧастиИсходнойРасшифровки];

        ИсходнаяСтавкаНДС = ПоступлениеНаРасчетныйСчетФормы.ПолучитьСтавкуНДСРасшифровкиДляПерезаполнения(ИсходнаяРасшифровкаПлатежа);
    КонецЕсли;
    
    Если ПустаяСтрока(Строка(ИсходнаяСтавкаНДС)) = Ложь И Форма.СвойстваПлатежа.СтавкаНДС <> ИсходнаяСтавкаНДС Тогда
        Форма.СвойстваПлатежа.СтавкаНДС = ИсходнаяСтавкаНДС;
        ОбщегоНазначения.СообщитьПользователю(
            "Внимание! Ставка НДС осталась без изменений (принята из первой строки расшифровки платежа)");
    КонецЕсли;

    #КонецВставки // Гарант+ Килипенко 31.01.2025 Сохранение ставки НДС при изменении Контрагента --
	
	// При изменении ключевых реквизитов не всегда требуется очищать информацию об услугах НПД.
	// Поэтому перед перезаполнением Расшифровки платежа запомним указанные услуги НПД и восстановим их.
	ШаблонЗаполнения = БанкИКассаФормы.ЭталонРасшифровкиПлатежа(
		Форма,
		Объект[ИмяТабличнойЧасти],
		ОплатаПлатежнойКартойКлиентСервер.ВидимостьУслугНПД(Форма));
	
	Объект[ИмяТабличнойЧасти].Очистить();
	
	Для Каждого СтрокаШаблон Из ШаблонЗаполнения Цикл
		
		СтрокаПлатеж = Объект[ИмяТабличнойЧасти].Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаПлатеж, СтрокаШаблон);
		
		Если Форма.ЭтоРасчетыСКонтрагентом Тогда
			
			ЗаполнитьЗначенияСвойств(СтрокаПлатеж, Форма.СвойстваПлатежа, , "УслугаНПД");
			ПерезаполнитьСчетаУчета(Форма, СтрокаПлатеж);
			
			Если Элементы.Найти(ИмяТабличнойЧасти) <> Неопределено Тогда
				Элементы[ИмяТабличнойЧасти].ТекущаяСтрока = СтрокаПлатеж.ПолучитьИдентификатор();
			КонецЕсли;
			
		Иначе
			СтрокаПлатеж.СтавкаНДС = УчетНДСКлиентСервер.СтавкаНДСПользователя(Объект.Дата, Форма.ПлательщикНДС);
			ОплатаПлатежнойКартойКлиентСервер.ПересчитатьСуммуНДС(СтрокаПлатеж);
		КонецЕсли;
		
	КонецЦикла;
	
	ЗаполнитьДобавленныеКолонкиТаблиц(Форма);
	
КонецПроцедуры // Гарант+ Килипенко 31.01.2025 Сохранение ставки НДС при изменении Контрагента --

#КонецОбласти // ПереопределениеСтандартногоФункционала
// Гарант+ Килипенко 22.01.2025 Сохранение ставки НДС при изменении Договора --
