// Гарант+ Килипенко 02.08.2024 [F00230765] перенос счетчиков ОИ (по среднему) ++
#Область ПрограммныйИнтерфейс

// Создает новые виртуальные счетчики для расчета ОИ
// Возвращаемое значение:
//  - Структура
//      * Успех - Булево
//      * СозданныеЭлементы - Массив из СправочникСсылка.КВП_Счетчики
Функция СоздатьСчетчикиВиртуальныеОИ() Экспорт
    РезультатФункции = Новый Структура("Успех, СозданныеЭлементы, ТекстСообщения", Истина, Новый Массив);

    ДанныеДляСоздания = ПолучитьДанныеСозданияСчетчиковОИ();

    НачатьТранзакцию();
    Попытка
        Для Каждого СтрокаДанных Из ДанныеДляСоздания Цикл
            СозданныйСчетчикСсылка = ГП_МиграцияПриборовУчета.СоздатьНовыйСчетчикПоСреднемуМетодуРасчетаОИ(
                СтрокаДанных, РезультатФункции.СозданныеЭлементы.Количество() + 1);

            РезультатФункции.СозданныеЭлементы.Добавить(СозданныйСчетчикСсылка);
        КонецЦикла;

        ЗафиксироватьТранзакцию();

    Исключение
        ОтменитьТранзакцию();

        ОбщаяЧастьСообщения = "Ошибка при создании счетчиков ОИ.";
        СтруктураОшибки = ГП_МиграцияОбщегоНазначения.ЗаписатьОшибкуВЖурнал(
                ОбщаяЧастьСообщения, ИнформацияОбОшибке(), Ложь, Истина);

        РезультатФункции.Успех = Ложь;
        РезультатФункции.ТекстСообщения = СтрШаблон(
                "%1
                |Информация об ошибке: %2",
                ОбщаяЧастьСообщения,
                СтруктураОшибки.КраткоеПредставлениеОшибки);

        РезультатФункции.СозданныеЭлементы.Очистить();
    КонецПопытки;

    Возврат РезультатФункции;
КонецФункции

Функция ПолучитьМассивВсехВиртуальныеСчетчикиОИ() Экспорт
    Запрос = Новый Запрос;
    Запрос.Текст =
        "ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	КВП_Счетчики.Ссылка КАК Ссылка
        |ИЗ
        |	Справочник.КВП_Счетчики КАК КВП_Счетчики
        |ГДЕ
        |	КВП_Счетчики.ЭтоГруппа = ЛОЖЬ
        |	И КВП_Счетчики.ПометкаУдаления = ЛОЖЬ
        |	И КВП_Счетчики.ГП_ЭтоПлатаЗаВодоснабжениеОИ = ИСТИНА
        |	И КВП_Счетчики.ГП_ЭтоВиртуальныйСчетчик = ИСТИНА
        |";

    РезультатЗапроса = Запрос.Выполнить();
    ТаблицаСчетчиковОИ = РезультатЗапроса.Выгрузить();

    Возврат ТаблицаСчетчиковОИ.ВыгрузитьКолонку("Ссылка");
КонецФункции

Функция УдалитьВсеВиртуальныеСчетчикиОИ() Экспорт
    РезультатФункции = Новый Структура("Успех, УдаленныеЭлементы, ТекстСообщения", Истина, Новый Массив);

    СчетчикиДляУдаления = ПолучитьМассивВсехВиртуальныеСчетчикиОИ();

    НачатьТранзакцию();
    Попытка
        Для каждого СчетчикСсылка Из СчетчикиДляУдаления Цикл
            СчетчикОбъект = СчетчикСсылка.ПолучитьОбъект();
            СчетчикОбъект.УстановитьПометкуУдаления(Истина);

            РезультатФункции.УдаленныеЭлементы.Добавить(СчетчикСсылка);
        КонецЦикла;

        ЗафиксироватьТранзакцию();

    Исключение
        ОтменитьТранзакцию();

        ОбщаяЧастьСообщения = "Ошибка при удалении счетчиков ОИ.";
        РезультатФункции.Успех = Ложь;
        РезультатФункции.ТекстСообщения = СтрШаблон(
                "%1
                |Информация об ошибке: %2",
                ОбщаяЧастьСообщения,
                ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));

        РезультатФункции.УдаленныеЭлементы.Очистить();
    КонецПопытки;

    Возврат РезультатФункции;
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс
// Гарант+ Килипенко 02.08.2024 [F00230765] перенос счетчиков ОИ (по среднему) --

// Гарант+ Килипенко 02.08.2024 [F00230765] перенос счетчиков ОИ (по среднему) ++
#Область СлужебныйПрограммныйИнтерфейс

// Возвращаемое значение:
//  - ТаблицаЗначений
//      * Услуга - СправочникСсылка.КВП_Услуги
//      * ИдентификаторЛС - Строка
//      * КонтрагентКод - Строка
//      * ОбъектАбонентаКод - Строка
//      * МетодРасчета - Строка
//      * МетодРасчетаКан - Строка
//      * НеНачислять - Булево
//      * НеНачислять - Булево
//      * ТолькоДляКанализации - Число
//      * ЭтоПлатаЗаХолодноеВодоснабжениеОИ - Булево
Функция ПолучитьДанныеСозданияСчетчиковОИ() Экспорт
    Запрос = Новый Запрос;
    Запрос.Текст = ПолучитьТекстЗапросаДанныхСозданияСчетчиковОИ();

    СтруктураУслугиХВОИ = ГП_МиграцияОбщегоНазначения.ПолучитьУслугуПлатаЗаХолодноеВодоснабжениеОИ();
    Запрос.УстановитьПараметр("УслугаХВОИ", СтруктураУслугиХВОИ.Ссылка);

    РезультатЗапроса = Запрос.Выполнить();
    ТаблицаСчетчиковОИ = РезультатЗапроса.Выгрузить();

    Возврат ТаблицаСчетчиковОИ;
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс
// Гарант+ Килипенко 02.08.2024 [F00230765] перенос счетчиков ОИ (по среднему) --

// Гарант+ Килипенко 02.08.2024 [F00230765] перенос счетчиков ОИ (по среднему) ++
#Область СлужебныеПроцедурыИФункции

// Возвращаемое значение:
//  - Строка
Функция ПолучитьТекстЗапросаДанныхСозданияСчетчиковОИ()
    ТекстЗапроса =
        "ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	КВП_Счетчики.Ссылка КАК Ссылка,
        |	КВП_Счетчики.Наименование КАК Наименование,
        |	КВП_Счетчики.ГП_Комментарий КАК Комментарий,
        |	КВП_Счетчики.ГП_ЭтоВиртуальныйСчетчик КАК ГП_ЭтоВиртуальныйСчетчик
        |ПОМЕСТИТЬ ВТ_СуществующиеСчетчикиОИ
        |ИЗ
        |	Справочник.КВП_Счетчики КАК КВП_Счетчики
        |ГДЕ
        |	КВП_Счетчики.ЭтоГруппа = ЛОЖЬ
        |	И КВП_Счетчики.ПометкаУдаления = ЛОЖЬ
        |	И КВП_Счетчики.ГП_ЭтоПлатаЗаВодоснабжениеОИ = ИСТИНА
        |	И КВП_Счетчики.ГП_ЭтоВиртуальныйСчетчик = ИСТИНА
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ЗданияБП77.КонтрагентКод КАК КонтрагентКод,
        |	ЗданияБП77.ОбъектАбонентаКод КАК ОбъектАбонентаКод,
        |	ЗданияБП77.ОбъектАбонентаКод + ""_"" + ЗданияБП77.КонтрагентКод КАК ИдентификаторЛС,
        |	ЗданияБП77.ОбъектАбонентаНаименование КАК ОбъектАбонентаНаименование,
        |	ЗданияБП77.ДоговорКод КАК ДоговорКод,
        |	ЗданияБП77.ТолькоДляКанализации КАК ТолькоДляКанализации,
        |	ЗданияБП77.ПроцентХВ КАК ПроцентХВ,
        |	ЗданияБП77.ПроцентГВ КАК ПроцентГВ,
        |	ЗданияБП77.ЭтоНегативноеВоздействиеЦСВ КАК ЭтоНегативноеВоздействиеЦСВ,
        |	ЗданияБП77.ЭтоПлатаЗаХолодноеВодоснабжениеОИ КАК ЭтоПлатаЗаХолодноеВодоснабжениеОИ,
        |	ЗданияБП77.МетодРасчетаХВ КАК МетодРасчетаХВ,
        |	ЗданияБП77.МетодРасчетаГВ КАК МетодРасчетаГВ,
        |	ЗданияБП77.МетодРасчетаКан КАК МетодРасчетаКан,
        |	ЗданияБП77.НеНачислять КАК НеНачислять,
        |	ЗданияБП77.СрКоличествоХВ КАК СрКоличествоХВ,
        |	ЗданияБП77.СрКоличествоГВ КАК СрКоличествоГВ,
        |	ЗданияБП77.ПроцентОтХВ КАК ПроцентОтХВ
        |ПОМЕСТИТЬ ВТ_ЗданияБП77ДляОИ
        |ИЗ
        |	РегистрСведений.ГП_ЗданияБП77 КАК ЗданияБП77
        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГП_КонтрагентыБП77 КАК КонтрагентыБП77
        |		ПО ЗданияБП77.КонтрагентКод = КонтрагентыБП77.Код
        |			И (ЗданияБП77.ЭтоПлатаЗаХолодноеВодоснабжениеОИ)
        |			//И (ЗданияБП77.СрКоличествоХВ + ЗданияБП77.СрКоличествоХВ > 0)
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |// Результат [0] Плата за холодное водоснабжение ОИ
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВТ_ЗданияБП77ДляОИ.КонтрагентКод КАК КонтрагентКод,
        |	ВТ_ЗданияБП77ДляОИ.ОбъектАбонентаКод КАК ОбъектАбонентаКод,
        |	ВТ_ЗданияБП77ДляОИ.ИдентификаторЛС КАК ИдентификаторЛС,
        |	ВТ_ЗданияБП77ДляОИ.ОбъектАбонентаНаименование КАК Наименование,
        |	ВТ_СуществующиеСчетчикиОИ.Ссылка КАК Ссылка,
        |	ВТ_ЗданияБП77ДляОИ.ТолькоДляКанализации КАК ТолькоДляКанализации,
        |	ВТ_ЗданияБП77ДляОИ.МетодРасчетаХВ КАК МетодРасчета,
        |	ВТ_ЗданияБП77ДляОИ.МетодРасчетаКан КАК МетодРасчетаКан,
        |	ВТ_ЗданияБП77ДляОИ.НеНачислять КАК НеНачислять,
        |	ВТ_ЗданияБП77ДляОИ.СрКоличествоХВ КАК КоличествоПоСреднему,
        |	&УслугаХВОИ КАК Услуга
        |ИЗ
        |	ВТ_ЗданияБП77ДляОИ КАК ВТ_ЗданияБП77ДляОИ
        |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СуществующиеСчетчикиОИ КАК ВТ_СуществующиеСчетчикиОИ
        |		ПО (ВТ_СуществующиеСчетчикиОИ.Наименование = ВЫРАЗИТЬ(""[[Плата за ОИ]] "" + ВТ_ЗданияБП77ДляОИ.ОбъектАбонентаНаименование КАК СТРОКА(100)))
        |			И (ВТ_СуществующиеСчетчикиОИ.Комментарий ПОДОБНО ""%(ид: ("" + ВТ_ЗданияБП77ДляОИ.ИдентификаторЛС + ""))"")
        |ГДЕ
        |	ВТ_ЗданияБП77ДляОИ.МетодРасчетаХВ <> ""Нет""
        |	И ВТ_СуществующиеСчетчикиОИ.Ссылка ЕСТЬ NULL
        |";

    Возврат ТекстЗапроса;
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
// Гарант+ Килипенко 02.08.2024 [F00230765] перенос счетчиков ОИ (по среднему) --
