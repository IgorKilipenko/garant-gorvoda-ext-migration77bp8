// Гарант+ Килипенко 07.10.2024 [F00230765] перенос счетчиков Потерь воды (по среднему) ++
#Область ПрограммныйИнтерфейс

// Создает новые виртуальные счетчики для расчета ОИ
// Возвращаемое значение:
//  - Структура
//      * Успех - Булево
//      * СозданныеЭлементы - Массив из СправочникСсылка.КВП_Счетчики
Функция СоздатьСчетчикиВиртуальныеПотерьВоды() Экспорт
    РезультатФункции = Новый Структура("Успех, СозданныеЭлементы, ТекстСообщения", Истина, Новый Массив);

    ДанныеДляСоздания = ПолучитьДанныеСозданияСчетчиковПотериВоды();

    НачатьТранзакцию();
    Попытка
        Для Каждого СтрокаДанных Из ДанныеДляСоздания Цикл
            СозданныйСчетчикСсылка = ГП_МиграцияПриборовУчета.СоздатьНовыйСчетчикПоСреднемуМетодуРасчетаПотерь(
                    СтрокаДанных, РезультатФункции.СозданныеЭлементы.Количество() + 1);

            РезультатФункции.СозданныеЭлементы.Добавить(СозданныйСчетчикСсылка);
        КонецЦикла;

        ЗафиксироватьТранзакцию();

    Исключение
        ОтменитьТранзакцию();

        ОбщаяЧастьСообщения = "Ошибка при создании счетчиков потерь воды.";
        СтруктураОшибки = ГП_МиграцияОбщегоНазначения.ЗаписатьОшибкуВЖурнал(
                ОбщаяЧастьСообщения, ИнформацияОбОшибке(), Ложь, Истина);

        РезультатФункции.Успех = Ложь;
        РезультатФункции.ТекстСообщения = СтрШаблон(
                "%1
                |Информация об ошибке: %2",
                ОбщаяЧастьСообщения,
                СтруктураОшибки.КраткоеПредставлениеОшибки);

        РезультатФункции.СозданныеЭлементы.Очистить();
    КонецПопытки;

    Возврат РезультатФункции;
КонецФункции

// Создает документы Установки виртуальных счетчиков потерь воды
// Параметры:
//  ДатаДокумента - Дата, Неопределено
//  ДатаВключенияПоУмолчанию - Булево
// Возвращаемое значение:
//  - Структура
//      * Успех - Булево
//      * УстановленныеСчетчики - Массив из СправочникСсылка.КВП_Счетчики
//      * ТекстСообщения - Строка, Неопределено
Функция СоздатьДокументыУстановкиВиртуальныхСчетчиковПотерьВоды(
        Знач ДатаДокумента = Неопределено, ДатаВключенияПоУмолчанию = Неопределено) Экспорт

    РезультатФункции = Новый Структура("Успех, УстановленныеСчетчики, ТекстСообщения", Истина, Новый Массив);

    ДатаДокумента = ?(ДатаДокумента = Неопределено, ТекущаяДатаСеанса(), ДатаДокумента);
    ДатаВключенияПоУмолчанию = ?(ДатаВключенияПоУмолчанию = Неопределено, Дата(2024, 01, 01), ДатаВключенияПоУмолчанию);

    // Только ХВС
    БазоваяУслуга = ГП_МиграцияОбщегоНазначения.ПолучитьУслугуХолодноеВодоснабжение().Ссылка;
    ДанныеДляЗаполнения = ПолучитьДанныеУстановкиВиртуальныхСчетчиковПотерьВоды(БазоваяУслуга);

    // Заполнение шапки документа КВП_УстановкаСчетчика
    НовыйУстановкаСчетчиковОбъект = Документы.КВП_УстановкаСчетчика.СоздатьДокумент();
    НовыйУстановкаСчетчиковОбъект.Дата = ДатаДокумента;
    НовыйУстановкаСчетчиковОбъект.ВидОперации = Перечисления.УПЖКХ_ВидыОперацийУстановкаСчетчика.ВключитьСчетчик;
    НовыйУстановкаСчетчиковОбъект.ВвестиНачальныеИлиКонечныеПоказания = Ложь; // Показания счетчиков не вводятся
    НовыйУстановкаСчетчиковОбъект.Ответственный = Пользователи.ТекущийПользователь();
    НовыйУстановкаСчетчиковОбъект.Комментарий = "#создан автоматически по данным из БП 7.7 (ГарантПлюс)";

    // Заполнение ТЧ Главная
    Для Каждого СтрокаДанных Из ДанныеДляЗаполнения Цикл
        // КВП_УстановкаСчетчика
        НоваяЗаписьУстановки = НовыйУстановкаСчетчиковОбъект.Главная.Добавить();
        НоваяЗаписьУстановки.Объект = СтрокаДанных.ЛицевойСчет;
        НоваяЗаписьУстановки.Счетчик = СтрокаДанных.Счетчик;
        НоваяЗаписьУстановки.ДатаВключения = ДатаВключенияПоУмолчанию;

        // Заполнение результата
        РезультатФункции.УстановленныеСчетчики.Добавить(СтрокаДанных.Счетчик);
    КонецЦикла;

    Попытка
        НовыйУстановкаСчетчиковОбъект.Записать(РежимЗаписиДокумента.Запись);
    Исключение
        РезультатФункции.УстановленныеСчетчики.Очистить();
        РезультатФункции.ТекстСообщения = СтрШаблон(
                "Ошибка при создании документа установки виртуальных счетчиков потерь воды.
                |Информация: %1", ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
        РезультатФункции.Успех = Ложь;
    КонецПопытки;

    Возврат РезультатФункции;
КонецФункции

// Параметры:
//  ДатаДокумента - Дата, Неопределено - По умолчанию 01.01.2024
//  ДатаНачалаДействияПоУмолчанию - Дата, Неопределено - По умолчанию 01.01.2024
//  ВыводитьСообщенияОбОшибках - Булево
// Возвращаемое значение:
//  - Структура
//      * Успех - Булево
//      * СообщенияОбОшибках - Массив из Строка
//      * ТекстСообщения - Строка
//      * СозданныеДокументыУправленияУслугами - Массив из ДокументСсылка.КВП_УправлениеУслугами
Функция СоздатьДокументНазначенияУслугиПотериВодыДляВиртуальныхСчетчиков(
        Знач ДатаДокумента = Неопределено, ДатаНачалаДействияПоУмолчанию = Неопределено, Знач ВыводитьСообщенияОбОшибках = Ложь) Экспорт

    ДатаДокумента = ?(ДатаДокумента = Неопределено, ТекущаяДатаСеанса(), ДатаДокумента);
    ДатаВключенияПоУмолчанию = ?(ДатаВключенияПоУмолчанию = Неопределено, Дата(2024, 01, 01), ДатаВключенияПоУмолчанию);

    // Только ХВС
    БазоваяУслуга = ГП_МиграцияОбщегоНазначения.ПолучитьУслугуХолодноеВодоснабжение().Ссылка;
    ДанныеДляЗаполнения = ПолучитьДанныеУстановкиВиртуальныхСчетчиковПотерьВоды(БазоваяУслуга);
    УслугаПотериВоды = ГП_МиграцияОбщегоНазначения.ПолучитьУслугуПотериВоды().Ссылка;

    РезультатФункции = ГП_НазначениеУслуг.СоздатьДокументНазначенияУслугиДляЛицевыхСчетов(
            ДанныеДляЗаполнения, УслугаПотериВоды, ДатаДокумента, ДатаВключенияПоУмолчанию, ВыводитьСообщенияОбОшибках);

    Возврат РезультатФункции;
КонецФункции

// Параметры:
//  Период - Дата, Неопределено - Начало месяца (принудительно), по умолчанию 01.01.2024
//  БазоваяУслуга - СправочникСсылка.КВП_Услуги - По умолчанию ХВС
// Возвращаемое значение:
//  - Структура
//      * Успех - Булево
//      * КоличествоЗаписейЗависимыхУслуг - Число
//      * ТекстСообщения - Строка
Функция ЗаполнитьЗависимостиКанализацииОтУслугПотериВоды(Знач Период = Неопределено, БазоваяУслуга = Неопределено) Экспорт
    РезультатФункции = Новый Структура("Успех, КоличествоЗаписейЗависимыхУслуг, ТекстСообщения", Истина, 0);

    БазоваяУслуга = ?(БазоваяУслуга = Неопределено,
            ГП_МиграцияОбщегоНазначения.ПолучитьУслугуХолодноеВодоснабжение().Ссылка, // ХВС по умолчанию
            БазоваяУслуга);
    ДанныеСчетчиковПотериВоды = ПолучитьДанныеУстановкиВиртуальныхСчетчиковПотерьВоды(БазоваяУслуга);
    ДанныеЗаполнения = ПолучитьДанныеУстановкиЗависимостиКанализацииОтПотерьВоды(ДанныеСчетчиковПотериВоды, БазоваяУслуга);

    // Значения по умолчанию
    ЗависимаяУслуга = ГП_МиграцияОбщегоНазначения.ПолучитьУслугуКанализации().Ссылка;
    ОсновнаяОрганизацияСсылка = УПЖКХ_ТиповыеМетодыВызовСервера.ПолучитьОсновнуюОрганизацию();
    Если Период = Неопределено Тогда
        Период = Дата(2024, 01, 01); // По ТЗ дата по умолчанию 01.01.2024г.
    Иначе
        Период = НачалоМесяца(Период);
    КонецЕсли;

    НачатьТранзакцию();
    Попытка
        Для Каждого СтрокаУслуги Из ДанныеЗаполнения Цикл
            Если СтрокаУслуги.Процент <= 0 Тогда
                ОбщегоНазначения.СообщитьПользователю(СтрШаблон(
                        "Зависимость услуги ""%1"" не назначена для объекта ""%2"" т.к. значение основания должно быть > 0",
                        Строка(СтрокаУслуги.Здание), Строка(СтрокаУслуги.ОсновнаяУслуга)));
                Продолжить;
            КонецЕсли;

            Если СтрокаУслуги.ЗависимаяУслуга <> ЗависимаяУслуга Тогда
                Продолжить; // Выполняем записи только для зависимой услуги - Канализация
            КонецЕсли;

            ОсновнаяУслуга = СтрокаУслуги.ОсновнаяУслуга;
            НовыйНаборЗаписей = ГП_МиграцияУслуг.СоздатьНаборЗаписейЗависимостиУслуг(
                    Период, СтрокаУслуги.Здание, ОсновнаяУслуга, ЗависимаяУслуга, ОсновнаяОрганизацияСсылка);

            НоваяЗапись = НовыйНаборЗаписей.Добавить();
            НоваяЗапись.Период = Период;
            НоваяЗапись.Объект = СтрокаУслуги.Здание;
            НоваяЗапись.УслугаОснование = ОсновнаяУслуга;
            НоваяЗапись.Услуга = ЗависимаяУслуга;
            НоваяЗапись.Организация = ОсновнаяОрганизацияСсылка;
            НоваяЗапись.Действует = Истина; // По ТЗ всегда Истина
            НоваяЗапись.Значение = 100.0; // Исправлено (числ/знам)
            НоваяЗапись.ЗначениеОснование = СтрокаУслуги.Процент;

            НовыйНаборЗаписей.Записать(Истина);
            РезультатФункции.КоличествоЗаписейЗависимыхУслуг = РезультатФункции.КоличествоЗаписейЗависимыхУслуг + 1;
        КонецЦикла;

        ЗафиксироватьТранзакцию();

    Исключение
        ОтменитьТранзакцию();

        РезультатФункции.КоличествоЗаписейЗависимыхУслуг = 0;
        РезультатФункции.ТекстСообщения = СтрШаблон(
                "Ошибка при записи зависимостей услуг канализации для счетчиков ОИ.
                |Информация:
                |%1", ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));

        РезультатФункции.Успех = Ложь;
    КонецПопытки;

    Возврат РезультатФункции;
КонецФункции

// Параметры:
//  ДатаДокумента - Дата
// Возвращаемое значение:
//  - Структура
//      * Успех - Булево
//      * СозданныеДокументы - Массив
//      * ТекстСообщения - Строка, Неопределено
Функция СоздатьДокументыУстановкиТарифовСчетчиковПотерьВоды(Знач ДатаДокумента, Знач БазоваяУслуга = Неопределено) Экспорт
    РезультатФункции = Новый Структура("Успех, СозданныеДокументы, ТекстСообщения", Истина, Новый Массив);

    БазоваяУслуга = ?(БазоваяУслуга = Неопределено,
            ГП_МиграцияОбщегоНазначения.ПолучитьУслугуХолодноеВодоснабжение().Ссылка, // ХВС по умолчанию
            БазоваяУслуга);
    ДанныеЗаполнения = ПолучитьДанныеДляУстановкиТарифовСчетчиковПотерьВоды(БазоваяУслуга);

    НачатьТранзакцию();
    Попытка
        // Создание документов установки тарифов
        Для Каждого СтрокаДанных Из ДанныеЗаполнения.Строки Цикл
            РезультатСозданияДокумента = ГП_МиграцияТарифовАбонентов.СоздатьДокументУстановкиТарифов(
                    СтрокаДанных.Строки,
                    СтрокаДанных.Услуга,
                    СтрокаДанных.ВидТарифа,
                    СтрокаДанных.Тариф,
                    ДатаДокумента,
                    Неопределено);

            Если РезультатСозданияДокумента.Успех = Истина Тогда
                РезультатФункции.СозданныеДокументы.Добавить(РезультатСозданияДокумента.СозданныйДокумент);
            Иначе
                ВызватьИсключение(РезультатСозданияДокумента.ТекстСообщения);
            КонецЕсли;
        КонецЦикла;

        ЗафиксироватьТранзакцию();

    Исключение
        ОтменитьТранзакцию();

        РезультатФункции.ТекстСообщения = СтрШаблон(
                "Ошибка при создании документов установки тарифов для счетчиков ОИ.
                |Информация:
                |%1", ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));

        РезультатФункции.Успех = Ложь;
        РезультатФункции.СозданныеДокументы.Очистить();
    КонецПопытки;

    Возврат РезультатФункции;
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс
// Гарант+ Килипенко 07.10.2024 [F00230765] перенос счетчиков Потерь воды (по среднему) --

// Гарант+ Килипенко 07.10.2024 [F00230765] перенос счетчиков Потерь воды (по среднему) ++
#Область СлужебныйПрограммныйИнтерфейс

// Параметры:
//  БазоваяУслуга - СправочникСсылка.КВП_Услуги - По умолчанию ХВС
// Возвращаемое значение:
//  - ТаблицаЗначений
//      * КонтрагентКод - Строка
//      * ОбъектАбонентаКод - Строка
//      * ИдентификаторЛС - Строка
//      * Наименование - Строка
//      * ЭтоПотериВоды - Булево
//      * КоличествоПоСреднему - Число
//      * МетодРасчета - Строка
//      * МетодРасчетаКан - Строка
//      * ЛицевойСчет - СправочникСсылка.КВП_ЛицевыеСчета
//      * Помещение
//      * Здание
//      * Услуга - СправочникСсылка.КВП_Услуги
//      * ИсходнаяУслуга - СправочникСсылка.КВП_Услуги
Функция ПолучитьДанныеСозданияСчетчиковПотериВоды(Знач БазоваяУслуга = Неопределено) Экспорт
    БазоваяУслуга = ?(БазоваяУслуга = Неопределено,
            ГП_МиграцияОбщегоНазначения.ПолучитьУслугуХолодноеВодоснабжение().Ссылка, // ХВС по умолчанию
            БазоваяУслуга);

    Запрос = Новый Запрос;
    СтруктураТекстаЗапроса = ПолучитьТекстЗапросаДанныхСозданияСчетчиковПотериВоды();
    Запрос.Текст = СтруктураТекстаЗапроса.ТекстЗапроса;

    Запрос.УстановитьПараметр(
        СтруктураТекстаЗапроса.Параметры.Услуга, БазоваяУслуга);
    Запрос.УстановитьПараметр(
        СтруктураТекстаЗапроса.Параметры.УслугаПотериВоды, ГП_МиграцияОбщегоНазначения.ПолучитьУслугуПотериВоды().Ссылка);
    Запрос.УстановитьПараметр(
        СтруктураТекстаЗапроса.Параметры.УслугаХВС, ГП_МиграцияОбщегоНазначения.ПолучитьУслугуХолодноеВодоснабжение().Ссылка);
    Запрос.УстановитьПараметр(
        СтруктураТекстаЗапроса.Параметры.УслугаГВС, ГП_МиграцияОбщегоНазначения.ПолучитьУслугуГорячееВодоснабжение().Ссылка);

    РезультатЗапроса = Запрос.Выполнить();
    РезультатФункции = РезультатЗапроса.Выгрузить();
    Возврат РезультатФункции;
КонецФункции

Функция ПолучитьДанныеУстановкиВиртуальныхСчетчиковПотерьВоды(Знач БазоваяУслуга = Неопределено) Экспорт
    Запрос = Новый Запрос;
    СтруктураТекстаЗапроса = ПолучитьТекстЗапросаДанныхУстановкиВиртуальныхСчетчиковПотерьВоды();
    Запрос.Текст = СтруктураТекстаЗапроса.ТекстЗапроса;

    Запрос.УстановитьПараметр(
        СтруктураТекстаЗапроса.Параметры.Услуга, БазоваяУслуга);
    Запрос.УстановитьПараметр(
        СтруктураТекстаЗапроса.Параметры.УслугаПотериВоды, ГП_МиграцияОбщегоНазначения.ПолучитьУслугуПотериВоды().Ссылка);
    Запрос.УстановитьПараметр(
        СтруктураТекстаЗапроса.Параметры.УслугаХВС, ГП_МиграцияОбщегоНазначения.ПолучитьУслугуХолодноеВодоснабжение().Ссылка);
    Запрос.УстановитьПараметр(
        СтруктураТекстаЗапроса.Параметры.УслугаГВС, ГП_МиграцияОбщегоНазначения.ПолучитьУслугуГорячееВодоснабжение().Ссылка);

    РезультатЗапроса = Запрос.Выполнить();
    РезультатФункции = РезультатЗапроса.Выгрузить();
    Возврат РезультатФункции;
КонецФункции

// Параметры:
//  ДанныеСчетчиковПотерьВоды - ТаблицаЗначений
//      * ЛицевойСчет - СправочникСсылка.КВП_ЛицевыеСчета
//      * КонтрагентКод - Строка
//      * ОбъектАбонентаКод - Строка
//      * Счетчик - СправочникСсылка.КВП_Счетчики
//  БазоваяУслуга - СправочникСсылка.КВП_Услуги, Неопределено - По умолчанию ХВС
// Возвращаемое значение:
//  - ТаблицаЗначений
//      * ЛицевойСчет - СправочникСсылка.КВП_ЛицевыеСчета
//      * Помещение
//      * Здание
//      * Процент - Число
//      * Счетчик - СправочникСсылка.КВП_Счетчики
//      * ОсновнаяУслуга - СправочникСсылка.КВП_Услуги
//      * ЗависимаяУслуга - СправочникСсылка.КВП_Услуги
Функция ПолучитьДанныеУстановкиЗависимостиКанализацииОтПотерьВоды(Знач ДанныеСчетчиковПотерьВоды, Знач БазоваяУслуга = Неопределено) Экспорт
    БазоваяУслуга = ?(БазоваяУслуга = Неопределено,
            ГП_МиграцияОбщегоНазначения.ПолучитьУслугуХолодноеВодоснабжение().Ссылка, // ХВС по умолчанию
            БазоваяУслуга);

    Запрос = Новый Запрос;
    Запрос.Текст =
        "ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ДанныеСчетчиковПотерьВоды.ЛицевойСчет КАК ЛицевойСчет,
        |	ДанныеСчетчиковПотерьВоды.КонтрагентКод КАК КонтрагентКод,
        |	ДанныеСчетчиковПотерьВоды.ОбъектАбонентаКод КАК ОбъектАбонентаКод,
        |	ДанныеСчетчиковПотерьВоды.Счетчик КАК Счетчик
        |ПОМЕСТИТЬ ВТ_СчетчикиПотерьВоды
        |ИЗ
        |	&ДанныеСчетчиковПотерьВоды КАК ДанныеСчетчиковПотерьВоды
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВТ_СчетчикиПотерьВоды.ЛицевойСчет КАК ЛицевойСчет,
        |	ВТ_СчетчикиПотерьВоды.ЛицевойСчет.Адрес КАК Помещение,
        |	ВТ_СчетчикиПотерьВоды.ЛицевойСчет.Адрес.Владелец КАК Здание,
        |   ВЫБОР
        |       КОГДА ВТ_СчетчикиПотерьВоды.Счетчик.ВидУслуги = &УслугаПотериВоды ТОГДА
        |           ВЫБОР
        |               КОГДА &БазоваяУслуга = &УслугаХВС ТОГДА ЗданияБП77.ПроцентХВ
        |               КОГДА &БазоваяУслуга = &УслугаГВС ТОГДА ЗданияБП77.ПроцентГВ
        |               ИНАЧЕ 0
        |           КОНЕЦ
        |       ИНАЧЕ 0
        |   КОНЕЦ КАК Процент,
        |	ВТ_СчетчикиПотерьВоды.Счетчик КАК Счетчик,
        |	ВТ_СчетчикиПотерьВоды.Счетчик.ВидУслуги КАК ОсновнаяУслуга,
        |	&УслугаКанализация КАК ЗависимаяУслуга
        |ИЗ
        |	ВТ_СчетчикиПотерьВоды КАК ВТ_СчетчикиПотерьВоды
        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГП_ЗданияБП77 КАК ЗданияБП77
        |		ПО ВТ_СчетчикиПотерьВоды.КонтрагентКод = ЗданияБП77.КонтрагентКод
        |			И ВТ_СчетчикиПотерьВоды.ОбъектАбонентаКод = ЗданияБП77.ОбъектАбонентаКод
        |			И (ЗданияБП77.МетодРасчетаКан = ""По проценту"")
        |";

    Запрос.УстановитьПараметр("УслугаПотериВоды", ГП_МиграцияОбщегоНазначения.ПолучитьУслугуПотериВоды().Ссылка);
    Запрос.УстановитьПараметр("БазоваяУслуга", БазоваяУслуга);
    Запрос.УстановитьПараметр("УслугаХВС", ГП_МиграцияОбщегоНазначения.ПолучитьУслугуХолодноеВодоснабжение().Ссылка);
    Запрос.УстановитьПараметр("УслугаГВС", ГП_МиграцияОбщегоНазначения.ПолучитьУслугуГорячееВодоснабжение().Ссылка);
    Запрос.УстановитьПараметр("УслугаКанализация", ГП_МиграцияОбщегоНазначения.ПолучитьУслугуКанализации().Ссылка);
    Запрос.УстановитьПараметр("ДанныеСчетчиковПотерьВоды", ДанныеСчетчиковПотерьВоды);

    РезультатЗапроса = Запрос.Выполнить();
    РезультатФункции = РезультатЗапроса.Выгрузить();
    Возврат РезультатФункции;
КонецФункции

Функция ПолучитьДанныеДляУстановкиТарифовСчетчиковПотерьВоды(Знач БазоваяУслуга = Неопределено) Экспорт
    БазоваяУслуга = ?(БазоваяУслуга = Неопределено,
            ГП_МиграцияОбщегоНазначения.ПолучитьУслугуХолодноеВодоснабжение().Ссылка, // ХВС по умолчанию
            БазоваяУслуга);
    ДанныеУстановкиСчетчиковПотерь = ПолучитьДанныеУстановкиВиртуальныхСчетчиковПотерьВоды(БазоваяУслуга);

    Запрос = Новый Запрос;
    Запрос.Текст = ПолучитьТекстЗапросаДанныхУстановкиТарифовСчетчиковПотерьВоды();

    Запрос.УстановитьПараметр("ДанныеУстановкиСчетчиковПотерь", ДанныеУстановкиСчетчиковПотерь);
    РезультатЗапроса = Запрос.Выполнить();

    РезультатФункции = РезультатЗапроса.Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
    Возврат РезультатФункции;
КонецФункции

#КонецОбласти // СлужебныйПрограммныйИнтерфейс
// Гарант+ Килипенко 07.10.2024 [F00230765] перенос счетчиков Потерь воды (по среднему) --

// Гарант+ Килипенко 07.10.2024 [F00230765] перенос счетчиков Потерь воды (по среднему) ++
#Область СлужебныеПроцедурыИФункции

// Параметры:
//  БазоваяУслуга - СправочникСсылка.КВП_Услуги - По умолчанию ХВС
// Возвращаемое значение:
//  - Структура
//      * ТекстЗапроса - Строка
//      * Параметры - Структура
//          ** Услуга - Строка
//          ** УслугаПотериВоды - Строка
//          ** УслугаХВС - Строка
//          ** УслугаГВС - Строка
Функция ПолучитьТекстЗапросаДанныхСозданияСчетчиковПотериВоды()
    РезультатФункции = Новый Структура("ТекстЗапроса, Параметры", "", Новый Структура);

    РезультатФункции.ТекстЗапроса =
        "ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ЗданияБП77.КонтрагентКод КАК КонтрагентКод,
        |	ЗданияБП77.ОбъектАбонентаКод КАК ОбъектАбонентаКод,
        |	ЗданияБП77.ОбъектАбонентаКод + ""_"" + ЗданияБП77.КонтрагентКод КАК ИдентификаторЛС,
        |	ЗданияБП77.ОбъектАбонентаНаименование КАК ОбъектАбонентаНаименование,
        |	ЗданияБП77.ТолькоДляКанализации КАК ТолькоДляКанализации,
        |	ЗданияБП77.ЭтоПотериВоды КАК ЭтоПотериВоды,
        |	ЗданияБП77.СрКоличествоХВ КАК СрКоличествоХВ,
        |	ЗданияБП77.СрКоличествоГВ КАК СрКоличествоГВ,
        |	ЗданияБП77.МетодРасчетаХВ КАК МетодРасчетаХВ,
        |	ЗданияБП77.МетодРасчетаГВ КАК МетодРасчетаГВ,
        |	ЗданияБП77.МетодРасчетаКан КАК МетодРасчетаКан
        |ПОМЕСТИТЬ ВТ_ЗданияБП77ДляПотерь
        |ИЗ
        |	РегистрСведений.ГП_ЗданияБП77 КАК ЗданияБП77
        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГП_КонтрагентыБП77 КАК КонтрагентыБП77
        |		ПО ЗданияБП77.КонтрагентКод = КонтрагентыБП77.Код
        |			И (ЗданияБП77.ЭтоПлатаЗаХолодноеВодоснабжениеОИ = ЛОЖЬ)
        |			И (ЗданияБП77.ЭтоНегативноеВоздействиеЦСВ = ЛОЖЬ)
        |			И (ЗданияБП77.ЭтоПотериВоды = ИСТИНА)
        |			И ВЫБОР
        |				КОГДА &Услуга = &УслугаХВС ТОГДА ЗданияБП77.МетодРасчетаХВ = ""По среднему""
        |				КОГДА &Услуга = &УслугаГВС ТОГДА ЗданияБП77.МетодРасчетаГВ = ""По среднему""
        |				ИНАЧЕ ЛОЖЬ
        |			КОНЕЦ
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	Счетчики.Ссылка КАК Ссылка,
        |	Счетчики.Наименование КАК Наименование,
        |	Счетчики.ГП_Комментарий КАК Комментарий
        |ПОМЕСТИТЬ ВТ_СуществующиеВиртуальныеСчетчики
        |ИЗ
        |	Справочник.КВП_Счетчики КАК Счетчики
        |ГДЕ
        |	Счетчики.ЭтоГруппа = ЛОЖЬ
        |	И Счетчики.ПометкаУдаления = ЛОЖЬ
        |	И Счетчики.ГП_ЭтоПлатаЗаВодоснабжениеОИ = ЛОЖЬ
        |	И Счетчики.ГП_ЭтоВиртуальныйСчетчик = ИСТИНА
        |	И Счетчики.ВидУслуги = &УслугаПотериВоды
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ЛицевыеСчета.Ссылка КАК ЛицевойСчет,
        |	ЛицевыеСчета.Адрес КАК Помещение,
        |	ЛицевыеСчета.Адрес.Владелец КАК Здание,
        |	ЛицевыеСчета.Идентификатор КАК Идентификатор
        |ПОМЕСТИТЬ ВТ_ЛицевыеСчетаПотерьВоды
        |ИЗ
        |	Справочник.КВП_ЛицевыеСчета КАК ЛицевыеСчета
        |ГДЕ
        |	ЛицевыеСчета.ЭтоГруппа = ЛОЖЬ
        |	И ЛицевыеСчета.ПометкаУдаления = ЛОЖЬ
        |	И ЛицевыеСчета.Идентификатор В
        |			(ВЫБРАТЬ
        |				Т.ИдентификаторЛС
        |			ИЗ
        |				ВТ_ЗданияБП77ДляПотерь КАК Т)
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВТ_ЗданияБП77ДляПотерь.КонтрагентКод КАК КонтрагентКод,
        |	ВТ_ЗданияБП77ДляПотерь.ОбъектАбонентаКод КАК ОбъектАбонентаКод,
        |	ВТ_ЗданияБП77ДляПотерь.ИдентификаторЛС КАК ИдентификаторЛС,
        |	ВТ_ЗданияБП77ДляПотерь.ОбъектАбонентаНаименование КАК ОбъектАбонентаНаименование,
        |	ВТ_ЗданияБП77ДляПотерь.ЭтоПотериВоды КАК ЭтоПотериВоды,
        |	ВТ_ЗданияБП77ДляПотерь.СрКоличествоХВ КАК СрКоличествоХВ,
        |	ВТ_ЗданияБП77ДляПотерь.СрКоличествоГВ КАК СрКоличествоГВ,
        |	ВТ_ЗданияБП77ДляПотерь.МетодРасчетаХВ КАК МетодРасчетаХВ,
        |	ВТ_ЗданияБП77ДляПотерь.МетодРасчетаГВ КАК МетодРасчетаГВ,
        |	ВТ_ЗданияБП77ДляПотерь.МетодРасчетаКан КАК МетодРасчетаКан,
        |	ВТ_ЗданияБП77ДляПотерь.ТолькоДляКанализации КАК ТолькоДляКанализации,
        |	ВТ_ЛицевыеСчетаПотерьВоды.ЛицевойСчет КАК ЛицевойСчет,
        |	ВТ_ЛицевыеСчетаПотерьВоды.Помещение КАК Помещение,
        |	ВТ_ЛицевыеСчетаПотерьВоды.Здание КАК Здание,
        |	ВТ_ЛицевыеСчетаПотерьВоды.Идентификатор КАК Идентификатор,
        |	&УслугаПотериВоды КАК УслугаПотериВоды,
        |	&Услуга КАК ИсходнаяУслуга
        |ПОМЕСТИТЬ ЗданияИЛицевыеСчетаПотерьВоды
        |ИЗ
        |	ВТ_ЗданияБП77ДляПотерь КАК ВТ_ЗданияБП77ДляПотерь
        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ЛицевыеСчетаПотерьВоды КАК ВТ_ЛицевыеСчетаПотерьВоды
        |		ПО ВТ_ЗданияБП77ДляПотерь.ИдентификаторЛС = ВТ_ЛицевыеСчетаПотерьВоды.Идентификатор
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ЗданияИЛицевыеСчетаПотерьВоды.КонтрагентКод КАК КонтрагентКод,
        |	ЗданияИЛицевыеСчетаПотерьВоды.ОбъектАбонентаКод КАК ОбъектАбонентаКод,
        |	ЗданияИЛицевыеСчетаПотерьВоды.ИдентификаторЛС КАК ИдентификаторЛС,
        |	ЗданияИЛицевыеСчетаПотерьВоды.ОбъектАбонентаНаименование КАК Наименование,
        |	ЗданияИЛицевыеСчетаПотерьВоды.ТолькоДляКанализации КАК ТолькоДляКанализации,
        |	ЗданияИЛицевыеСчетаПотерьВоды.ЭтоПотериВоды КАК ЭтоПотериВоды,
        |	ВЫБОР
        |		КОГДА ЗданияИЛицевыеСчетаПотерьВоды.ИсходнаяУслуга = &УслугаХВС ТОГДА ЗданияИЛицевыеСчетаПотерьВоды.СрКоличествоХВ
        |		КОГДА ЗданияИЛицевыеСчетаПотерьВоды.ИсходнаяУслуга = &УслугаГВС ТОГДА ЗданияИЛицевыеСчетаПотерьВоды.СрКоличествоГВ
        |		ИНАЧЕ 0
        |	КОНЕЦ КАК КоличествоПоСреднему,
        |	ВЫБОР
        |		КОГДА ЗданияИЛицевыеСчетаПотерьВоды.ИсходнаяУслуга = &УслугаХВС ТОГДА ЗданияИЛицевыеСчетаПотерьВоды.МетодРасчетаХВ
        |		КОГДА ЗданияИЛицевыеСчетаПотерьВоды.ИсходнаяУслуга = &УслугаГВС ТОГДА ЗданияИЛицевыеСчетаПотерьВоды.МетодРасчетаГВ
        |		ИНАЧЕ ""Нет""
        |	КОНЕЦ КАК МетодРасчета,
        |	ЗданияИЛицевыеСчетаПотерьВоды.МетодРасчетаКан КАК МетодРасчетаКан,
        |	ЗданияИЛицевыеСчетаПотерьВоды.ЛицевойСчет КАК ЛицевойСчет,
        |	ЗданияИЛицевыеСчетаПотерьВоды.Помещение КАК Помещение,
        |	ЗданияИЛицевыеСчетаПотерьВоды.Здание КАК Здание,
        |	ЗданияИЛицевыеСчетаПотерьВоды.УслугаПотериВоды КАК Услуга,
        |	ЗданияИЛицевыеСчетаПотерьВоды.ИсходнаяУслуга КАК ИсходнаяУслуга
        |ИЗ
        |	ЗданияИЛицевыеСчетаПотерьВоды КАК ЗданияИЛицевыеСчетаПотерьВоды
        |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СуществующиеВиртуальныеСчетчики КАК ВТ_СуществующиеВиртуальныеСчетчики
        |			ПО (ВТ_СуществующиеВиртуальныеСчетчики.Наименование = (ВЫРАЗИТЬ(""[[Потери воды]] "" + ЗданияИЛицевыеСчетаПотерьВоды.ОбъектАбонентаНаименование КАК СТРОКА(100))))
        |			И (ВТ_СуществующиеВиртуальныеСчетчики.Комментарий ПОДОБНО ""%(ид: ("" + ЗданияИЛицевыеСчетаПотерьВоды.ИдентификаторЛС + ""))"")
        |ГДЕ
        |	ВТ_СуществующиеВиртуальныеСчетчики.Ссылка ЕСТЬ NULL";

    РезультатФункции.Параметры.Вставить("УслугаХВС", "УслугаХВС");
    РезультатФункции.Параметры.Вставить("УслугаГВС", "УслугаГВС");
    РезультатФункции.Параметры.Вставить("Услуга", "Услуга");
    РезультатФункции.Параметры.Вставить("УслугаПотериВоды", "УслугаПотериВоды");

    Возврат РезультатФункции;
КонецФункции

// Возвращаемое значение:
//  - Структура
//      * ТекстЗапроса - Строка
//      * Параметры - Структура
//          ** Услуга - Строка
//          ** УслугаПотериВоды - Строка
//          ** УслугаХВС - Строка
//          ** УслугаГВС - Строка
Функция ПолучитьТекстЗапросаДанныхУстановкиВиртуальныхСчетчиковПотерьВоды()
    РезультатФункции = Новый Структура("ТекстЗапроса, Параметры", "", Новый Структура);

    РезультатФункции.ТекстЗапроса =
        "ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ЗданияБП77.КонтрагентКод КАК КонтрагентКод,
        |	ЗданияБП77.ОбъектАбонентаКод КАК ОбъектАбонентаКод,
        |	ЗданияБП77.ОбъектАбонентаКод + ""_"" + ЗданияБП77.КонтрагентКод КАК ИдентификаторЛС,
        |	ЗданияБП77.ОбъектАбонентаНаименование КАК ОбъектАбонентаНаименование,
        |	ЗданияБП77.ЭтоПотериВоды КАК ЭтоПотериВоды,
        |	ЗданияБП77.СрКоличествоХВ КАК СрКоличествоХВ,
        |	ЗданияБП77.СрКоличествоГВ КАК СрКоличествоГВ,
        |	ЗданияБП77.МетодРасчетаХВ КАК МетодРасчетаХВ,
        |	ЗданияБП77.МетодРасчетаГВ КАК МетодРасчетаГВ,
        |	ЗданияБП77.МетодРасчетаКан КАК МетодРасчетаКан
        |ПОМЕСТИТЬ ВТ_ЗданияБП77ДляПотерь
        |ИЗ
        |	РегистрСведений.ГП_ЗданияБП77 КАК ЗданияБП77
        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГП_КонтрагентыБП77 КАК КонтрагентыБП77
        |		ПО ЗданияБП77.КонтрагентКод = КонтрагентыБП77.Код
        |			И (ЗданияБП77.ЭтоПлатаЗаХолодноеВодоснабжениеОИ = ЛОЖЬ)
        |			И (ЗданияБП77.ЭтоНегативноеВоздействиеЦСВ = ЛОЖЬ)
        |			И (ЗданияБП77.ЭтоПотериВоды = ИСТИНА)
        |			И ВЫБОР
        |				КОГДА &Услуга = &УслугаХВС ТОГДА ЗданияБП77.МетодРасчетаХВ = ""По среднему""
        |				КОГДА &Услуга = &УслугаГВС ТОГДА ЗданияБП77.МетодРасчетаГВ = ""По среднему""
        |				ИНАЧЕ ЛОЖЬ
        |			КОНЕЦ
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	Счетчики.Ссылка КАК Ссылка,
        |	Счетчики.Наименование КАК Наименование,
        |	Счетчики.ГП_Комментарий КАК Комментарий
        |ПОМЕСТИТЬ ВТ_СуществующиеВиртуальныеСчетчики
        |ИЗ
        |	Справочник.КВП_Счетчики КАК Счетчики
        |ГДЕ
        |	Счетчики.ЭтоГруппа = ЛОЖЬ
        |	И Счетчики.ПометкаУдаления = ЛОЖЬ
        |	И Счетчики.ГП_ЭтоПлатаЗаВодоснабжениеОИ = ЛОЖЬ
        |	И Счетчики.ГП_ЭтоВиртуальныйСчетчик = ИСТИНА
        |	И Счетчики.ГП_ЭтоСчетчикПотерьВоды = ИСТИНА
        |	И Счетчики.ВидУслуги = &УслугаПотериВоды
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ЛицевыеСчета.Ссылка КАК ЛицевойСчет,
        |	ЛицевыеСчета.Адрес КАК Помещение,
        |	ЛицевыеСчета.Адрес.Владелец КАК Здание,
        |	ЛицевыеСчета.Идентификатор КАК Идентификатор
        |ПОМЕСТИТЬ ВТ_ЛицевыеСчетаПотерьВоды
        |ИЗ
        |	Справочник.КВП_ЛицевыеСчета КАК ЛицевыеСчета
        |ГДЕ
        |	ЛицевыеСчета.ЭтоГруппа = ЛОЖЬ
        |	И ЛицевыеСчета.ПометкаУдаления = ЛОЖЬ
        |	И ЛицевыеСчета.Идентификатор В
        |			(ВЫБРАТЬ
        |				Т.ИдентификаторЛС
        |			ИЗ
        |				ВТ_ЗданияБП77ДляПотерь КАК Т)
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВТ_ЗданияБП77ДляПотерь.КонтрагентКод КАК КонтрагентКод,
        |	ВТ_ЗданияБП77ДляПотерь.ОбъектАбонентаКод КАК ОбъектАбонентаКод,
        |	ВТ_ЗданияБП77ДляПотерь.ИдентификаторЛС КАК ИдентификаторЛС,
        |	ВТ_ЗданияБП77ДляПотерь.ОбъектАбонентаНаименование КАК ОбъектАбонентаНаименование,
        |	ВТ_ЗданияБП77ДляПотерь.ЭтоПотериВоды КАК ЭтоПотериВоды,
        |	ВТ_ЗданияБП77ДляПотерь.СрКоличествоХВ КАК СрКоличествоХВ,
        |	ВТ_ЗданияБП77ДляПотерь.СрКоличествоГВ КАК СрКоличествоГВ,
        |	ВТ_ЗданияБП77ДляПотерь.МетодРасчетаХВ КАК МетодРасчетаХВ,
        |	ВТ_ЗданияБП77ДляПотерь.МетодРасчетаГВ КАК МетодРасчетаГВ,
        |	ВТ_ЗданияБП77ДляПотерь.МетодРасчетаКан КАК МетодРасчетаКан,
        |	ВТ_ЛицевыеСчетаПотерьВоды.ЛицевойСчет КАК ЛицевойСчет,
        |	ВТ_ЛицевыеСчетаПотерьВоды.Помещение КАК Помещение,
        |	ВТ_ЛицевыеСчетаПотерьВоды.Здание КАК Здание,
        |	ВТ_ЛицевыеСчетаПотерьВоды.Идентификатор КАК Идентификатор,
        |	&УслугаПотериВоды КАК УслугаПотериВоды,
        |	&Услуга КАК ИсходнаяУслуга
        |ПОМЕСТИТЬ ЗданияИЛицевыеСчетаПотерьВоды
        |ИЗ
        |	ВТ_ЗданияБП77ДляПотерь КАК ВТ_ЗданияБП77ДляПотерь
        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ЛицевыеСчетаПотерьВоды КАК ВТ_ЛицевыеСчетаПотерьВоды
        |		ПО ВТ_ЗданияБП77ДляПотерь.ИдентификаторЛС = ВТ_ЛицевыеСчетаПотерьВоды.Идентификатор
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ЗданияИЛицевыеСчетаПотерьВоды.КонтрагентКод КАК КонтрагентКод,
        |	ЗданияИЛицевыеСчетаПотерьВоды.ОбъектАбонентаКод КАК ОбъектАбонентаКод,
        |	ЗданияИЛицевыеСчетаПотерьВоды.ИдентификаторЛС КАК ИдентификаторЛС,
        |	ЗданияИЛицевыеСчетаПотерьВоды.ЛицевойСчет КАК ЛицевойСчет,
        |	ЗданияИЛицевыеСчетаПотерьВоды.Помещение КАК Помещение,
        |	ЗданияИЛицевыеСчетаПотерьВоды.Здание КАК Здание,
        |	ЗданияИЛицевыеСчетаПотерьВоды.УслугаПотериВоды КАК Услуга,
        |	ВТ_СуществующиеВиртуальныеСчетчики.Ссылка КАК Счетчик
        |ИЗ
        |	ЗданияИЛицевыеСчетаПотерьВоды КАК ЗданияИЛицевыеСчетаПотерьВоды
        |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СуществующиеВиртуальныеСчетчики КАК ВТ_СуществующиеВиртуальныеСчетчики
        |			ПО (ВТ_СуществующиеВиртуальныеСчетчики.Наименование = (ВЫРАЗИТЬ(""[[Потери воды]] "" + ЗданияИЛицевыеСчетаПотерьВоды.ОбъектАбонентаНаименование КАК СТРОКА(100))))
        |			И (ВТ_СуществующиеВиртуальныеСчетчики.Комментарий ПОДОБНО ""%(ид: ("" + ЗданияИЛицевыеСчетаПотерьВоды.ИдентификаторЛС + ""))"")
        |ГДЕ
        |	НЕ ВТ_СуществующиеВиртуальныеСчетчики.Ссылка ЕСТЬ NULL
        |";

    РезультатФункции.Параметры.Вставить("УслугаХВС", "УслугаХВС");
    РезультатФункции.Параметры.Вставить("УслугаГВС", "УслугаГВС");
    РезультатФункции.Параметры.Вставить("Услуга", "Услуга");
    РезультатФункции.Параметры.Вставить("УслугаПотериВоды", "УслугаПотериВоды");

    Возврат РезультатФункции;
КонецФункции

Функция ПолучитьТекстЗапросаДанныхУстановкиТарифовСчетчиковПотерьВоды()
    РезультатФункции =
        "ВЫБРАТЬ
        |	ДанныеУстановкиСчетчиковПотерь.ЛицевойСчет КАК ЛицевойСчет,
        |	ДанныеУстановкиСчетчиковПотерь.Помещение КАК Помещение,
        |	ДанныеУстановкиСчетчиковПотерь.Здание КАК Здание,
        |	ДанныеУстановкиСчетчиковПотерь.ИдентификаторЛС КАК Идентификатор,
        |	ДанныеУстановкиСчетчиковПотерь.КонтрагентКод КАК КонтрагентКод,
        |	ДанныеУстановкиСчетчиковПотерь.ОбъектАбонентаКод КАК ОбъектАбонентаКод,
        |	ВЫРАЗИТЬ(ДанныеУстановкиСчетчиковПотерь.Счетчик КАК Справочник.КВП_Счетчики) КАК Счетчик
        |ПОМЕСТИТЬ ВТ_ДанныеУстановкиСчетчиковПотерь
        |ИЗ
        |	&ДанныеУстановкиСчетчиковПотерь КАК ДанныеУстановкиСчетчиковПотерь
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВТ_ДанныеУстановкиСчетчиковПотерь.ЛицевойСчет КАК ЛицевойСчет,
        |	ВТ_ДанныеУстановкиСчетчиковПотерь.Помещение КАК Помещение,
        |	ВТ_ДанныеУстановкиСчетчиковПотерь.Здание КАК Здание,
        |	ВТ_ДанныеУстановкиСчетчиковПотерь.Идентификатор КАК Идентификатор,
        |	ВТ_ДанныеУстановкиСчетчиковПотерь.КонтрагентКод КАК КонтрагентКод,
        |	ВТ_ДанныеУстановкиСчетчиковПотерь.ОбъектАбонентаКод КАК ОбъектАбонентаКод,
        |	ВТ_ДанныеУстановкиСчетчиковПотерь.Счетчик КАК Счетчик,
        |	ТарифыОбъектовАбонентовБП77.ТарифКод КАК ТарифКод,
        |	ТарифыОбъектовАбонентовБП77.ТарифВода КАК ТарифВода,
        |	ТарифыОбъектовАбонентовБП77.ТарифКанализация КАК ТарифКанализация,
        |	ТарифыОбъектовАбонентовБП77.ТарифНаименование КАК ТарифНаименование,
        |	ТарифыОбъектовАбонентовБП77.КонтрагентНаименование КАК КонтрагентНаименование,
        |	ТарифыОбъектовАбонентовБП77.ОбъектАбонентаНаименование КАК ОбъектАбонентаНаименование
        |ПОМЕСТИТЬ ВТ_ДанныеДляНачзначенияТарифов
        |ИЗ
        |	ВТ_ДанныеУстановкиСчетчиковПотерь КАК ВТ_ДанныеУстановкиСчетчиковПотерь
        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГП_ТарифыОбъектовАбонентовБП77 КАК ТарифыОбъектовАбонентовБП77
        |		ПО ВТ_ДанныеУстановкиСчетчиковПотерь.КонтрагентКод = ТарифыОбъектовАбонентовБП77.КонтрагентКод
        |			И ВТ_ДанныеУстановкиСчетчиковПотерь.ОбъектАбонентаКод = ТарифыОбъектовАбонентовБП77.ОбъектАбонентаКод
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВидыТарифов.Ссылка КАК Ссылка,
        |	ВидыТарифов.Код КАК Код,
        |	ВидыТарифов.Наименование КАК Наименование
        |ПОМЕСТИТЬ ВТ_ВидыТарифов
        |ИЗ
        |	Справочник.lc_ВидыТарифов КАК ВидыТарифов
        |ГДЕ
        |	ВидыТарифов.ПометкаУдаления = ЛОЖЬ
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВТ_ДанныеДляНачзначенияТарифов.ЛицевойСчет КАК ЛицевойСчет,
        |	ВТ_ДанныеДляНачзначенияТарифов.Помещение КАК Помещение,
        |	ВТ_ДанныеДляНачзначенияТарифов.Здание КАК Здание,
        |	ВТ_ДанныеДляНачзначенияТарифов.Идентификатор КАК Идентификатор,
        |	ВТ_ДанныеДляНачзначенияТарифов.КонтрагентКод КАК КонтрагентКод,
        |	ВТ_ДанныеДляНачзначенияТарифов.ОбъектАбонентаКод КАК ОбъектАбонентаКод,
        |	ВТ_ДанныеДляНачзначенияТарифов.ТарифКод КАК ТарифКод,
        |	ВТ_ДанныеДляНачзначенияТарифов.ТарифВода КАК ТарифВода,
        |	ВТ_ДанныеДляНачзначенияТарифов.ТарифКанализация КАК ТарифКанализация,
        |	ВТ_ДанныеДляНачзначенияТарифов.ТарифНаименование КАК ТарифНаименование,
        |	ВТ_ДанныеДляНачзначенияТарифов.КонтрагентНаименование КАК КонтрагентНаименование,
        |	ВТ_ДанныеДляНачзначенияТарифов.ОбъектАбонентаНаименование КАК ОбъектАбонентаНаименование,
        |	ВТ_ДанныеДляНачзначенияТарифов.Счетчик КАК Счетчик,
        |	ВТ_ДанныеДляНачзначенияТарифов.Счетчик.ВидУслуги КАК Услуга,
        |	ВТ_ВидыТарифов.Ссылка КАК ВидТарифа,
        |	ВТ_ВидыТарифов.Код КАК КодВидаТарифа,
        |	ВТ_ВидыТарифов.Наименование КАК НаименованиеВидаТарифа
        |ПОМЕСТИТЬ ВТ_Результат
        |ИЗ
        |	ВТ_ДанныеДляНачзначенияТарифов КАК ВТ_ДанныеДляНачзначенияТарифов
        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ВидыТарифов КАК ВТ_ВидыТарифов
        |		ПО ВТ_ДанныеДляНачзначенияТарифов.ТарифНаименование = ВТ_ВидыТарифов.Наименование
        |			И (ВТ_ДанныеДляНачзначенияТарифов.ТарифКод <> """")
        |			И (ВТ_ВидыТарифов.Код ПОДОБНО ""%"" + ВТ_ДанныеДляНачзначенияТарифов.ТарифКод)
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВТ_Результат.ВидТарифа КАК ВидТарифа,
        |	ВТ_Результат.Услуга КАК Услуга,
        |	ВТ_Результат.ЛицевойСчет КАК ЛицевойСчет,
        |	ВТ_Результат.Помещение КАК Помещение,
        |	ВТ_Результат.Здание КАК Здание,
        |	ВТ_Результат.Идентификатор КАК Идентификатор,
        |	ВТ_Результат.КонтрагентКод КАК КонтрагентКод,
        |	ВТ_Результат.ОбъектАбонентаКод КАК ОбъектАбонентаКод,
        |	ВТ_Результат.ТарифКод КАК ТарифКод,
        |	ВТ_Результат.ТарифВода КАК Тариф,
        |	ВТ_Результат.ТарифКанализация КАК ТарифКанализация,
        |	ВТ_Результат.ТарифНаименование КАК ТарифНаименование,
        |	ВТ_Результат.КонтрагентНаименование КАК КонтрагентНаименование,
        |	ВТ_Результат.ОбъектАбонентаНаименование КАК ОбъектАбонентаНаименование,
        |	ВТ_Результат.Счетчик КАК Счетчик,
        |	ВТ_Результат.КодВидаТарифа КАК КодВидаТарифа,
        |	ВТ_Результат.НаименованиеВидаТарифа КАК НаименованиеВидаТарифа
        |ИЗ
        |	ВТ_Результат КАК ВТ_Результат
        |ИТОГИ
        |	МАКСИМУМ(Услуга),
        |	МАКСИМУМ(Тариф),
        |	МАКСИМУМ(ТарифКанализация)
        |ПО
        |	ВидТарифа
        |";

    Возврат РезультатФункции;
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
// Гарант+ Килипенко 07.10.2024 [F00230765] перенос счетчиков Потерь воды (по среднему) --
