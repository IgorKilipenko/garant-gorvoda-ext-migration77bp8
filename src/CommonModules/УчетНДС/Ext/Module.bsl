// Гарант+ Килипенко 19.09.2024 [F00228718] Доработка заполнения отражения начислений в рег. учете ++
#Область СлужебныеПроцедурыИФункции

#Область ПереопределениеСтандартныхФункций

// Переход на использование макета ГП_ПФ_MXL_СчетФактура534 для внесения изменений в ПФ
&ИзменениеИКонтроль("ПечатьСчетовФактур534")
Функция ГП_ПечатьСчетовФактур534(МассивОбъектов, ОбъектыПечати, ТекстЗапросаПоСчетамФактурам, ПараметрыПечати, ПараметрыВывода)

    УстановитьПривилегированныйРежим(Истина);

    ТабДокумент = Новый ТабличныйДокумент;
    ТабДокумент.АвтоМасштаб = Истина;
    ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
    ТабДокумент.ЭкземпляровНаСтранице = 1;

    ТабДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СчетФактураВыданный_СчетФактура534";

    УстановкаМинимальныхПолейДляПечати(ТабДокумент);

    Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_СчетФактура534");
    #Вставка
    // Гарант+ Килипенко 19.09.2024 [F00228718] Доработка заполнения отражения начислений в рег. учете ++
    Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ГП_ПФ_MXL_СчетФактура534");
    // Гарант+ Килипенко 19.09.2024 [F00228718] Доработка заполнения отражения начислений в рег. учете --
    #КонецВставки

    Возврат ТабличныйДокументСчетаФактуры1137(
    Макет, МассивОбъектов, ОбъектыПечати, ТабДокумент, ТекстЗапросаПоСчетамФактурам, ПараметрыПечати, ПараметрыВывода);

КонецФункции

&ИзменениеИКонтроль("ПечатьСчетовФактур1096")
Функция ГП_ПечатьСчетовФактур1096(МассивОбъектов, ОбъектыПечати, ТекстЗапросаПоСчетамФактурам, ПараметрыПечати, ПараметрыВывода)

    УстановитьПривилегированныйРежим(Истина);

    ТабДокумент = Новый ТабличныйДокумент;
    ТабДокумент.АвтоМасштаб = Истина;
    ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
    ТабДокумент.ЭкземпляровНаСтранице = 1;

    ТабДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СчетФактураВыданный_СчетФактура1096";

    УстановкаМинимальныхПолейДляПечати(ТабДокумент);

    Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_СчетФактура1096");
    #Вставка
    // Гарант+ Килипенко 20.01.2025 [F00228718] Доработка заполнения отражения начислений в рег. учете ++
    Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ГП_ПФ_MXL_СчетФактура1096");
    // Гарант+ Килипенко 20.01.2025 [F00228718] Доработка заполнения отражения начислений в рег. учете --
    #КонецВставки

    Возврат ТабличныйДокументСчетаФактуры1137(
    Макет, МассивОбъектов, ОбъектыПечати, ТабДокумент, ТекстЗапросаПоСчетамФактурам, ПараметрыПечати, ПараметрыВывода);

КонецФункции

// Гарант+ Килипенко 29.01.2025 [F00233995] Очистка поля ПоДокументу ПФ Счет-фактуры ++
&Перед("ВывестиСчетФактуруВТабличныйДокумент")
Процедура ГП_ВывестиСчетФактуруВТабличныйДокумент(ТабДокумент, Макет, ВыборкаСФ, ЭтоУниверсальныйПередаточныйДокумент)
    ВыборкаСФ.ДанныеШапки.ПоДокументу = ""; // По требованию заказчика поле принудительно очищается

    // Замена наименований номенклатур псевдонимами
    Для Каждого СтрокаТаблицыДокумента Из ВыборкаСФ.ТаблицаДокумента Цикл
        СтрокаТаблицыДокумента.ТоварНаименование = ГП_РаботаСНоменклатурами.ЗаменитьНаименованиеНоменклатурыПсевдонимом(
            СтрокаТаблицыДокумента.ТоварНаименование, СтрокаТаблицыДокумента.Товар);
    КонецЦикла;

    // ->>> Гарант+ Килипенко 27.05.2025 [F00238550] Номера приказов счет-фактур

    ГП_ПредставлениеНомераПриказа = ГП_ПолучитьПредставлениеПриказаСчетФактуры(
        ?(ЗначениеЗаполнено(ВыборкаСФ.ДанныеШапки.Дата), ВыборкаСФ.ДанныеШапки.Дата, Неопределено), Истина);
    Если ГП_ПредставлениеНомераПриказа = Неопределено Тогда
        ГП_ПредставлениеНомераПриказа = "";
    КонецЕсли;
    ВыборкаСФ.ДанныеШапки.Вставить("ГП_ПредставлениеПриказа", ГП_ПредставлениеНомераПриказа);

    // <<<- Гарант+ Килипенко 27.05.2025 [F00238550] Номера приказов счет-фактур
КонецПроцедуры // Гарант+ Килипенко 29.01.2025 [F00233995] Очистка поля ПоДокументу ПФ Счет-фактуры --

#КонецОбласти // ПереопределениеСтандартныхФункций

// ->>> Гарант+ Килипенко 27.05.2025 [F00238550] Номера приказов счет-фактур
//
// Параметры:
//  ДатаАктуальности - Дата, Неопределено
//  ИспользоватьПривилегированныйРежим - Булево - По умолчанию Ложь
// Возвращаемое значение:
//  - Строка, Неопределено
Функция ГП_ПолучитьПредставлениеПриказаСчетФактуры(Знач ДатаАктуальности = Неопределено, Знач ИспользоватьПривилегированныйРежим = Ложь)
    РезультатФункции = Неопределено;

    Запрос = Новый Запрос;
    Запрос.Текст =
        "ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |   ПриказыДляСчетФактур.НомерПриказа КАК НомерПриказа
        |ИЗ
        |   РегистрСведений.ГП_ПриказыДляСчетФактур.СрезПоследних(
        |       &ДатаАктуальности, Сотрудник = ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)) КАК ПриказыДляСчетФактур
        |";

    Если ДатаАктуальности = Неопределено Тогда
        Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДатаАктуальности", "");
    Иначе
        Запрос.УстановитьПараметр("ДатаАктуальности", ДатаАктуальности);
    КонецЕсли;

    Если ИспользоватьПривилегированныйРежим Тогда
        УстановитьПривилегированныйРежим(Истина);
    КонецЕсли;

    РезультатЗапроса = Запрос.Выполнить();
    ТаблицаПриказов = РезультатЗапроса.Выгрузить();
    Если ТаблицаПриказов.Количество() > 0 Тогда
        РезультатФункции = ТаблицаПриказов[0].НомерПриказа;
    КонецЕсли;

    Возврат РезультатФункции;
КонецФункции // <<<- Гарант+ Килипенко 27.05.2025 [F00238550] Номера приказов счет-фактур

#КонецОбласти // СлужебныеПроцедурыИФункции
