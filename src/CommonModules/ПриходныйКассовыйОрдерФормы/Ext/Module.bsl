// Гарант+ Килипенко 22.01.2025 Сохранение ставки НДС при изменении Договора ++ {
#Область ПереопределениеСтандартногоФункционала

&ИзменениеИКонтроль("РасшифровкаПлатежаДоговорКонтрагентаПриИзмененииНаСервере")
Процедура ГП_РасшифровкаПлатежаДоговорКонтрагентаПриИзмененииНаСервере(Форма, СтрокаПлатеж)

	Объект = Форма.Объект;

	ПараметрыОбъекта = ПриходныйКассовыйОрдерФормыКлиентСервер.ТекущиеПараметрыОбъекта(Форма);
	ПараметрыОбъекта.ДоговорКонтрагента = СтрокаПлатеж.ДоговорКонтрагента;
	Форма.СвойстваПлатежа  = ПриходныйКассовыйОрдерФормыКлиентСервер.СвойстваСтрокРасшифровкиПлатежа(ПараметрыОбъекта, Ложь);
    #Вставка // Гарант+ Килипенко 22.01.2025 Сохранение ставки НДС при изменении Договора ++

    Если Форма.Объект.ВидОперации = Перечисления.ВидыОперацийПКО.ОплатаПокупателя
            И Форма.СвойстваПлатежа.Свойство("СтавкаНДС")
            И Форма.СвойстваПлатежа.СтавкаНДС <> СтрокаПлатеж.СтавкаНДС
            И ПустаяСтрока(Строка(СтрокаПлатеж.СтавкаНДС)) = Ложь Тогда

        Форма.СвойстваПлатежа.СтавкаНДС = СтрокаПлатеж.СтавкаНДС; // Сохраняем исходную ставку НДС
        ОбщегоНазначения.СообщитьПользователю("Внимание! Ставка НДС осталась без изменений");
    КонецЕсли;

    #КонецВставки // Гарант+ Килипенко 22.01.2025 Сохранение ставки НДС при изменении Договора --
	ЗаполнитьЗначенияСвойств(СтрокаПлатеж, Форма.СвойстваПлатежа);

	Если ЗначениеЗаполнено(СтрокаПлатеж.ДоговорКонтрагента) Тогда
		Если ЗначениеЗаполнено(СтрокаПлатеж.СчетНаОплату) Тогда
			ДоговорСчетаПокупателю = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаПлатеж.СчетНаОплату, "ДоговорКонтрагента");
			Если ЗначениеЗаполнено(ДоговорСчетаПокупателю)
				И ДоговорСчетаПокупателю <> СтрокаПлатеж.ДоговорКонтрагента Тогда
				СтрокаПлатеж.СчетНаОплату = Неопределено;
			КонецЕсли;
		КонецЕсли;
		Форма.ПредлагатьНовыйДоговор = Ложь;
	КонецЕсли;

	ПараметрыЗаполненияСчетовУчета = ПриходныйКассовыйОрдерФормыКлиентСервер.НачатьЗаполнениеСчетовУчета(
		"РасшифровкаПлатежа.ДоговорКонтрагента",
		Объект,
		СтрокаПлатеж);
	Изменения = СчетаУчетаВДокументах.ЗаполнитьРеквизитыПриИзменении(
		Документы.ПриходныйКассовыйОрдер,
		ПараметрыЗаполненияСчетовУчета.КЗаполнению,
		Объект,
		"РасшифровкаПлатежа",
		СтрокаПлатеж);
	ЗаполнитьЗначенияСвойств(Форма.СвойстваПлатежа, Изменения);
	ЗаполнитьЗначенияСвойств(СтрокаПлатеж,    Изменения);

	Если Форма.ПрименениеУСН Тогда
		ПриходныйКассовыйОрдерФормыКлиентСервер.ЗаполнитьОтражениеСтрокиВУСН(СтрокаПлатеж, Форма);
	КонецЕсли;

	ЗаполнитьЗначенияСвойств(СтрокаПлатеж, Форма.СвойстваПлатежа);

	ПриходныйКассовыйОрдерФормыКлиентСервер.ПересчитатьСуммуНДС(СтрокаПлатеж);
	ПриходныйКассовыйОрдерФормыКлиентСервер.РассчитатьСуммуВзаиморасчетов(СтрокаПлатеж, Форма);

	Если ПриходныйКассовыйОрдерФормыКлиентСервер.ФормаДокументаОднострочная(Форма) Тогда
		Если ЗначениеЗаполнено(Форма.РасшифровкаПлатежаДоговорКонтрагента) Тогда
			Форма.ЕстьРасчетыВУсловныхЕдиницах = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
				Форма.РасшифровкаПлатежаДоговорКонтрагента, "РасчетыВУсловныхЕдиницах");
		Иначе
			Форма.ЕстьРасчетыВУсловныхЕдиницах = Ложь;
		КонецЕсли;
	Иначе
		Форма.ЕстьРасчетыВУсловныхЕдиницах = Форма.ЕстьРасчетыВУсловныхЕдиницах ИЛИ СтрокаПлатеж.ДоговорКонтрагентаРасчетыВУсловныхЕдиницах;
	КонецЕсли;

	Если Не ПриходныйКассовыйОрдерФормыКлиентСервер.ФормаДокументаОднострочная(Форма) Тогда
		ПриходныйКассовыйОрдерФормыКлиентСервер.ЗаполнитьНадписиВРасшифровке(СтрокаПлатеж);
	КонецЕсли;

	Если ЗначениеЗаполнено(СтрокаПлатеж.ПорядокОтраженияАванса) Тогда
		ПредставлениеДохода = БанкИКассаФормыКлиентСервер.ВариантОтраженияДоходовПредставление(
			СтрокаПлатеж.ПорядокОтраженияАванса,
			БанкИКассаФормыКлиентСервер.ВариантыОтраженияДоходов(Форма));
		СтрокаПлатеж.ОтражениеАвансаПредставление = ПредставлениеДохода;
		СтрокаПлатеж.ОтражениеДоходаПредставление = ПредставлениеДохода;
	КонецЕсли;

	УправлениеФормой(Форма); // возможно нужно отобразить реквизиты курсов валюты

КонецПроцедуры

&ИзменениеИКонтроль("КонтрагентПриИзмененииСервер")
Процедура ГП_КонтрагентПриИзмененииСервер(Форма)
	Объект = Форма.Объект;
	ПричиныИзменения = Новый Массив;
	ПричиныИзменения.Добавить("Контрагент");

	ПараметрыОбъекта = ПриходныйКассовыйОрдерФормыКлиентСервер.ТекущиеПараметрыОбъекта(Форма);
	НовыеПараметры   = НовыеПараметрыПриИзмененииКонтрагента(ПараметрыОбъекта);

	Если ПриходныйКассовыйОрдерФормыКлиентСервер.ЕстьРасшифровкаПлатежа(Объект.ВидОперации) Тогда
		Если Объект.ВидОперации = Перечисления.ВидыОперацийПКО.РозничнаяВыручка Тогда
			ЗаполнитьЗначенияСвойств(Объект, НовыеПараметры, "ВыручкаСНТТ, СтавкаНДС");
			ЗаполнитьЗначенияСвойств(Форма, НовыеПараметры, "НТТНаЕНВД");
		Иначе
			ИнициализироватьСвойстваПлатежа(Форма, Истина);
			ЗаполнитьЗначенияСвойств(Форма.СвойстваПлатежа, НовыеПараметры);
		КонецЕсли;
		УстановитьОтражениеДоходов(Форма);
	КонецЕсли;

	ЗаполнитьЗначенияСвойств(Объект, НовыеПараметры, "ПринятоОт");

	ЗаполнитьСчетаУчета(Форма, "Контрагент", ПричиныИзменения);

	Если Форма.ПрименениеУСН Тогда
		ЗаполнитьОтражениеВУСН(Форма);
	КонецЕсли;
	УправлениеЭлементамиОтраженияВУСННаСервере(Форма);

    #Вставка // Гарант+ Килипенко 08.04.2025 [F00236135] Сохранение ставки НДС при изменении Контрагента ++ {

    ИсходнаяСтавкаНДС = Неопределено;
    Если Объект.ВидОперации = Перечисления.ВидыОперацийПКО.ОплатаПокупателя
            И Форма.СвойстваПлатежа.Свойство("СтавкаНДС") Тогда

        ИсходнаяРасшифровкаПлатежа = ПриходныйКассовыйОрдерФормыКлиентСервер.ПолучитьРасшифровкаПлатежа(Форма);
        ИсходнаяСтавкаНДС = ПолучитьСтавкуНДСРасшифровкиДляПерезаполнения(ИсходнаяРасшифровкаПлатежа);
    КонецЕсли;

    Если ПустаяСтрока(Строка(ИсходнаяСтавкаНДС)) = Ложь И Форма.СвойстваПлатежа.СтавкаНДС <> ИсходнаяСтавкаНДС Тогда
        Форма.СвойстваПлатежа.СтавкаНДС = ИсходнаяСтавкаНДС;
        ОбщегоНазначения.СообщитьПользователю(
            "Внимание! Ставка НДС осталась без изменений (принята из первой строки расшифровки платежа)");
    КонецЕсли;

    #КонецВставки // Гарант+ Килипенко 08.04.2025 [F00236135] Сохранение ставки НДС при изменении Контрагента -- }
	ПерезаполнитьРасшифровкуПлатежа(Форма);
	УправлениеФормой(Форма);

	ИзменитьУсловияФормированияСпискаВыбораДоговораСКонтрагентом(Форма);

	НастройкиНалоговИОтчетовПредупреждениеФормы.ОтобразитьПредупреждение(Форма, Объект.Организация, Объект.Дата, ПроверятьПатент(Форма));

	Если Объект.ВидОперации = Перечисления.ВидыОперацийПКО.РозничнаяВыручка
		И Объект.ВыручкаСНТТ И Форма.УчетВПродажныхЦенах Тогда
		УчетПСН.УстановитьПатентПриРеализации(Форма);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти // ПереопределениеСтандартногоФункционала
// Гарант+ Килипенко 22.01.2025 Сохранение ставки НДС при изменении Договора -- }

// Гарант+ Килипенко 08.04.2025 [F00236135] Сохранение ставки НДС при изменении Контрагента ++ {
#Область СлужебныйПрограммныйИнтерфейс

Функция ПолучитьСтавкуНДСРасшифровкиДляПерезаполнения(Знач РасшифровкаПлатежа) Экспорт
    Если РасшифровкаПлатежа.Количество() = 0 Тогда
        Возврат Неопределено;
    КонецЕсли;

    Возврат РасшифровкаПлатежа[0].СтавкаНДС;
КонецФункции

#КонецОбласти // СлужебныйПрограммныйИнтерфейс
// Гарант+ Килипенко 08.04.2025 [F00236135] Сохранение ставки НДС при изменении Контрагента -- }
