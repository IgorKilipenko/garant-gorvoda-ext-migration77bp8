// ++ Гарант+ Килипенко 02.08.2024 [F00226689] перенос зданий из БП77 ++
#Область ПрограммныйИнтерфейс

// Выполняет чтение данных зданий (из ИБ БП 7.7) из файла и записывает данные в регистр ГП_ЗданияБП77
// Параметры:
//  ДвоичныеДанныеФайла - ДвоичныеДанные
//  АдресХранилища - Строка, Неопределено
// Возвращаемое значение:
//  - Структура
//      * Успех - Булево
//      * КоличествоЗаписанныхЗданий - Число
//      * ТекстСообщения - Строка
//  - Неопределено
Функция ЗагрузитьДанныеЗданийВРегистр(ДвоичныеДанныеФайла, АдресХранилища = Неопределено) Экспорт
    РезультатФункции = Новый Структура("Успех, КоличествоЗаписанныхЗданий, ТекстСообщения", Ложь, 0);

    // Чтение данных из файла XML
    РезультатЧтенияДанных = ГП_МиграцияПриборовУчета.ПрочитатьДанныеИзФайлаXML(ДвоичныеДанныеФайла, "Здание");
    Если РезультатЧтенияДанных.Успех = Ложь Тогда
        РезультатФункции.ТекстСообщения = РезультатЧтенияДанных.ТекстСообщения;
        Возврат ПоместитьВХранилищеИВернуть(РезультатФункции, АдресХранилища);
    КонецЕсли;

    // Проверка структуры данных
    Если ТипЗнч(РезультатЧтенияДанных.Данные) <> Тип("СписокXDTO") Тогда
        РезультатФункции.ТекстСообщения = "Ошибка структуры данных зданий.";
        Возврат ПоместитьВХранилищеИВернуть(РезультатФункции, АдресХранилища);
    КонецЕсли;

    // Запись данных зданий в регистр
    УслугиБП77 = РезультатЧтенияДанных.Данные;
    РезультатЗаписи = ЗаписатьДанныеЗданийВРегистр(УслугиБП77, Истина);

    // Формирование результата
    РезультатФункции.Успех = РезультатЗаписи.Успех;
    РезультатФункции.ТекстСообщения = РезультатЗаписи.СообщениеОбОшибке;
    РезультатФункции.КоличествоЗаписанныхЗданий = РезультатЗаписи.КоличествоЗаписанныхЗданий;

    Возврат ПоместитьВХранилищеИВернуть(РезультатФункции, АдресХранилища);
КонецФункции

// Устанавливает связи Зданий (объектов абонентов) БП77 с объектами Зданий из ИБ
//  (записывает ссылку Здание поле регистра ГП_ЗданияБП77)
// Параметры:
//  РазрыватьСуществующиеСвязи - Булево
// Возвращаемое значение:
//  - Структура
//      * Успех - Булево
//      * ОбновленныеСвязи - Массив из СправочникСсылка.КВП_Здания
//      * ТекстСообщения - Строка
Функция УстановитьСоответствияЗданийПоЛицевымСчетам(Знач РазрыватьСуществующиеСвязи = Истина) Экспорт
    РезультатФункции = Новый Структура("Успех, ОбновленныеСвязи, ТекстСообщения", Истина, Новый Массив, "");

    ТаблицаСоответствияЗданий = ПолучитьЗданияОбъектовАбонентовДляСопоставленияПоЛицевымСетам(Ложь);

    НачатьТранзакцию();
    Попытка
        Для Каждого ЗданиеБП77 Из ТаблицаСоответствияЗданий Цикл
            Если ЗданиеБП77.Здание = ЗданиеБП77.ЗданиеСопоставление Тогда
                Продолжить; // Связь Здания не изменилась (связь уже установлена)
            КонецЕсли;

            // Разрыв связи с лицевым счетом
            Если ЗначениеЗаполнено(ЗданиеБП77.ЗданиеСопоставление) = Ложь Тогда
                Если РазрыватьСуществующиеСвязи Тогда
                    ЗданиеБП77.Здание = Справочники.КВП_Здания.ПустаяСсылка();
                Иначе
                    Продолжить;
                КонецЕсли;
            КонецЕсли;

            // Отбор записей регистра по составному коду
            НаборЗаписейУслуг = СоздатьНаборЗаписейЗданийБП77(ЗданиеБП77);
            НаборЗаписейУслуг.Прочитать();

            // Проверка валидности набора записей (набор должен содержать не более одной записи)
            НоваяЗапись = Неопределено;
            Если НаборЗаписейУслуг.Количество() = 0 Тогда
                НоваяЗапись = НаборЗаписейУслуг.Добавить();
            ИначеЕсли НаборЗаписейУслуг.Количество() = 1 Тогда
                НоваяЗапись = НаборЗаписейУслуг[0];
            КонецЕсли;
            Если НоваяЗапись = Неопределено Тогда
                ВызватьИсключение("Ошибка создания связей для зданий. Неоднозначные параметры отбора составного кода услуги.");
            КонецЕсли;

            // Запись связи с лицевым счетом
            НоваяЗапись.Здание = ЗданиеБП77.ЗданиеСопоставление;
            НаборЗаписейУслуг.Записать(Истина);
            РезультатФункции.ОбновленныеСвязи.Добавить(НоваяЗапись.Здание);
        КонецЦикла;

        ЗафиксироватьТранзакцию(); // Записано успешно
    Исключение
        ОтменитьТранзакцию();
        ОбщаяЧастьСообщения = "Ошибка при записи соответствий зданий в регистр ""ГП_ЗданияБП77"".";
        СтруктураОшибки = ГП_МиграцияОбщегоНазначения.ЗаписатьОшибкуВЖурнал(
                ОбщаяЧастьСообщения, Ложь, Истина);

        РезультатФункции.Успех = Ложь;
        РезультатФункции.ТекстСообщения = СтрШаблон(
                "%1
                |Информация об ошибке: %2",
                ОбщаяЧастьСообщения,
                СтруктураОшибки.КраткоеПредставлениеОшибки);

        РезультатФункции.ОбновленныеСвязи.Очистить();
    КонецПопытки;

    Возврат РезультатФункции;
КонецФункции

// Создает новые объекты зданий и помещений по данным из БП77 (из регистра ГП_ЗданияБП77).
//  Создаются здания только для тех записей где нет связи со Зданием (не заполнено поле Здание).
//  Перед созданием выполняется процедура установки связей для записей регистра
// Возвращаемое значение:
//  - Структура
//      * Успех - Булево
//      * СозданныеЗдания - Массив из СправочникСсылка.КВП_Здания
//      * СозданныеПомещения - Массив из СправочникСсылка.УПЖКХ_Помещения
//      * ТекстСообщения - Строка
Функция СоздатьЗданияПоДаннымЗаписейБП77() Экспорт
    РезультатФункции = Новый Структура(
            "Успех, СозданныеЗдания, СозданныеПомещения, ТекстСообщения", Истина, Новый Массив, Новый Массив, "");

    // Выполняем установку (обновление) связей зданий
    // РезультатУстановкиСвязей = УстановитьСоответствияЗданийПоЛицевымСчетам(Истина);
    // Если РезультатУстановкиСвязей.Успех = Ложь Тогда
    //     РезультатФункции.Успех = Ложь;
    //     РезультатФункции.ТекстСообщения = РезультатУстановкиСвязей.ТекстСообщения;
    //
    //     Возврат РезультатФункции; // Ошибка при создании (обновлении связей)
    // КонецЕсли;

    // Создание зданий
    ТаблицаЗданийБП77 = ПолучитьЗданияБП77ДляСозданияОбъектов();
    НачатьТранзакцию();
    Попытка
        Для Каждого ЗданиеБП77 Из ТаблицаЗданийБП77 Цикл
            Если ЗначениеЗаполнено(ЗданиеБП77.Здание) Тогда
                ВызватьИсключение("Таблица данных для создания зданий не должна иметь заполненных значений зданий.");
            КонецЕсли;

            // Создание здания
            НовыйЗданиеОбъект = СоздатьНовыйЭлементЗдания(Ложь);
            ЗаполнитьЗначенияПолейЗданияПоУмолчанию(НовыйЗданиеОбъект);
            НовыйЗданиеОбъект.Наименование = ЗданиеБП77.ОбъектАбонентаНаименование;
            НовыйЗданиеОбъект.ГП_ИдентификаторБП77 = СформироватьПростойСоставнойКодЗдания(
                    ЗданиеБП77.КонтрагентКод, ЗданиеБП77.ОбъектАбонентаКод);

            // Расшифровка адреса
            СтруктураАдреса = ГП_МиграцияОбщегоНазначения.ПолучитьАдресПоНаименованиюОбъектаАбонента(
                    ЗданиеБП77.ОбъектАбонентаНаименование);
            Если СтруктураАдреса.Успех Тогда
                НовыйЗданиеОбъект.НомерДома = СтруктураАдреса.Дом;
            КонецЕсли;

            НовыйЗданиеОбъект.Записать();
            ЗданиеБП77.Здание = НовыйЗданиеОбъект.Ссылка;
            РезультатФункции.СозданныеЗдания.Добавить(НовыйЗданиеОбъект.Ссылка);
        КонецЦикла;

        ЗафиксироватьТранзакцию(); // Записано успешно
    Исключение
        ОтменитьТранзакцию();

        ОбщаяЧастьСообщения = "Ошибка при создании зданий.";
        СтруктураОшибки = ГП_МиграцияОбщегоНазначения.ЗаписатьОшибкуВЖурнал(
                ОбщаяЧастьСообщения, Ложь, Истина);

        РезультатФункции.Успех = Ложь;
        РезультатФункции.ТекстСообщения = СтрШаблон(
                "%1
                |Информация об ошибке: %2",
                ОбщаяЧастьСообщения,
                СтруктураОшибки.КраткоеПредставлениеОшибки);

        РезультатФункции.СозданныеЗдания.Очистить();
        РезультатФункции.СозданныеПомещения.Очистить();
    КонецПопытки;

    Если РезультатФункции.Успех = Ложь Тогда
        Возврат РезультатФункции;
    КонецЕсли;

    // Создание помещений
    РезультатСозданияПомещений = СоздатьПомещенияПоДаннымЗаписейБП77(ТаблицаЗданийБП77);
    Если РезультатСозданияПомещений.Успех = Ложь Тогда
        // Удаление всех созданных зданий
        РезультатФункции.Успех = Ложь;
        РезультатФункции.ТекстСообщения = РезультатСозданияПомещений.ТекстСообщения;

        УдалитьСозданныеЗдания(РезультатФункции.СозданныеЗдания);

        РезультатФункции.СозданныеЗдания.Очистить();
        Возврат РезультатФункции;
    КонецЕсли;

    РезультатФункции.СозданныеПомещения = РезультатСозданияПомещений.СозданныеПомещения;

    // Запись связи здания в регистр
    РезультатЗаписейСвязей = ЗаписатьСозданныеЗданияКакСвязиВРегистр(ТаблицаЗданийБП77);
    Если РезультатЗаписейСвязей.Успех = Ложь Тогда
        // Удаление всех созданных зданий и помещений
        РезультатФункции.Успех = Ложь;
        РезультатФункции.ТекстСообщения = РезультатЗаписейСвязей.ТекстСообщения;

        УдалитьСозданныеПомещения(РезультатФункции.СозданныеЗдания);
        УдалитьСозданныеЗдания(РезультатФункции.СозданныеПомещения);

        РезультатФункции.СозданныеЗдания.Очистить();
        РезультатФункции.СозданныеПомещения.Очистить();
    КонецЕсли;

    Возврат РезультатФункции;
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс
// -- Гарант+ Килипенко 02.08.2024 [F00226689] перенос зданий из БП77 --

// ++ Гарант+ Килипенко 02.08.2024 [F00226689] перенос зданий из БП77 ++
#Область СлужебныйПрограммныйИнтерфейс

// Выполняет запись исходных данных зданий в регистр ГП_ЗданияП77 (по данным из БП77)
//  Существующие записи по отбору составного кода будут перезаписаны
// Параметры:
//  ИсходныеДанныеЗданий - Массив из Структура
//  СохранятьСвязи - Булево - Если Истина, связи с лицевыми счетами будут сохранены
// Возвращаемое значение:
//  Структура:
//      * Успех - Булево
//      * КоличествоЗаписанныхСчетчиков - Число
//      * КоличествоСохраненныхСвязей - Число
//      * СообщениеОбОшибке - Строка, Неопределено
Функция ЗаписатьДанныеЗданийВРегистр(Знач ИсходныеДанныеЗданий, Знач СохранятьСвязи = Истина) Экспорт
    РезультатФункции = Новый Структура("Успех, КоличествоЗаписанныхЗданий, КоличествоСохраненныхСвязей, СообщениеОбОшибке", Истина, 0, 0);

    Если ИсходныеДанныеЗданий = Неопределено ИЛИ ИсходныеДанныеЗданий.Количество() = 0 Тогда
        Возврат РезультатФункции; // Нет данных
    КонецЕсли;

    // Поля для предобработки (приведения типов)
    КлючиУсловийСчетчика = "ТолькоДляКанализации,ЭтоНегативноеВоздействиеЦСВ,НеНачислять";
    КлючиЧиселСчетчика = "ПроцентХВ,ПроцентГВ,КоличествоПроживающих";

    // Транзакция записи счетчиков в регистр ГП_СчетчикиБП77
    НачатьТранзакцию();
    Попытка

        Для Каждого ЗданиеБП77 Из ИсходныеДанныеЗданий Цикл
            НаборЗаписейЗданий = СоздатьНаборЗаписейЗданийБП77(ЗданиеБП77);

            // Данные для сохранения связанного здания записи
            СвязанноеЗдание = Неопределено;
            Если СохранятьСвязи Тогда
                НаборЗаписейЗданий.Прочитать();
                Если НаборЗаписейЗданий.Количество() > 0 Тогда
                    СвязанноеЗдание = НаборЗаписейЗданий[0].Здание;
                    СвязанноеЗдание = ?(ЗначениеЗаполнено(СвязанноеЗдание), СвязанноеЗдание, Неопределено);
                    НаборЗаписейЗданий.Очистить();
                КонецЕсли;
            КонецЕсли;
            НоваяЗапись = НаборЗаписейЗданий.Добавить();

            // Заполнение данных здания
            ЗаполнитьЗначенияСвойств(НоваяЗапись, ЗданиеБП77, ,
                СтрШаблон("%1,%2", КлючиУсловийСчетчика, КлючиЧиселСчетчика));

            Для Каждого Ключ Из СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(КлючиУсловийСчетчика, ",", Ложь, Истина) Цикл
                НоваяЗапись[Ключ] = ?(ЗданиеБП77[Ключ] = "1", Истина, Ложь);
            КонецЦикла;
            Для Каждого Ключ Из СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(КлючиЧиселСчетчика, ",", Ложь, Истина) Цикл
                НоваяЗапись[Ключ] = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(ЗданиеБП77[Ключ]);
            КонецЦикла;

            // Сохранение связанного здания записи
            Если СвязанноеЗдание <> Неопределено Тогда
                НоваяЗапись.Здание = СвязанноеЗдание;
                РезультатФункции.КоличествоСохраненныхСвязей = РезультатФункции.КоличествоСохраненныхСвязей + 1;
            КонецЕсли;

            // Заполнение признака ЭтоПлатаЗаХолодноеВодоснабжениеОИ
            ЭтоПлатаЗаХолодноеВодоснабжениеОИ =
                ГП_МиграцияОбщегоНазначения.ЭтоНаименованиеУслугиХолодногоВодоснабженияОИ(ЗданиеБП77.ОбъектАбонентаНаименование);
            НоваяЗапись.ЭтоПлатаЗаХолодноеВодоснабжениеОИ = ЭтоПлатаЗаХолодноеВодоснабжениеОИ;

            // Заполнение признака ЭтоНегативноеВоздействиеЦСВ
            ЭтоНегативноеВоздействиеЦСВ =
                ГП_МиграцияОбщегоНазначения.ЭтоНаименованиеУслугиНегативногоВоздействия(ЗданиеБП77.ОбъектАбонентаНаименование);
            НоваяЗапись.ЭтоНегативноеВоздействиеЦСВ = ЭтоНегативноеВоздействиеЦСВ;

            // Заполнение поля ДоговорНомер
            СтруктураНомера = ГП_МиграцияОбщегоНазначения.ПолучитьНомерДоговораПоНаименованию(ЗданиеБП77.ДоговорНаименование);
            Если СтруктураНомера.Успех = Истина Тогда
                НоваяЗапись.ДоговорНомер = СтруктураНомера.Номер + ?(ПустаяСтрока(СтруктураНомера.Суффикс),
                        "", СтрШаблон("-%1", СтруктураНомера.Суффикс));
            КонецЕсли;

            НаборЗаписейЗданий.Записать(Истина);
            РезультатФункции.КоличествоЗаписанныхЗданий = РезультатФункции.КоличествоЗаписанныхЗданий + 1;
        КонецЦикла;

        ЗафиксироватьТранзакцию(); // Записано успешно
    Исключение
        ОтменитьТранзакцию();

        ЖурналРегистрации.ДобавитьСообщениеДляЖурналаРегистрации(
            "Ошибка при загрузке данных зданий в регистр ""ГП_ЗданияБП77"".",
            УровеньЖурналаРегистрации.Ошибка, , ,
            ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));

        РезультатФункции.КоличествоЗаписанныхЗданий = 0;
        РезультатФункции.КоличествоСохраненныхСвязей = 0;
        РезультатФункции.СообщениеОбОшибке = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
        РезультатФункции.Успех = Ложь;
    КонецПопытки;

    Возврат РезультатФункции;
КонецФункции

// Получает таблицу соответствий (связей) зданий (объектов абонентов) из БП77 и Зданий из ИБ
// Параметры:
//  ДляВсехЗданий - Булево - Если Ложь, будут выбраны только здания без признаков: [ЭтоПлатаЗаХолодноеВодоснабжениеОИ,
//      ЭтоНегативноеВоздействиеЦСВ], иначе все здания из регистра. По умолчанию = Ложь
// Возвращаемое значение:
//  - ТаблицаЗначений
//      * КонтрагентКод - Строка
//      * ОбъектАбонентаКод - Строка
//      * Здание - СправочникСсылка.УВП_Здания
//      * ЗданиеСопоставление - СправочникСсылка.УВП_Здания
//      * ЛицевойСчетСопоставление - СправочникСсылка.УВП_ЛицевыеСчета
Функция ПолучитьЗданияОбъектовАбонентовДляСопоставленияПоЛицевымСетам(Знач ДляВсехЗданий = Ложь) Экспорт
    Запрос = Новый Запрос;
    Запрос.Текст =
        "ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ГП_ЗданияБП77.КонтрагентКод КАК КонтрагентКод,
        |	ГП_ЗданияБП77.ОбъектАбонентаКод КАК ОбъектАбонентаКод,
        |	ГП_ЗданияБП77.Здание КАК Здание,
        |	ГП_ЗданияБП77.ОбъектАбонентаКод + ""_"" + ГП_ЗданияБП77.КонтрагентКод КАК СоставнойИдентификатор
        |ПОМЕСТИТЬ ВТ_ЗданияБП77
        |ИЗ
        |	РегистрСведений.ГП_ЗданияБП77 КАК ГП_ЗданияБП77
        |%УсловиеОбораЗданий%
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВТ_ЗданияБП77.КонтрагентКод КАК КонтрагентКод,
        |	ВТ_ЗданияБП77.ОбъектАбонентаКод КАК ОбъектАбонентаКод,
        |	ВТ_ЗданияБП77.Здание КАК Здание,
        |   ВЫБОР
        |       КОГДА (КВП_ЛицевыеСчета.Ссылка ЕСТЬ NULL) ИЛИ (КВП_ЛицевыеСчета.Ссылка = &ПустаяСсылкаЛС)
        |           ТОГДА &ПустаяСсылкаЗдание
        |       ИНАЧЕ
        |           КВП_ЛицевыеСчета.Адрес.Владелец
        |   КОНЕЦ КАК ЗданиеСопоставление,
        |	ЕСТЬNULL(КВП_ЛицевыеСчета.Ссылка, &ПустаяСсылкаЛС) КАК ЛицевойСчетСопоставление
        |ИЗ
        |	ВТ_ЗданияБП77 КАК ВТ_ЗданияБП77
        |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КВП_ЛицевыеСчета КАК КВП_ЛицевыеСчета
        |		ПО ВТ_ЗданияБП77.СоставнойИдентификатор = КВП_ЛицевыеСчета.Идентификатор
        |";

    УсловиеОбораЗданий = "";
    Если ДляВсехЗданий Тогда
        УсловиеОбораЗданий =
            "ГДЕ
            |   ГП_ЗданияБП77.ЭтоПлатаЗаХолодноеВодоснабжениеОИ = ЛОЖЬ
            |   И ГП_ЗданияБП77.ЭтоНегативноеВоздействиеЦСВ = ЛОЖЬ
            |";
    КонецЕсли;
    Запрос.Текст = СтрЗаменить(Запрос.Текст, "%УсловиеОбораЗданий%", УсловиеОбораЗданий);

    Запрос.УстановитьПараметр("ПустаяСсылкаЛС", Справочники.КВП_ЛицевыеСчета.ПустаяСсылка());
    Запрос.УстановитьПараметр("ПустаяСсылкаЗдание", Справочники.КВП_Здания.ПустаяСсылка());

    РезультатЗапроса = Запрос.Выполнить();
    РезультатФункции = РезультатЗапроса.Выгрузить();
    Возврат РезультатФункции;
КонецФункции

// Параметры:
//  ЗданиеБП77 - Структура
//      * КонтрагентКод - Строка
//      * ЗданиеКод - Строка
// Возвращаемое значение:
//  - РегистрСведений.ГП_ЗданияБП77.НаборЗаписей
Функция СоздатьНаборЗаписейЗданийБП77(Знач ЗданиеБП77) Экспорт
    НаборЗаписейУслуг = РегистрыСведений.ГП_ЗданияБП77.СоздатьНаборЗаписей();
    НаборЗаписейУслуг.Отбор.КонтрагентКод.Установить(ЗданиеБП77.КонтрагентКод);
    НаборЗаписейУслуг.Отбор.ОбъектАбонентаКод.Установить(ЗданиеБП77.ОбъектАбонентаКод);

    Возврат НаборЗаписейУслуг;
КонецФункции

// Формирует наименование помещения как номер договора
// Параметры:
//  НаименованиеДоговора - Строка
// Возвращаемое значение:
//  - Структура
//      * Успех - Булево
//      * НаименованиеПомещения - Строка, Неопределено
Функция СформироватьНаименованиеПомещенияИзНаименованияДоговора(Знач НаименованиеДоговора) Экспорт
    РезультатФункции = Новый Структура("Успех, НаименованиеПомещения", Ложь);

    СтруктураНомера = ГП_МиграцияОбщегоНазначения.ПолучитьНомерДоговораПоНаименованию(НаименованиеДоговора);

    Если СтруктураНомера.Успех = Ложь Тогда
        Возврат РезультатФункции; // Номер не распознан
    КонецЕсли;

    РезультатФункции.Успех = Истина;
    РезультатФункции.НаименованиеПомещения = СтруктураНомера.Номер + ?(ПустаяСтрока(СтруктураНомера.Суффикс),
            "", СтрШаблон("-%1", СтруктураНомера.Суффикс));
    Возврат РезультатФункции;
КонецФункции

// Получает все записи зданий БП77 для которых не созданы здания в ИБ (отбор выполняется по критерию отсутствия связи для Здания)
// Возвращаемое значение:
//  - ТаблицаЗначений
//      * КонтрагентКод - Строка
//      * ОбъектАбонентаКод - ОбъектАбонентаКод
//      * Здание - СправочникСсылка.КВП_Здания
//      * ОбъектАбонентаНаименование - Строка
//      * ДоговорНаименование - Строка
Функция ПолучитьЗданияБП77ДляСозданияОбъектов() Экспорт
    Запрос = Новый Запрос;
    Запрос.Текст =
        "ВЫБРАТЬ
        |   ГП_ЗданияБП77.КонтрагентКод КАК КонтрагентКод,
        |   ГП_ЗданияБП77.ОбъектАбонентаКод КАК ОбъектАбонентаКод,
        |   ГП_ЗданияБП77.Здание КАК Здание,
        |   ГП_ЗданияБП77.ОбъектАбонентаНаименование КАК ОбъектАбонентаНаименование,
        |   ГП_ЗданияБП77.ДоговорНаименование КАК ДоговорНаименование
        |ИЗ
        |   РегистрСведений.ГП_ЗданияБП77 КАК ГП_ЗданияБП77
        |ГДЕ
        |   ГП_ЗданияБП77.ЭтоПлатаЗаХолодноеВодоснабжениеОИ = ЛОЖЬ
        |   И ГП_ЗданияБП77.ЭтоНегативноеВоздействиеЦСВ = ЛОЖЬ
        |   И ГП_ЗданияБП77.Здание = &ПустаяСсылкаЗдание
        |";

    Запрос.УстановитьПараметр("ПустаяСсылкаЗдание", Справочники.КВП_Здания.ПустаяСсылка());

    РезультатЗапроса = Запрос.Выполнить();
    Возврат РезультатЗапроса.Выгрузить();
КонецФункции

// Устарела. Не используется
//
// Параметры:
//  ТолькоСозданныеАвтоматически - Булево
// Возвращаемое значение:
//  - ТаблицаЗначений
//      * Ссылка - СправочникСсылка.КВП_Здания
//      * ГП_ИдентификаторБП77 - Строка
//      * ГП_СозданАвтоматически - Булево
//      * ПометкаУдаления - Булево
Функция ПолучитьСозданныеЗданияПоДаннымБП77(Знач ТолькоСозданныеАвтоматически = Истина) Экспорт
    Запрос = Новый Запрос;
    Запрос.Текст =
        "ВЫБРАТЬ
        |   КВП_Здания.Ссылка КАК Ссылка,
        |   КВП_Здания.ГП_ИдентификаторБП77 КАК ИдентификаторБП77,
        |   КВП_Здания.ГП_СозданАвтоматически КАК СозданАвтоматически,
        |   КВП_Здания.ПометкаУдаления КАК ПометкаУдаления
        |ИЗ
        |   Справочник.КВП_Здания КАК КВП_Здания
        |ГДЕ
        |   КВП_Здания.ГП_ИдентификаторБП77 ПОДОБНО ""%[0-9][_-][0-9]%""
        |   И КВП_Здания.ГП_СозданАвтоматически = ИСТИНА
        |";

    Если ТолькоСозданныеАвтоматически = Ложь Тогда
        Запрос.Текст = СтрЗаменитьПоРегулярномуВыражению(Запрос.Текст, "И КВП_Здания.ГП_СозданАвтоматически = ИСТИНА\s*$", "");
    КонецЕсли;

    РезультатЗапроса = Запрос.Выполнить();
    Возврат РезультатЗапроса.Выгрузить();
КонецФункции

// Параметры:
//  КонтрагентКод - Строка
//  ОбъектАбонентаКод - Строка
// Возвращаемое значение:
//  - Строка
Функция СформироватьПростойСоставнойКодЗдания(Знач КонтрагентКод, Знач ОбъектАбонентаКод) Экспорт
    Возврат СтрШаблон("%1_%2", КонтрагентКод, ОбъектАбонентаКод);
КонецФункции

#КонецОбласти // СлужебныйПрограммныйИнтерфейс
// -- Гарант+ Килипенко 02.08.2024 [F00226689] перенос зданий из БП77 --

// ++ Гарант+ Килипенко 02.08.2024 [F00226689] перенос зданий из БП77 ++
#Область СлужебныеПроцедурыИФункции

// Служебная функция. Помещает результат во временное хранилище или выполняет возврат результата
// Параметры:
//  Результат - Произвольный
//  АдресХранилища - Строка, Неопределено
// Возвращаемое значение:
//  - Произвольный
Функция ПоместитьВХранилищеИВернуть(Знач Результат, Знач АдресХранилища = Неопределено) Экспорт
    Возврат ГП_МиграцияОбщегоНазначения.ПоместитьВХранилищеИВернуть(Результат, АдресХранилища);
КонецФункции

// Параметры:
//  ЗаполнитьЗначенияПоУмолчанию - Булево
// Возвращаемое значение:
//  - СправочникОбъект.КВП_Здания
Функция СоздатьНовыйЭлементЗдания(Знач ЗаполнитьЗначенияПоУмолчанию = Ложь)
    НовоеЗданиеОбъект = Справочники.КВП_Здания.СоздатьЭлемент();

    Если ЗаполнитьЗначенияПоУмолчанию = Истина Тогда
        ЗаполнитьЗначенияПолейЗданияПоУмолчанию(НовоеЗданиеОбъект);
    КонецЕсли;

    Возврат НовоеЗданиеОбъект;
КонецФункции

// Параметры:
//  ЗданиеОбъект - СправочникОбъект.КВП_Здания
Процедура ЗаполнитьЗначенияПолейЗданияПоУмолчанию(Знач ЗданиеОбъект)
    ЗданиеОбъект.ГП_СозданАвтоматически = Истина;
    ЗданиеОбъект.ТипСооружения = Перечисления.УПЖКХ_ТипСооружения.Здание;
    ЗданиеОбъект.НомерДома = "1";
    ЗданиеОбъект.Многоквартирное = Истина;
    ЗданиеОбъект.Этажей = 1;

    //	Тип собственности здания определяется по виду собственности помещений данного здания:
    //  если в квартирах здания все квартиры муниципальные - дом муниципальный;
    //	если в квартирах здания все квартиры государственные - дом государственный;
    //	в противном случае дом общий.
    ЗданиеОбъект.Собственность = Перечисления.КВП_ТипыСобственностиЗданий.Частная; // Уточнить!
КонецПроцедуры

// Параметры:
//  ЗаполнитьЗначенияПоУмолчанию - Булево
// Возвращаемое значение:
//  - СправочникОбъект.УПЖКХ_Помещения
Функция СоздатьНовыйЭлементПомещение(Знач ЗаполнитьЗначенияПоУмолчанию = Истина)
    НовоеПомещениеОбъект = Справочники.УПЖКХ_Помещения.СоздатьЭлемент();

    Если ЗаполнитьЗначенияПоУмолчанию Тогда
        НовоеПомещениеОбъект.ГП_СозданАвтоматически = Истина;
        НовоеПомещениеОбъект.ВидПомещения = ПолучитьВидПомещенияПоУмолчанию();
    КонецЕсли;

    Возврат НовоеПомещениеОбъект;
КонецФункции

// Возвращаемое значение:
//  - Справочники.УПЖКХ_ВидыПомещений
Функция ПолучитьВидПомещенияПоУмолчанию()
    РезультатФункции = Справочники.УПЖКХ_ВидыПомещений.НайтиПоКоду("00-000001");
    Возврат РезультатФункции;
КонецФункции

// Устарела. Не используется
Функция УдалитьНазначитьВладельцевПомещений(Знач МассивПомещений, Знач МассивЗданий, Знач УдалятьЕслиОшибка)
    РезультатФункции = Новый Структура("Успех, ТекстСообщения", Истина);

    Если МассивПомещений.Количество() <> МассивЗданий.Количество() Тогда
        Возврат РезультатФункции;
    КонецЕсли;

    НачатьТранзакцию();
    Попытка
        Для Счетчик = 0 По МассивЗданий.Количество() - 1 Цикл
            ПомещениеОбъект = МассивПомещений[Счетчик].ПолучитьОбъект();
            ПомещениеОбъект.Владелец = МассивЗданий[Счетчик];
            ПомещениеОбъект.Записать();
        КонецЦикла;

        ЗафиксироватьТранзакцию(); // Записано успешно
    Исключение
        ОтменитьТранзакцию();

        ОбщаяЧастьСообщения = "Ошибка при назначении владельцев помещений.";
        СтруктураОшибки = ГП_МиграцияОбщегоНазначения.ЗаписатьОшибкуВЖурнал(
                ОбщаяЧастьСообщения, Ложь, Истина);

        РезультатФункции.Успех = Ложь;
        РезультатФункции.ТекстСообщения = СтрШаблон(
                "%1
                |Информация об ошибке: %2",
                ОбщаяЧастьСообщения,
                СтруктураОшибки.КраткоеПредставлениеОшибки);
    КонецПопытки;

    Если УдалятьЕслиОшибка И РезультатФункции.Успех = Ложь Тогда
        // Удаление всех созданных объектов
        Для Счетчик = 0 По МассивЗданий.Количество() - 1 Цикл
            ПомещениеОбъект = МассивЗданий[Счетчик].ПолучитьОбъект();
            ПомещениеОбъект.Удалить();

            ЗданиеОбъект = МассивПомещений[Счетчик].ПолучитьОбъект();
            ЗданиеОбъект.Удалить();
        КонецЦикла;
    КонецЕсли;

    Возврат РезультатФункции;
КонецФункции

// Параметры:
//  ТаблицаЗданийБП77 - ТаблицаЗначений
//      * КонтрагентКод - Строка
//      * ОбъектАбонентаКод - Строка
//      * Здание - СправочникСсылка.КВП_Здания
// Возвращаемое значение:
//  - Структура
//      * Успех - Булево
//      * СозданныеПомещения - Массив из СправочникСсылка.УПЖКХ_Помещения
//      * ТекстСообщения - Строка
Функция СоздатьПомещенияПоДаннымЗаписейБП77(Знач ТаблицаЗданийБП77)
    РезультатФункции = Новый Структура(
            "Успех, СозданныеПомещения, ТекстСообщения", Истина, Новый Массив, "");

    НачатьТранзакцию();
    Попытка
        Для Каждого ЗданиеБП77 Из ТаблицаЗданийБП77 Цикл
            Если ЗначениеЗаполнено(ЗданиеБП77.Здание) = Ложь Тогда
                ВызватьИсключение("Для создания помещений все здания должны быть записаны.");
            КонецЕсли;

            // Создание помещения
            НовыйПомещениеОбъект = СоздатьНовыйЭлементПомещение(Истина);
            НовыйПомещениеОбъект.Наименование = "";
            СтруктураНомера = ГП_МиграцияОбщегоНазначения.ПолучитьНомерДоговораПоНаименованию(ЗданиеБП77.ДоговорНаименование);
            Если СтруктураНомера.Успех Тогда
                НовыйПомещениеОбъект.Наименование = СтруктураНомера.Номер + ?(ПустаяСтрока(СтруктураНомера.Суффикс),
                        "", СтрШаблон("-%1", СтруктураНомера.Суффикс));
            КонецЕсли;
            НовыйПомещениеОбъект.ГП_ИдентификаторБП77 = СформироватьПростойСоставнойКодЗдания(
                    ЗданиеБП77.КонтрагентКод, ЗданиеБП77.ОбъектАбонентаКод);
            НовыйПомещениеОбъект.Владелец = ЗданиеБП77.Здание;
            НовыйПомещениеОбъект.Записать();
            РезультатФункции.СозданныеПомещения.Добавить(НовыйПомещениеОбъект.Ссылка);
        КонецЦикла;

        ЗафиксироватьТранзакцию(); // Записано успешно
    Исключение
        ОтменитьТранзакцию();

        ОбщаяЧастьСообщения = "Ошибка при создании помещений.";
        СтруктураОшибки = ГП_МиграцияОбщегоНазначения.ЗаписатьОшибкуВЖурнал(
                ОбщаяЧастьСообщения, Ложь, Истина);

        РезультатФункции.Успех = Ложь;
        РезультатФункции.ТекстСообщения = СтрШаблон(
                "%1
                |Информация об ошибке: %2",
                ОбщаяЧастьСообщения,
                СтруктураОшибки.КраткоеПредставлениеОшибки);

        РезультатФункции.СозданныеПомещения.Очистить();
    КонецПопытки;

    Возврат РезультатФункции;
КонецФункции

// Параметры:
//  ТаблицаЗданийБП77 - ТаблицаЗначений
//      * КонтрагентКод - Строка
//      * ОбъектАбонентаКод - Строка
//      * Здание - СправочникСсылка.КВП_Здания
// Возвращаемое значение:
//  - Структура
//      * Успех - Булево
//      * ТекстСообщения - Строка
Функция ЗаписатьСозданныеЗданияКакСвязиВРегистр(Знач ТаблицаЗданийБП77)
    РезультатФункции = Новый Структура("Успех, ТекстСообщения", Истина);

    НачатьТранзакцию();
    Попытка
        Для Каждого ЗданиеБП77 Из ТаблицаЗданийБП77 Цикл
            НаборЗаписейЗданиеБП77 = СоздатьНаборЗаписейЗданийБП77(ЗданиеБП77);
            НаборЗаписейЗданиеБП77.Прочитать();
            Если НаборЗаписейЗданиеБП77.Количество() > 1 Тогда
                ВызватьИсключение("Запись в регистре ""Здания БП 7.7"" не уникальна.");
            КонецЕсли;
            НаборЗаписейЗданиеБП77[0].Здание = ЗданиеБП77.Здание;
            НаборЗаписейЗданиеБП77.Записать(Истина);
        КонецЦикла;

        ЗафиксироватьТранзакцию(); // Записано успешно
    Исключение
        ОтменитьТранзакцию();

        ОбщаяЧастьСообщения = "Ошибка при записи связей созданных зданий в регистр.";
        СтруктураОшибки = ГП_МиграцияОбщегоНазначения.ЗаписатьОшибкуВЖурнал(
                ОбщаяЧастьСообщения, Ложь, Истина);

        РезультатФункции.Успех = Ложь;
        РезультатФункции.ТекстСообщения = СтрШаблон(
                "%1
                |Информация об ошибке: %2",
                ОбщаяЧастьСообщения,
                СтруктураОшибки.КраткоеПредставлениеОшибки);
    КонецПопытки;

    Возврат РезультатФункции;
КонецФункции

// Параметры:
//  СозданныеЗдания - Массив из СправочникСсылка.КВП_Здания
Процедура УдалитьСозданныеЗдания(Знач СозданныеЗдания)
    Для Каждого НовоеЗдание Из СозданныеЗдания Цикл
        Попытка
            НовоеЗданиеОбъект = НовоеЗдание.ПолучитьОбъект();
            НовоеЗданиеОбъект.Удалить();
        Исключение
        КонецПопытки;
    КонецЦикла;
КонецПроцедуры

// Параметры:
//  СозданныеПомещения - Массив из СправочникСсылка.УПЖКХ_Помещения
Процедура УдалитьСозданныеПомещения(Знач СозданныеПомещения)
    Для Каждого НовоеПомещение Из СозданныеПомещения Цикл
        Попытка
            НовоеПомещениеОбъект = НовоеПомещение.ПолучитьОбъект();
            НовоеПомещениеОбъект.Удалить();
        Исключение
        КонецПопытки;
    КонецЦикла;
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
// -- Гарант+ Килипенко 02.08.2024 [F00226689] перенос зданий из БП77 --
