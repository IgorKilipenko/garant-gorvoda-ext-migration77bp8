// Гарант+ Килипенко 20.12.2024 [F00232332] Расшифровка поступления платежей за услуги ЖКХ ++
#Область ПрограммныйИнтерфейс

// Получает остатки взаиморасчетов на период для указанных контрагентов
// Параметры:
//  СписокКонтрагентов - Массив, СписокЗначений из СправочникСсылка.Контрагенты
//  Период - Дата - Дата остатков
//  ТолькоДолг - Дата
//  Организация - СправочникСсылка.Организации, Неопределено
// Возвращаемое значение:
//  - ТаблицаЗначений
//      * Контрагент - СправочникСсылка.Контрагенты
//      * НоменклатурнаяГруппа - СправочникСсылка.НоменклатурныеГруппы
//      * Номенклатура - СправочникСсылка.Номенклатура
//      * Сумма - Число
Функция ПолучитьДанныеПоВзаиморасчетамКонтрагентов(
        Знач СписокКонтрагентов, Знач Период, Знач ТолькоДолг = Ложь, Знач Организация = Неопределено) Экспорт

    Организация = ?(Организация = Неопределено, УПЖКХ_ТиповыеМетодыВызовСервера.ПолучитьОсновнуюОрганизацию(), Организация);

    Запрос = Новый Запрос;
    Запрос.Текст = ПолучитьТекстЗапросаДанныхПоВзаиморасчетамКонтрагентов();

    Если Период = Неопределено Тогда
        Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Период", "");
    Иначе
        Запрос.УстановитьПараметр("Период", Период);
    КонецЕсли;
    Запрос.УстановитьПараметр("Организация", Организация);
    Запрос.УстановитьПараметр("СписокКонтрагентов", СписокКонтрагентов);
    Запрос.УстановитьПараметр("ТолькоДолг", ТолькоДолг);

    РезультатЗапроса = Запрос.Выполнить();
    РезультатФункции = РезультатЗапроса.Выгрузить();
    Возврат РезультатФункции;
КонецФункции

// Параметры:
//  Контрагент - СправочникСсылка.Контрагенты
//  Период - Дата - Дата остатков
//  Организация - СправочникСсылка.Организации, Неопределено
// Возвращаемое значение:
//  - ТаблицаЗначений
//      * Контрагент - СправочникСсылка.Контрагенты
//      * НоменклатурнаяГруппа - СправочникСсылка.НоменклатурныеГруппы
//      * Номенклатура - СправочникСсылка.Номенклатура
//      * КоэффициентРаспределения - Число
Функция ПолучитьКоэффициентыРаспределенияПлатежейКонтрагента(Знач Контрагент, Знач Период, Знач Организация = Неопределено) Экспорт
    Организация = ?(Организация = Неопределено, УПЖКХ_ТиповыеМетодыВызовСервера.ПолучитьОсновнуюОрганизацию(), Организация);

    Запрос = Новый Запрос;
    Запрос.Текст =
        "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
        |	РеализацияУслуг.Регистратор КАК ДокументРеализации
        |ПОМЕСТИТЬ ВТ_ДокументыРеализации
        |ИЗ
        |	РегистрНакопления.РеализацияУслуг.Обороты(
        |			НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(&КонецПериода, ГОД, -&КоличествоПериодовИстории), ГОД),
        |			&КонецПериода,
        |			Регистратор,
        |			Организация = &Организация
        |				И НоменклатурнаяГруппа <> ЗНАЧЕНИЕ(Справочник.НоменклатурныеГруппы.ПустаяСсылка)) КАК РеализацияУслуг
        |ГДЕ
        |	ИСТИНА
        |	И ВЫБОР
        |			КОГДА РеализацияУслуг.Регистратор ССЫЛКА Документ.РеализацияТоваровУслуг
        |				ТОГДА ВЫРАЗИТЬ(РеализацияУслуг.Регистратор КАК Документ.РеализацияТоваровУслуг).Контрагент В (&СписокКонтрагентов)
        |			ИНАЧЕ ИСТИНА
        |		КОНЕЦ
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВЫРАЗИТЬ(РеализацияУслугОборотыБУ.Субконто1 КАК Справочник.Контрагенты) КАК Контрагент,
        |	ВЫРАЗИТЬ(РеализацияУслугОборотыБУ.КорСубконто1 КАК Справочник.НоменклатурныеГруппы) КАК НоменклатурнаяГруппа,
        |	ВЫРАЗИТЬ(РеализацияУслугОборотыБУ.КорСубконто3 КАК Справочник.Номенклатура) КАК Номенклатура,
        |	ВЫРАЗИТЬ(0 КАК ЧИСЛО(16, 8)) КАК КоэффициентРаспределения,
        |	СУММА(РеализацияУслугОборотыБУ.СуммаОборот) КАК Сумма
        |ИЗ
        |	РегистрБухгалтерии.Хозрасчетный.Обороты(НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(&КонецПериода, ГОД, -&КоличествоПериодовИстории), ГОД), &КонецПериода, Авто, , &АналитикаСчета, , , &АналитикаСчетаКор) КАК РеализацияУслугОборотыБУ
        |ГДЕ
        |	ИСТИНА
        |	И РеализацияУслугОборотыБУ.Субконто3 В
        |			(ВЫБРАТЬ РАЗЛИЧНЫЕ
        |				Т.ДокументРеализации
        |			ИЗ
        |				ВТ_ДокументыРеализации КАК Т)
        |	И ВЫРАЗИТЬ(РеализацияУслугОборотыБУ.Субконто1 КАК Справочник.Контрагенты) В (&СписокКонтрагентов)
        |
        |СГРУППИРОВАТЬ ПО
        |	ВЫРАЗИТЬ(РеализацияУслугОборотыБУ.Субконто1 КАК Справочник.Контрагенты),
        |	ВЫРАЗИТЬ(РеализацияУслугОборотыБУ.КорСубконто1 КАК Справочник.НоменклатурныеГруппы),
        |	ВЫРАЗИТЬ(РеализацияУслугОборотыБУ.КорСубконто3 КАК Справочник.Номенклатура)
        |";

    АналитикаСчета = Новый Массив;
    АналитикаСчета.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
    АналитикаСчета.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры);
    АналитикаСчета.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами);
    Запрос.УстановитьПараметр("АналитикаСчета", АналитикаСчета);

    АналитикаСчетаКор = Новый Массив;
    АналитикаСчетаКор.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы);
    АналитикаСчетаКор.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтавкиНДС);
    АналитикаСчетаКор.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
    Запрос.УстановитьПараметр("АналитикаСчетаКор", АналитикаСчетаКор);

    Запрос.УстановитьПараметр("КонецПериода", Период);
    Запрос.УстановитьПараметр("СписокКонтрагентов", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Контрагент));
    Запрос.УстановитьПараметр("Организация", Организация);
    Запрос.УстановитьПараметр("КоличествоПериодовИстории", 2);

    РезультатЗапроса = Запрос.Выполнить();
    ТаблицаДанныхРаспределения = РезультатЗапроса.Выгрузить();
    ОбщаяСуммаПоКонтрагенту = ТаблицаДанныхРаспределения.Итог("Сумма");

    Для Каждого СтрокаДанных Из ТаблицаДанныхРаспределения Цикл
        Если ОбщаяСуммаПоКонтрагенту > 0 Тогда
            СтрокаДанных.КоэффициентРаспределения = СтрокаДанных.Сумма / ОбщаяСуммаПоКонтрагенту;
        Иначе
            СтрокаДанных.КоэффициентРаспределения = 0;
        КонецЕсли;
    КонецЦикла;

    ТаблицаДанныхРаспределения.Колонки.Удалить("Сумма");

    ГП_ДиагностикаКлиентСервер.Утверждение(Окр(ТаблицаДанныхРаспределения.Итог("КоэффициентРаспределения"), 5, 2) = 1,
            "Не выполняется контроль суммы коэффициентов распределения");

    Возврат ТаблицаДанныхРаспределения;
КонецФункции

// Распределение по сумме долга взаиморасчетов
Функция РаспределитьСуммуДокументаОплатыУслуг(Знач ДанныеДокумента, Знач ТолькоДолг = Истина) Экспорт
    ДанныеВзаиморасчетов = ПолучитьДанныеПоВзаиморасчетамКонтрагентов(
            ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДанныеДокумента.Контрагент),
            ДанныеДокумента.Дата,
            ТолькоДолг,
            ДанныеДокумента.Организация);

    РезультатРаспределения = РаспределитьОплатуПоДаннымВзаиморасчетов(
            ДанныеДокумента.Контрагент, ДанныеВзаиморасчетов, ДанныеДокумента.СуммаОплаты);

    Возврат РезультатРаспределения;
КонецФункции

Функция РаспределитьОстатокПлатежаВПропорцииНачислений(Знач ДанныеДокумента) Экспорт
    ДанныеРаспределения = ПолучитьКоэффициентыРаспределенияПлатежейКонтрагента(
            ДанныеДокумента.Контрагент,
            ДанныеДокумента.Дата,
            ДанныеДокумента.Организация);

    РезультатРаспределения = РаспределитьОплатуПоДаннымКоэффициентовРаспределения(
            ДанныеДокумента.Контрагент, ДанныеРаспределения, ДанныеДокумента.СуммаОплаты);

    Возврат РезультатРаспределения;
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс
// Гарант+ Килипенко 20.12.2024 [F00232332] Расшифровка поступления платежей за услуги ЖКХ --

// Гарант+ Килипенко 20.12.2024 [F00232332] Расшифровка поступления платежей за услуги ЖКХ ++
#Область СлужебныеПроцедурыИФункции

Функция ПолучитьТекстЗапросаДанныхПоВзаиморасчетамКонтрагентов()
    ТекстЗапроса =
        "ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	&Период КАК Период,
        |	ВЫБОР
        |		КОГДА КОНЕЦПЕРИОДА(&Период, МЕСЯЦ) <> КОНЕЦПЕРИОДА(ВзаиморасчетыПоЛицевымСчетамОстатки.МесяцНачисления, МЕСЯЦ)
        |			ТОГДА ВзаиморасчетыПоЛицевымСчетамОстатки.МесяцНачисления
        |		ИНАЧЕ &Период
        |	КОНЕЦ КАК ПериодНачисления,
        |	ВзаиморасчетыПоЛицевымСчетамОстатки.МесяцНачисления КАК МесяцНачисления,
        |	ВзаиморасчетыПоЛицевымСчетамОстатки.ЛицевойСчет КАК ЛицевойСчет,
        |	ВзаиморасчетыПоЛицевымСчетамОстатки.Услуга КАК Услуга,
        |	ВзаиморасчетыПоЛицевымСчетамОстатки.Договор КАК Договор,
        |	ВзаиморасчетыПоЛицевымСчетамОстатки.Договор.Владелец КАК Контрагент,
        |	ВзаиморасчетыПоЛицевымСчетамОстатки.СуммаНачисленияОстаток КАК СуммаНачисленияОстаток
        |ПОМЕСТИТЬ ВТ_НачисленияУслугПоВзаиморасчетам_Подготовка
        |ИЗ
        |	РегистрНакопления.КВП_ВзаиморасчетыПоЛицевымСчетам.Остатки(&Период, ) КАК ВзаиморасчетыПоЛицевымСчетамОстатки
        |ГДЕ
        |	ИСТИНА
        |	И ВзаиморасчетыПоЛицевымСчетамОстатки.Организация = &Организация
        |	И ВзаиморасчетыПоЛицевымСчетамОстатки.Договор.Владелец В(&СписокКонтрагентов)
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВТ_НачисленияУслугПоВзаиморасчетам_Подготовка.ПериодНачисления КАК ПериодНачисления,
        |	ВТ_НачисленияУслугПоВзаиморасчетам_Подготовка.МесяцНачисления КАК МесяцНачисления,
        |	ВТ_НачисленияУслугПоВзаиморасчетам_Подготовка.ЛицевойСчет КАК ЛицевойСчет,
        |	ВТ_НачисленияУслугПоВзаиморасчетам_Подготовка.Услуга КАК Услуга,
        |	ВТ_НачисленияУслугПоВзаиморасчетам_Подготовка.Договор КАК Договор,
        |	ВТ_НачисленияУслугПоВзаиморасчетам_Подготовка.Договор.Владелец КАК Контрагент,
        |	СУММА(ВТ_НачисленияУслугПоВзаиморасчетам_Подготовка.СуммаНачисленияОстаток) КАК СуммаНачисленияОстаток
        |ПОМЕСТИТЬ ВТ_НачисленияУслугПоВзаиморасчетам
        |ИЗ
        |	ВТ_НачисленияУслугПоВзаиморасчетам_Подготовка КАК ВТ_НачисленияУслугПоВзаиморасчетам_Подготовка
        |
        |СГРУППИРОВАТЬ ПО
        |	ВТ_НачисленияУслугПоВзаиморасчетам_Подготовка.ПериодНачисления,
        |	ВТ_НачисленияУслугПоВзаиморасчетам_Подготовка.МесяцНачисления,
        |	ВТ_НачисленияУслугПоВзаиморасчетам_Подготовка.ЛицевойСчет,
        |	ВТ_НачисленияУслугПоВзаиморасчетам_Подготовка.Услуга,
        |	ВТ_НачисленияУслугПоВзаиморасчетам_Подготовка.Договор,
        |	ВТ_НачисленияУслугПоВзаиморасчетам_Подготовка.Договор.Владелец
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	МАКСИМУМ(СведенияДляВзаиморасчетовПоЛС.Период) КАК Период,
        |	СведенияДляВзаиморасчетовПоЛС.ЛицевойСчет КАК ЛицевойСчет,
        |	СведенияДляВзаиморасчетовПоЛС.Договор КАК Договор
        |ПОМЕСТИТЬ ВТ_СведенияДляВзаиморасчетовПоЛС_ЗаПериод
        |ИЗ
        |	РегистрСведений.УПЖКХ_СведенияДляВзаиморасчетовПоЛС КАК СведенияДляВзаиморасчетовПоЛС
        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_НачисленияУслугПоВзаиморасчетам КАК ВТ_НачисленияУслугПоВзаиморасчетам
        |		ПО СведенияДляВзаиморасчетовПоЛС.ЛицевойСчет = ВТ_НачисленияУслугПоВзаиморасчетам.ЛицевойСчет
        |			И (СведенияДляВзаиморасчетовПоЛС.Контрагент В (&СписокКонтрагентов))
        |			И СведенияДляВзаиморасчетовПоЛС.Период <= ВТ_НачисленияУслугПоВзаиморасчетам.ПериодНачисления
        |			И СведенияДляВзаиморасчетовПоЛС.Договор = ВТ_НачисленияУслугПоВзаиморасчетам.Договор
        |			И (СведенияДляВзаиморасчетовПоЛС.Организация = &Организация)
        |			И (СведенияДляВзаиморасчетовПоЛС.Активность = ИСТИНА)
        |
        |СГРУППИРОВАТЬ ПО
        |	СведенияДляВзаиморасчетовПоЛС.ЛицевойСчет,
        |	СведенияДляВзаиморасчетовПоЛС.Договор
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	СведенияДляВзаиморасчетовПоЛС.Период КАК Период,
        |	СведенияДляВзаиморасчетовПоЛС.ЛицевойСчет КАК ЛицевойСчет,
        |	СведенияДляВзаиморасчетовПоЛС.Договор КАК Договор,
        |	СведенияДляВзаиморасчетовПоЛС.Контрагент КАК Контрагент
        |ПОМЕСТИТЬ ВТ_СведенияДляВзаиморасчетовПоЛСАктуальные
        |ИЗ
        |	РегистрСведений.УПЖКХ_СведенияДляВзаиморасчетовПоЛС КАК СведенияДляВзаиморасчетовПоЛС
        |ГДЕ
        |	СведенияДляВзаиморасчетовПоЛС.Организация = &Организация
        |	И (СведенияДляВзаиморасчетовПоЛС.Период, СведенияДляВзаиморасчетовПоЛС.ЛицевойСчет, СведенияДляВзаиморасчетовПоЛС.Договор) В
        |			(ВЫБРАТЬ
        |				Т.Период,
        |				Т.ЛицевойСчет,
        |				Т.Договор
        |			ИЗ
        |				ВТ_СведенияДляВзаиморасчетовПоЛС_ЗаПериод КАК Т)
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ЛицевыеСчета.Период КАК ПериодАктуальностиЛС,
        |	ЛицевыеСчета.ЛицевойСчет КАК ЛицевойСчет
        |ПОМЕСТИТЬ ВТ_ЗакрытыеЛицевыеСчетаНаКонецПериода
        |ИЗ
        |	РегистрСведений.КВП_ЛицевыеСчета.СрезПоследних(&Период, ) КАК ЛицевыеСчета
        |ГДЕ
        |	ЛицевыеСчета.Действует = ЛОЖЬ
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВТ_НачисленияУслугПоВзаиморасчетам.ПериодНачисления КАК ПериодНачисления,
        |	ВТ_НачисленияУслугПоВзаиморасчетам.МесяцНачисления КАК МесяцНачисления,
        |	ВТ_НачисленияУслугПоВзаиморасчетам.ЛицевойСчет КАК ЛицевойСчет,
        |	ВТ_НачисленияУслугПоВзаиморасчетам.Услуга КАК Услуга,
        |	ВТ_НачисленияУслугПоВзаиморасчетам.Услуга.Услуга КАК Номенклатура,
        |	ВТ_НачисленияУслугПоВзаиморасчетам.Услуга.Услуга.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
        |	ВТ_НачисленияУслугПоВзаиморасчетам.Договор КАК Договор,
        |	ЕСТЬNULL(ВТ_СведенияДляВзаиморасчетовПоЛСАктуальные.Контрагент, ВТ_НачисленияУслугПоВзаиморасчетам.Контрагент) КАК Контрагент,
        |	ВТ_НачисленияУслугПоВзаиморасчетам.СуммаНачисленияОстаток КАК СуммаНачисленияОстаток
        |ПОМЕСТИТЬ ВТ_ОстаткиПоВзаиморасчетам
        |ИЗ
        |	ВТ_НачисленияУслугПоВзаиморасчетам КАК ВТ_НачисленияУслугПоВзаиморасчетам
        |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СведенияДляВзаиморасчетовПоЛСАктуальные КАК ВТ_СведенияДляВзаиморасчетовПоЛСАктуальные
        |		ПО ВТ_НачисленияУслугПоВзаиморасчетам.ЛицевойСчет = ВТ_СведенияДляВзаиморасчетовПоЛСАктуальные.ЛицевойСчет
        |			И ВТ_НачисленияУслугПоВзаиморасчетам.Договор = ВТ_СведенияДляВзаиморасчетовПоЛСАктуальные.Договор
        |			И ВТ_НачисленияУслугПоВзаиморасчетам.ПериодНачисления >= ВТ_СведенияДляВзаиморасчетовПоЛСАктуальные.Период
        |ГДЕ
        |	НЕ ВТ_НачисленияУслугПоВзаиморасчетам.ЛицевойСчет В
        |				(ВЫБРАТЬ
        |					Т.ЛицевойСчет
        |				ИЗ
        |					ВТ_ЗакрытыеЛицевыеСчетаНаКонецПериода КАК Т)
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВТ_ОстаткиПоВзаиморасчетам.Контрагент КАК Контрагент,
        |	ВТ_ОстаткиПоВзаиморасчетам.Номенклатура КАК Номенклатура,
        |	ВТ_ОстаткиПоВзаиморасчетам.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
        |	СУММА(ВТ_ОстаткиПоВзаиморасчетам.СуммаНачисленияОстаток) КАК СуммаНачисления
        |ПОМЕСТИТЬ ВТ_Результат_Подготовка
        |ИЗ
        |	ВТ_ОстаткиПоВзаиморасчетам КАК ВТ_ОстаткиПоВзаиморасчетам
        |
        |СГРУППИРОВАТЬ ПО
        |	ВТ_ОстаткиПоВзаиморасчетам.Контрагент,
        |	ВТ_ОстаткиПоВзаиморасчетам.НоменклатурнаяГруппа,
        |	ВТ_ОстаткиПоВзаиморасчетам.Номенклатура
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	ВТ_Результат_Подготовка.Контрагент КАК Контрагент,
        |	ВТ_Результат_Подготовка.Номенклатура КАК Номенклатура,
        |	ВТ_Результат_Подготовка.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
        |	ВТ_Результат_Подготовка.СуммаНачисления КАК Сумма
        |ИЗ
        |	ВТ_Результат_Подготовка КАК ВТ_Результат_Подготовка
        |ГДЕ
        |	ИСТИНА
        |	И ВЫБОР
        |		КОГДА &ТолькоДолг = ИСТИНА ТОГДА ВТ_Результат_Подготовка.СуммаНачисления > 0
        |		ИНАЧЕ ИСТИНА
        |	КОНЕЦ
        |";

    Возврат ТекстЗапроса;
КонецФункции

Функция РаспределитьОплатуПоДаннымВзаиморасчетов(Знач Контрагент, Знач ДанныеВзаиморасчетов, Знач СуммаОплаты)
    ТаблицаРаспределения = НовыйТаблицаРаспределенияОплат();
    ОбщаяСуммаОстатковВзаиморасчетов = ДанныеВзаиморасчетов.Итог("Сумма");

    Если ОбщаяСуммаОстатковВзаиморасчетов = СуммаОплаты Тогда
        КоэффициентРаспределения = 1;
    ИначеЕсли ОбщаяСуммаОстатковВзаиморасчетов > СуммаОплаты Тогда
        КоэффициентРаспределения = СуммаОплаты / ОбщаяСуммаОстатковВзаиморасчетов;
    Иначе
        КоэффициентРаспределения = 1;
        СуммаОплаты = Мин(Мин(ОбщаяСуммаОстатковВзаиморасчетов, 0), СуммаОплаты);
    КонецЕсли;

    НераспределенныйОстаток = СуммаОплаты;
    СтрокаМаксимальнойСуммы = Неопределено;
    Для Каждого СтрокаДанных Из ДанныеВзаиморасчетов Цикл
        НоваяСтрокаРаспределения = ТаблицаРаспределения.Добавить();
        НоваяСтрокаРаспределения.Контрагент = Контрагент;
        НоваяСтрокаРаспределения.НоменклатурнаяГруппа = СтрокаДанных.НоменклатурнаяГруппа;
        НоваяСтрокаРаспределения.Номенклатура = СтрокаДанных.Номенклатура;
        Если КоэффициентРаспределения = 1 Тогда
            НоваяСтрокаРаспределения.Сумма = Окр(СтрокаДанных.Сумма, 2, 2);
        Иначе
            НоваяСтрокаРаспределения.Сумма = Окр(КоэффициентРаспределения * СтрокаДанных.Сумма, 2, 2);
        КонецЕсли;

        Если СтрокаМаксимальнойСуммы = Неопределено ИЛИ СтрокаМаксимальнойСуммы.Сумма < НоваяСтрокаРаспределения.Сумма Тогда
            СтрокаМаксимальнойСуммы = НоваяСтрокаРаспределения;
        КонецЕсли;

        НераспределенныйОстаток = НераспределенныйОстаток - НоваяСтрокаРаспределения.Сумма;
    КонецЦикла;

    Если СтрокаМаксимальнойСуммы <> Неопределено И НераспределенныйОстаток <> 0 Тогда
        СтрокаМаксимальнойСуммы.Сумма = СтрокаМаксимальнойСуммы.Сумма + НераспределенныйОстаток;
    КонецЕсли;

    Возврат Новый Структура("ТаблицаРаспределения, КоэффициентРаспределения",
        ТаблицаРаспределения, КоэффициентРаспределения);
КонецФункции

Функция РаспределитьОплатуПоДаннымКоэффициентовРаспределения(Знач Контрагент, Знач ДанныеКоэффициентовРаспределения, Знач СуммаОплаты)
    ТаблицаРаспределения = НовыйТаблицаРаспределенияОплат();

    НераспределенныйОстаток = СуммаОплаты;
    СтрокаМаксимальнойСуммы = Неопределено;
    Для Каждого СтрокаДанных Из ДанныеКоэффициентовРаспределения Цикл
        НоваяСтрокаРаспределения = ТаблицаРаспределения.Добавить();
        НоваяСтрокаРаспределения.Контрагент = Контрагент;
        НоваяСтрокаРаспределения.НоменклатурнаяГруппа = СтрокаДанных.НоменклатурнаяГруппа;
        НоваяСтрокаРаспределения.Номенклатура = СтрокаДанных.Номенклатура;
        НоваяСтрокаРаспределения.Сумма = Окр(СтрокаДанных.КоэффициентРаспределения * СуммаОплаты, 2, 2);

        Если СтрокаМаксимальнойСуммы = Неопределено ИЛИ СтрокаМаксимальнойСуммы.Сумма < НоваяСтрокаРаспределения.Сумма Тогда
            СтрокаМаксимальнойСуммы = НоваяСтрокаРаспределения;
        КонецЕсли;

        НераспределенныйОстаток = НераспределенныйОстаток - НоваяСтрокаРаспределения.Сумма;
    КонецЦикла;

    Если СтрокаМаксимальнойСуммы <> Неопределено И НераспределенныйОстаток <> 0 Тогда
        СтрокаМаксимальнойСуммы.Сумма = СтрокаМаксимальнойСуммы.Сумма + НераспределенныйОстаток;
    КонецЕсли;

    Возврат Новый Структура("ТаблицаРаспределения", ТаблицаРаспределения);
КонецФункции

Функция НовыйТаблицаРаспределенияОплат()
    ТаблицаРаспределения = Новый ТаблицаЗначений;
    ТаблицаРаспределения.Колонки.Добавить("Контрагент", Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
    ТаблицаРаспределения.Колонки.Добавить("НоменклатурнаяГруппа", Новый ОписаниеТипов("СправочникСсылка.НоменклатурныеГруппы"));
    ТаблицаРаспределения.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
    ТаблицаРаспределения.Колонки.Добавить("Сумма", ОбщегоНазначения.ОписаниеТипаЧисло(21, 2));

    Возврат ТаблицаРаспределения;
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
// Гарант+ Килипенко 20.12.2024 [F00232332] Расшифровка поступления платежей за услуги ЖКХ --
