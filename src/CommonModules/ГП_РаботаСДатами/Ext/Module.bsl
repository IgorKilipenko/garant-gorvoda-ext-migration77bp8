#Область ПрограммныйИнтерфейс

// Параметры:
//  ДатаНачала - Дата
//  ДатаОкончания - Дата
// Возвращаемое значение:
//  - Массив из Дата
Функция ПолучитьМесяцыПериода(Знач ДатаНачала, Знач ДатаОкончания) Экспорт
    РезультатФункции = Новый Массив;

    ТекущееЗначениеМесяца = НачалоМесяца(ДатаНачала);
    ПоследнееЗначениеМесяца = НачалоМесяца(ДатаОкончания);

    РезультатФункции.Добавить(ТекущееЗначениеМесяца);
    Пока ТекущееЗначениеМесяца < ПоследнееЗначениеМесяца Цикл
        ТекущееЗначениеМесяца = ДобавитьМесяц(ТекущееЗначениеМесяца, 1);
        РезультатФункции.Добавить(ТекущееЗначениеМесяца);
    КонецЦикла;

    Возврат РезультатФункции;
КонецФункции

// Параметры:
//  ДатаНачала - Дата
//  ДатаОкончания - Дата
// Возвращаемое значение:
//  - Строка
Функция ПолучитьПредставлениеСпискаМесяцевПериода(Знач ДатаНачала, Знач ДатаОкончания) Экспорт
    РезультатФункции = "";

    МассивМесяцев = ПолучитьМесяцыПериода(ДатаНачала, ДатаОкончания);
    Для Каждого ЭлементМесяц Из МассивМесяцев Цикл
        РезультатФункции = СтрШаблон("%1, %2г.", РезультатФункции, Формат(ЭлементМесяц, "ДФ='ММММ гггг'"));
    КонецЦикла;
    Если СтрДлина(РезультатФункции) > 0 Тогда
        РезультатФункции = Сред(РезультатФункции, 3);
    КонецЕсли;

    Возврат РезультатФункции;
КонецФункции

// Параметры:
//  ДатаНачала - Дата
//  ДатаОкончания - Дата
// Возвращаемое значение:
//  - Строка
Функция ПолучитьПредставлениеПериодаНачисления(Знач ДатаНачала, Знач ДатаОкончания) Экспорт
    РезультатФункции = "";

    ПервоеЗначениеМесяца = НачалоМесяца(ДатаНачала);
    ПоследнееЗначениеМесяца = НачалоМесяца(ДатаОкончания);

    Если ПервоеЗначениеМесяца = ПоследнееЗначениеМесяца Тогда
        РезультатФункции = СтрШаблон("за %1г.", Формат(ПервоеЗначениеМесяца, "ДФ='ММММ гггг'"));
    Иначе
        ФорматПредставленияДаты = "ДФ='дд.ММ.гггг'";
        РезультатФункции = СтрШаблон("с %1г. по %2г.",
                Формат(ПервоеЗначениеМесяца, ФорматПредставленияДаты),
                Формат(ПоследнееЗначениеМесяца, ФорматПредставленияДаты));
    КонецЕсли;

    Возврат РезультатФункции;
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс
