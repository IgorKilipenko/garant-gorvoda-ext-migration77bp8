// Гарант+ Килипенко 18.09.2024 [F00228484] Доработка отражения в рег учете ++
#Область ПрограммныйИнтерфейс

// Параметры:
//  ДокументНачисленияУслугСсылка - ДокументСсылка.КВП_НачислениеУслуг
// Возвращаемое значение:
//  ТаблицаЗначений
//      * Контрагент - СправочникСсылка.Контрагенты
//      * Договор - СправочникСсылка.ДоговорыКонтрагентов
//      * НомерСтроки - Число
Функция ПолучитьКонтрагентовПриборовУчетаДокумента(Знач ДокументНачисленияУслугСсылка) Экспорт
    Запрос = Новый Запрос;
    Запрос.Текст =
        "ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	НачисленияУслугиПоПоказаниямПУ.ЛицевойСчет КАК ЛицевойСчет,
        |	НачисленияУслугиПоПоказаниямПУ.Договор КАК Договор,
        |	НачисленияУслугиПоПоказаниямПУ.Договор.Владелец КАК Контрагент,
        |	МИНИМУМ(НачисленияУслугиПоПоказаниямПУ.НомерСтроки) КАК НомерСтроки
        |ПОМЕСТИТЬ ВТ_КонтрагентыЛицевыхСчетов
        |ИЗ
        |	Документ.КВП_НачислениеУслуг.УслугиПоПоказаниямПУ КАК НачисленияУслугиПоПоказаниямПУ
        |ГДЕ
        |	НачисленияУслугиПоПоказаниямПУ.Ссылка = &ДокументНачисленияУслугСсылка
        |
        |СГРУППИРОВАТЬ ПО
        |	НачисленияУслугиПоПоказаниямПУ.ЛицевойСчет,
        |	НачисленияУслугиПоПоказаниямПУ.Договор,
        |	НачисленияУслугиПоПоказаниямПУ.Договор.Владелец
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |	МИНИМУМ(ВТ_КонтрагентыЛицевыхСчетов.НомерСтроки) КАК НомерСтроки,
        |	ВТ_КонтрагентыЛицевыхСчетов.Контрагент КАК Контрагент,
        |	ВТ_КонтрагентыЛицевыхСчетов.Договор КАК Договор
        |ИЗ
        |	ВТ_КонтрагентыЛицевыхСчетов КАК ВТ_КонтрагентыЛицевыхСчетов
        |
        |СГРУППИРОВАТЬ ПО
        |	ВТ_КонтрагентыЛицевыхСчетов.Контрагент,
        |	ВТ_КонтрагентыЛицевыхСчетов.Договор
        |
        |УПОРЯДОЧИТЬ ПО
        |	НомерСтроки
        |";

    Запрос.УстановитьПараметр("ДокументНачисленияУслугСсылка", ДокументНачисленияУслугСсылка);
    РезультатЗапроса = Запрос.Выполнить();
    РезультатФункции = РезультатЗапроса.Выгрузить();
    Возврат РезультатФункции;
КонецФункции

// Гарант+ Килипенко 28.10.2024 [F00229783] Отражение начислений в разрезе л/с ++
//
// Параметры:
//  ДвиженияНачисления - ТаблицаЗначений
//  ИсходныеДвиженияОтражения - ТаблицаЗначений - Изменяемый параметр
// Возвращаемое значение:
//  - Структура
//      * Успех - Булево
Функция ДополнитьДвиженияОтраженияДаннымиЛС(Знач ДвиженияНачисления, Знач ИсходныеДвиженияОтражения) Экспорт
    РезультатФункции = Новый Структура("Успех", Истина);

    Запрос = Новый Запрос;
    Запрос.Текст =
        "ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |   *
        |ПОМЕСТИТЬ ВТ_ДвиженияНачисления
        |ИЗ
        |   &ДвиженияНачисления КАК ДвиженияНачисления
        |;
        |
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |   *
        |ПОМЕСТИТЬ ВТ_ИсходныеДвиженияОтражения
        |ИЗ
        |   &ИсходныеДвиженияОтражения КАК ИсходныеДвиженияОтражения
        |;
        |
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |   ВТ_ДвиженияНачисления.Период КАК Период,
        |   ВТ_ДвиженияНачисления.Организация КАК Организация,
        |   ВТ_ДвиженияНачисления.ЛицевойСчет КАК ЛицевойСчет,
        |   ВТ_ДвиженияНачисления.Услуга КАК Услуга,
        |   ВТ_ДвиженияНачисления.Договор КАК Договор,
        |   ВТ_ДвиженияНачисления.УслугаОснование КАК УслугаОснование,
        |   ВТ_ДвиженияНачисления.СоставнаяУслуга КАК СоставнаяУслуга,
        |   ВТ_ДвиженияНачисления.РазделУчета КАК РазделУчета,
        |   ВТ_ДвиженияНачисления.ВидНачисления КАК ВидНачисления,
        |   ВТ_ДвиженияНачисления.ИсточникОплаты КАК ИсточникОплаты,
        |   ВТ_ДвиженияНачисления.МесяцНачисления КАК МесяцНачисления,
        |   ВТ_ДвиженияНачисления.Тариф КАК Тариф,
        |   ВТ_ДвиженияНачисления.ВидТарифа КАК ВидТарифа,
        |   ВТ_ДвиженияНачисления.МесяцРассрочки КАК МесяцРассрочки,
        |   ВТ_ДвиженияНачисления.Количество КАК Количество,
        |   ВТ_ДвиженияНачисления.СуммаНачисления КАК СуммаНачисления,
        |   ВТ_ДвиженияНачисления.ДнейНачислено КАК ДнейНачислено,
        |   ВТ_ДвиженияНачисления.Начало КАК Начало,
        |   ВТ_ДвиженияНачисления.Окончание КАК Окончание,
        |   ВТ_ДвиженияНачисления.Тарифность КАК Тарифность,
        |   ВТ_ДвиженияНачисления.КоличествоУчетное КАК КоличествоУчетное
        |ПОМЕСТИТЬ ВТ_ДвиженияНачисленияПодготовка
        |ИЗ
        |   ВТ_ДвиженияНачисления КАК ВТ_ДвиженияНачисления
        |ГДЕ
        |   ВТ_ДвиженияНачисления.СуммаНачисления <> 0
        |;
        |
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |   ВТ_ДвиженияНачисленияПодготовка.Период КАК Период,
        |   ВТ_ДвиженияНачисленияПодготовка.Организация КАК Организация,
        |   ВТ_ДвиженияНачисленияПодготовка.ЛицевойСчет КАК ГП_ЛицевойСчет,
        |   ВТ_ДвиженияНачисленияПодготовка.Услуга КАК Услуга,
        |   ВТ_ДвиженияНачисленияПодготовка.Услуга.Услуга КАК Номенклатура,
        |   ВТ_ДвиженияНачисленияПодготовка.Услуга.Услуга.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
        |   ВТ_ДвиженияНачисленияПодготовка.Договор КАК ДоговорКонтрагента,
        |   ВТ_ДвиженияНачисленияПодготовка.Тариф КАК Тариф,
        |   ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
        |   ЗНАЧЕНИЕ(Перечисление.УПЖКХ_ВариантыРасчетовСПоставщикамиУслуг.КупляПродажаУслуг) КАК ВариантПоставкиУслуг,
        |   ВТ_ДвиженияНачисленияПодготовка.РазделУчета = ЗНАЧЕНИЕ(Перечисление.УПЖКХ_РазделыУчета.Льготы) КАК ЭтоЛьгота,
        |   ВТ_ДвиженияНачисленияПодготовка.РазделУчета = ЗНАЧЕНИЕ(Перечисление.УПЖКХ_РазделыУчета.Пени) КАК ЭтоПени,
        |   СУММА(ВТ_ДвиженияНачисленияПодготовка.Количество) КАК Количество,
        |   СУММА(ВТ_ДвиженияНачисленияПодготовка.СуммаНачисления) КАК Сумма,
        |   СУММА(ВТ_ДвиженияНачисленияПодготовка.КоличествоУчетное) КАК КоличествоУчетное
        |ИЗ
        |   ВТ_ДвиженияНачисленияПодготовка КАК ВТ_ДвиженияНачисленияПодготовка
        |
        |СГРУППИРОВАТЬ ПО
        |   Период,
        |   Организация,
        |   ЛицевойСчет,
        |   Услуга,
        |   Договор,
        |   Тариф,
        |   ВТ_ДвиженияНачисленияПодготовка.РазделУчета = ЗНАЧЕНИЕ(Перечисление.УПЖКХ_РазделыУчета.Льготы),
        |   ВТ_ДвиженияНачисленияПодготовка.РазделУчета = ЗНАЧЕНИЕ(Перечисление.УПЖКХ_РазделыУчета.Пени)
        |";

    Запрос.УстановитьПараметр("ДвиженияНачисления", ДвиженияНачисления);
    Запрос.УстановитьПараметр("ИсходныеДвиженияОтражения", ИсходныеДвиженияОтражения);

    РезультатЗапроса = Запрос.Выполнить();
    ДанныеЗаполнения = РезультатЗапроса.Выгрузить();

    ИсходныеДвиженияОтражения.Очистить();

    Для Каждого СтрокаДанных Из ДанныеЗаполнения Цикл
        НоваяСтрока = ИсходныеДвиженияОтражения.Добавить();
        ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДанных);
    КонецЦикла;

    Возврат РезультатФункции;
КонецФункции // Гарант+ Килипенко 28.10.2024 [F00229783] Отражение начислений в разрезе л/с --

// Выполняет перерасчет сумм движений взаиморасчетов с учетом НДС
// Параметры:
//  * ДвиженияНачисления - НаборЗаписей
//  * СуммаВключаетНДС - Булево - По умолчанию Ложь
//  * СтавкаНДС - ПеречислениеСсылка.СтавкиНДС, Неопределено - По умолчанию Перечисления.СтавкиНДС.НДС20_120
Процедура ПересчитатьСуммыДвиженийНачисленийИВзаиморасчетовСУчетомНДС(
        ДвиженияНачисления, Знач СуммаВключаетНДС = Ложь, Знач СтавкаНДС = Неопределено) Экспорт

    СтавкаНДС = ?(СтавкаНДС = Неопределено, Перечисления.СтавкиНДС.НДС20_120, СтавкаНДС);

    Для Каждого СтрокаДанных Из ДвиженияНачисления Цикл
        СтрокаДанных.СуммаНачисления = СтрокаДанных.СуммаНачисления + УчетНДСКлиентСервер.РассчитатьСуммуНДС(СтрокаДанных.СуммаНачисления,
                СуммаВключаетНДС, УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(СтавкаНДС));
    КонецЦикла;
КонецПроцедуры

#КонецОбласти // ПрограммныйИнтерфейс
// Гарант+ Килипенко 18.09.2024 [F00228484] Доработка отражения в рег учете ++
