#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура ВыполнитьЗагрузкуДанныеВзаиморасчетов(Команда)
    РезультатДиалога = Ждать ГП_ОбщегоНазначенияКлиент.СпроситьПользователяОГотовностиВыполнятьОперациюАсинх(
            "Данные взаиморасчетов будут загружены (обновлены) в регистр.");
    Если РезультатДиалога = Ложь Тогда
        Возврат;
    КонецЕсли;
    
    ОткрытьФайлДляЧтения("ЗагрузитьДанныеВзаиморасчетов");
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОткрытьФайлДляЧтения(Знач КомандаОбработки)
    ПараметрыЗагрузкиФайла = ФайловаяСистемаКлиент.ПараметрыЗагрузкиФайла();
    ПараметрыЗагрузкиФайла.Диалог.Заголовок = НСтр("ru = 'Выберите файл XML для загрузки'");
    ПараметрыЗагрузкиФайла.Диалог.Фильтр = НСтр("ru = 'Файлы XML (*.xml)|*.xml'");
    ПараметрыЗагрузкиФайла.ИдентификаторФормы = ЭтотОбъект.УникальныйИдентификатор;
    ФайловаяСистемаКлиент.ЗагрузитьФайлы(Новый ОписаниеОповещения("ЗагрузкаФайлаЗавершение", 
        ЭтотОбъект, Новый Структура("КомандаОбработки", КомандаОбработки)), ПараметрыЗагрузкиФайла);
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДанныеВзаиморасчетов()   
    РезультатЗагрузки = ЗагрузитьДанныеВзаимроасчетовНаСервере(
            ЭтотОбъект.ИнформацияОЗагруженныхФайлах[0].Хранение);

    ТекстСообщенияРезультатОперации = "";
    Если РезультатЗагрузки.Успех = Ложь Тогда
        ТекстСообщенияРезультатОперации = СтрШаблон("Загрузка данных взаиморасчетов абонентов не выполнена.
                |Подробная информация: %1", РезультатЗагрузки.ТекстСообщения);
    Иначе
        ТекстСообщенияРезультатОперации = СтрШаблон("Загрузка данных взаиморасчетов абонентов выполнена успешно.
                |Записано (обновлено) %1 записей регистра", РезультатЗагрузки.КоличествоЗаписанных);
    КонецЕсли;

    ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщенияРезультатОперации);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗагрузитьДанныеВзаимроасчетовНаСервере(Знач АдресФайла)
    Если АдресФайла = Неопределено Тогда
        ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не удалось получить данные для загрузки'"));
        Возврат Неопределено;
    КонецЕсли;

    ПараметрыЗагрузки = Новый Структура("ДвоичныеДанныеФайла");
    ПараметрыЗагрузки.ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(АдресФайла);
    Возврат ГП_МиграцияВзаиморасчетов.ЗагрузитьДанныеВзаиморасчетовПоАбонентамВРегистр(ПараметрыЗагрузки, Неопределено);
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции

#Область ФоновыеОперацииИнфраструктура

&НаКлиенте
Процедура ЗагрузкаФайлаЗавершение(ПомещенныеФайлы, ДополнительныеПараметры) Экспорт
    Если ТипЗнч(ПомещенныеФайлы) <> Тип("Массив") Тогда
        Возврат;
    КонецЕсли;

    ЭтотОбъект.ИнформацияОЗагруженныхФайлах.Очистить();
    ЭтотОбъект.ИмяВыбранногоФайла = "";
    Для Каждого Файл Из ПомещенныеФайлы Цикл
        НоваяСтрокаИнформации = ЭтотОбъект.ИнформацияОЗагруженныхФайлах.Добавить();
        ЗаполнитьЗначенияСвойств(НоваяСтрокаИнформации, Файл);
    КонецЦикла;

    Если ЭтотОбъект.ИнформацияОЗагруженныхФайлах.Количество() > 0 Тогда
        ЭтотОбъект.ИмяВыбранногоФайла = ЭтотОбъект.ИнформацияОЗагруженныхФайлах[0].ПолноеИмя;
    КонецЕсли;
    
    Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
        И ДополнительныеПараметры.Свойство("КомандаОбработки")
        И ДополнительныеПараметры.КомандаОбработки = "ЗагрузитьДанныеВзаиморасчетов" Тогда
        
        ЗагрузитьДанныеВзаиморасчетов();
    КонецЕсли;
КонецПроцедуры

#КонецОбласти // ФоновыеОперацииИнфраструктура