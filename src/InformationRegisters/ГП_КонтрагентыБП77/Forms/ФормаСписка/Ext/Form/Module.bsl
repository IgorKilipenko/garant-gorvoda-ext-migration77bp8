// Гарант+ Килипенко 08.08.2024 [F00227208] перенос контрагентов из БП77 ++
#Область ОбработчикиКомандФормы

// Выполняет создание новых контрагентов (абонентов) по данным из регистра ГП_КонтрагентыБП77.
//  Создаются только контрагенты для которых не найдены лицевые счета (т.е. выполняется перенос контрагентов
//  из БП77 но только тех контрагентов которые не были перенесены ранее собственными силами клиента)
&НаКлиенте
Асинх Процедура СоздатьАбонентовДляНесвязанныхЗаписей(Команда)
    // Предупреждение пользователю о необратимости операции
    РезультатДиалога = Ждать ГП_ОбщегоНазначенияКлиент.СпроситьПользователяОГотовностиВыполнятьОперациюАсинх(
            "Будет выполнена операция создания контрагентов (абонентов)"
            + " для лицевых счетов созданных автоматически по данным БП 7.7.");
    Если РезультатДиалога = Ложь Тогда
        Возврат; // Пользователь отказался от выполнения операции
    КонецЕсли;

    // Создание объектов контрагентов (абонентов)
    РезультатОбновления = СоздатьАбонентовДляНесвязанныхЗаписейНаСервере();

    // Вывод сообщения о результате
    ТекстСообщенияРезультатОперации = "";
    Если РезультатОбновления.Успех = Ложь Тогда
        ТекстСообщенияРезультатОперации = СтрШаблон("Создание контрагентов не выполнено.
                |Сообщение о причине: %1", РезультатОбновления.ТекстСообщения);
    Иначе
        ТекстСообщенияРезультатОперации = СтрШаблон("Создание контрагентов выполнено успешно.
                |Создано ""%1"" контрагентов (абонентов).", РезультатОбновления.СозданныеКонтрагенты.Количество());
    КонецЕсли;
    ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщенияРезультатОперации);
КонецПроцедуры

&НаКлиенте
Асинх Процедура СопоставитьАбонентов(Команда)
    // Предупреждение пользователю о необратимости операции
    РезультатДиалога = Ждать ГП_ОбщегоНазначенияКлиент.СпроситьПользователяОГотовностиВыполнятьОперациюАсинх(
            "Будет выполнена операция сопоставления абонентов из БП77 с контрагентами ИБ.");
    Если РезультатДиалога = Ложь Тогда
        Возврат; // Пользователь отказался от выполнения операции
    КонецЕсли;

    // Обновление связей абонентов
    РезультатОбновления = СопоставитьАбонентовНаСервере(Истина);

    // Вывод сообщения о результате
    ТекстСообщенияРезультатОперации = "";
    Если РезультатОбновления.Успех = Ложь Тогда
        ТекстСообщенияРезультатОперации = СтрШаблон("Установка связей контрагентов не выполнена.
                |Сообщение о причине: %1", РезультатОбновления.ТекстСообщения);
    Иначе
        ТекстСообщенияРезультатОперации = СтрШаблон("Установка связей контрагентов выполнена успешно.
                |Обновлено ""%1"" записей.", РезультатОбновления.ОбновленныеСвязи.Количество());
    КонецЕсли;
    ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщенияРезультатОперации);

    ЭтотОбъект.Элементы.Список.Обновить();
КонецПроцедуры

// Устарела. Используется только в процессе разработки для оперативного исправления перенесенных данных
//
// Обновляет (перезаписывает) контактную информацию созданных автоматически по данным БП77 контрагентов
&НаКлиенте
Асинх Процедура ИсправитьКонтактнуюИнформациюПеренесенныхАбонентов(Команда)
    // Предупреждение пользователю о необратимости операции
    РезультатДиалога = Ждать ГП_ОбщегоНазначенияКлиент.СпроситьПользователяОГотовностиВыполнятьОперациюАсинх(
            "Будет выполнена операция исправления (перезаписи) контактной информации для перенесенных абонентов.");
    Если РезультатДиалога = Ложь Тогда
        Возврат; // Пользователь отказался от выполнения операции
    КонецЕсли;

    // Исправление контактной информации абонентов
    РезультатОбновления = ИсправитьКонтактнуюИнформациюПеренесенныхАбонентовНаСервере();

    // Вывод сообщения о результате
    ТекстСообщенияРезультатОперации = "";
    Если РезультатОбновления.Успех = Ложь Тогда
        ТекстСообщенияРезультатОперации = СтрШаблон("Исправление контактной информации не выполнено.
                |Сообщение о причине: %1", РезультатОбновления.ТекстСообщения);
    Иначе
        ТекстСообщенияРезультатОперации = СтрШаблон("Исправление контактной информации выполнено успешно.
                |Контактная информация исправлено для ""%1"" абонентов.", РезультатОбновления.КоличествоОбновленныхАбонентов);
    КонецЕсли;
    ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщенияРезультатОперации);

    ЭтотОбъект.Элементы.Список.Обновить();
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы
// Гарант+ Килипенко 08.08.2024 [F00227208] перенос контрагентов из БП77 --

// Гарант+ Килипенко 08.08.2024 [F00227208] перенос контрагентов из БП77 ++
#Область СлужебныеПроцедурыИФункции

// Создает новые объекты Контрагентов (типа абонент) по данным из регистра ГП_КонтрагентыБП77.
//  Создаются только контрагенты для которых не найдены лицевые счета (т.е. выполняется перенос контрагентов
//  из БП77 но только тех контрагентов которые не были перенесены ранее собственными силами клиента)
// Возвращаемое значение:
//  - Структура
//      * Успех - Булево
//      * СозданныеКонтрагенты - Массив из СправочникСсылка.Контрагенты
//      * ТекстСообщения - Строка, Неопределено
&НаСервереБезКонтекста
Функция СоздатьАбонентовДляНесвязанныхЗаписейНаСервере()
    Возврат ГП_МиграцияКонтрагентов.СоздатьАбонентовДляПеренесенныхЛицевыхСчетов();
КонецФункции

// Устанавливает связи Абонентов (контрагентов) БП77 с объектами Контрагентов из ИБ
//  (записывает ссылку Контрагент поле регистра ГП_КонтрагентыБП77)
// Параметры:
//  РазрыватьСуществующиеСвязи - Булево
// Возвращаемое значение:
//  - Структура
//      * Успех - Булево
//      * ОбновленныеСвязи - Массив из СправочникСсылка.Контрагенты
//      * ТекстСообщения - Строка
&НаСервереБезКонтекста
Функция СопоставитьАбонентовНаСервере(Знач РазрыватьСуществующиеСвязи)
    Возврат ГП_МиграцияКонтрагентов.УстановитьСоответствияАбонентов(РазрыватьСуществующиеСвязи);
КонецФункции

// Устарела. Используется только в процессе разработки для оперативного исправления перенесенных данных
//
// Обновляет (перезаписывает) контактную информацию созданных автоматически по данным БП77 контрагентов
// Возвращаемое значение:
//  - Структура
//      * Успех - Булево
//      * КоличествоОбновленныхАбонентов - Число
//      * ТекстСообщения - Строка, Неопределено
&НаСервереБезКонтекста
Функция ИсправитьКонтактнуюИнформациюПеренесенныхАбонентовНаСервере()
    Возврат ГП_МиграцияКонтрагентов.ИсправитьКонтактнуюИнформациюПеренесенныхАбонентов();
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
// Гарант+ Килипенко 08.08.2024 [F00227208] перенос контрагентов из БП77 --
