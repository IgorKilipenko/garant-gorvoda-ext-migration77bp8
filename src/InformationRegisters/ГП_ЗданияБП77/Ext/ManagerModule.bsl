// ++ Гарант+ Килипенко 01.08.2024 [F00226689] перенос объектов абонентов из БП77 ++
#Область ПрограммныйИнтерфейс

// Заполняет значения признака `ЭтоНегативноеВоздействиеЦСВ` для всех записей регистра.
//  Заполнение выполняется по данным наименования объекта абонента
// Возвращаемое значение:
//  - Структура
//      * Успех - Булево
//      * КоличествоИзмененныхСтрок - Число
Функция ЗаполнитьПризнакНегативноеВоздействие() Экспорт
    РезультатФункции = Новый Структура("Успех, КоличествоИзмененныхСтрок, СообщениеОбОшибке", Истина, 0);

    НаборВсехЗаписейРегистра = СоздатьПустойНаборЗаписей();
    НаборВсехЗаписейРегистра.Прочитать();

    Для Каждого ТекущаяСтрока Из НаборВсехЗаписейРегистра Цикл
        ТекущееЗначениеЭтоНегативноеВоздействие = ТекущаяСтрока.ЭтоНегативноеВоздействиеЦСВ;
        ТекущаяСтрока.ЭтоНегативноеВоздействиеЦСВ =
            ГП_МиграцияОбщегоНазначения.ЭтоНаименованиеУслугиНегативногоВоздействия(
                ТекущаяСтрока.ОбъектАбонентаНаименование);

        Если ТекущееЗначениеЭтоНегативноеВоздействие <> ТекущаяСтрока.ЭтоНегативноеВоздействиеЦСВ Тогда
            РезультатФункции.КоличествоИзмененныхСтрок = РезультатФункции.КоличествоИзмененныхСтрок + 1;
        КонецЕсли;
    КонецЦикла;

    Попытка
        НаборВсехЗаписейРегистра.Записать();
    Исключение
        ЖурналРегистрации.ДобавитьСообщениеДляЖурналаРегистрации(
            "Ошибка при записи в регистр сведений ""Услуги БП 7.7"" при обновлении значений признака негативного воздействия.",
            УровеньЖурналаРегистрации.Ошибка, , ,
            ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));

        РезультатФункции.КоличествоИзмененныхСтрок = 0;
        РезультатФункции.СообщениеОбОшибке = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
        РезультатФункции.Успех = Ложь;
    КонецПопытки;

    Возврат РезультатФункции;
КонецФункции

// Заполняет значения признака `ЭтоПлатаЗаХолодноеВодоснабжениеОИ` для всех записей регистра.
//  Заполнение выполняется по данным наименования объекта абонента
// Возвращаемое значение:
//  - Структура
//      * Успех - Булево
//      * КоличествоИзмененныхСтрок - Число
Функция ЗаполнитьПризнакПлатаЗаХолодноеВодоснабжениеОИ() Экспорт
    РезультатФункции = Новый Структура("Успех, КоличествоИзмененныхСтрок, СообщениеОбОшибке", Истина, 0);

    НаборВсехЗаписейРегистра = СоздатьПустойНаборЗаписей();
    НаборВсехЗаписейРегистра.Прочитать();

    Для Каждого ТекущаяСтрока Из НаборВсехЗаписейРегистра Цикл
        ТекущееЗначениеЭтоПлатаЗаХолодноеВодоснабжениеОИ = ТекущаяСтрока.ЭтоПлатаЗаХолодноеВодоснабжениеОИ;
        ТекущаяСтрока.ЭтоПлатаЗаХолодноеВодоснабжениеОИ =
            ГП_МиграцияОбщегоНазначения.ЭтоНаименованиеУслугиХолодногоВодоснабженияОИ(
                ТекущаяСтрока.ОбъектАбонентаНаименование);

        Если ТекущееЗначениеЭтоПлатаЗаХолодноеВодоснабжениеОИ <> ТекущаяСтрока.ЭтоПлатаЗаХолодноеВодоснабжениеОИ Тогда
            РезультатФункции.КоличествоИзмененныхСтрок = РезультатФункции.КоличествоИзмененныхСтрок + 1;
        КонецЕсли;
    КонецЦикла;

    Попытка
        НаборВсехЗаписейРегистра.Записать();
    Исключение
        ЖурналРегистрации.ДобавитьСообщениеДляЖурналаРегистрации(
            "Ошибка при записи в регистр сведений ""Услуги БП 7.7"" при обновлении значений признака платы за холодное водоснабжение ОИ.",
            УровеньЖурналаРегистрации.Ошибка, , ,
            ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));

        РезультатФункции.КоличествоИзмененныхСтрок = 0;
        РезультатФункции.СообщениеОбОшибке = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
        РезультатФункции.Успех = Ложь;
    КонецПопытки;

    Возврат РезультатФункции;
КонецФункции

// Заполняет значения поля `ДоговорНомер` для всех записей регистра.
//  Заполнение выполняется по данным наименования договора
// Возвращаемое значение:
//  - Структура
//      * Успех - Булево
//      * КоличествоИзмененныхСтрок - Число
Функция ЗаполнитьНомераДоговоров() Экспорт
    РезультатФункции = Новый Структура("Успех, КоличествоИзмененныхСтрок, СообщениеОбОшибке", Истина, 0);

    НаборВсехЗаписейРегистра = СоздатьПустойНаборЗаписей();
    НаборВсехЗаписейРегистра.Прочитать();

    Для Каждого ТекущаяСтрока Из НаборВсехЗаписейРегистра Цикл
        ТекущееЗначениеДоговорНомер = ТекущаяСтрока.ДоговорНомер;
        СтруктураНомера = ГП_МиграцияОбщегоНазначения.ПолучитьНомерДоговораПоНаименованию(
                ТекущаяСтрока.ДоговорНаименование);

        Если СтруктураНомера.Успех = Ложь Тогда
            Продолжить; // Номер не распознан
        КонецЕсли;

        ТекущаяСтрока.ДоговорНомер = СтруктураНомера.Номер + ?(ПустаяСтрока(СтруктураНомера.Суффикс),
                "", СтрШаблон("-%1", СтруктураНомера.Суффикс));

        Если ТекущееЗначениеДоговорНомер <> ТекущаяСтрока.ДоговорНомер Тогда
            РезультатФункции.КоличествоИзмененныхСтрок = РезультатФункции.КоличествоИзмененныхСтрок + 1;
        КонецЕсли;
    КонецЦикла;
    
    Попытка
        НаборВсехЗаписейРегистра.Записать();
    Исключение
        СтруктураОшибки =
            ГП_МиграцияОбщегоНазначения.ЗаписатьОшибкуВЖурнал(
                "Ошибка при записи в регистр сведений ""Услуги БП 7.7""
                |при обновлении значений поля Номер договора.", Ложь, Истина);

        РезультатФункции.КоличествоИзмененныхСтрок = 0;
        РезультатФункции.СообщениеОбОшибке = СтруктураОшибки.КраткоеПредставлениеОшибки;
        РезультатФункции.Успех = Ложь;
    КонецПопытки;

    Возврат РезультатФункции;
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс
// -- Гарант+ Килипенко 01.08.2024 [F00226689] перенос объектов абонентов из БП77 --

// ++ Гарант+ Килипенко 01.08.2024 [F00226689] перенос объектов абонентов из БП77 ++
#Область СлужебныеПроцедурыИФункции

Функция СоздатьПустойНаборЗаписей()
    Возврат РегистрыСведений.ГП_ЗданияБП77.СоздатьНаборЗаписей();
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
// -- Гарант+ Килипенко 01.08.2024 [F00226689] перенос объектов абонентов из БП77 --
