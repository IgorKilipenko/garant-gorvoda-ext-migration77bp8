#Область ПереопределениеСтандартногоФункционала

// Гарант+ Килипенко 28.03.2025 Отмена регистрации док-а УстановкаЦенНоменклатуры обмена БП-ERP(HOFF) ++ {
//
&ИзменениеИКонтроль("ПользовательскийСоставПланаОбмена")
Функция ГП_ПользовательскийСоставПланаОбмена(Знач Получатель)

	УстановитьПривилегированныйРежим(Истина);

	Результат = Новый Соответствие;

	СвойстваПолучателя = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Получатель,
		ОбщегоНазначения.ИменаРеквизитовПоТипу(Получатель, Тип("ПеречислениеСсылка.РежимыВыгрузкиОбъектовОбмена")));

	Приоритеты = ПриоритетыРежимовВыгрузкиОбъектов();
	ИмяПланаОбмена = ОбменДаннымиПовтИсп.ПолучитьИмяПланаОбмена(Получатель);
	Правила = ОбменДаннымиПовтИсп.ПравилаРегистрацииОбъектовПланаОбмена(ИмяПланаОбмена);
	Правила.Индексы.Добавить("ОбъектМетаданныхИмя");

	Для Каждого Элемент Из Метаданные.ПланыОбмена[ИмяПланаОбмена].Состав Цикл

		ИмяОбъекта = Элемент.Метаданные.ПолноеИмя();
		ПравилаОбъекта = Правила.НайтиСтроки(Новый Структура("ОбъектМетаданныхИмя", ИмяОбъекта));
		РежимВыгрузки = Неопределено;

		Если ПравилаОбъекта.Количество() = 0 Тогда // Правила регистрации не заданы.

			РежимВыгрузки = Перечисления.РежимыВыгрузкиОбъектовОбмена.ВыгружатьВсегда;

		Иначе // заданы правила регистрации

			Для Каждого ПРО Из ПравилаОбъекта Цикл

				Если ЗначениеЗаполнено(ПРО.ИмяРеквизитаФлага) Тогда
					РежимВыгрузки = МаксимальныйРежимВыгрузкиОбъекта(СвойстваПолучателя[ПРО.ИмяРеквизитаФлага], РежимВыгрузки, Приоритеты);
				КонецЕсли;

			КонецЦикла;

			Если РежимВыгрузки = Неопределено
				ИЛИ РежимВыгрузки = Перечисления.РежимыВыгрузкиОбъектовОбмена.ПустаяСсылка() Тогда
				РежимВыгрузки = Перечисления.РежимыВыгрузкиОбъектовОбмена.ВыгружатьПоУсловию;
            КонецЕсли;
            #Вставка // Гарант+ Килипенко 28.03.2025 Отмена регистрации док-а УстановкаЦенНоменклатуры обмена БП-ERP(HOFF) ++ {

            Если РежимВыгрузки <> Перечисления.РежимыВыгрузкиОбъектовОбмена.НеВыгружать Тогда
                // Получаем настройку синхронизации БП-УП(HOFF) по коду УП
                ПолучательУПХОФФ = ПланыОбмена.СинхронизацияДанныхЧерезУниверсальныйФормат.НайтиПоКоду("УП");

                ЭтоПолучательУПХОФФ = Ложь;
                Если ПолучательУПХОФФ <> Неопределено И ЗначениеЗаполнено(ПолучательУПХОФФ) Тогда
                    ЭтоПолучательУПХОФФ = (Получатель = ПолучательУПХОФФ);
                КонецЕсли;

                Если ЭтоПолучательУПХОФФ Тогда
                    ЭтоДокументУстановкиЦен = Ложь;
                    ЭтоДокументУстановкиЦен = (ИмяОбъекта = "Документ.УстановкаЦенНоменклатуры");
                    Если ЭтоДокументУстановкиЦен Тогда
                        РежимВыгрузки = Перечисления.РежимыВыгрузкиОбъектовОбмена.НеВыгружать; // Отмена регистрации
                    КонецЕсли;
                КонецЕсли;
            КонецЕсли;

            #КонецВставки // Гарант+ Килипенко 28.03.2025 Отмена регистрации док-а УстановкаЦенНоменклатуры обмена БП-ERP(HOFF) -- }

		КонецЕсли;

		Результат.Вставить(ИмяОбъекта, РежимВыгрузки);

	КонецЦикла;

	УстановитьПривилегированныйРежим(Ложь);

	Возврат Результат;
КонецФункции // Гарант+ Килипенко 28.03.2025 Отмена регистрации док-а УстановкаЦенНоменклатуры обмена БП-ERP(HOFF) -- }

#КонецОбласти // ПереопределениеСтандартногоФункционала
