// Гарант+ Килипенко 30.10.2024 [F00230212] запись динамических показателей ++
#Область ПрограммныйИнтерфейс

// Выполняет запись данных в регистры сведений [lc_ДинамическиеПоказателиЛицевыхСчетов, ГП_ВидыТарифовПотребителейЛицевыхСчетов]
// Параметры:
//  ЛицевойСчет - СправочникСсылка.КВП_ЛицевыеСчета
//  СтруктураДанных - Структура
//      * ВидПотребления
//      * Финансирование
//      * Водозабор
//      * Договородержатель
//      * Контролер
//      * Характеристика
//      * ВидТарифаПотребителя
//  Период - Дата, Неопределено
// Возвращаемое значение:
//  - Структура
//      * Успех - Булево
Функция ГП_ЗаписатьЗначенияДинамическихПоказателейДляЛС(Знач ЛицевойСчет, Знач СтруктураДанных, Знач Период = Неопределено) Экспорт
    РезультатФункции = Новый Структура("Успех, ТекстСообщения", Истина);

    Период = ?(Период = Неопределено, ТекущаяДатаСеанса(), Период);
    Период = НачалоДня(Период);

    // Запись данных динамических показателей
    НаборЗаписейДинамическихПоказателей = РегистрыСведений.lc_ДинамическиеПоказателиЛицевыхСчетов.СоздатьНаборЗаписей();
    НаборЗаписейДинамическихПоказателей.Отбор.Период.Установить(Период);
    НаборЗаписейДинамическихПоказателей.Отбор.ЛицевойСчет.Установить(ЛицевойСчет);

    НоваяЗаписьПоказателей = НаборЗаписейДинамическихПоказателей.Добавить();
    НоваяЗаписьПоказателей.Период = Период;
    НоваяЗаписьПоказателей.ЛицевойСчет = ЛицевойСчет;

    // НоваяЗаписьПоказателей.ПроцентВодоотведенияХВС = СтруктураДанных.ПроцентВодоотведенияХВС;
    // НоваяЗаписьПоказателей.ПроцентВодоотведенияГВС = СтруктураДанных.ПроцентВодоотведенияГВС;

    НоваяЗаписьПоказателей.ВидПотребления = СтруктураДанных.ВидПотребления;
    НоваяЗаписьПоказателей.Финансирование = СтруктураДанных.Финансирование;
    НоваяЗаписьПоказателей.Водозабор = СтруктураДанных.Водозабор;
    НоваяЗаписьПоказателей.Договородержатель = СтруктураДанных.Договородержатель;
    НоваяЗаписьПоказателей.Контролер = СтруктураДанных.Контролер;
    НоваяЗаписьПоказателей.Характеристика = СтруктураДанных.Характеристика;

    // Запись данных тарифов потребителей
    НаборЗаписейТарифовПотребителей = РегистрыСведений.ГП_ВидыТарифовПотребителейЛицевыхСчетов.СоздатьНаборЗаписей();
    НаборЗаписейТарифовПотребителей.Отбор.Период.Установить(Период);
    НаборЗаписейТарифовПотребителей.Отбор.ЛицевойСчет.Установить(ЛицевойСчет);

    НоваяЗаписьТарифовПотребителей = НаборЗаписейТарифовПотребителей.Добавить();
    НоваяЗаписьТарифовПотребителей.Период = Период;
    НоваяЗаписьТарифовПотребителей.ЛицевойСчет = ЛицевойСчет;

    НоваяЗаписьТарифовПотребителей.ВидТарифаПотребителя = СтруктураДанных.ВидТарифаПотребителя;

    НачатьТранзакцию();
    Попытка
        НаборЗаписейДинамическихПоказателей.Записать(Истина);
        НаборЗаписейТарифовПотребителей.Записать(Истина);

        ЗафиксироватьТранзакцию();
    Исключение
        ОтменитьТранзакцию();

        ОбщаяЧастьСообщения = "Запись динамических показателей";
        СтруктураОшибки = ГП_МиграцияОбщегоНазначения.ЗаписатьОшибкуВЖурнал(
                ОбщаяЧастьСообщения, ИнформацияОбОшибке(), Ложь, Истина);

        РезультатФункции.Успех = Ложь;
        РезультатФункции.ТекстСообщения = СтрШаблон(
                "%1
                |Информация об ошибке: %2",
                ОбщаяЧастьСообщения,
                СтруктураОшибки.КраткоеПредставлениеОшибки);
    КонецПопытки;

    Возврат РезультатФункции;
КонецФункции

// Возвращаемое значение:
//  - Структура
//      * ВидПотребления - Неопределено
//      * Финансирование - Неопределено
//      * Водозабор - Неопределено
//      * Договородержатель - Неопределено
//      * Контролер - Неопределено
//      * Характеристика - Неопределено
//      * ВидТарифаПотребителя - Неопределено
Функция ГП_НовыйСтруктураДинамическихПоказателей() Экспорт
    РезультатФункции = Новый Структура;

    РезультатФункции.Вставить("ВидПотребления");
    РезультатФункции.Вставить("Финансирование");
    РезультатФункции.Вставить("Водозабор");
    РезультатФункции.Вставить("Договородержатель");
    РезультатФункции.Вставить("Контролер");
    РезультатФункции.Вставить("Характеристика");
    РезультатФункции.Вставить("ВидТарифаПотребителя");

    Возврат РезультатФункции;
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс
// Гарант+ Килипенко 30.10.2024 [F00230212] запись динамических показателей --
