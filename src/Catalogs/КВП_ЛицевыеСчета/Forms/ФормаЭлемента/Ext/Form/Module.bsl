// Гарант+ Килипенко 02.04.2025 [F00236140] Адреса л/с для печати ++ {
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ГП_ПриСозданииНаСервереПеред(Отказ, СтандартнаяОбработка)
    ГП_СоздатьДополнительныеЭлементыНаСервере();

    ГП_ЗаполнитьДанныеДополнительныхПолей();
КонецПроцедуры

&НаСервере
Процедура ГП_ПриЧтенииНаСервереПосле(ТекущийОбъект)
    ГП_СоздатьДополнительныеЭлементыНаСервере();

    ГП_ЗаполнитьДанныеДополнительныхПолей();
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы
// Гарант+ Килипенко 02.04.2025 [F00236140] Адреса л/с для печати -- }

// Гарант+ Килипенко 02.04.2025 [F00236140] Адреса л/с для печати ++ {
#Область СлужебныеПроцедурыИФункции

Процедура ГП_ЗаполнитьДанныеДополнительныхПолей()
    ГП_ЗаполнитьЗначениеАдресаДляПечати();
КонецПроцедуры

Процедура ГП_ЗаполнитьЗначениеАдресаДляПечати()
    Если НЕ ЭтотОбъект.Объект.Ссылка.Пустая() Тогда
        ЭтотОбъект.ГП_АдресЛицевогоСчетаДляПечати = ГП_РаботаСЛицевымиСчетами.ПолучитьПредставлениеАдресаПечатиЛицевогоСчета(
            ЭтотОбъект.Объект.Ссылка);
    Иначе
        ЭтотОбъект.ГП_АдресЛицевогоСчетаДляПечати = "";
    КонецЕсли;
КонецПроцедуры

#Область НастройкаФормы

&НаСервере
Процедура ГП_СоздатьДополнительныеЭлементыНаСервере()
    ГП_СоздатьПолеАдресЛицевогоСчетаДляПечатиНаСервере();
КонецПроцедуры

&НаСервере
Функция ГП_СоздатьПолеАдресЛицевогоСчетаДляПечатиНаСервере()
    РезультатФункции = Новый Структура("Элемент, Реквизит");

    ИмяЭлементаРодителя = "Общая";

    // Создание элемента
    БазовоеНаименованиеСоздаваемогоРеквизита = "АдресЛицевогоСчетаДляПечати";
    НаименованиеСоздаваемогоРеквизита = СтрШаблон("ГП_%1", БазовоеНаименованиеСоздаваемогоРеквизита);

    // Проверка наличия поля формы
    Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ЭтотОбъект.Элементы, НаименованиеСоздаваемогоРеквизита) Тогда
        РезультатФункции.Элемент = ЭтотОбъект.Элементы[НаименованиеСоздаваемогоРеквизита];

        Возврат РезультатФункции; // Элемент уже существует
    КонецЕсли;

    ТипРеквизита = ОбщегоНазначения.ОписаниеТипаСтрока(500);
    СозданныйРеквизит = Новый РеквизитФормы(
            НаименованиеСоздаваемогоРеквизита, ТипРеквизита,
            "", "Адрес для печати");
    ЭтотОбъект.ИзменитьРеквизиты(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СозданныйРеквизит));
    РезультатФункции.Реквизит = СозданныйРеквизит;

    ИмяСоздаваемогоЭлемента = СтрШаблон("ГП_%1", БазовоеНаименованиеСоздаваемогоРеквизита);
    ПараметрыЭлемента = Новый Структура("ПутьКДанным");
    ПараметрыЭлемента.ПутьКДанным = СозданныйРеквизит.Имя;

    // РезультатФункции.Элемент = ГП_РаботаСФормамиКлиентСервер.СоздатьПолеНадписи(ИмяСоздаваемогоЭлемента, ПараметрыЭлемента, ЭтотОбъект,
    //         ИмяЭлементаРодителя, ЭтотОбъект.Элементы.ТекЗдание.Имя);
    // РезультатФункции.Элемент.Гиперссылка = Истина;

    РезультатФункции.Элемент = ГП_РаботаСФормамиКлиентСервер.СоздатьПолеФормы(ИмяСоздаваемогоЭлемента, ПараметрыЭлемента, ЭтотОбъект,
            ИмяЭлементаРодителя, ЭтотОбъект.Элементы.ТекЗдание.Имя);
    РезультатФункции.Элемент.ТолькоПросмотр = Истина;
    РезультатФункции.Элемент.АвтоМаксимальнаяШирина = Ложь;

    Возврат РезультатФункции;
КонецФункции

#КонецОбласти // НастройкаФормы

#КонецОбласти // СлужебныеПроцедурыИФункции
// Гарант+ Килипенко 02.04.2025 [F00236140] Адреса л/с для печати -- }
