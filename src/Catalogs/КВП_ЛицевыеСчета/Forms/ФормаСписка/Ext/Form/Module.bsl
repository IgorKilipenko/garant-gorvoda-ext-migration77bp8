// Гарант+ Килипенко 11.09.2024 [F00227694] Доработка документа ввод показаний счетчика ++
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ГП_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
    ГП_СоздатьКомандуЗаполнитьОбъектАрендыНаСервере();
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы
// Гарант+ Килипенко 11.09.2024 [F00227694] Доработка документа ввод показаний счетчика --

// Гарант+ Килипенко 11.09.2024 [F00227694] Доработка документа ввод показаний счетчика ++
#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура ГП_УстановитьЗначенияСчетчиковПриНажатии(Команда)
    ТекущийЛицевойСчет = ГП_ПолучитьТекущийЛицевойСчет();
    Если ТекущийЛицевойСчет = Неопределено Тогда
        ОбщегоНазначенияКлиент.СообщитьПользователю("Не выбрана строка лицевого счета.");
        Возврат;
    КонецЕсли;

    ПараметрыВводаПоказаний = ГП_СформироватьПараметрыВводаПоказанийНаСервере(ТекущийЛицевойСчет);
    ФормаВводаПоказаний = ОткрытьФорму("Документ.УПЖКХ_ВводПоказанийСчетчика.Форма.ФормаДокумента",
            ПараметрыВводаПоказаний, , , , , , РежимОткрытияОкнаФормы.Независимый);

    ЗаполнитьЗначенияСвойств(ФормаВводаПоказаний.Объект, ПараметрыВводаПоказаний);
    // ГП_ЗаполнитьДеревоПоказаний(ФормаВводаПоказаний.ДеревоПоказаний, ТекущийЛицевойСчет);
    // ГП_ЗаполнитьТаблицуПоказаний(ФормаВводаПоказаний.Объект.Главная, ТекущийЛицевойСчет);

КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы
// Гарант+ Килипенко 11.09.2024 [F00227694] Доработка документа ввод показаний счетчика --

// Гарант+ Килипенко 10.09.2024 [F00227690] Заполнение динамических показателей ++
#Область СлужебныеПроцедурыИФункции

&НаКлиенте
&После ("ОбновитьПодчиненныеТаблицы")
Процедура ГП_ОбновитьПодчиненныеТаблицы()
    ЛицевойСчетОпределен = ?(ЭтотОбъект.мЗначениеОтбора = Неопределено ИЛИ ЭтотОбъект.мЗначениеОтбора.Пустая(),
            Ложь, Истина);
    ТекСтраница = Элементы.ПанельДополнительнойИнформации.ТекущаяСтраница;
    Если ТекСтраница <> ЭтотОбъект.Элементы.ГП_ДинамическиеПоказатели ИЛИ ЛицевойСчетОпределен = Ложь Тогда
        Возврат;
    КонецЕсли;

    Если ЭтотОбъект.Элементы.Список.ТекущиеДанные = Неопределено Тогда
        ГП_ОчиститьТаблицуДинамическихПоказателей();
        Возврат;
    КонецЕсли;

    ГП_ОчиститьТаблицуДинамическихПоказателей();
    ГП_ОбновитьТаблицуДинамическихПоказателейНаСервере(ЭтотОбъект.мЗначениеОтбора);
КонецПроцедуры

&НаСервере
Процедура ГП_ОбновитьТаблицуДинамическихПоказателейНаСервере(Знач ЛицевойСчетСсылка)
    Запрос = Новый Запрос;
    Запрос.Текст =
        "ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |   Период,
        |   ПроцентВодоотведенияХВС,
        |   ПроцентВодоотведенияГВС,
        |   ВидПотребления,
        |   Финансирование,
        |   Водозабор,
        |   Договородержатель,
        |   Контролер,
        |   Характеристика
        |ИЗ
        |   РегистрСведений.lc_ДинамическиеПоказателиЛицевыхСчетов.СрезПоследних(, ЛицевойСчет = &ЛицевойСчет) КАК ДинамическиеПоказателиЛицевыхСчетов
        |УПОРЯДОЧИТЬ ПО
        |   Период
        |";

    Запрос.УстановитьПараметр("ЛицевойСчет", ЛицевойСчетСсылка);

    РезультатЗапроса = Запрос.Выполнить();
    ТаблицаРезультата = РезультатЗапроса.Выгрузить();

    ЭтотОбъект.ГП_ТаблицаДинамическихПоказателей.Загрузить(ТаблицаРезультата);
КонецПроцедуры

&НаКлиенте
Процедура ГП_ОчиститьТаблицуДинамическихПоказателей()
    ЭтотОбъект.ГП_ТаблицаДинамическихПоказателей.Очистить();
КонецПроцедуры

// Гарант+ Килипенко 11.09.2024 [F00227694] Доработка документа ввод показаний счетчика ++
#Область ГП_НастройкаФормы

// Создает кнопку формы Установить значения счетчиков
// Параметры:
//  ИмяГруппыРасположенияКнопки - Строка - Имя группы расположения кнопки (по умолчанию - ФормаПользовательскиеКоманды)
// Возвращаемое значение:
//  - Структура
//      * Кнопка
//      * Команда
&НаСервере
Функция ГП_СоздатьКомандуЗаполнитьОбъектАрендыНаСервере(Знач ИмяГруппыРасположенияКнопки = Неопределено)
    Результат = Новый Структура("Кнопка, Команда");

    // Имя группы расположения кнопки на форме
    ИмяГруппыРасположенияКнопки = ?(ИмяГруппыРасположенияКнопки = Неопределено,
            "ФормаПользовательскиеКоманды", ИмяГруппыРасположенияКнопки);

    // Создание команды формы
    Результат.Команда = ЭтотОбъект.Команды.Добавить("ГП_УстановитьЗначенияСчетчиков");
    Результат.Команда.Заголовок = "Установить значения счетчиков";
    Результат.Команда.Действие = "ГП_УстановитьЗначенияСчетчиковПриНажатии";

    // Создание элемента формы
    Результат.Кнопка = ЭтотОбъект.Элементы.Добавить(
            "ГП_ЗаполнитьОбъектАренды",
            Тип("КнопкаФормы"),
            ЭтотОбъект.Элементы[ИмяГруппыРасположенияКнопки]);
    Результат.Кнопка.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
    Результат.Кнопка.ИмяКоманды = Результат.Команда.Имя;

    Возврат Результат;
КонецФункции

#КонецОбласти // ГП_НастройкаФормы
// Гарант+ Килипенко 11.09.2024 [F00227694] Доработка документа ввод показаний счетчика --

// Параметры:
//  ЛицевойСчетСсылка - СправочникСсылка.КВП_ЛицевыеСчета
// Возвращаемое значение:
//  - Структура
//      * ВидОперации - ПеречислениеСсылка.КВП_ВидыОперацийВводаПоказанийСчетчика
&НаСервереБезКонтекста
Функция ГП_СформироватьПараметрыВводаПоказанийНаСервере(Знач ЛицевойСчетСсылка)
    РезультатФункции = Новый Структура("ВидОперации");
    РезультатФункции.ВидОперации = Перечисления.КВП_ВидыОперацийВводаПоказанийСчетчика.ВводПоказанииСчетчикаНаЛицевойСчет;

    ТаблицаПоказаний = ГП_ПолучитьТаблицуПоказанийНаСервере(ЛицевойСчетСсылка);
    РезультатФункции.Вставить("ГП_ЗначенияПоказанийДляЗаполнения", ТаблицаПоказаний);

    Возврат РезультатФункции;
КонецФункции

&НаКлиенте
Функция ГП_ПолучитьТекущийЛицевойСчет()
    ЛицевойСчетОпределен = ГП_ЭтоСуществующаяСсылкаЛицевогоСчета(ЭтотОбъект.мЗначениеОтбора)
        И ГП_ЭтоГруппаЛицевыхСчетовНаСервере(ЭтотОбъект.мЗначениеОтбора) = Ложь;
    Если ЛицевойСчетОпределен = Ложь Тогда
        Возврат Неопределено;
    КонецЕсли;

    Возврат ЭтотОбъект.мЗначениеОтбора;
КонецФункции

// Параметры:
//  ЛицевойСчетСсылка - СправочникСсылка.КВП_ЛицевыеСчета
// Возвращаемое значение:
//  - Булево
&НаКлиентеНаСервереБезКонтекста
Функция ГП_ЭтоСуществующаяСсылкаЛицевогоСчета(Знач ЛицевойСчетСсылка)
    Возврат ТипЗнч(ЛицевойСчетСсылка) = Тип("СправочникСсылка.КВП_ЛицевыеСчета")
        И ЛицевойСчетСсылка.Пустая() = Ложь;
КонецФункции

// Параметры:
//  ЛицевойСчетСсылка - СправочникСсылка.КВП_ЛицевыеСчета
// Возвращаемое значение:
//  - Булево
&НаСервереБезКонтекста
Функция ГП_ЭтоГруппаЛицевыхСчетовНаСервере(Знач ЛицевойСчетСсылка)
    Если ГП_ЭтоСуществующаяСсылкаЛицевогоСчета(ЛицевойСчетСсылка) = Ложь Тогда
        Возврат Ложь;
    КонецЕсли;

    Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЛицевойСчетСсылка, "ЭтоГруппа");
КонецФункции

// Параметры:
//  ПриемникДанных - ДанныеФормыДерево
//  ЛицевойСчетСсылка - СправочникСсылка.КВП_ЛицевыеСчета
// Возвращаемое значение:
//  - Число - Количество добавленных строк
&НаКлиенте
Функция ГП_ЗаполнитьДеревоПоказаний(Знач ПриемникДанных, Знач ЛицевойСчетСсылка)
    ЭлементыДереваПриемника = ПриемникДанных.ПолучитьЭлементы();
    ЭлементыДереваПриемника.Очистить();

    АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
    ГП_ПолучитьПриборыУчетаДоговораПоЛицевомуСчетуНаСервере(ЛицевойСчетСсылка,
        Новый Структура("Адрес", АдресХранилища));
    ПриборыУчетаДоговора = ПолучитьИзВременногоХранилища(АдресХранилища);

    НомерСтрокиПоказаний = 1;
    Для Каждого СтрокаПриборУчета Из ПриборыУчетаДоговора Цикл
        НоваяСтрокаПоказаний = ЭлементыДереваПриемника.Добавить();
        НоваяСтрокаПоказаний.Номер = НомерСтрокиПоказаний;
        НоваяСтрокаПоказаний.Услуга = СтрокаПриборУчета.Услуга;
        НоваяСтрокаПоказаний.Счетчик = СтрокаПриборУчета.ПриборУчета;
        НоваяСтрокаПоказаний.Объект = СтрокаПриборУчета.Объект;
        //НоваяСтрокаПоказаний.КоэффициентТрансформации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаПриборУчета.Счетчик, "Коэффициент");
        //НоваяСтрокаПоказаний.ТемпературныйКоэффициент = 1.0; // !!Проверить

        НомерСтрокиПоказаний = НомерСтрокиПоказаний + 1;
    КонецЦикла;

    Возврат НомерСтрокиПоказаний - 1;
КонецФункции

// Параметры:
//  ПриемникДанных - ДанныеФормыДерево
//  ЛицевойСчетСсылка - СправочникСсылка.КВП_ЛицевыеСчета
// Возвращаемое значение:
//  - Число - Количество добавленных строк
&НаКлиенте
Функция ГП_ЗаполнитьТаблицуПоказаний(Знач ПриемникДанных, Знач ЛицевойСчетСсылка)
    ПриемникДанных.Очистить();

    АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
    ГП_ПолучитьПриборыУчетаДоговораПоЛицевомуСчетуНаСервере(ЛицевойСчетСсылка,
        Новый Структура("Адрес", АдресХранилища));
    ПриборыУчетаДоговора = ПолучитьИзВременногоХранилища(АдресХранилища);

    Для Каждого СтрокаПриборУчета Из ПриборыУчетаДоговора Цикл
        НоваяСтрокаПоказаний = ПриемникДанных.Добавить();
        НоваяСтрокаПоказаний.Услуга = СтрокаПриборУчета.Услуга;
        НоваяСтрокаПоказаний.Счетчик = СтрокаПриборУчета.ПриборУчета;
        НоваяСтрокаПоказаний.Объект = СтрокаПриборУчета.Объект;
        //НоваяСтрокаПоказаний.КоэффициентТрансформации = СтрокаПриборУчета.Счетчик.КоэффициентТрансформации;
        //НоваяСтрокаПоказаний.КоэффициентТрансформации = СтрокаПриборУчета.Счетчик.КоэффициентТрансформации;
    КонецЦикла;

    Возврат ПриемникДанных.Количество();
КонецФункции

&НаСервереБезКонтекста
Функция ГП_ПолучитьТаблицуПоказанийНаСервере(Знач ЛицевойСчетСсылка)
    РезультатФункции = Новый Массив;

    КлючиСтруктурыДанных = "Услуга, Счетчик, Объект, КоэффициентТрансформации, ТемпературныйКоэффициент";

    ПриборыУчетаДоговора = ГП_ПолучитьПриборыУчетаДоговораПоЛицевомуСчетуНаСервере(ЛицевойСчетСсылка);

    Для Каждого СтрокаПриборУчета Из ПриборыУчетаДоговора Цикл
        НоваяСтрокаПоказаний = Новый Структура(КлючиСтруктурыДанных);
        НоваяСтрокаПоказаний.Услуга = СтрокаПриборУчета.Услуга;
        НоваяСтрокаПоказаний.Счетчик = СтрокаПриборУчета.ПриборУчета;
        НоваяСтрокаПоказаний.Объект = СтрокаПриборУчета.Объект;
        НоваяСтрокаПоказаний.КоэффициентТрансформации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаПриборУчета.ПриборУчета, "Коэффициент");
        НоваяСтрокаПоказаний.ТемпературныйКоэффициент = 1.0; // !!Проверить

        РезультатФункции.Добавить(НоваяСтрокаПоказаний);
    КонецЦикла;

    Возврат РезультатФункции;
КонецФункции

// Параметры:
//  ЛицевойСчетСсылка - СправочникСсылка.КВП_ЛицевыеСчета
//  СтруктураХранилища - Структура, Неопределено
//      * Адрес - Строка
// Возвращаемое значение:
//  - ТаблицаЗначений - Когда ПоместитьВХранилище = Неопределено
//  - Строка - Адрес хранилища, когда заполнено СтруктураХранилища
&НаСервереБезКонтекста
Функция ГП_ПолучитьПриборыУчетаДоговораПоЛицевомуСчетуНаСервере(Знач ЛицевойСчетСсылка, Знач СтруктураХранилища = Неопределено)
    ТаблицаРезультатаФункции = ГП_ПриборыУчета.ПолучитьПриборыУчетаДоговораПоЛицевомуСчету(ЛицевойСчетСсылка, Истина);

    РезультатФункции = Неопределено;
    Если СтруктураХранилища <> Неопределено Тогда
        ТаблицаРезультатаФункции = ОбщегоНазначения.ТаблицаЗначенийВМассив(ТаблицаРезультатаФункции);
        РезультатФункции = ПоместитьВоВременноеХранилище(ТаблицаРезультатаФункции, СтруктураХранилища.Адрес);
    Иначе
        РезультатФункции = ТаблицаРезультатаФункции;
    КонецЕсли;

    Возврат РезультатФункции;
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
// Гарант+ Килипенко 10.09.2024 [F00227690] Заполнение динамических показателей --
