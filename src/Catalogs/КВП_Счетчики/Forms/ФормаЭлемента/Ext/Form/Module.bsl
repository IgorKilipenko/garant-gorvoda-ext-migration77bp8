// Гарант+ Килипенко 15.07.2024 [F00225713] Перенос из БП 7.7 в БП3 справочника Счетчики ++
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ГП_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
    // Добавление полей формы
    ГП_ДобавитьДополнительныеЭлементыФормыНаСервере();

    ГП_НастроитьВидимостьЭлементовФормы(ЭтотОбъект);

    ОбновитьЗначениеТекущегоКоличестваПоСреднему(ЭтотОбъект, ЭтотОбъект.Объект.Ссылка);
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы
// Гарант+ Килипенко 15.07.2024 [F00225713] Перенос из БП 7.7 в БП3 справочника Счетчики --

// Гарант+ Килипенко 11.11.2024 [F00230850] Сохранению показаний счетчика ++
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ГП_ЭтоСчетчикДляПерерасчетовПриИзменении(Элемент)
    ИсходноеЗначениеТарифности = ЭтотОбъект.Объект.Тарифность;
    Если ЭтотОбъект.Объект.ГП_ЭтоСчетчикДляПерерасчетов = Истина Тогда
        ЭтотОбъект.Объект.Тарифность = ПредопределенноеЗначение("Перечисление.КВП_ТарифностьСчетчиков.Двухтарифный");
    Иначе
        ЭтотОбъект.Объект.Тарифность = ПредопределенноеЗначение("Перечисление.КВП_ТарифностьСчетчиков.Однотарифный");
    КонецЕсли;

    Если ИсходноеЗначениеТарифности <> ЭтотОбъект.Объект.Тарифность Тогда
        ОбщегоНазначенияКлиент.СообщитьПользователю("Внимание! изменена тарифность счетчика");
    КонецЕсли;
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы
// Гарант+ Килипенко 11.11.2024 [F00230850] Сохранению показаний счетчика --

// Гарант+ Килипенко 15.07.2024 [F00225713] Перенос из БП 7.7 в БП3 справочника Счетчики ++
#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ГП_ТекущееКоличествоПоСреднемуНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
    СтандартнаяОбработка = Ложь;

    Если ЭтотОбъект.Объект.Ссылка.Пустая() Тогда
        ОбщегоНазначенияКлиент.СообщитьПользователю("Прибор учета должен быть записан (создан)");
        Возврат;
    КонецЕсли;

    ФормаЗаписи = ОткрытьФорму("РегистрСведений.ГП_ИсторияЗначенийПоСреднемуСчетчиков.ФормаЗаписи",
            Новый Структура, ЭтотОбъект, ЭтотОбъект.УникальныйИдентификатор, , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
    ФормаЗаписи.Запись.Период = ОбщегоНазначенияКлиент.ДатаСеанса();
    ФормаЗаписи.Запись.ПриборУчета = ЭтотОбъект.Объект.Ссылка;
    ФормаЗаписи.Запись.КоличествоПоСреднему = ЭтотОбъект.ГП_ТекущееКоличествоПоСреднему;
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовШапкиФормы

#Область ГП_СозданиеЭлементовФормы

&НаСервере
Процедура ГП_ДобавитьДополнительныеЭлементыФормыНаСервере()
    ГП_ДобавитьЭлементФормыДатаУстановкиНаСервере();
    ГП_ДобавитьЭлементФормыСоставнойКодБП77НаСервере();
    ГП_ДобавитьЭлементФормыТолькоДляКанализацииНаСервере();
    ГП_ДобавитьЭлементФормыЭтойСчетчикДляПерерасчетовНаСервере();
    ГП_ДобавитьЭлементФормыЭтоПлатаЗаВодоснабжениеОИНаСервере();
    ГП_ДобавитьЭлементФормыЭтоСчетчикПотерьВодыНаСервере();
    ГП_ДобавитьЭлементФормыКоличествоПоСреднемуНаСервере();
    ГП_ДобавитьЭлементФормыЭтоВиртуальныйСчетчикНаСервере();
    ГП_ДобавитьЭлементФормыКомментарийНаСервере();
КонецПроцедуры

// Добавляет поле формы ГП_ДатаУстановки
// Возвращаемое значение:
//  - ПолеФормы
&НаСервере
Функция ГП_ДобавитьЭлементФормыДатаУстановкиНаСервере()
    НовыйЭлементФормы = ГП_ДобавитьПолеФормыНаСервере(
            "ГП_ДатаУстановки", "Объект.ГП_ДатаУстановки", ЭтотОбъект.Элементы.ГруппаПоверка);

    Возврат НовыйЭлементФормы;
КонецФункции

// Добавляет поле формы ГП_ИдентификаторБП77
// Возвращаемое значение:
//  - ПолеФормы
&НаСервере
Функция ГП_ДобавитьЭлементФормыСоставнойКодБП77НаСервере()
    НовыйЭлементФормы = ГП_ДобавитьПолеФормыНаСервере(
            "ГП_ИдентификаторБП77", "Объект.ГП_ИдентификаторБП77", ЭтотОбъект.Элементы.ГруппаОбщиеСведения);
    НовыйЭлементФормы.ТолькоПросмотр = Истина;
    НовыйЭлементФормы.Видимость = ЗначениеЗаполнено(ЭтотОбъект.Объект.ГП_ИдентификаторБП77);

    Возврат НовыйЭлементФормы;
КонецФункции

// Добавляет поле формы ГП_Комментарий
// Возвращаемое значение:
//  - ПолеФормы
&НаСервере
Функция ГП_ДобавитьЭлементФормыКомментарийНаСервере()
    НовыйЭлементФормы = ГП_ДобавитьПолеФормыНаСервере(
            "ГП_Комментарий", "Объект.ГП_Комментарий", ЭтотОбъект.Элементы.ГруппаОбщиеСведения);

    Возврат НовыйЭлементФормы;
КонецФункции

// Добавляет поле формы ГП_ТолькоДляКанализации
// Возвращаемое значение:
//  - ПолеФормы
&НаСервере
Функция ГП_ДобавитьЭлементФормыТолькоДляКанализацииНаСервере()
    НовыйЭлементФормы = ГП_ДобавитьПолеФормыНаСервере(
            "ГП_ТолькоДляКанализации", "Объект.ГП_ТолькоДляКанализации", ЭтотОбъект.Элементы.ГруппаОбщиеСведения);

    НовыйЭлементФормы.Вид = ВидПоляФормы.ПолеФлажка;

    Возврат НовыйЭлементФормы;
КонецФункции

// Добавляет поле формы ГП_ЭтоПлатаЗаВодоснабжениеОИ
// Возвращаемое значение:
//  - ПолеФормы
&НаСервере
Функция ГП_ДобавитьЭлементФормыЭтоПлатаЗаВодоснабжениеОИНаСервере()
    НовыйЭлементФормы = ГП_ДобавитьПолеФормыНаСервере(
            "ГП_ЭтоПлатаЗаВодоснабжениеОИ", "Объект.ГП_ЭтоПлатаЗаВодоснабжениеОИ", ЭтотОбъект.Элементы.ГруппаОбщиеСведения);

    НовыйЭлементФормы.Вид = ВидПоляФормы.ПолеФлажка;

    Возврат НовыйЭлементФормы;
КонецФункции

// Добавляет поле формы ГП_ЭтоСчетчикПотерьВоды
// Возвращаемое значение:
//  - ПолеФормы
&НаСервере
Функция ГП_ДобавитьЭлементФормыЭтоСчетчикПотерьВодыНаСервере()
    НовыйЭлементФормы = ГП_ДобавитьПолеФормыНаСервере(
            "ГП_ЭтоСчетчикПотерьВоды", "Объект.ГП_ЭтоСчетчикПотерьВоды", ЭтотОбъект.Элементы.ГруппаОбщиеСведения);

    НовыйЭлементФормы.Вид = ВидПоляФормы.ПолеФлажка;

    Возврат НовыйЭлементФормы;
КонецФункции

// Добавляет поле формы ГП_ТекущееКоличествоПоСреднему
// Возвращаемое значение:
//  - ПолеФормы
&НаСервере
Функция ГП_ДобавитьЭлементФормыКоличествоПоСреднемуНаСервере()
    // Создание реквизита
    НаименованиеСоздаваемогоРеквизита = "ГП_ТекущееКоличествоПоСреднему";
    ТипРеквизита = ОбщегоНазначения.ОписаниеТипаЧисло(15, 3);
    НовыйРеквизит = Новый РеквизитФормы(НаименованиеСоздаваемогоРеквизита, ТипРеквизита, "", "Количество по среднему");
    ЭтотОбъект.ИзменитьРеквизиты(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(НовыйРеквизит));

    ЭтотОбъект[НаименованиеСоздаваемогоРеквизита] = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();

    НовыйЭлементФормы = ГП_ДобавитьПолеФормыНаСервере(
            "ГП_ТекущееКоличествоПоСреднему", НаименованиеСоздаваемогоРеквизита, ЭтотОбъект.Элементы.ГруппаОбщиеСведения);
    НовыйЭлементФормы.КнопкаВыбора = Истина;
    НовыйЭлементФормы.РедактированиеТекста = Ложь;
    НовыйЭлементФормы.КнопкаСоздания = Истина;
    НовыйЭлементФормы.УстановитьДействие("НачалоВыбора", НаименованиеСоздаваемогоРеквизита + "НачалоВыбора");

    Возврат НовыйЭлементФормы;
КонецФункции

// Добавляет поле формы ГП_ЭтоВиртуальныйСчетчик
// Возвращаемое значение:
//  - ПолеФормы
&НаСервере
Функция ГП_ДобавитьЭлементФормыЭтоВиртуальныйСчетчикНаСервере()
    НовыйЭлементФормы = ГП_ДобавитьПолеФормыНаСервере(
            "ГП_ЭтоВиртуальныйСчетчик", "Объект.ГП_ЭтоВиртуальныйСчетчик", ЭтотОбъект.Элементы.ГруппаОбщиеСведения);

    НовыйЭлементФормы.Вид = ВидПоляФормы.ПолеФлажка;
    НовыйЭлементФормы.Доступность = Ложь;

    Возврат НовыйЭлементФормы;
КонецФункции

// Гарант+ Килипенко 11.11.2024 [F00230850] Сохранению показаний счетчика ++
//
// Добавляет поле формы ГП_ЭтоВиртуальныйСчетчик
// Возвращаемое значение:
//  - ПолеФормы
&НаСервере
Функция ГП_ДобавитьЭлементФормыЭтойСчетчикДляПерерасчетовНаСервере()
    НовыйЭлементФормы = ГП_ДобавитьПолеФормыНаСервере(
            "ГП_ЭтоСчетчикДляПерерасчетов", "Объект.ГП_ЭтоСчетчикДляПерерасчетов", ЭтотОбъект.Элементы.ГруппаОбщиеСведения);

    НовыйЭлементФормы.Вид = ВидПоляФормы.ПолеФлажка;
    НовыйЭлементФормы.Доступность = Истина;
    НовыйЭлементФормы.УстановитьДействие("ПриИзменении", "ГП_ЭтоСчетчикДляПерерасчетовПриИзменении");

    Возврат НовыйЭлементФормы;
КонецФункции // Гарант+ Килипенко 11.11.2024 [F00230850] Сохранению показаний счетчика --

// Параметры:
//  Имя - Строка
//  Путь - Строка
//  Родитель - ГруппаФормы, ТаблицаФормы, ФормаКлиентскогоПриложения, Неопределено
// Возвращаемое значение:
//  - ПолеФормы
&НаСервере
Функция ГП_ДобавитьПолеФормыНаСервере(Знач Имя, Знач Путь, Знач Родитель = Неопределено)
    НовыйЭлементФормы = Неопределено;
    Если Родитель <> Неопределено Тогда
        НовыйЭлементФормы = ЭтотОбъект.Элементы.Добавить(Имя, Тип("ПолеФормы"), Родитель);
    Иначе
        НовыйЭлементФормы = ЭтотОбъект.Элементы.Добавить(Имя, Тип("ПолеФормы"));
    КонецЕсли;

    НовыйЭлементФормы.Вид = ВидПоляФормы.ПолеВвода;
    НовыйЭлементФормы.ПутьКДанным = Путь;
    НовыйЭлементФормы.АвтоОтметкаНезаполненного = Ложь;
    НовыйЭлементФормы.Видимость = Истина;

    Возврат НовыйЭлементФормы;
КонецФункции

#КонецОбласти // ГП_СозданиеЭлементовФормы

&НаКлиентеНаСервереБезКонтекста
Процедура ГП_НастроитьВидимостьЭлементовФормы(Форма)
    Форма.Элементы.Коэффициент.Видимость = Ложь;
    Форма.Элементы.ИспользоватьТемпературныйКоэффициент.Видимость = Ложь;
    Форма.Элементы.НадписьИспользоватьТемпературныйКоэффициент.Видимость = Ложь;
    Форма.Элементы.ИспользуетсяДляПерерасчетаРасходовНаОплатуСОИ.Видимость = Ложь;
    Форма.Элементы.ПринимаетПоказанияОтСистемыСбораПоказанийСчетчиков.Видимость = Ложь;
    Форма.Элементы.ПризнакВыходногоСчетчика.Видимость = Ложь;
    Форма.Элементы.НадписьПризнакВыходногоСчетчика.Видимость = Ложь;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьЗначениеТекущегоКоличестваПоСреднему(Форма, Знач ПриборУчетаСсылка)
    Форма.ГП_ТекущееКоличествоПоСреднему = ГП_ПолучитьТекущееКоличествоПоСреднемуНаСервере(ПриборУчетаСсылка);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ГП_ПолучитьТекущееКоличествоПоСреднемуНаСервере(Знач ПриборУчетаСсылка)
    РезультатФункции = 0;

    Если ПриборУчетаСсылка.Пустая() Тогда
        Возврат РезультатФункции;
    КонецЕсли;

    СтруктураКоличества = ГП_ПриборыУчета.ПолучитьТекущееКоличествоПоСреднемуСчетчика(ПриборУчетаСсылка, Неопределено);
    Если СтруктураКоличества.Успех = Истина Тогда
        РезультатФункции = СтруктураКоличества.Значение;
    КонецЕсли;

    Возврат РезультатФункции;
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
// Гарант+ Килипенко 15.07.2024 [F00225713] Перенос из БП 7.7 в БП3 справочника Счетчики --
