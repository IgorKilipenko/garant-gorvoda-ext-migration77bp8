#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ГП_ПередЗаписьюПосле(Отказ, ПараметрыЗаписи)
    Если ПустаяСтрока(ЭтотОбъект.Объект.Номер) = Истина
        И ГП_ЭтоДоговорАбонентскогоОтделаНаСервере() = Истина Тогда

        ГП_ЗаполнитьНовыйНомерАбонентскогоОтдела();
        ГП_ЗаполнитьНаименованиеДоговора();
    КонецЕсли;
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ГП_СформироватьНаименованиеДоговораНаСервере(Знач НомерДоговора)
    Возврат СтрШаблон("Договор № %1", НомерДоговора);
КонецФункции

&НаСервереБезКонтекста
Функция ГП_СформироватьНовыйНомерДоговораНаСервере(Знач ДоговорСсылка)
    СтруктураНомера = ГП_РаботаСДоговорами.ПолучитьМаксимальныйНомерДоговораАбонентскогоОтдела(ДоговорСсылка);
    Если СтруктураНомера.Успех = Ложь Тогда
        Возврат Неопределено;
    КонецЕсли;

    Возврат Формат(СтруктураНомера.Значение + 1, "ЧДЦ=0; ЧГ=0");
КонецФункции

&НаКлиенте
Процедура ГП_ЗаполнитьНаименованиеДоговора()
    Если ЗначениеЗаполнено(ЭтотОбъект.Объект.Наименование) = Ложь Тогда
        ЭтотОбъект.Объект.Наименование = ГП_СформироватьНаименованиеДоговораНаСервере(ЭтотОбъект.Объект.Номер);
    КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ГП_ЗаполнитьНовыйНомерАбонентскогоОтдела()
    СформированныйНомер = ГП_СформироватьНовыйНомерДоговораНаСервере(ЭтотОбъект.Объект.Ссылка);

    Если СформированныйНомер <> Неопределено Тогда
        ЭтотОбъект.Объект.Номер = СформированныйНомер;
    Иначе
        ОбщегоНазначенияКлиент.СообщитьПользователю("Не удалось получить последний номер для формирования");
    КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ГП_ЭтоДоговорАбонентскогоОтделаНаСервере()
    Возврат ЭтотОбъект.Объект.lc_ВидДоговора = ГП_РаботаСДоговорами.ПолучитьДополнительныйВидДоговораАбонентскийОтдел().Ссылка;
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
