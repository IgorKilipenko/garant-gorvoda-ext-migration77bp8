#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ГП_ПриСозданииНаСервереПеред(Отказ, СтандартнаяОбработка)
    Если ЭтотОбъект.Параметры.Отбор.Свойство("ВидДоговораДополнительный") Тогда
        ГП_ДобавитьОтборПоВидуДоговораДополнительномуНаСервере(ЭтотОбъект.Параметры.Отбор.ВидДоговораДополнительный);
    КонецЕсли;
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ГП_ДобавитьОтборПоВидуДоговораДополнительномуНаСервере(Знач ВидДоговораДополнительный)
    ВидСравненияПоВидыДоговора = Неопределено;

    Если ТипЗнч(ВидДоговораДополнительный) = Тип("СписокЗначений") Тогда
        ВидСравненияПоВидыДоговора = ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии;

    ИначеЕсли ТипЗнч(ВидДоговораДополнительный) = Тип("СправочникСсылка.lc_ВидыДоговоров")
        И ЗначениеЗаполнено(ВидДоговораДополнительный) Тогда

        Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидДоговораДополнительный, "ЭтоГруппа") Тогда
            ВидСравненияПоВидыДоговора = ВидСравненияКомпоновкиДанных.ВИерархии;
        Иначе
            ВидСравненияПоВидыДоговора = ВидСравненияКомпоновкиДанных.Равно;
        КонецЕсли;
    КонецЕсли;

    Если ВидСравненияПоВидыДоговора <> Неопределено Тогда
        // Установим собственный отбор.
        ОтборыСписковКлиентСервер.УстановитьЭлементОтбораСписка(ЭтотОбъект.Список, "lc_ВидДоговора",
            ВидДоговораДополнительный,
            ВидСравненияПоВидыДоговора);
        // Исключим стандартный отбор.
        ЭтотОбъект.Параметры.Отбор.Удалить("ВидДоговораДополнительный");
    КонецЕсли;
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
