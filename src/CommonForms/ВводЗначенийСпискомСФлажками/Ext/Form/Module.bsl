#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ГП_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
    ГП_СоздатьКомандуОчиститьСписок();
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ГП_ОчиститьСписокПриНажатии(Команда)
    ЭтотОбъект.Список.Очистить();
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ГП_СоздатьКомандуОчиститьСписок()
    РезультатФункции = Новый Структура("Кнопка, Команда");

    // Создание команды формы
    РезультатФункции.Команда = ЭтотОбъект.Команды.Добавить("ГП_ОчиститьСписок");
    РезультатФункции.Команда.Заголовок = "Очистить список";
    РезультатФункции.Команда.Действие = РезультатФункции.Команда.Имя + "ПриНажатии";

    // Создание элемента формы
    ПараметрыКнопки = Новый Структура("Заголовок, ИмяКоманды, Видимость, Вид");
    ПараметрыКнопки.Заголовок = РезультатФункции.Команда.Заголовок;
    ПараметрыКнопки.ИмяКоманды = РезультатФункции.Команда.Имя;
    ПараметрыКнопки.Видимость = Истина;
    ПараметрыКнопки.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;

    РезультатФункции.Кнопка = ГП_РаботаСФормамиКлиентСервер.СоздатьКнопкуФормы(РезультатФункции.Команда.Имя, ПараметрыКнопки, ЭтотОбъект,
            "ФормаКоманднаяПанель", "СписокПеремещение");

    РезультатФункции.Кнопка.Отображение = ОтображениеКнопки.Авто;
    РезультатФункции.Кнопка.Картинка = БиблиотекаКартинок.Закрыть;

    Возврат РезультатФункции;
КонецФункции

#КонецОбласти
