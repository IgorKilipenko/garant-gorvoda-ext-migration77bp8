// ++ Гарант+ Килипенко 25.07.2024 [F00226454] заполнение документов Установка счетчика ++
#Область ПрограммныйИнтерфейс

// Параметры:
//  ДатаДокумента - Дата, Неопределено
// Возвращаемое значение:
//  - Структура:
//      * Успех - Булево
//      * УстановленныеСчетчики - Массив из СправочникСсылка.КВП_Счетчики
//      * СообщениеОбОшибке - Строка, Неопределено
Функция СоздатьДокументыУстановкиСчетчиковБезВводаПоказаний(Знач ДатаДокумента = Неопределено) Экспорт
    РезультатФункции = Новый Структура("Успех, УстановленныеСчетчики, СообщениеОбОшибке", Истина, Новый Массив);

    ДанныеДляЗаполнения = ГП_МиграцияПриборовУчета.ПолучитьДанныеДляУстановкиСчетчиков(ДатаДокумента);
    Если ДанныеДляЗаполнения.Количество() = 0 Тогда
        Возврат РезультатФункции;
    КонецЕсли;

    ДатаДокумента = ?(ДатаДокумента = Неопределено, ТекущаяДатаСеанса(), ДатаДокумента);

    // Заполнение шапки
    НовыйУстановкаСчетчиковОбъект = Документы.КВП_УстановкаСчетчика.СоздатьДокумент();
    НовыйУстановкаСчетчиковОбъект.Дата = ДатаДокумента;
    НовыйУстановкаСчетчиковОбъект.ВидОперации = Перечисления.УПЖКХ_ВидыОперацийУстановкаСчетчика.ВключитьСчетчик;
    НовыйУстановкаСчетчиковОбъект.ВвестиНачальныеИлиКонечныеПоказания = Ложь; // Показания счетчиков не вводятся
    НовыйУстановкаСчетчиковОбъект.Ответственный = Пользователи.ТекущийПользователь();
    НовыйУстановкаСчетчиковОбъект.Комментарий = "#создан автоматически по данным из БП 7.7 (ГарантПлюс)";

    // Заполнение ТЧ Главная
    Для каждого СтрокаДанных Из ДанныеДляЗаполнения Цикл
        НоваяЗапись = НовыйУстановкаСчетчиковОбъект.Главная.Добавить();
        НоваяЗапись.Объект = СтрокаДанных.ЛицевойСчет;
        НоваяЗапись.Счетчик = СтрокаДанных.Счетчик;
        НоваяЗапись.ДатаВключения = СтрокаДанных.ДатаУстановки;
        НоваяЗапись.ДатаПоверки = СтрокаДанных.ДатаПоверки;

        РезультатФункции.УстановленныеСчетчики.Добавить(СтрокаДанных.Счетчик);
    КонецЦикла;

    // Запись документа
    Попытка
        НовыйУстановкаСчетчиковОбъект.Записать(РежимЗаписиДокумента.Запись);
    Исключение
        ЖурналРегистрации.ДобавитьСообщениеДляЖурналаРегистрации(
            "Ошибка при создании документа Установка счетчика при заполнении по данным из БП 7.7.",
            УровеньЖурналаРегистрации.Ошибка, , ,
            ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));

        РезультатФункции.КоличествоЗаписанныхПоказаний = 0;
        РезультатФункции.СообщениеОбОшибке = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
        РезультатФункции.Успех = Ложь;
    КонецПопытки;

    Возврат РезультатФункции;
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс
// -- Гарант+ Килипенко 25.07.2024 [F00226454] заполнение документов Установка счетчика --
