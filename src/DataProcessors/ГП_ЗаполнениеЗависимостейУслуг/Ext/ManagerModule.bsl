// ++ Гарант+ Килипенко 24.07.2024 [F00226318] заполнение регистра сведений Зависимость услуг ++
#Область ПрограммныйИнтерфейс

// Выполняет запись (обновление) зависимостей услуг в регистр сведений "Зависимости услуг".
// Параметры:
//  Период - Дата, Неопределено - Дата (начало месяца), по умолчанию 01.01.2024г
// Возвращаемое значение:
//  - Структура
//      * Успех - Булево
//      * КоличествоЗаписейЗависимыхУслуг - Число
//      * СообщениеОбОшибке - Строка, Неопределено
Функция ЗаполнитьЗависимостиУслуг(Знач Период = Неопределено) Экспорт
    РезультатФункции = Новый Структура("Успех, КоличествоЗаписейЗависимыхУслуг, СообщениеОбОшибке", Истина, 0);

    РезультатПолученияДанных = ПолучитьДанныеЗависимостейУслугАбонентов();
    Если РезультатПолученияДанных.Успех = Ложь Тогда
        РезультатФункции.Успех = Ложь;
        Возврат РезультатФункции;
    КонецЕсли;

    ТаблицаСоответствийКодовУслуг = ПолучитьСоответствиеКВПУслуг();

    // Значения по умолчанию
    ЗависимаяУслуга = ТаблицаСоответствийКодовУслуг["00-008"]; // Канализация
    ОсновнаяОрганизацияСсылка = УПЖКХ_ТиповыеМетодыВызовСервера.ПолучитьОсновнуюОрганизацию();
    Если Период = Неопределено Тогда
        Период = Дата(2004, 01, 01); // По ТЗ дата по умолчанию 01.01.2024г.
    Иначе
        Период = НачалоМесяца(Период);
    КонецЕсли;

    СписокОсновныхУслуг = Новый Массив;
    СписокОсновныхУслуг.Добавить("ХолоднаяВода");
    СписокОсновныхУслуг.Добавить("ГорячаяВода");
    СписокОсновныхУслуг.Добавить("ХолоднаяВодаВодоотвод");

    НачатьТранзакцию();
    Попытка
        Для Каждого КлючУслуги Из СписокОсновныхУслуг Цикл
            Для Каждого СтрокаУслуги Из РезультатПолученияДанных[КлючУслуги] Цикл
                ОсновнаяУслуга = ТаблицаСоответствийКодовУслуг[СтрокаУслуги.КодУслуги];
                НовыйНаборЗаписей = СоздатьНаборЗаписейЗависимостиУслуг(
                        Период, СтрокаУслуги.Здание, ОсновнаяУслуга, ЗависимаяУслуга, ОсновнаяОрганизацияСсылка);

                НоваяЗапись = НовыйНаборЗаписей.Добавить();
                НоваяЗапись.Период = Период;
                НоваяЗапись.Объект = СтрокаУслуги.Здание;
                НоваяЗапись.УслугаОснование = ОсновнаяУслуга;
                НоваяЗапись.Услуга = ЗависимаяУслуга;
                НоваяЗапись.Организация = ОсновнаяОрганизацияСсылка;
                НоваяЗапись.Действует = Истина; // По ТЗ всегда Истина
                НоваяЗапись.ЗначениеОснование = 100.0;
                НоваяЗапись.Значение = СтрокаУслуги.Процент;

                НовыйНаборЗаписей.Записать(Истина);
                РезультатФункции.КоличествоЗаписейЗависимыхУслуг = РезультатФункции.КоличествоЗаписейЗависимыхУслуг + 1;
            КонецЦикла;
        КонецЦикла;

        ЗафиксироватьТранзакцию(); // Записано успешно

    Исключение
        ОтменитьТранзакцию();

        ЖурналРегистрации.ДобавитьСообщениеДляЖурналаРегистрации(
            "Ошибка при записи зависимостей услуг в регистр ""КВП_ЗависимостиУслуг"".",
            УровеньЖурналаРегистрации.Ошибка, , ,
            ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));

        РезультатФункции.КоличествоЗаписейЗависимыхУслуг = 0;
        РезультатФункции.СообщениеОбОшибке = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
        РезультатФункции.Успех = Ложь;
    КонецПопытки;

    Возврат РезультатФункции;
КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс
// -- Гарант+ Килипенко 24.07.2024 [F00226318] заполнение регистра сведений Зависимость услуг --

// ++ Гарант+ Килипенко 24.07.2024 [F00226318] заполнение регистра сведений Зависимость услуг ++
#Область СлужебныеПроцедурыИФункции

// Возвращаемое значение:
//  - Структура
//      * Успех - Булево
//      * ХолоднаяВода - ТаблицаЗначений, Неопределено
//      * ГорячаяВода - ТаблицаЗначений, Неопределено
//      * Канализация - ТаблицаЗначений, Неопределено
//      * ХолоднаяВодаВодоотвод - ТаблицаЗначений, Неопределено
//          ** ОбъектАбонентаКод - Строка
//          ** КонтрагентКод - Строка
//          ** ЛицевойСчет - СправочникСсылка.КВП_ЛицевыеСчета
//          ** Здание - СправочникСсылка.КВП_Здания
//          ** Процент - Число - Процент использования В %
//          ** ЭтоХолоднаяВода - Булево
//          ** КодУслуги - Строка
Функция ПолучитьДанныеЗависимостейУслугАбонентов()
    РезультатФункции = Новый Структура(
            "Успех, ХолоднаяВода, ГорячаяВода, ХолоднаяВодаВодоотвод", Истина);

    Запрос = Новый Запрос;
    #Область ТекстЗапроса
    Запрос.Текст =
        "////////////////////////////////////////////////////////////////////////////////
        |// Сбор данных объектов абонентов из регистра ГП_УслугиБП77
        |//
        |ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
        |	ГП_УслугиБП77.ОбъектАбонентаКод КАК ОбъектАбонентаКод,
        |	ГП_УслугиБП77.КонтрагентКод КАК КонтрагентКод,
        |	ГП_УслугиБП77.ЛицевойСчет КАК ЛицевойСчет,
        |	ГП_УслугиБП77.ТолькоДляКанализации КАК ТолькоДляКанализации,
        |	ГП_УслугиБП77.МетодРасчетаХВ КАК МетодРасчетаХВ,
        |	ГП_УслугиБП77.МетодРасчетаГВ КАК МетодРасчетаГВ,
        |	ГП_УслугиБП77.МетодРасчетаКан КАК МетодРасчетаКан,
        |	ГП_УслугиБП77.ПроцентХВ КАК ПроцентХВ,
        |	ГП_УслугиБП77.ПроцентГВ КАК ПроцентГВ
        |ПОМЕСТИТЬ ВТ_ОбъектыАбонентовПодготовка
        |ИЗ
        |	РегистрСведений.ГП_УслугиБП77 КАК ГП_УслугиБП77
        |ГДЕ
        |   ГП_УслугиБП77.ЛицевойСчет <> &ПустаяСсылкаЛС
        |   И ГП_УслугиБП77.МетодРасчетаКан <> ""Нет""
        |   И (ГП_УслугиБП77.ПроцентХВ > 0 ИЛИ ГП_УслугиБП77.ПроцентГВ > 0)
        |	И (ГП_УслугиБП77.МетодРасчетаХВ <> ""Нет"" ИЛИ ГП_УслугиБП77.МетодРасчетаГВ <> ""Нет"")
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |// Результат [1] - Холодная вода
        |ВЫБРАТЬ
        |	ВТ_ОбъектыАбонентовПодготовка.ОбъектАбонентаКод КАК ОбъектАбонентаКод,
        |	ВТ_ОбъектыАбонентовПодготовка.КонтрагентКод КАК КонтрагентКод,
        |	ВТ_ОбъектыАбонентовПодготовка.ЛицевойСчет КАК ЛицевойСчет,
        |	ВТ_ОбъектыАбонентовПодготовка.ЛицевойСчет.Адрес.Владелец КАК Здание,
        |	ВТ_ОбъектыАбонентовПодготовка.ПроцентХВ КАК Процент,
        |	""00-001"" КАК КодУслуги
        |ИЗ
        |   ВТ_ОбъектыАбонентовПодготовка КАК ВТ_ОбъектыАбонентовПодготовка
        |ГДЕ
        |   ВТ_ОбъектыАбонентовПодготовка.МетодРасчетаХВ <> ""Нет""
        |   И ВТ_ОбъектыАбонентовПодготовка.ТолькоДляКанализации = ЛОЖЬ
        |   И ВТ_ОбъектыАбонентовПодготовка.ПроцентХВ > 0
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |// Результат [2] - Холодная вода (водоотвод)
        |ВЫБРАТЬ
        |	ВТ_ОбъектыАбонентовПодготовка.ОбъектАбонентаКод КАК ОбъектАбонентаКод,
        |	ВТ_ОбъектыАбонентовПодготовка.КонтрагентКод КАК КонтрагентКод,
        |	ВТ_ОбъектыАбонентовПодготовка.ЛицевойСчет КАК ЛицевойСчет,
        |	ВТ_ОбъектыАбонентовПодготовка.ЛицевойСчет.Адрес.Владелец КАК Здание,
        |	ВТ_ОбъектыАбонентовПодготовка.ПроцентХВ КАК Процент,
        |	""00-005"" КАК КодУслуги
        |ИЗ
        |   ВТ_ОбъектыАбонентовПодготовка КАК ВТ_ОбъектыАбонентовПодготовка
        |ГДЕ
        |   ВТ_ОбъектыАбонентовПодготовка.МетодРасчетаХВ <> ""Нет""
        |   И ВТ_ОбъектыАбонентовПодготовка.ТолькоДляКанализации = ИСТИНА
        |   И ВТ_ОбъектыАбонентовПодготовка.ПроцентХВ > 0
        |;
        |
        |////////////////////////////////////////////////////////////////////////////////
        |// Результат [3] - Горячая вода
        |ВЫБРАТЬ
        |	ВТ_ОбъектыАбонентовПодготовка.ОбъектАбонентаКод КАК ОбъектАбонентаКод,
        |	ВТ_ОбъектыАбонентовПодготовка.КонтрагентКод КАК КонтрагентКод,
        |	ВТ_ОбъектыАбонентовПодготовка.ЛицевойСчет КАК ЛицевойСчет,
        |	ВТ_ОбъектыАбонентовПодготовка.ЛицевойСчет.Адрес.Владелец КАК Здание,
        |	ВТ_ОбъектыАбонентовПодготовка.ПроцентГВ КАК Процент,
        |	""00-003"" КАК КодУслуги
        |ИЗ
        |   ВТ_ОбъектыАбонентовПодготовка КАК ВТ_ОбъектыАбонентовПодготовка
        |ГДЕ
        |   ВТ_ОбъектыАбонентовПодготовка.МетодРасчетаГВ <> ""Нет""
        |   И ВТ_ОбъектыАбонентовПодготовка.ПроцентГВ > 0
        |;";

    #КонецОбласти // ТекстЗапроса

    Запрос.УстановитьПараметр("ПустаяСсылкаЛС", Справочники.КВП_ЛицевыеСчета.ПустаяСсылка());

    ТаблицыПакета = Запрос.ВыполнитьПакет();
    РезультатФункции.ХолоднаяВода = ТаблицыПакета[1].Выгрузить();
    РезультатФункции.ГорячаяВода = ТаблицыПакета[2].Выгрузить();
    РезультатФункции.ХолоднаяВодаВодоотвод = ТаблицыПакета[3].Выгрузить();

    РезультатФункции.Успех = (РезультатФункции.ХолоднаяВода.Количество()
            + РезультатФункции.ГорячаяВода.Количество()
            + РезультатФункции.ХолоднаяВодаВодоотвод.Количество()) > 0;

    Возврат РезультатФункции;
КонецФункции

// Параметры:
//  Период - Дата
//  Объект - СправочникСсылка.КВП_Здания
//  УслугаОснованиеСсылка - СправочникСсылка.КВП_Услуги
//  ЗависимаяУслугаСсылка - СправочникСсылка.КВП_Услуги
//  ОрганизацияСсылка - СправочникСсылка.Организации, Неопределено
// Возвращаемое значение:
//  - РегистрСведений.ГП_СчетчикиБП77.НаборЗаписей
Функция СоздатьНаборЗаписейЗависимостиУслуг(
        Знач Период, Знач Объект, Знач УслугаОснованиеСсылка,
        Знач ЗависимаяУслугаСсылка = Неопределено, Знач ОрганизацияСсылка = Неопределено)

    НаборЗаписейСчетчиков = РегистрыСведений.КВП_ЗависимостиУслуг.СоздатьНаборЗаписей();
    НаборЗаписейСчетчиков.Отбор.Период.Установить(НачалоМесяца(Период));
    НаборЗаписейСчетчиков.Отбор.Объект.Установить(Объект);
    НаборЗаписейСчетчиков.Отбор.УслугаОснование.Установить(УслугаОснованиеСсылка);
    Если ЗависимаяУслугаСсылка <> Неопределено Тогда
        НаборЗаписейСчетчиков.Отбор.Услуга.Установить(ЗависимаяУслугаСсылка);
    КонецЕсли;
    Если ОрганизацияСсылка <> Неопределено Тогда
        НаборЗаписейСчетчиков.Отбор.Услуга.Установить(ЗависимаяУслугаСсылка);
    КонецЕсли;

    Возврат НаборЗаписейСчетчиков;
КонецФункции

// Возвращаемое значение:
//  - Соответствие, Неопределено
Функция ПолучитьСоответствиеКВПУслуг()
    Запрос = Новый Запрос;
    Запрос.Текст =
        "ВЫБРАТЬ
        |   КВП_Услуги.Ссылка КАК Ссылка,
        |   КВП_Услуги.Код КАК Код
        |ИЗ
        |   Справочник.КВП_Услуги КАК КВП_Услуги
        |ГДЕ
        |   КВП_Услуги.Код В (&КодыУслуг)
        |";

    МассивКодовУслуг = Новый Массив;
    МассивКодовУслуг.Добавить("00-001");
    МассивКодовУслуг.Добавить("00-002");
    МассивКодовУслуг.Добавить("00-003");
    МассивКодовУслуг.Добавить("00-004");
    МассивКодовУслуг.Добавить("00-005");
    МассивКодовУслуг.Добавить("00-006");
    МассивКодовУслуг.Добавить("00-008");

    Запрос.УстановитьПараметр("КодыУслуг", МассивКодовУслуг);

    РезультатЗапроса = Запрос.Выполнить();
    Если РезультатЗапроса.Пустой() Тогда
        Возврат Неопределено;
    КонецЕсли;

    ТаблицаКВПУслуг = РезультатЗапроса.Выгрузить();

    РезультатФункции = Новый Соответствие;
    Для Каждого СтрокаУслуг Из ТаблицаКВПУслуг Цикл
        РезультатФункции.Вставить(СтрокаУслуг.Код, СтрокаУслуг.Ссылка);
    КонецЦикла;

    Возврат РезультатФункции;
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
// -- Гарант+ Килипенко 24.07.2024 [F00226318] заполнение регистра сведений Зависимость услуг --
