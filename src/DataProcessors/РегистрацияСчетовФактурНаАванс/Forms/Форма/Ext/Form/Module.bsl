// Гарант+ Килипенко 31.01.2025 [F00234069] Доп отбор в РегистрацияСчетовФактурНаАванс ++
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ГП_ПриСозданииНаСервереПеред(Отказ, СтандартнаяОбработка)
    СоздатьДополнительныеЭлементыФормыНаСервере();
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы
// Гарант+ Килипенко 31.01.2025 [F00234069] Доп отбор в РегистрацияСчетовФактурНаАванс --

// Гарант+ Килипенко 31.01.2025 [F00234069] Доп отбор в РегистрацияСчетовФактурНаАванс ++
#Область ПереопределениеСтандартногоФункционала

&НаСервере
&ИзменениеИКонтроль("ЗаполнитьСчетаФактурыНаСервере")
Функция ГП_ЗаполнитьСчетаФактурыНаСервере(ИБФайловая)

	Объект.Список.Очистить();

	СтруктураПараметров = Обработки.РегистрацияСчетовФактурНаАванс.ПараметрыЗаполнения();
	ЗаполнитьЗначенияСвойств(СтруктураПараметров, Объект);
    #Вставка // Гарант+ Килипенко 31.01.2025 [F00234069] Доп отбор в РегистрацияСчетовФактурНаАванс ++

    // Поле дополнительного отбора (опциональное)
    Если ЗначениеЗаполнено(ЭтотОбъект.ГП_ВидДоговораДополнительно) Тогда
        СтруктураПараметров.Вставить("ГП_ВидДоговораДополнительно", ЭтотОбъект.ГП_ВидДоговораДополнительно);
        ОбщегоНазначения.СообщитьПользователю(
            "Информация. При отборе списка использован дополнительный отбор: ""Вид договора (доп.)""");
    КонецЕсли;

    #КонецВставки // Гарант+ Килипенко 31.01.2025 [F00234069] Доп отбор в РегистрацияСчетовФактурНаАванс --

	Если ИБФайловая Тогда
		ЭтаФорма.АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, ЭтаФорма.УникальныйИдентификатор);
		Обработки.РегистрацияСчетовФактурНаАванс.ПодготовитьДанныеДляЗаполнения(СтруктураПараметров, АдресХранилища);
		Результат = Новый Структура("ЗаданиеВыполнено", Истина);

	Иначе
		НаименованиеФоновогоЗадания = НСтр("ru = 'Заполнение таблицы ""Регистрация счетов-фактур на аванс""'");
		Результат = ДлительныеОперации.ЗапуститьВыполнениеВФоне(
			УникальныйИдентификатор,
			"Обработки.РегистрацияСчетовФактурНаАванс.ПодготовитьДанныеДляЗаполнения",
			СтруктураПараметров,
			НаименованиеФоновогоЗадания);

		АдресХранилища = Результат.АдресХранилища;
	КонецЕсли;

	Если Результат.ЗаданиеВыполнено Тогда
		ЗагрузитьПодготовленныеДанные();
	КонецЕсли;

	Возврат Результат;

КонецФункции

#КонецОбласти // ПереопределениеСтандартногоФункционала
// Гарант+ Килипенко 31.01.2025 [F00234069] Доп отбор в РегистрацияСчетовФактурНаАванс --

// Гарант+ Килипенко 31.01.2025 [F00234069] Доп отбор в РегистрацияСчетовФактурНаАванс ++
#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СоздатьДополнительныеЭлементыФормыНаСервере()
    СозданныеЭлементы = Новый Массив;
    СозданныеЭлементы.Добавить(ГП_СоздатьЭлементВидДоговораДополнительноНаСервере());
    Возврат СозданныеЭлементы;
КонецФункции

&НаСервере
Функция ГП_СоздатьЭлементВидДоговораДополнительноНаСервере()
    РезультатФункции = Новый Структура("Элемент, Реквизит, ГруппаФормы");

    НаименованиеСоздаваемогоРеквизита = "ГП_ВидДоговораДополнительно";

    // Создание реквизита
    ТипРеквизита = Новый ОписаниеТипов("СправочникСсылка.lc_ВидыДоговоров");
    РезультатФункции.Реквизит = Новый РеквизитФормы(НаименованиеСоздаваемогоРеквизита, ТипРеквизита, "", "Вид договора (доп.)");
    ЭтотОбъект.ИзменитьРеквизиты(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(РезультатФункции.Реквизит));

    ЭтотОбъект[НаименованиеСоздаваемогоРеквизита] = Справочники.lc_ВидыДоговоров.ПустаяСсылка();

    // Создание элемента формы
    РезультатФункции.Элемент = ГП_РаботаСФормамиКлиентСервер.СоздатьПолеФормы(НаименованиеСоздаваемогоРеквизита,
            Новый Структура, ЭтотОбъект, "ГруппаВыборПериода", Неопределено);
    РезультатФункции.Элемент.Вид = ВидПоляФормы.ПолеВвода;
    РезультатФункции.Элемент.ПутьКДанным = НаименованиеСоздаваемогоРеквизита;
    РезультатФункции.Элемент.КнопкаОчистки = Истина;
    // РезультатФункции.Элемент.УстановитьДействие("ПриИзменении", "ГП_ВидДоговораДополнительноПриИзменении");

    Возврат РезультатФункции;
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
// Гарант+ Килипенко 31.01.2025 [F00234069] Доп отбор в РегистрацияСчетовФактурНаАванс --
