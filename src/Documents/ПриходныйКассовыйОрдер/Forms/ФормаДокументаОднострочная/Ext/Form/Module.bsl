// Гарант+ Килипенко 27.11.2024 [F00230427] Создание регистрации оплаты на основании поступления ++
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ГП_ПриСозданииНаСервереПеред(Отказ, СтандартнаяОбработка)
    ГП_СоздатьКомандуСоздатьДокументРасчетовНаСервере();

    // ГП_ИнициализацияНаСервере();
КонецПроцедуры

&НаСервере
Процедура ГП_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
    // ИсходнаяСтавкаНДС = Неопределено;
    // Если Объект.ВидОперации = Перечисления.ВидыОперацийПКО.ОплатаПокупателя
    //         И Форма.СвойстваПлатежа.Свойство("СтавкаНДС") Тогда
    //
    //     ИсходнаяРасшифровкаПлатежа = ПриходныйКассовыйОрдерФормыКлиентСервер.ПолучитьРасшифровкаПлатежа(Форма);
    //     ИсходнаяСтавкаНДС = ПолучитьСтавкуНДСРасшифровкиДляПерезаполнения(ИсходнаяРасшифровкаПлатежа);
    // КонецЕсли;
    //
    // Если ПустаяСтрока(Строка(ИсходнаяСтавкаНДС)) = Ложь И Форма.СвойстваПлатежа.СтавкаНДС <> ИсходнаяСтавкаНДС Тогда
    //     Форма.СвойстваПлатежа.СтавкаНДС = ИсходнаяСтавкаНДС;
    //     ОбщегоНазначения.СообщитьПользователю(
    //         "Внимание! Ставка НДС осталась без изменений (принята из первой строки расшифровки платежа)");
    // КонецЕсли;

    ГП_ИнициализацияСтавкиНДСНаСервере();
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы
// Гарант+ Килипенко 27.11.2024 [F00230427] Создание регистрации оплаты на основании поступления --

// Гарант+ Килипенко 27.11.2024 [F00230427] Создание регистрации оплаты на основании поступления ++
#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура ГП_СоздатьДокументРасчетовПриНажатии()
    Ждать ОткрытьНовыйДокументРегистрацииОплаты();
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы
// Гарант+ Килипенко 27.11.2024 [F00230427] Создание регистрации оплаты на основании поступления --

// Гарант+ Килипенко 27.11.2024 [F00230427] Создание регистрации оплаты на основании поступления ++
#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Асинх Функция ОткрытьНовыйДокументРегистрацииОплаты()
    Если ЭтотОбъект.Объект.Проведен = Ложь Тогда
        ОбщегоНазначенияКлиент.СообщитьПользователю("Документ должен быть проведен");
        Возврат Неопределено;
    КонецЕсли;

    РезультатКонтроля = ГП_ВыполнитьКонтрольРанееРаспределенныхСуммДокументаНаСервере();
    Если РезультатКонтроля.Успех = Ложь ИЛИ РезультатКонтроля.ДокументыРегистрации.Количество() > 0 Тогда
        РезультатДиалога = Ждать ГП_ОбщегоНазначенияКлиент.СпроситьПользователяОГотовностиВыполнятьОперациюАсинх(
                СтрШаблон("Внимание! По текущему документу ранее распределена сумма: %1. Остаток: %2",
                    ГП_ОбщегоНазначенияКлиент.ФорматСумм(РезультатКонтроля.ОбщаяРаспределеннаяСумма),
                    ГП_ОбщегоНазначенияКлиент.ФорматСумм(ЭтотОбъект.Объект.СуммаДокумента - РезультатКонтроля.ОбщаяРаспределеннаяСумма)));
        Если РезультатДиалога = Ложь Тогда
            Возврат Неопределено; // Пользователь отказался от выполнения операции
        КонецЕсли;
    КонецЕсли;

    ПараметрыРегистрацииОплаты = Новый Структура("ГП_ДанныеЗаполнения", Новый Структура);

    ПараметрыРегистрацииОплаты.ГП_ДанныеЗаполнения.Вставить("Дата", ЭтотОбъект.Объект.Дата);
    ПараметрыРегистрацииОплаты.ГП_ДанныеЗаполнения.Вставить("ДокументОплаты", ЭтотОбъект.Объект.Ссылка);
    ПараметрыРегистрацииОплаты.ГП_ДанныеЗаполнения.Вставить("Контрагент", ЭтотОбъект.Объект.Контрагент);
    ПараметрыРегистрацииОплаты.ГП_ДанныеЗаполнения.Вставить("ВидОперации",
        ПредопределенноеЗначение("Перечисление.КВП_ВидыОперацийРегистрацииОплаты.ПоступлениеВКассу"));
    ПараметрыРегистрацииОплаты.ГП_ДанныеЗаполнения.Вставить("СуммаДокументаОплаты", ЭтотОбъект.Объект.СуммаДокумента);

    ФормаРегистрацияОплаты = ОткрытьФорму("Документ.КВП_РегистрацияОплаты.Форма.ФормаДокумента",
            ПараметрыРегистрацииОплаты, , ЭтотОбъект.УникальныйИдентификатор, , , , РежимОткрытияОкнаФормы.Независимый);

    Возврат ФормаРегистрацияОплаты;
КонецФункции

&НаСервере
Функция ГП_СоздатьКомандуСоздатьДокументРасчетовНаСервере()
    Результат = Новый Структура("Кнопка, Команда");

    // Создание команды формы
    Результат.Команда = ЭтотОбъект.Команды.Добавить("ГП_СоздатьДокументРасчетов");
    Результат.Команда.Заголовок = "Создать док. расчетов";
    Результат.Команда.Действие = Результат.Команда.Имя + "ПриНажатии";

    // Создание элемента формы
    ПараметрыКнопки = Новый Структура("Заголовок, ИмяКоманды, Видимость");
    ПараметрыКнопки.Заголовок = Результат.Команда.Заголовок;
    ПараметрыКнопки.ИмяКоманды = Результат.Команда.Имя;
    ПараметрыКнопки.Видимость = Истина;

    Результат.Кнопка = ГП_РаботаСФормамиКлиентСервер.СоздатьКнопкуФормы(Результат.Команда.Имя, ПараметрыКнопки, ЭтотОбъект,
            "ГруппаВажныеКоманды", "ФормаОбщаяКомандаПоказатьДвиженияДокумента");

    Возврат Результат;
КонецФункции

// Возвращаемое значение:
//  - Структура
//      * Успех - Булево
//      * ОбщаяРаспределеннаяСумма - Число
//      * ДокументыРегистрации - Массив из ДокументСсылка
&НаСервере
Функция ГП_ВыполнитьКонтрольРанееРаспределенныхСуммДокументаНаСервере()
    РезультатКонтроля = ГП_РегистрацияОплатыСлужебный.ВыполнитьКонтрольРанееРаспределенныхСуммДокумента(
        ЭтотОбъект.Объект.Ссылка, ЭтотОбъект.Объект.СуммаДокумента);
    Возврат РезультатКонтроля;
КонецФункции

&НаСервере
Процедура ГП_ИнициализацияСтавкиНДСНаСервере()
    // Заполнение поля СтавкаНДС по умолчанию
    Если ЭтотОбъект.Объект.Ссылка.Пустая() И ЭтотОбъект.Параметры.Ключ.Пустая()
        И ЭтотОбъект.Объект.ВидОперации = Перечисления.ВидыОперацийПКО.ОплатаПокупателя
        И ЭтотОбъект.СвойстваПлатежа = Неопределено
        И ЭтотОбъект.Объект.РасшифровкаПлатежа.Количество() = 1 Тогда

        //ЭтотОбъект.СвойстваПлатежа = ПриходныйКассовыйОрдерФормыКлиентСервер.ПустаяСтруктураСвойстваПлатежа();
        //ЭтотОбъект.СвойстваПлатежа.СтавкаНДС = ГП_ОбщегоНазначения.ПолучитьОбщуюСтавкуНДСНачисленийЖКХ(ТекущаяДатаСеанса());
        // Если ПриходныйКассовыйОрдерФормыКлиентСервер.ФормаДокументаОднострочная(Форма) Тогда
        //     ПриходныйКассовыйОрдерФормыКлиентСервер.ЗаполнитьРеквизитыРасшифровкаПлатежа(Форма, Истина);
        // КонецЕсли;

        //ПриходныйКассовыйОрдерФормыКлиентСервер.ЗаполнитьРеквизитыРасшифровкаПлатежа(ЭтотОбъект, Истина);
        //ЭтотОбъект.РасшифровкаПлатежаСтавкаНДС = ЭтотОбъект.СвойстваПлатежа.СтавкаНДС;
        //ПриходныйКассовыйОрдерФормыКлиент.РасшифровкаПлатежаСтавкаНДСПриИзменении(ЭтотОбъект, ЭтотОбъект.Элементы.РасшифровкаПлатежаСтавкаНД);

        СтрокаПлатеж = ПриходныйКассовыйОрдерФормыКлиентСервер.РасшифровкаПлатежаТекущиеДанные(ЭтотОбъект);
        Если СтрокаПлатеж <> Неопределено И ЗначениеЗаполнено(СтрокаПлатеж.СуммаПлатежа) = Ложь Тогда
            СтрокаПлатеж.СтавкаНДС = ГП_ОбщегоНазначения.ПолучитьОбщуюСтавкуНДСНачисленийЖКХ(ТекущаяДатаСеанса());
            ПриходныйКассовыйОрдерФормыКлиентСервер.ПересчитатьСуммуНДС(СтрокаПлатеж);
            ПриходныйКассовыйОрдерФормыКлиентСервер.ДоступностьСтавкиНДС(ЭтотОбъект);
            ПриходныйКассовыйОрдерФормыКлиентСервер.ЗаполнитьРеквизитыРасшифровкаПлатежа(ЭтотОбъект, Истина, СтрокаПлатеж);
            ЭтотОбъект.Объект.РасшифровкаПлатежа[0].СтавкаНДС = СтрокаПлатеж.СтавкаНДС;
        КонецЕсли;
    КонецЕсли;
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
// Гарант+ Килипенко 27.11.2024 [F00230427] Создание регистрации оплаты на основании поступления --
