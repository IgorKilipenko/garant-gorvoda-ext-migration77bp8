// Гарант+ Килипенко 16.12.2024 [F00232332] Создание РН.ОплатаУслуг ++
#Область ОбработчикиСобытий

&После ("ОбработкаПроведения")
Процедура ГП_ОбработкаПроведения(Отказ, РежимПроведения)
    Если Отказ Тогда
        Возврат;
    КонецЕсли;

    ЭтотОбъект.Движения.ГП_ОплатаПоУслугам.Записывать = Истина;
    ЭтотОбъект.Движения.ГП_ОплатаПоУслугам.Очистить();

    Если ЭтотОбъект.ВидОперации <> Перечисления.ВидыОперацийПоступлениеДенежныхСредств.ОплатаПокупателя Тогда
        Возврат; // заполнение строк только для ОплатаПокупателя
    КонецЕсли;

    РасшифровкаПлатежаКопия = ЭтотОбъект.РасшифровкаПлатежа.Выгрузить( ,
            "ДоговорКонтрагента, ГП_Номенклатура, ГП_НоменклатурнаяГруппа, СуммаПлатежа, СуммаНДС, СтавкаНДС");
    РасшифровкаПлатежаКопия.Свернуть("ДоговорКонтрагента, ГП_Номенклатура, ГП_НоменклатурнаяГруппа, СтавкаНДС",
        "СуммаПлатежа, СуммаНДС");

    ВидДвиженияОплатаПоУслугам = ВидДвиженияНакопления.Приход;
    Для Каждого СтрокаРасшифровки Из РасшифровкаПлатежаКопия Цикл
        НоваяЗаписьОплатаПоУслугам = ЭтотОбъект.Движения.ГП_ОплатаПоУслугам.Добавить();
        НоваяЗаписьОплатаПоУслугам.ВидДвижения = ВидДвиженияОплатаПоУслугам;

        НоваяЗаписьОплатаПоУслугам.Период = ЭтотОбъект.Дата;
        НоваяЗаписьОплатаПоУслугам.Контрагент = ЭтотОбъект.Контрагент;
        НоваяЗаписьОплатаПоУслугам.Договор = СтрокаРасшифровки.ДоговорКонтрагента;
        НоваяЗаписьОплатаПоУслугам.Номенклатура = СтрокаРасшифровки.ГП_Номенклатура;
        НоваяЗаписьОплатаПоУслугам.НоменклатурнаяГруппа = СтрокаРасшифровки.ГП_НоменклатурнаяГруппа;
        НоваяЗаписьОплатаПоУслугам.Сумма = СтрокаРасшифровки.СуммаПлатежа;
        НоваяЗаписьОплатаПоУслугам.СуммаНДС = СтрокаРасшифровки.СуммаНДС;
        НоваяЗаписьОплатаПоУслугам.СтавкаНДС = СтрокаРасшифровки.СтавкаНДС;
    КонецЦикла;
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий
// Гарант+ Килипенко 16.12.2024 [F00232332] Создание РН.ОплатаУслуг --
