// Гарант+ Килипенко 16.12.2024 [F00232332] Создание РН.ОплатаУслуг ++
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ГП_ПриСозданииНаСервереПеред(Отказ, СтандартнаяОбработка)
    ГП_СоздатьЭлементРасшифровкаПлатежаНоменклатураНаСервере();
    ГП_СоздатьКомандуЗаполнитьПоБУНаСервере();
КонецПроцедуры

&НаСервере
Процедура ГП_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
    ГП_УстановитьВидимостьДополнительныхЭлементов(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы
// Гарант+ Килипенко 16.12.2024 [F00232332] Создание РН.ОплатаУслуг --

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ГП_ЗаполнитьПоБУПриНажатии(Команда)
    РезультатЗаполнения = ГП_ЗаполнитьРасшифровкуПоБУНаСервере();
    Если РезультатЗаполнения = Истина Тогда
        ПоступлениеНаРасчетныйСчетФормыКлиентСервер.ОбновитьИтоги(ЭтотОбъект);
        ЭтотОбъект.Модифицированность = Истина;
    КонецЕсли;
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

// Гарант+ Килипенко 16.12.2024 [F00232332] Создание РН.ОплатаУслуг ++
#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ГП_СоздатьЭлементРасшифровкаПлатежаНоменклатураНаСервере()
    РезультатФункции = Новый Структура("Группа, Номенклатура, НоменклатурнаяГруппа");

    ИмяТаблицыРасшифровки = "РасшифровкаПлатежа";

    // Создание группы колонок
    ИмяСоздаваемогоЭлемента = СтрШаблон("ГП_%1ГруппаНоменклатура", ИмяТаблицыРасшифровки);
    ПараметрыЭлемента = Новый Структура("Заголовок, Группировка, ОтображатьЗаголовок");
    ПараметрыЭлемента.Заголовок = "Номенклатура / Номенклатурная группа";
    ПараметрыЭлемента.Группировка = ГруппировкаКолонок.Вертикальная;
    ПараметрыЭлемента.ОтображатьЗаголовок = Истина;
    РезультатФункции.Группа = ГП_РаботаСФормамиКлиентСервер.СоздатьГруппуКолонок(ИмяСоздаваемогоЭлемента, ПараметрыЭлемента, ЭтотОбъект,
            ИмяТаблицыРасшифровки, "РасшифровкаПлатежаСуммаПлатежа");

    // Создание элемента Номенклатура
    НаименованиеСоздаваемогоРеквизита = "ГП_Номенклатура";
    ТипРеквизита = Новый ОписаниеТипов("СправочникСсылка.Номенклатура");
    СозданныйРеквизит = Новый РеквизитФормы(
            НаименованиеСоздаваемогоРеквизита, ТипРеквизита,
            ИмяТаблицыРасшифровки, "Номенклатура");
    ЭтотОбъект.ИзменитьРеквизиты(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СозданныйРеквизит));

    ИмяСоздаваемогоЭлемента = СтрШаблон("ГП_%1Номенклатура", ИмяТаблицыРасшифровки);
    ПараметрыЭлемента = Новый Структура("ПутьКДанным, ОтображатьВПодвале");
    ПараметрыЭлемента.ПутьКДанным = СтрШаблон("%1.%2", ИмяТаблицыРасшифровки, СозданныйРеквизит.Имя);
    ПараметрыЭлемента.ОтображатьВПодвале = Ложь;

    РезультатФункции.Номенклатура = ГП_РаботаСФормамиКлиентСервер.СоздатьПолеФормы(ИмяСоздаваемогоЭлемента, ПараметрыЭлемента, ЭтотОбъект,
            РезультатФункции.Группа.Имя, Неопределено);

    // Создание элемента НоменклатурнаяГруппа
    НаименованиеСоздаваемогоРеквизита = "ГП_НоменклатурнаяГруппа";
    ТипРеквизита = Новый ОписаниеТипов("СправочникСсылка.НоменклатурныеГруппы");
    СозданныйРеквизит = Новый РеквизитФормы(
            НаименованиеСоздаваемогоРеквизита, ТипРеквизита,
            ИмяТаблицыРасшифровки, "Номенклатурная группа");
    ЭтотОбъект.ИзменитьРеквизиты(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СозданныйРеквизит));

    ИмяСоздаваемогоЭлемента = СтрШаблон("ГП_%1НоменклатурнаяГруппа", ИмяТаблицыРасшифровки);
    ПараметрыЭлемента.ПутьКДанным = СтрШаблон("%1.%2", ИмяТаблицыРасшифровки, СозданныйРеквизит.Имя);

    РезультатФункции.Номенклатура = ГП_РаботаСФормамиКлиентСервер.СоздатьПолеФормы(ИмяСоздаваемогоЭлемента, ПараметрыЭлемента, ЭтотОбъект,
            РезультатФункции.Группа.Имя, Неопределено);

    Возврат РезультатФункции;
КонецФункции

&НаСервере
Функция ГП_СоздатьКомандуЗаполнитьПоБУНаСервере()
    РезультатФункции = Новый Структура("Кнопка, Команда");

    // Создание команды формы
    РезультатФункции.Команда = ЭтотОбъект.Команды.Добавить("ГП_ЗаполнитьПоБУ");
    РезультатФункции.Команда.Заголовок = "Заполнить по БУ";
    РезультатФункции.Команда.Действие = "ГП_ЗаполнитьПоБУПриНажатии";

    ИмяСоздаваемогоЭлемента = "ГП_ЗаполнитьПоБУ";
    ПараметрыКнопки = Новый Структура("Заголовок, ИмяКоманды, Видимость");
    ПараметрыКнопки.Заголовок = РезультатФункции.Команда.Заголовок;
    ПараметрыКнопки.ИмяКоманды = РезультатФункции.Команда.Имя;
    ПараметрыКнопки.Видимость = Истина;
    ПараметрыКнопки.Вставить("Вид", ВидКнопкиФормы.КнопкаКоманднойПанели);
    ПараметрыКнопки.Вставить("Отображение", ОтображениеКнопки.КартинкаИТекст);

    РезультатФункции.Кнопка = ГП_РаботаСФормамиКлиентСервер.СоздатьКнопкуФормы(ИмяСоздаваемогоЭлемента, ПараметрыКнопки, ЭтотОбъект,
            "РасшифровкаПлатежаКоманднаяПанель");

    РезультатФункции.Кнопка.Картинка = БиблиотекаКартинок.ЗаполнитьФорму;

    Возврат РезультатФункции;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ГП_УстановитьВидимостьДополнительныхЭлементов(Форма)
    Если Форма.ВидОперации = ПредопределенноеЗначение(
            "Перечисление.ВидыОперацийПоступлениеДенежныхСредств.ОплатаПокупателя") Тогда

        Форма.Элементы.ГП_РасшифровкаПлатежаГруппаНоменклатура.Видимость = Истина;
        Форма.Элементы.ГП_ЗаполнитьПоБУ.Видимость = Истина;
    Иначе
        Форма.Элементы.ГП_РасшифровкаПлатежаГруппаНоменклатура.Видимость = Ложь;
        Форма.Элементы.ГП_ЗаполнитьПоБУ.Видимость = Ложь;
    КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ГП_ЗаполнитьРасшифровкуПоБУНаСервере()
    Если ЗначениеЗаполнено(ЭтотОбъект.Контрагент) = Ложь Тогда
        ОбщегоНазначения.СообщитьПользователю("Не заполнен Контрагент");
        Возврат Ложь;
    КонецЕсли;

    ОбщиеДанныеЗаполненияРасшифровки = Неопределено;
    Если ЭтотОбъект.РасшифровкаПлатежа.Количество() > 0 Тогда
        ОбщиеДанныеЗаполненияРасшифровки = ЭтотОбъект.РасшифровкаПлатежа.Выгрузить(
            ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ЭтотОбъект.РасшифровкаПлатежа[0]));
        ОбщиеДанныеЗаполненияРасшифровки.Колонки.Удалить("НомерСтроки");
        ОбщиеДанныеЗаполненияРасшифровки =
            ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ОбщиеДанныеЗаполненияРасшифровки[0]);
        ЗаполнитьЗначенияСвойств(ОбщиеДанныеЗаполненияРасшифровки, ЭтотОбъект.РасшифровкаПлатежа[0]);
    КонецЕсли;

    ДоговорДляРасшифровки = ?(ОбщиеДанныеЗаполненияРасшифровки <> Неопределено,
            ОбщиеДанныеЗаполненияРасшифровки.ДоговорКонтрагента, Неопределено);
    Если ДоговорДляРасшифровки = Неопределено ИЛИ ЗначениеЗаполнено(ДоговорДляРасшифровки) = Ложь Тогда
        ОбщегоНазначения.СообщитьПользователю("Не заполнен Договор");
        Возврат Ложь;
    КонецЕсли;

    ДанныеРаспределения = ГП_РаботаСПоступлениемПлатежей.РаспределитьСуммуДокументаОплатыУслуг(
        Новый Структура("Контрагент, Дата, Организация, СуммаОплаты",
            ЭтотОбъект.Контрагент, ТекущаяДатаСеанса(), ЭтотОбъект.Организация, ЭтотОбъект.СуммаДокумента));
    Если ДанныеРаспределения.ТаблицаРаспределения.Количество() > 0 Тогда
        ЭтотОбъект.РасшифровкаПлатежа.Очистить();
    Иначе
        ОбщегоНазначения.СообщитьПользователю("Не удалось выполнить заполнение расшифровки");
        Возврат Ложь;
    КонецЕсли;

    Для Каждого СтрокаДанных Из ДанныеРаспределения.ТаблицаРаспределения Цикл
        НоваяСтрокаРасшифровки = ЭтотОбъект.РасшифровкаПлатежа.Добавить();
        ЗаполнитьЗначенияСвойств(НоваяСтрокаРасшифровки, ОбщиеДанныеЗаполненияРасшифровки);
        НоваяСтрокаРасшифровки.ДоговорКонтрагента = ДоговорДляРасшифровки;
        НоваяСтрокаРасшифровки.ГП_Номенклатура = СтрокаДанных.Номенклатура;
        НоваяСтрокаРасшифровки.ГП_НоменклатурнаяГруппа = СтрокаДанных.НоменклатурнаяГруппа;
        НоваяСтрокаРасшифровки.СуммаПлатежа = СтрокаДанных.Сумма;

        ПоступлениеНаРасчетныйСчетФормыКлиентСервер.ПересчитатьСуммуНДС(НоваяСтрокаРасшифровки);
    КонецЦикла;

    Возврат Истина;
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
// Гарант+ Килипенко 16.12.2024 [F00232332] Создание РН.ОплатаУслуг --
