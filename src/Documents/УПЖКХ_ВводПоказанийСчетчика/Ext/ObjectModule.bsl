// Гарант+ Килипенко 01.10.2024 [F00228899] Формирование комментария со списком договоров док. ввода показаний ++
#Область ОбработчикиСобытий

&После ("ПередЗаписью")
Процедура ГП_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
    Если Отказ ИЛИ ПустаяСтрока(ЭтотОбъект.Комментарий) = Ложь Тогда
        Возврат;
    КонецЕсли;

    СтрокаСпискаДоговоров = ГП_СформироватьСтрокуСпискаДоговоровПоПУ();
    Если ПустаяСтрока(СтрокаСпискаДоговоров) Тогда
        Возврат;
    КонецЕсли;

    ЭтотОбъект.Комментарий = "#По договорам: " + СтрокаСпискаДоговоров;
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий
// Гарант+ Килипенко 01.10.2024 [F00228899] Формирование комментария со списком договоров док. ввода показаний --

// Гарант+ Килипенко 01.10.2024 [F00228899] Формирование комментария со списком договоров док. ввода показаний ++
#Область СлужебныеПроцедурыИФункции

// Возвращаемое значение:
//  - Строка
Функция ГП_СформироватьСтрокуСпискаДоговоровПоПУ()
    МассивПУ = Новый Массив;
    Для Каждого СтрокаГлавная Из ЭтотОбъект.Главная Цикл
        Если СтрокаГлавная.Счетчик = Справочники.КВП_Счетчики.ПустаяСсылка() Тогда
            Продолжить;
        КонецЕсли;

        МассивПУ.Добавить(СтрокаГлавная.Счетчик);
    КонецЦикла;

    Если МассивПУ.Количество() = 0 Тогда
        Возврат "";
    КонецЕсли;

    МассивДоговоров = ГП_ПриборыУчета.ПолучитьМассивДоговоровВзаиморасчетовДляПриборовУчетаПоЛС(МассивПУ);

    СтрокаСпискаДоговоров = "";
    Для Каждого Договор Из МассивДоговоров Цикл
        СтрокаСпискаДоговоров = СтрШаблон("%1; %2", СтрокаСпискаДоговоров, Строка(Договор));
    КонецЦикла;
    СтрокаСпискаДоговоров = Сред(СтрокаСпискаДоговоров, 3);

    Возврат СтрокаСпискаДоговоров;
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
// Гарант+ Килипенко 01.10.2024 [F00228899] Формирование комментария со списком договоров док. ввода показаний --
