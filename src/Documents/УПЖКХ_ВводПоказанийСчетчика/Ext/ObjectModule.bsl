// Гарант+ Килипенко 16.10.2024 [F00229732] Отключение контроля даты поверки ПУ при начислении ++
#Область ПереопределениеФункционала

&ИзменениеИКонтроль("ПолучитьЗакрепленныеСчетчики")
Функция ГП_ПолучитьЗакрепленныеСчетчики(СписокОбъектов, СписокСчетчиков)

    // Запрос выполняется в несколько этапов. На первом этапе получаются данные из регистра сведений "История состояний 
    // приборов учета интервальный" о действующих приборах учета согласно критериям отбора (с помощью процедуры 
    // УПЖКХ_ПериодическиеРегистрыПриборыУчета.СформироватьТаблицуЗакрепленныхПриборовУчета).
    // На втором этапе полученные данные фильтруются по объектам учета.
    // Но т.к. прибор учета может быть установлен одновременно на несколько объектов учета (например, общий счетчик
    // на несколько квартир), то для корректного распределения показаний нужно получить данные и по другим объектам 
    // учета, на которые установлены полученные счетчики, даже если они не входят в условия отбора по полю "Объект".
    // По этой причине на третьем этапе снова обращаемся к данным, полученным на первом этапе из регистра сведений 
    // "История состояний приборов учета интервальный", но уже с отбором по счетчикам, входящих в результат, 
    // полученный на втором этапе.

    // АЛГОРИТМ ВЫПОЛНЕНИЯ ЗАПРОСА:
    // 1. Получаем сведения о приборах учета, действующих в текущем месяце, согласно критериям отбора по всем объектам.
    // 2. Обращаемся к полученным сведениям и отбираем данные по приборам учета по нужным объектам.
    // 3. Т.к. прибор учета может быть установлен одновременно на несколько объектов учета (например, общий счетчик
    //    на несколько квартир), дополняем данные по другим объектам, на которые установлены полученные счетчики.

    СтруктураНастроек = ПолучитьНастройкиПолученияЗакрепленныхСчетчиков(СписокОбъектов, СписокСчетчиков);

    Запрос = Новый Запрос;

    // Параметры для построения таблицы действующих приборов учета.
    ПараметрыПостроения = УПЖКХ_ПериодическиеРегистрыПриборыУчета.ПараметрыПостроенияЗапросаИсторииСостоянийПУ();
    ПараметрыПостроения.ПризнакДействия            = Истина;
    ПараметрыПостроения.НачалоПериода              = СтруктураНастроек.ДатаНачалоМесяца;
    ПараметрыПостроения.КонецПериода               = СтруктураНастроек.ДатаКонецМесяца;
    ПараметрыПостроения.ИзменениеСостоянияВПериоде = Ложь;
    #Вставка // Гарант+ Килипенко 16.10.2024 [F00229732] Отключение контроля даты поверки ПУ при начислении ++
    
    // Добавление признака настройки для игнорирования фильта отключенных счетчиков на поверку
    //  Т.е. недействуюшие счетчики с причиной отключения - Поверка будут попадать в отбор
    ПараметрыПостроения.Вставить("ГП_ИгнорироватьОтключениеНаПовереку");
    
    #КонецВставки // Гарант+ Килипенко 16.10.2024 [F00229732] Отключение контроля даты поверки ПУ при начислении --

    ОтбиратьПоУслуге = НЕ Услуга.Пустая();

    Если ОтбиратьПоУслуге Тогда
        ПараметрыПостроения.СписокУслуг = Услуга;
    КонецЕсли;

    Если СтруктураНастроек.ОтбиратьПоТипуОбъекта Тогда
        ПараметрыПостроения.СписокТиповОбъекта = СтруктураНастроек.ТипОбъекта;
    КонецЕсли;

    Если СтруктураНастроек.ОтбиратьПоСчетчикам Тогда
        ПараметрыПостроения.СписокПриборов = СтруктураНастроек.СписокСчетчиков;
    КонецЕсли;

    // Помещаем в менеджер временных таблиц запроса таблицу действующих в периоде приборов учета.
    УПЖКХ_ПериодическиеРегистрыПриборыУчета.СформироватьТаблицуЗакрепленныхПриборовУчета(Запрос, ПараметрыПостроения);

    Запрос.Текст = 
    "ВЫБРАТЬ РАЗЛИЧНЫЕ
    |	втЗакрепленныеПриборыУчета.ПриборУчета КАК ПриборУчета
    |ПОМЕСТИТЬ СчетчикиЗакрепленныеНаОбъекты
    |ИЗ
    |	втЗакрепленныеПриборыУчета КАК втЗакрепленныеПриборыУчета
    |ГДЕ
    |	(НЕ &ОтбиратьПоОбъектам
    |			ИЛИ втЗакрепленныеПриборыУчета.Объект В (&Объект))
    |	И (НЕ &ВводНачальныхПоказаний
    |			ИЛИ НЕ втЗакрепленныеПриборыУчета.ПриборУчета.СпособРегистрацииПоказаний = ЗНАЧЕНИЕ(Перечисление.КВП_СпособыРегистрацииПоказанийСчетчиков.РасходЗаПериодРасчета))
    |
    |ИНДЕКСИРОВАТЬ ПО
    |	ПриборУчета
    |;
    |
    |////////////////////////////////////////////////////////////////////////////////
    |ВЫБРАТЬ РАЗЛИЧНЫЕ
    |	втЗакрепленныеПриборыУчета.ПриборУчета КАК Счетчик,
    |	втЗакрепленныеПриборыУчета.Услуга КАК Услуга,
    |	втЗакрепленныеПриборыУчета.Объект КАК Объект,
    |	&ДатаДокумента КАК ДатаПоказания
    |ИЗ
    |	втЗакрепленныеПриборыУчета КАК втЗакрепленныеПриборыУчета
    |ГДЕ
    |	втЗакрепленныеПриборыУчета.ПриборУчета В
    |			(ВЫБРАТЬ
    |				СчетчикиЗакрепленныеНаОбъекты.ПриборУчета
    |			ИЗ
    |				СчетчикиЗакрепленныеНаОбъекты)";

    Запрос.УстановитьПараметр("ДатаДокумента",          Дата);
    Запрос.УстановитьПараметр("ОтбиратьПоОбъектам",     СтруктураНастроек.ОтбиратьПоОбъектам);
    Запрос.УстановитьПараметр("Объект",                 СтруктураНастроек.СписокОбъектов);
    Запрос.УстановитьПараметр("ВводНачальныхПоказаний", ВводНачальныхПоказаний);

    Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

#КонецОбласти // ПереопределениеФункционала
// Гарант+ Килипенко 16.10.2024 [F00229732] Отключение контроля даты поверки ПУ при начислении --

// Гарант+ Килипенко 01.10.2024 [F00228899] Формирование комментария со списком договоров док. ввода показаний ++
#Область ОбработчикиСобытий

&После ("ПередЗаписью")
Процедура ГП_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
    ПрефиксСпискаДоговоров = "#";

    Если Отказ
        ИЛИ (ПустаяСтрока(ЭтотОбъект.Комментарий) = Ложь И СтрНачинаетсяС(ЭтотОбъект.Комментарий, ПрефиксСпискаДоговоров) = Ложь) Тогда
        Возврат; // Если комментарий заполнен пользователем - тогда не формируем список
    КонецЕсли;

    КоллекцияСчетчиковФормирования = ГП_ПолучитьКоллекциюСчетчиковДокумента();
    СтрокаСпискаДоговоров = ГП_ПриборыУчета.СформироватьСтрокуСпискаДоговоровПоПУ(КоллекцияСчетчиковФормирования);
    Если СтрокаСпискаДоговоров = Неопределено Тогда
        Возврат; // Список не сформирован
    КонецЕсли;

    // Записываем в комментарий список договоров счетчиков
    ЭтотОбъект.Комментарий = СтрШаблон("%1 %2", ПрефиксСпискаДоговоров, СтрокаСпискаДоговоров);
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий
// Гарант+ Килипенко 01.10.2024 [F00228899] Формирование комментария со списком договоров док. ввода показаний --

// Гарант+ Килипенко 01.10.2024 [F00228899] Формирование комментария со списком договоров док. ввода показаний ++
#Область СлужебныеПроцедурыИФункции

// Возвращаемое значение:
//  - Строка
Функция ГП_ПолучитьКоллекциюСчетчиковДокумента()
    МассивПУ = Новый Массив;
    Для Каждого СтрокаГлавная Из ЭтотОбъект.Главная Цикл
        Если СтрокаГлавная.Счетчик = Справочники.КВП_Счетчики.ПустаяСсылка() Тогда
            Продолжить;
        КонецЕсли;

        МассивПУ.Добавить(СтрокаГлавная.Счетчик);
    КонецЦикла;

    Возврат МассивПУ;
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
// Гарант+ Килипенко 01.10.2024 [F00228899] Формирование комментария со списком договоров док. ввода показаний --
