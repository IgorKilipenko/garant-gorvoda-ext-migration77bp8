// Гарант+ Килипенко 11.09.2024 [F00227694] Доработка документа ввод показаний счетчика ++
#Область ОбработчикиСобытийФормы

// Для заполнения ТЧ Главная при создании документа
&НаСервере
Процедура ГП_ПриСозданииНаСервереПеред(Отказ, СтандартнаяОбработка)
    ГП_СоздатьКомандуНачисленияПоПоказаниям();

    ЭтоНовыйДокументБезКопирования = Объект.Ссылка.Пустая()
        И (ЭтотОбъект.Параметры.Свойство("ЗначениеКопирования") = Ложь
            ИЛИ ЗначениеЗаполнено(ЭтотОбъект.Параметры.ЗначениеКопирования));
    Если Отказ ИЛИ ЭтоНовыйДокументБезКопирования Тогда
        Возврат;
    КонецЕсли;

    Если ЭтотОбъект.Параметры.Свойство("ГП_ВидОперации") Тогда
        ЭтотОбъект.Объект.ВидОперации = ЭтотОбъект.Параметры.ГП_ВидОперации;
    КонецЕсли;

    Если Параметры.Свойство("ГП_ЗначенияПоказанийДляЗаполнения") Тогда
        ЭтотОбъект.Объект.Главная.Очистить();

        // Заполнение ТЧ Главная
        ДатаЗаполнения = ТекущаяДатаСеанса();
        Для Каждого СтрокаИсходныхПоказаний Из ЭтотОбъект.Параметры.ГП_ЗначенияПоказанийДляЗаполнения Цикл
            НоваяСтрокаПоказаний = ЭтотОбъект.Объект.Главная.Добавить();
            НоваяСтрокаПоказаний.ДатаПоказания = ДатаЗаполнения;
            ЗаполнитьЗначенияСвойств(НоваяСтрокаПоказаний, СтрокаИсходныхПоказаний);
        КонецЦикла;

        ОбновитьТабличнуюЧастьНаСервере();
    КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ГП_ПриОткрытииПосле(Отказ)
    ГП_НастроитьВидимостьЭлементовФормы();
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы
// Гарант+ Килипенко 11.09.2024 [F00227694] Доработка документа ввод показаний счетчика --

// Гарант+ Килипенко 16.09.2024 [F00227727] Доработать заполнение документа начисление по приборам учета ++
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ГП_ВидОперацииПриИзмененииПосле(Элемент)
    ГП_НастроитьВидимостьЭлементовФормы();
КонецПроцедуры

&НаКлиенте
Процедура ГП_ВидОперацииОбработкаВыбораПосле(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
    ГП_НастроитьВидимостьЭлементовФормы();
КонецПроцедуры

&НаКлиенте
Процедура ГП_ВводНачальныхПоказанийПриИзмененииПосле(Элемент)
    ГП_НастроитьВидимостьЭлементовФормы();
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовШапкиФормы
// Гарант+ Килипенко 16.09.2024 [F00227727] Доработать заполнение документа начисление по приборам учета --

// Гарант+ Килипенко 16.09.2024 [F00227727] Доработать заполнение документа начисление по приборам учета ++
#Область ОбработчикиКомандФормы

// Создает документ ввода показаний счетчиков по текущей строке списка (с отбором по договору л/с текущей строки)
&НаКлиенте
Асинх Процедура ГП_НачислитьУслугиПоПоказаниямПриНажатии(Команда)
    Если ЭтотОбъект.Объект.Ссылка.Пустая() Тогда
        ОбщегоНазначенияКлиент.СообщитьПользователю("Документ должен быть записан.");
        РезультатДиалога = Ждать ГП_ОбщегоНазначенияКлиент.СпроситьПользователяОГотовностиВыполнятьОперациюАсинх(
                "Для продолжения необходимо записать документ.");
        Если РезультатДиалога = Ложь Тогда
            Возврат; // Пользователь отказался от выполнения операции
        Иначе
            ЭтотОбъект.Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Запись));
        КонецЕсли;
    КонецЕсли;

    ПараметрыВводаПоказаний = Новый Структура("ГП_ВидОперации",
            ПредопределенноеЗначение("Перечисление.КВП_ВидыОперацийНачисленияУслуг.НачисленияПоПоказаниямПУ"));

    СписокЛС = ГП_ПолучитьСписокЛицевыхСчетовДокументаНаСервере(ЭтотОбъект.Объект.Ссылка);
    ПараметрыВводаПоказаний.Вставить("ГП_СписокЛицевыхСчетов", СписокЛС);

    ФормаНачисленияУслуг = ОткрытьФорму("Документ.КВП_НачислениеУслуг.Форма.ФормаДокумента",
            ПараметрыВводаПоказаний, , , , , , РежимОткрытияОкнаФормы.Независимый);
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы
// Гарант+ Килипенко 16.09.2024 [F00227727] Доработать заполнение документа начисление по приборам учета --

// Гарант+ Килипенко 16.09.2024 [F00227727] Доработать заполнение документа начисление по приборам учета ++
#Область СлужебныеПроцедурыИФункции

// Создает кнопку формы Начислить услуги по показаниям
// Возвращаемое значение:
//  - Структура
//      * Кнопка
//      * Команда
&НаСервере
Функция ГП_СоздатьКомандуНачисленияПоПоказаниям()
    Результат = Новый Структура("Кнопка, Команда");

    // Создание команды формы
    Результат.Команда = ЭтотОбъект.Команды.Добавить("ГП_НачислитьУслугиПоПоказаниям");
    Результат.Команда.Заголовок = "Начислить услуги по показаниям";
    Результат.Команда.Действие = "ГП_НачислитьУслугиПоПоказаниямПриНажатии";

    // Создание элемента формы
    Результат.Кнопка = ЭтотОбъект.Элементы.Добавить(
            "ГП_НачислитьУслугиПоПоказаниям",
            Тип("КнопкаФормы"),
            ЭтотОбъект.Элементы.ФормаКоманднаяПанель);
    Результат.Кнопка.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
    Результат.Кнопка.ИмяКоманды = Результат.Команда.Имя;

    Возврат Результат;
КонецФункции

&НаКлиенте
Процедура ГП_НастроитьВидимостьЭлементовФормы()
    ЭтотОбъект.Элементы.ГП_НачислитьУслугиПоПоказаниям.Видимость = (ЭтотОбъект.Объект.ВводНачальныхПоказаний = Ложь
            И ЭтотОбъект.Объект.ВидОперации =
            ПредопределенноеЗначение("Перечисление.КВП_ВидыОперацийВводаПоказанийСчетчика.ВводПоказанииСчетчикаНаЛицевойСчет"));
КонецПроцедуры

&НаСервереБезКонтекста
Функция ГП_ПолучитьСписокЛицевыхСчетовДокументаНаСервере(Знач ДокументСсылка)
    Запрос = Новый Запрос;
    Запрос.Текст =
        "ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |   ВЫРАЗИТЬ(ВводПоказанийСчетчикаГлавная.Объект КАК Справочник.КВП_ЛицевыеСчета) КАК ЛицевойСчет
        |ИЗ
        |   Документ.УПЖКХ_ВводПоказанийСчетчика.Главная КАК ВводПоказанийСчетчикаГлавная
        |ГДЕ
        |   ВводПоказанийСчетчикаГлавная.Ссылка = &ДокументСсылка
        |   И ВводПоказанийСчетчикаГлавная.Объект ССЫЛКА Справочник.КВП_ЛицевыеСчета
        |   И ВводПоказанийСчетчикаГлавная.Объект <> ЗНАЧЕНИЕ(Справочник.КВП_ЛицевыеСчета.ПустаяСсылка)
        |
        |СГРУППИРОВАТЬ ПО
        |   ВЫРАЗИТЬ(ВводПоказанийСчетчикаГлавная.Объект КАК Справочник.КВП_ЛицевыеСчета)
        |";

    Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);

    РезультатЗапроса = Запрос.Выполнить();
    ТаблицаГлавная = РезультатЗапроса.Выгрузить();

    // Формирование списка значений
    МассивЛицевыхСчетов = ТаблицаГлавная.ВыгрузитьКолонку("ЛицевойСчет");
    РезультатФункции = Новый СписокЗначений;
    Для Каждого ТекущийЛицевойСчет Из МассивЛицевыхСчетов Цикл
        РезультатФункции.Добавить(ТекущийЛицевойСчет);
    КонецЦикла;

    Возврат РезультатФункции;
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
// Гарант+ Килипенко 16.09.2024 [F00227727] Доработать заполнение документа начисление по приборам учета --
