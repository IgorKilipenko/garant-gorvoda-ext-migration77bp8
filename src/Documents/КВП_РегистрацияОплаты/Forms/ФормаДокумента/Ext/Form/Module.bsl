#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ГП_ПриСозданииНаСервереПеред(Отказ, СтандартнаяОбработка)
    Если ЗначениеЗаполнено(ЭтотОбъект.Объект.Ссылка) = Ложь И ЭтотОбъект.Параметры.Свойство("ГП_ДанныеЗаполнения") = Истина Тогда
        // ЗаполнитьЗначенияСвойств(ЭтотОбъект.Объект, ЭтотОбъект.Параметры.ГП_ДанныеЗаполнения);
        ДокументОбъект = ЭтотОбъект.РеквизитФормыВЗначение("Объект");

        ДокументОбъект.Заполнить(ЭтотОбъект.Параметры.ГП_ДанныеЗаполнения);

        ДокументОбъект.Дата = ЭтотОбъект.Параметры.ГП_ДанныеЗаполнения.Дата;
        ДокументОбъект.УстановитьВремя(РежимАвтоВремя.Последним);

        ДокументОбъект.Контрагент = ЭтотОбъект.Параметры.ГП_ДанныеЗаполнения.Контрагент;

        ДокументОбъект.ДокументыПоступленияДенежныхСредств.Очистить();
        НоваяСтрокаДокументов = ДокументОбъект.ДокументыПоступленияДенежныхСредств.Добавить();
        НоваяСтрокаДокументов.Документ = ЭтотОбъект.Параметры.ГП_ДанныеЗаполнения.ДокументОплаты;

        ЭтотОбъект.ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");

        ГП_ЗаполнитьТаблицуЛицевыхСчетовПоКонтрагентуНаСервере(
            ЭтотОбъект.Параметры.ГП_ДанныеЗаполнения.СуммаДокументаОплаты,
            ЭтотОбъект.Параметры.ГП_ДанныеЗаполнения.ДокументОплаты);
    КонецЕсли;
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

#Область СлужебныеПроцедурыИФункции

// Устарела - нужно перенести в модуль
&НаСервереБезКонтекста
Функция ГП_ПолучитьЛицевыеСчетаКонтрагентаНаСервере(Знач КонтрагентСсылка, Знач ОрганизацияСсылка, Знач ДатаАктуальности = Неопределено)
    Запрос = Новый Запрос;
    Запрос.Текст =
        "ВЫБРАТЬ РАЗРЕШЕННЫЕ
        |   СведенияДляВзаиморасчетовПоЛС.Период КАК Период,
        |   СведенияДляВзаиморасчетовПоЛС.ЛицевойСчет КАК ЛицевойСчет
        |ИЗ
        |   РегистрСведений.УПЖКХ_СведенияДляВзаиморасчетовПоЛС.СрезПоследних(&ДатаАктуальности, Контрагент = &Контрагент И Организация = &Организация) КАК СведенияДляВзаиморасчетовПоЛС
        |";

    Если ДатаАктуальности <> Неопределено Тогда
        Запрос.УстановитьПараметр("ДатаАктуальности", ДатаАктуальности);
    Иначе
        Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДатаАктуальности", "");
    КонецЕсли;

    ОрганизацияСсылка = ?(ОрганизацияСсылка = Неопределено,
            УПЖКХ_ТиповыеМетодыВызовСервера.ПолучитьОсновнуюОрганизацию(), ОрганизацияСсылка);
        Запрос.УстановитьПараметр("Организация", ОрганизацияСсылка);

    Запрос.УстановитьПараметр("Контрагент", КонтрагентСсылка);

    РезультатЗапроса = Запрос.Выполнить();
    РезультатФункции = РезультатЗапроса.Выгрузить();
    Возврат РезультатФункции;
КонецФункции

&НаСервере
Процедура ГП_ЗаполнитьТаблицуЛицевыхСчетовПоКонтрагентуНаСервере(Знач СуммаРаспределенияПоЛС, Знач ДокументОплаты)
    ТаблицаЛСКонтрагента = ГП_ПолучитьЛицевыеСчетаКонтрагентаНаСервере(
            ЭтотОбъект.Объект.Контрагент, ЭтотОбъект.Объект.Организация, ЭтотОбъект.Объект.Дата);

    ЭтотОбъект.Объект.ЛицевыеСчета.Очистить();

    НераспределеннаяСумма = СуммаРаспределенияПоЛС;
    СтрокаМаксимальнойСуммы = Неопределено;
    Для Каждого СтрокаЛС Из ТаблицаЛСКонтрагента Цикл
        КоэффициентРаспределенияЛС = 1.0 / ТаблицаЛСКонтрагента.Количество();

        НоваяСтрокаЛС = ЭтотОбъект.Объект.ЛицевыеСчета.Добавить();
        НоваяСтрокаЛС.Объект = СтрокаЛС.ЛицевойСчет;
        НоваяСтрокаЛС.ДокументОплаты = ДокументОплаты;
        НоваяСтрокаЛС.Сумма = Окр(СуммаРаспределенияПоЛС * КоэффициентРаспределенияЛС, 2, 0);
        НоваяСтрокаЛС.РаспределятьПоУказаннымУслугам = Ложь;
        НоваяСтрокаЛС.ФлагРедактирования = Ложь;
        НоваяСтрокаЛС.ФлагРедактированияНастроек = 1;
        НоваяСтрокаЛС.ВариантРаспределения = "";

        НераспределеннаяСумма = НераспределеннаяСумма - НоваяСтрокаЛС.Сумма;

        Если СтрокаМаксимальнойСуммы = Неопределено Тогда
            СтрокаМаксимальнойСуммы = НоваяСтрокаЛС;
        КонецЕсли;

        Если СтрокаМаксимальнойСуммы.Сумма < НоваяСтрокаЛС.Сумма Тогда
            СтрокаМаксимальнойСуммы = НоваяСтрокаЛС;
        КонецЕсли;
    КонецЦикла;

    Если НераспределеннаяСумма <> 0 И СтрокаМаксимальнойСуммы <> Неопределено Тогда
        СтрокаМаксимальнойСуммы.Сумма = СтрокаМаксимальнойСуммы.Сумма + НераспределеннаяСумма;
    КонецЕсли;
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
