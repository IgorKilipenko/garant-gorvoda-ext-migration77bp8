#Область ОбработчикиСобытий

&После ("ПередЗаписью")
Процедура ГП_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
    Если Отказ ИЛИ ЭтотОбъект.ОбменДанными.Загрузка Тогда
        Возврат;
    КонецЕсли;

    ГП_ЗаполнитьКомментарийСпискомДоговоров();
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытий

#Область ПрограммныйИнтерфейс

Процедура ГП_ЗаполнитьКомментарийСпискомДоговоров() Экспорт
    ПрефиксСпискаДоговоров = "#";

    Если ЭтотОбъект.СписокОбъектов.Количество() = 0 Тогда
        Возврат; // Нет данных
    КонецЕсли;

    Если (ПустаяСтрока(ЭтотОбъект.Комментарий) = Ложь
        И СтрНачинаетсяС(ЭтотОбъект.Комментарий, ПрефиксСпискаДоговоров) = Ложь
        И СтрНачинаетсяС(ЭтотОбъект.Комментарий, "#создан автоматически") = Ложь) Тогда

        Возврат; // Если комментарий заполнен пользователем - тогда не формируем список
    КонецЕсли;

    ТаблицаЗданийПомещений = ЭтотОбъект.СписокОбъектов.Выгрузить(, "Здание, Помещение");
    ТаблицаЗданийПомещений.Свернуть("Помещение");

    МассивПомещений = ТаблицаЗданийПомещений.ВыгрузитьКолонку("Помещение");
    ТаблицаДоговоровВзаиморасчетовПоЛС = ГП_РаботаСДоговорами.ПолучитьТаблицуДоговоровВзаиморасчетовПоЛСПомещений(МассивПомещений);

    МассивДоговоров = ТаблицаДоговоровВзаиморасчетовПоЛС.ВыгрузитьКолонку("Договор");
    Если МассивДоговоров.Количество() = 0 Тогда
        Возврат; // Нет договоров
    КонецЕсли;
    ОбщегоНазначенияБПВызовСервера.УдалитьПовторяющиесяЭлементыМассива(МассивДоговоров);

    СтрокаСпискаДоговоров = ГП_ПриборыУчета.СформироватьСтрокуСпискаДоговоров(МассивДоговоров);
    Если СтрокаСпискаДоговоров = Неопределено Тогда
        Возврат; // Список не сформирован
    КонецЕсли;

    // Записываем в комментарий список договоров счетчиков
    ЭтотОбъект.Комментарий = СтрШаблон("%1 %2", ПрефиксСпискаДоговоров, СтрокаСпискаДоговоров);
КонецПроцедуры

#КонецОбласти // ПрограммныйИнтерфейс
