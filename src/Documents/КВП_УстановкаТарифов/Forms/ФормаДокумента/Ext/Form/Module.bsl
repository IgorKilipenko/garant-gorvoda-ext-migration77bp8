// Гарант+ Килипенко 17.09.2024 [F00228438] Вид тарифа потребителя для Установки тарифа ++
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ГП_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
    // Создание элементов формы
    ГП_СоздатьПолеВидТарифаПотребителя();
    ГП_СоздатьКомандуНачисленияПоПоказаниям();
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы
// Гарант+ Килипенко 17.09.2024 [F00228438] Вид тарифа потребителя для Установки тарифа --

#Область ОбработчикиКомандФормы

// Создает документ ввода показаний счетчиков по текущей строке списка (с отбором по договору л/с текущей строки)
&НаКлиенте
Асинх Процедура ГП_ЗаполнитьПоВидуТарифаПотребителяПриНажатии(Команда)
    Если ЗначениеЗаполнено(ЭтотОбъект.Объект.ГП_ВидТарифаПотребителя) = Ложь Тогда
        ОбщегоНазначенияКлиент.СообщитьПользователю("Не указан вид тарифа потребителя.");
        Возврат;
    КонецЕсли;

    Если ЗначениеЗаполнено(ЭтотОбъект.Объект.Услуга) = Ложь Тогда
        ОбщегоНазначенияКлиент.СообщитьПользователю("Не выбрана услуга.");
        Возврат;
    КонецЕсли;

    ЗаполнитьСписокОбъектовПоВидуТарифаНаСервере(ЭтотОбъект.Объект.Услуга, ЭтотОбъект.Объект.ГП_ВидТарифаПотребителя);
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

// Гарант+ Килипенко 17.09.2024 [F00228438] Вид тарифа потребителя для Установки тарифа ++
#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ГП_СоздатьПолеВидТарифаПотребителя()
    ПолеВидТарифаПотребителя = ЭтотОбъект.Элементы.Добавить(
            "ГП_ВидТарифаПотребителя",
            Тип("ПолеФормы"),
            ЭтотОбъект.Элементы.ГруппаСправа);
    ПолеВидТарифаПотребителя.Вид = ВидПоляФормы.ПолеВвода;
    ПолеВидТарифаПотребителя.ПутьКДанным = "Объект.ГП_ВидТарифаПотребителя";

    Возврат ПолеВидТарифаПотребителя;
КонецФункции

// Создает кнопку формы Заполнить объекты по Виду тарифа потребителя
// Возвращаемое значение:
//  - Структура
//      * Кнопка
//      * Команда
&НаСервере
Функция ГП_СоздатьКомандуНачисленияПоПоказаниям()
    Результат = Новый Структура("Кнопка, Команда");

    // Создание команды формы
    Результат.Команда = ЭтотОбъект.Команды.Добавить("ГП_ЗаполнитьПоВидуТарифаПотребителя");
    Результат.Команда.Заголовок = "Заполнить по виду тарифа";
    Результат.Команда.Действие = "ГП_ЗаполнитьПоВидуТарифаПотребителяПриНажатии";

    // Создание элемента формы
    Результат.Кнопка = ЭтотОбъект.Элементы.Добавить(
            "ГП_ЗаполнитьПоВидуТарифаПотребителя",
            Тип("КнопкаФормы"),
            ЭтотОбъект.Элементы.СписокОбъектовКоманднаяПанель);
    Результат.Кнопка.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
    Результат.Кнопка.ИмяКоманды = Результат.Команда.Имя;

    Возврат Результат;
КонецФункции

&НаСервере
Процедура ЗаполнитьСписокОбъектовПоВидуТарифаНаСервере(Знач Услуга, Знач ВидТарифаПотребителя)
    ЭтотОбъект.Объект.СписокОбъектов.Очистить();

    ДанныеДляЗаполнения = ГП_РаботаСТарифами.ПолучитьПомещенияДляУстановкиТарифов(
            Новый Структура("Услуга, ВидТарифаПотребителя", Услуга, ВидТарифаПотребителя));

    Для Каждого СтрокаДанных Из ДанныеДляЗаполнения Цикл
        НоваяСтрокаОбъектов = ЭтотОбъект.Объект.СписокОбъектов.Добавить();
        НоваяСтрокаОбъектов.Здание = СтрокаДанных.Здание;
        НоваяСтрокаОбъектов.Помещение = СтрокаДанных.Помещение;
    КонецЦикла;
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
// Гарант+ Килипенко 17.09.2024 [F00228438] Вид тарифа потребителя для Установки тарифа --
